! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ================================= !
! Inari Sámi morphological analyser
! ================================= !

Multichar_Symbols

 +N +A +Adv +V
 +Pron +CS +CC +Adp +Po +Pr +Interj +Pcle
 +Pers +Dem +Interr +Refl +Recipr +Rel +Indef
 +Sg +Pl 
 +Nom +Gen +Acc +Ill +Ine +Ela +Com +Ess +Par +Abe 
 +PxSg1 +PxSg2 +PxSg3 +PxDu1 +PxDu2 +PxDu3 +PxPl1 +PxPl2 +PxPl3 
 +Comp +Superl +Actor
 +Qst +Foc +Attr
 +Pass +Ind +Prs +Prt +Pot +Cond +Imprt +ImprtII +Sup
 +Sg1 +Sg2 +Sg3 +Du1 +Du2 +Du3 +Pl1 +Pl2 +Pl3
 +Inf +Ger +GerII +ConNeg +Neg +PrsPrc +PrfPrc +VGen +VAbess
 +Prop +Ani +Mal +Fem +Sur +Plc +Org +Obj ! Semantic properties of names

 +CLB +PUNCT +HYPH +PAR +LEFT +RIGHT
 e1 i1 o1 u1 
 g2 m2 n2 h2
 b3 d3 g3 m3 h3
 %^WG1 ! Weak grade
 X2    ! Weak grade
          X4 X5 X6 X7 X8 X9
 Q1 Q2 Q3 Q4 Q5 Q6 Q7 Q8 Q9
 Y1 Y2 Y3 Y4 Y5 Y6 Y7 Y8 Y9
 
%^SHV  ! 2nd syll vowel shortenes
%^CPL  ! Weak grade and Compensatory lengthening tullâ tuulâ
%^SCPL ! Strong grade but still Compensatory lengthening kussâ : kuussâ (sic)
%^VLOW ! Vowel lowering in 3rd sg of contract verbs tuhhid:tohhe
%^XST  ! Extra strong Ess Par
%^IA  ! á in front of Com Sg and Ill Pl i
%^ODDSHV ! Cns shortening in C|# and shortvow in Inf etc
%^ODDLV ! Cns shortening in C|# and longvow in inf etc
%^ILL ! á and root vowel change in Ill Sg of i-stems

! Documenting the dummies 
! %^WG1 = WeG Half-long in verbs
! X2 = WeG (EVEN_WEAKSHORT) tullâ : tulâX2
! %^SVL = Sg Com, Pl Ill
! X4 = Pl Nom

! Symbols that need to be escaped on the lower side (towards twolc):
 »7     ! »
 «7     ! «
 %[%>%] ! >
 %[%<%] ! <


! =================================================
! Basic lexica, pointing to the other lexicon files
! =================================================
  
LEXICON Root
 NounRoot ;
 ProperNoun ;
 AdjectiveRoot ;
 VerbRoot ;
 Adverb ;
 Particle ;
 Subjunction ;
 Conjunction ;
 Adposition ;
 Interjection ;
 Pronoun ;
 Numeral ;
 Punctuation ;



! ======================
! Sublexica for NounRoot
! ======================


! Even-syllable stems
! -------------------

! 1. 2syll stems
! - - - - - -

! 1.1. Vowel final
! 1.1.1 Even-syllabic â-, u- and i-stems

LEXICON SOLLA ! SM1
 +N: EVEN-NEP ; ! Strong grade
 +N:%^CPL EVEN_WEAK ;
 +N:X2  EVEN_WEAKSHORT ; ! Abe + Com Sg

LEXICON LUCCA ! SM3
 +N: EVEN-NEP ;          ! Strong grade
 +N:%^CPL EVEN_WEAK ;    ! %^CPL WeG and lengthening
 +N:X2  EVEN_WEAKSHORT ; ! X2 WeG, no lengthening


LEXICON KIETA ! SM5
 +N: EVEN-N ;           ! Strong grade kietâ
 +N:%^XST EVEN-EP ;     ! Stronger grade kiettâ 
 +N: ILL-SG ;           ! Weak grade kieđâ, can be X2
 +N:%^CPL NON-ILL-OBL ; ! Weak grade kieđâ, can be X2
 +N:X2 EVEN_WEAKSHORT ;


LEXICON KYELI ! i-e-á
 +N: EVEN-N ;           ! Strong grade kietâ
 +N:%^XST EVEN-EP ;     ! Stronger grade kiettâ 
 +N: ILL-SG ;           ! Weak grade kieđâ, can be X2
 +N:%^CPL NON-ILL-OBL ; ! Weak grade kieđâ, can be X2
 +N:X2 EVEN_WEAKSHORT ;

LEXICON ERGI ! i-i-á
 +N: EVEN-N ;           ! Strong grade kietâ
 +N:%^XST EVEN-EP ;     ! Stronger grade kiettâ 
 +N:%^ILL ILL-SG ;           ! Weak grade kieđâ, can be X2
 +N:%^CPL NON-ILL-OBL ; ! Weak grade kieđâ, can be X2
 +N:X2 EVEN_WEAKSHORT ;

LEXICON KUMPPI ! i-i-i
SOLLA ;

LEXICON KUSSA ! SM6
 +N: EVEN-NEP ; ! Strong grade
 +N: EVEN_MIXED_OBL ;
 +N:X2  EVEN_WEAKSHORT ;

LEXICON EVEN_MIXED_OBL 
 +Sg+Gen:%^CPL K ;   	    ! kuusâ
 +Sg+Acc:%^CPL K ;		    ! kuusâ
 +Sg+Ill:%^CPL%>n K ;	    ! kuusân   tuuḷân half/long ḷ
 +Sg+Loc:%^SCPL%>st K ;		! kuusâst
 +Sg+Com:%>%^IAin K ;   ! kuussáin
 +Pl+Nom:%^SCPL%>h K ;	    ! kuusah
 +Pl+Gen:%^SCPL%>i K ;		! kuusâi
 +Pl+Acc:%^SCPL%>id K ;       ! kuusâid
 +Pl+Ill:%^SCPL%>id K ;       ! kuus´did
 +Pl+Loc:%^SCPL%>in K ;   ! kuusâin



LEXICON KUOSA ! SM7
KIETA ; ! fix

! 1.1.2 Even-syllabic á/a- and o-stems

LEXICON PINO ! SM14
 +N: EVEN-NEP ;       ! Strong grade
 +N:%^CPL PINO-OBL ;
 +N:X2 EVEN_WEAKSHORT ;


LEXICON RITA ! SM15
PINO ; ! Identical?


LEXICON ALGE
+N:e EVEN-NEP ;





! Nominal substems
! - - - - - - - -  

LEXICON EVEN-NEP
 EVEN-N ;   ! strong:   tullâ, kietâ
 EVEN-EP ;  ! stronger: tullân, kiettân

LEXICON EVEN-N
 +Sg+Nom: K ;     ! tullâ

LEXICON EVEN-EP
 +Ess:%>n K ;       ! tullân
 +Par:%>d K ;       ! tullâd

LEXICON EVEN_WEAK
 ILL-SG ;
 NON-ILL-OBL ;


LEXICON NON-ILL-OBL 
 +Sg+Gen: K ;   	    ! tuulâ
 +Sg+Acc: K ;		    ! tuulâ
 +Sg+Loc:%>st K ;		! tuulâst
 +Sg+Com:%>%^IAin K ;   ! tullááin
 +Pl+Nom:X4%>h K ;	    ! tuulah
 +Pl+Gen:%>i K ;		! tuulâi
 +Pl+Acc:%>id K ;       ! tuulâid
 +Pl+Ill:%>id K ;       ! tuul´did
 +Pl+Loc:%>%^IAin K ;   ! tullâin

LEXICON ILL-SG
 +Sg+Ill:%>n K ;	    ! tuulân   tuuḷân half/long ḷ


LEXICON EVEN_WEAKSHORT
 +Sg+Abe:%>ttáá K ;   ! tulâttáá
 +Pl+Com:%>iguin K ;
 +Pl+Abe:%>ittáá K ;


LEXICON PINO-OBL ! Now = solla, fix!!!
 +Sg+Gen: K ;   	    ! tuulâ
 +Sg+Acc: K ;		    ! tuulâ
 +Sg+Ill:%>n K ;		! tuulân   tuuḷân half/long ḷ
 +Sg+Loc:%>st K ;       ! tuulâst
 +Sg+Com:%>%^IAin K ;   ! tulláin
 +Pl+Nom:%>X4h K ;	    ! tuulah
 +Pl+Gen:%>i K ;	    ! tuulâi
 +Pl+Acc:%>id K ;	    ! tuulâid
 +Pl+Ill:%>id K ;       ! tuul´did
 +Pl+Loc:%>%^IAin K ;   ! tullâin














! Odd-syllable stems
! ------------------



LEXICON KASNATTAS
LITTER ;

LEXICON LITTER ! 2nd syll vowel does not alternate
 +N: ODD-SUFF-LONGSTEM ;
 +N: ODD-SUFF-SHORTSTEM ;

LEXICON JIEREEH ! 2nd syll vowel shortening
 +N:      ODD-SUFF-LONGSTEM ;
 +N:%^SHV ODD-SUFF-SHORTSTEM ;

LEXICON IMMEEL
LITTER ; ! fix

LEXICON KOLGOOH
LITTER ; ! fix

LEXICON UAIVAT
LITTER ; ! fix



! Contracted stems
! ----------------



! 3.6.2. Gen sg odd-syllabic

LEXICON SAMMILAS ! SM150
LAS-COMMON ;
:%>ii LAS-SUF ;

LEXICON COHASAS
LAS-COMMON ;
:%>áá LAS-SUF ;

LEXICON MAJEMUS
LAS-COMMON ;
:%>ui LAS-SUF ;

LEXICON LAS-COMMON 
+N+Sg+Nom:âš K ;
+N+Sg+Ill:âžân K ;
+N+Sg+Abe:ááttáá K ;
+N+Ess:âžžân K ;
+N+Par:âžžâd K ;

LEXICON LAS-SUF
+N+Sg+Gen: K ;
+N+Sg+Acc: K ;
+N+Sg+Loc:%>st K ;
+N+Sg+Com:%>jn K ;
+N+Pl+Nom:%>h K ;
+N+Pl+Gen:%>j K ;
+N+Pl+Acc:%>jd K ;
+N+Pl+Ill:%>jd K ;
+N+Pl+Loc:%>jn K ;
+N+Pl+Com:%>jguin K ;
+N+Pl+Abe:%>h K ;





! Odd-syllable stems, sublexica
! - - - - - - - - - - - - - - - 

LEXICON ODD-SUFF-LONGSTEM
 +Sg+Nom: K ;
 +Sg+Gen: K ;
 +Sg+Acc: K ;
 +Sg+Ill:%>ân K ;

LEXICON ODD-SUFF-SHORTSTEM
 +Sg+Loc:%>ist K ;
 +Sg+Com:%>áin K ;
! +Sg+Com:%>ááin K ;
 +Sg+Abe:%>ttáá K ;
 +Ess:%>in K ;
 +Par:%>id K ;
 +Pl+Nom:%>eh K ;
 +Pl+Gen:%>ij K ;
 +Pl+Acc:%>ijd K ;
 +Pl+Ill:%>áid K ;
! +Pl+Ill:%>ááid K ;
 +Pl+Loc:%>ijn K ;
! +Pl+Com:%>ijgijn K ;
 +Pl+Com:%>ijguin K ;
 +Pl+Abe:%>ijttáá K ;

!LEXICON ODD-SUFF-MIXSTEM
! # ;




! ===========================
! Sublexica for AdjectiveRoot
! ===========================

! Even-syllable stems
! -------------------

! Odd-syllable stems
! ------------------
! Contracted stems
! ----------------

! Comparation
! -----------
 

! Comparative
! -----------


! Superlative
! -----------





! ======================
! Sublexica for VerbRoot
! ======================

! Even-syllable stems
! -------------------

LEXICON VUOLLAD
+V:%>â  VINF ;
+V: EVEN-I ;

! Odd-syllable stems
! ------------------

LEXICON LEKKASID ! SM51, no length change in stem
+V:       ODDVLEX ;


LEXICON CIELATTID ! SM57, consonant change only
+V:%^ODDC ODDVLEX ; ! Dia to shorten C before C|#

LEXICON VYEJETTID ! SM53. short V in Inf
+V:%^ODDSHV ODDVLEX ; ! Dia to shorten V 

LEXICON IALLAATTID ! SM52, long V in Inf
+V:%^ODDLV ODDVLEX ;

LEXICON ODDVLEX
+Ind+Prs:%>â VSUF_SG1 ;
+Ind+Prs:%>â VSUF_SG2 ;
+Ind+Prs:    VSUF_SG3 ;
+Ind+Prs:    VSUF_DU1 ;
+Ind+Prs:    VSUF_DU2 ;
+Ind+Prs:    VSUF_DU3 ;
+Ind+Prs:    VSUF_PL1 ;
+Ind+Prs:%>á VSUF_PL2 ;
+Ind+Prs:%>je VSUF_PL3 ;
+Ind+Prt:%>i ODD-II ;
:%>i         VINF		   ;
:%>ijj       VPRSPRC	   ;
:%>â         VPRFPRCACT	   ;
:%>u         VPRFPRCPASS   ;
:            VNEGPRSINDIMP ;
:%>i         VNEGCOND	   ;
:%>i         VNEGPOT	   ;
             VABE		   ;
             VGER		   ;
             VGERII		   ;
             VGERIII	   ;
             VACTIO		   ;



! Contracted stems
! ----------------


LEXICON COKKAD
+V:%>  VINF ;
+V+Ind+Prs:%>á VSUF_SG1 ;
+V+Ind+Prs:%>á VSUF_SG2 ;
+V+Ind+Prs:%>á VSUF_SG3 ;
+V+Ind+Prs:%>jee VSUF_DU1 ;
+V+Ind+Prs:%>á VSUF_DU2 ;
+V+Ind+Prs: VSUF_DU3 ;
+V+Ind+Prs: VSUF_PL1 ;
+V+Ind+Prs:%>á VSUF_PL2 ;
+V+Ind+Prs:%je VSUF_PL3 ;


LEXICON PALLAD

+V:  VINF ;
+V+Ind+Prs:%>a VSUF_SG1 ;
+V+Ind+Prs:%>a VSUF_SG2 ;
+V+Ind+Prs:%>a VSUF_SG3 ;
+V+Ind+Prs:%>jee VSUF_DU1 ;
+V+Ind+Prs:%>a VSUF_DU2 ;
+V+Ind+Prs:%> VSUF_DU3 ;
+V+Ind+Prs:%> VSUF_PL1 ;
+V+Ind+Prs:%>a VSUF_PL2 ;
+V+Ind+Prs:%je VSUF_PL3 ;

LEXICON TUHHID

+V:%>i  VINF ;
+V+Ind+Prs:%>ii VSUF_SG1 ;
+V+Ind+Prs:%>ii VSUF_SG2 ;
+V+Ind+Prs:%^VLOW%>ee VSUF_SG3 ;
+V+Ind+Prs:%^VLOW%>ejee VSUF_DU1 ;
+V+Ind+Prs:%>ii VSUF_DU2 ;
+V+Ind+Prs:%>i VSUF_DU3 ;
+V+Ind+Prs:%>i VSUF_PL1 ;
+V+Ind+Prs:%>ii VSUF_PL2 ;
+V+Ind+Prs:%^VLOW%>eje VSUF_PL3 ;

LEXICON RAMMUD

+V:  VINF ;
+V+Ind+Prs:%>uu VSUF_SG1 ;
+V+Ind+Prs:%>uu VSUF_SG2 ;
+V+Ind+Prs:%^VLOW%>oo VSUF_SG3 ;
+V+Ind+Prs:%^VLOW%>ojee VSUF_DU1 ;
+V+Ind+Prs:%>uu VSUF_DU2 ;
+V+Ind+Prs:%>u VSUF_DU3 ;
+V+Ind+Prs:%>u VSUF_PL1 ;
+V+Ind+Prs:%>uu VSUF_PL2 ;
+V+Ind+Prs:%^VLOW%>oje VSUF_PL3 ;


! Intermediate suffix lexica
! --------------------------


! Final suffix lexica
! -------------------


LEXICON VINF
+Inf:đ K ;

LEXICON VPRSPRC
+PrsPrc:ee K ;

LEXICON VPRFPRCACT
+PrfPrc:m K ;

LEXICON VPRFPRCPASS
+PrfPrc+Pass:m K ;

LEXICON VNEGPRSINDIMP
+ConNeg+Ind+Prs: K ;
+ConNeg+Imp+Prs: K ;

LEXICON VNEGCOND
+ConNeg+Cond:ččii K ;

LEXICON VNEGPOT
+ConNeg+Pot:žžii K ;

LEXICON VABE
+Abe:%>hánnáá K ;
+Abe:%>hinnáá K ;
+Abe:%>hennáá K ;

LEXICON VGER
+Ger:%>dijn K ;
+Ger+PxSg1:%>dijnán K ;
+Ger+PxSg1:%>dijnân K ;
+Ger+PxSg2:%>dijnád K ;
+Ger+PxSg2:%>dijnâd K ;
+Ger+PxSg3:%>dijnes K ;

LEXICON VGERII
+GerII:%>min K ;

LEXICON VGERIII
+GerIII:%>mist K ;

LEXICON VACTIO
+Actio:%>im K ;


! Suffix series I
! - - - - - - - - 

LEXICON EVEN-I
+Sg1:%>m K ;
+Sg2:%>h K ;
+Sg3:%>%^WG1 K ;
+Du1:%>een K ;
+Du2:%>vettee K ;
+Du3:%>v K ;
+Pl1:%>jm K ;
+Pl2:%>jd K ;
+Pl3:%>i K ;

LEXICON POT-I
+Sg1:%>eem K ;
+Sg2:%>eeh K ;
+Sg3: K ;
+Du1:%>een K ;
+Du2:%>vettee K ;
+Du3:%>uv K ;
+Pl1:%>ep K ;
+Pl2:%>vetteđ K ;
+Pl3:%>eh K ;


! Suffix series II
! - - - - - - - - 

LEXICON ODD-II
+Sg1:%>m K ;
+Sg2:%>h K ;
+Sg3:%>j K ;
+Du1:%>jm K ;
+Du2:%>jd K ;
+Du3:%>jn K ;
+Pl1:%>jm K ;
+Pl2:%>jd K ;
+Pl3:%>i K ;

LEXICON ODD-II-DU
+Du1:%>jm K ;
+Du2:%>jd K ;
+Du3:%>jn K ;

! Person-number suffixes

LEXICON V_SG12
VSUF_SG2 ;
VSUF_SG2 ;


LEXICON V_DU
+Du1:%>een K ;
+Du2:%>vettee K ;
+Du3:%>v K ;

LEXICON V_PL
+Pl1:%>jm K ;
+Pl2:%>jd K ;
+Pl3:%>i K ;



! Other series
! - - - - - - 


! Auxiliary verbs
! ---------------

LEXICON NEG
+Prs+Sg1:jiem K ;
+Prs+Sg2:jieh K ;
+Prs+Sg3:ij K ;
+Prs+Du1:iän K ;
+Prs+Du1:eän K ;
+Prs+Du2:eppee K ;
+Prs+Du3:iävá K ;
+Prs+Du3:eävá K ;
+Prs+Pl1:ep K ;
+Prs+Pl2:eppeđ K ;
+Prs+Pl3:iä K ;
+Prs+Pl3:eävá K ;

+Imprt+Sg1:iällum  K ;
+Imprt+Sg1:eällum  K ;
+Imprt+Sg2:ele K ;
+Imprt+Sg3:iällus K ;
+Imprt+Sg3:eällus K ;
+Imprt+Du1:iälloon K ;
+Imprt+Du1:eälloon K ;
+Imprt+Du2:ellee K ;
+Imprt+Du3:iällus K ;
+Imprt+Du3:eällus K ;
+Imprt+Pl1:iällup K ;
+Imprt+Pl1:eällup K ;
+Imprt+Pl2:elleđ K ;
+Imprt+Pl3:iällus K ;
+Imprt+Pl3:eällus K ;

+Opt+Sg2:eälu K ;
+Opt+Sg2:iälu K ;
+Opt+Du2:eällu K ;
+Opt+Du2:iällu K ;
+Opt+Pl2:eälluđ K ;
+Opt+Pl2:iälluđ K ;

+Sup:amâs K ;
+Sup:emis K ;

LEXICON LEDE
+Inf:leđe K ;

! Present
+Prs+Sg1:lam K ;
+Prs+Sg2:lah K ;
+Prs+Sg3:lii K ;
+Prs+Du1:leen K ;
+Prs+Du1:laan K ;
+Prs+Du1:láán K ;
+Prs+Du2:leppee K ;
+Prs+Du3:lava K ;
+Prs+Du3:lává K ;
+Prs+Pl1:lep K ;
+Prs+Pl2:leppeđ K ;
+Prs+Pl3:laa K ;
+Prs+Pl3:láá K ;

! Past
+Prt+Sg1:lijjim K ;
+Prt+Sg2:lijjih K ;
+Prt+Sg3:lai K ;
+Prt+Sg3:lei K ;
+Prt+Du1:láim K ;
+Prt+Du1:lááim K ;
+Prt+Du1:leim K ;
+Prt+Du2:láid K ;
+Prt+Du2:lááid K ;
+Prt+Du2:leid K ;
+Prt+Du3:láin K ;
+Prt+Du3:lááin K ;
+Prt+Du3:lein K ;
+Prt+Pl1:leim K ;
+Prt+Pl2:leid K ;
+Prt+Pl3:lijjii K ;

! Conditional
+Cond+Prs:ličč ODD-II ;
+Cond+Prs:ličči ODD-II-DU ;

!Potential
+Pot+Prs:ležž POT-I ;


! Person suffixes

LEXICON VSUF_SG1
+Sg1:%>m K ;

LEXICON VSUF_SG2
+Sg2:%>h K ;

LEXICON VSUF_SG3
+Sg3:%> K ;

LEXICON VSUF_DU1
+Du1:%>n K ;

LEXICON VSUF_DU2
+Du2:%>vettee K ;

LEXICON VSUF_DU3
+Du3:%>v K ;

LEXICON VSUF_PL1
+Pl1:%>p K ;

LEXICON VSUF_PL2
+Pl2:%>vetteđ K ;

LEXICON VSUF_PL3
+Pl3:%>h K ;




! Sublexica for possessive suffixes
! =================================


LEXICON K
        # ; ! clitic-less forms
+Qst:uv # ;
+Foc:gân # ;
+Foc:gâs # ;
+Foc:ges # ;
+Foc:gis # ;
+Foc:gin # ;

