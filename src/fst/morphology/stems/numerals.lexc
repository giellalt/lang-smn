! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no

!! # Inari Saami numerals

!! ## Main lexicon

LEXICON Numeral !!≈ * **@CODE@**

         HUNDREDSM       ; ! 200M
   čyeti HUNDREDM        ; ! 100M
         TENSM           ; ! 20-99M
         TEENSM          ; ! 10-19M
         ONESM           ; ! 1-9M
         HUNDREDST       ; ! 200000-999999
         HUNDREDST       ; ! 200000-999999
   čyeti HUNDREDT        ; ! 100000-100999
         TENST           ; ! 20000-99999,10000-10999
         TEENST          ; ! 11000-19999
         ONEST           ; ! 2000-9999
 tuhháát THOUSAND        ; ! 1000-1999
         UNDERTHOUSAND   ; ! 100-999
         TENS            ; ! 20-99
         TEENS           ; ! 10-19
         ONES            ; ! 1-9
         ARABIC-COLL     ;

         num-imprecise   ; ! kalle, ...
         MISCNUM         ; ! pelnub
         ARABICS         ; ! for the arabic numerals !
         ROMAN           ; ! for the roman numerals  !
         NUM-PREFIXES    ; ! for §34 etc.            !
         ISOLATED-NUMEXP ; ! for ½ etc.                      !
         LOHKAI          ;

!! ## The million layer

LEXICON HUNDREDSM   !!≈ * **@CODE@**
 kyehti CUODIM ;
  kulmâ CUODIM ;
  nelji CUODIM ;
  vittâ CUODIM ;
  kuttâ CUODIM ;
 čiččâm CUODIM ;
 käävci CUODIM ;
  oovce CUODIM ;


LEXICON CUODIM  !!≈ * **@CODE@**
 čyeti HUNDREDM ;

LEXICON HUNDREDM  !!≈ * **@CODE@**
  TENSM  ;
  TEENSM ;
  ONESM  ;
  MILJON ;

LEXICON TEENSM  !!≈ * **@CODE@**
  TEENM ;

LEXICON TEENM  !!≈ * **@CODE@**
         ohtâ:oht^RVâ LOHKAIM ;
       kyehti         LOHKAIM ;
        kulmâ         LOHKAIM ;
        nelji         LOHKAIM ;
 vitt+Use/NG:vittâ   LOHKAIM ;
 kutt+Use/NG:kuttâ   LOHKAIM ;
         vitt         LOHKAIM ;
         kutt         LOHKAIM ;
       čiččâm         LOHKAIM ;
       käävci         LOHKAIM ;
        oovce         LOHKAIM ;

LEXICON LOHKAIM  !!≈ * **@CODE@**
 nubáloh MILJON ;

LEXICON TENSM  !!≈ * **@CODE@**
   love MILJON ;
 kyehti LOGIM  ;
  kulmâ LOGIM  ;
  nelji LOGIM  ;
  vittâ LOGIM  ;
  kuttâ LOGIM  ;
 čiččâm LOGIM  ;
 käävci LOGIM  ;
  oovce LOGIM  ;

LEXICON LOGIM  !!≈ * **@CODE@**
 love MILJON ;
 love ONESM  ;

LEXICON ONESM            !!≈ * **@CODE@**
 miljon%  OVERTHOUSANDS ;
   kyehti MILJON        ;
    kulmâ MILJON        ;
    nelji MILJON        ;
    vittâ MILJON        ;
    kuttâ MILJON        ;
   čiččâm MILJON        ;
   käävci MILJON        ;
    oovce MILJON        ;

LEXICON MILJON  !!≈ * **@CODE@**
 % miljona%   OVERTHOUSANDS ;

! =================
!! ## The under the million
! =================

!! In principle as the above.

LEXICON OVERTHOUSANDS !!≈ * **@CODE@**
         HUNDREDST     ; ! x00.000.000
   čyeđe HUNDREDT      ; ! 100.000.000
         TENST         ; !  20.000.000
         TEENST        ; !  10.000.000
 tuhháát THOUSAND      ; !   1.000.000
         UNDERTHOUSAND ;
         TENS          ;
         TEENS         ;
         ONES          ;
         num           ; !  x000000


LEXICON HUNDREDST !!≈ * **@CODE@**
          kyeht        CYEDET   ; ! 200000-299999
 kyeht+Err/Orth:kyehti CYEDET   ; ! 200000-299999
          kulmâ        CYEDET   ; ! 300000-399999
          nelji        CYEDET   ; ! 400000-499999
           vitt        CYEDET   ; ! 500000-599999
           kutt        CYEDET   ; ! 600000-699999
  vitt+Err/Orth:vit    CYEDET   ; ! 500000-599999
  kutt+Err/Orth:kut    CYEDET   ; ! 600000-699999
  vitt+Err/Orth:vittâ  CYEDET   ; ! 500000-599999
  kutt+Err/Orth:kuttâ  CYEDET   ; ! 600000-699999
         čiččâm        CYEDET ; ! 700000-799999
         käävci        CYEDET ; ! 800000-899999
          oovce        CYEDET ; ! 900000-999999

LEXICON CYEDET !!≈ * **@CODE@**
 čyeđe HUNDREDT ; ! 100000-199999


LEXICON HUNDREDT	 !!≈ * **@CODE@**  X = 1-9, Y = 0-9
              TENST        ; ! X2XYYY, X10YYY
              TEENST       ; ! X1XYYY
 ohtâ:oht^RVâ THOUSANDS ;    ! Same tuhháát as 2000
              THOUSANDS    ; ! X00YYY
              ONEST        ; ! XX0YYY

LEXICON TEENST !!≈ * **@CODE@**
  TEENT ;

LEXICON TEENT !!≈ * **@CODE@**
         love         THOUSANDS ;
         ohtâ:oht^RVâ LOHKAIT   ;
       kyehti         LOHKAIT   ;
        kulmâ         LOHKAIT   ;
        nelji         LOHKAIT   ;
         vitt         LOHKAIT   ;
 vittâ+Use/NG:vittâ   LOHKAIT   ;
         kutt         LOHKAIT   ;
 kuttâ+Use/NG:kuttâ   LOHKAIT   ;
       čiččâm         LOHKAIT   ;
       käävci         LOHKAIT   ;
        oovce         LOHKAIT   ;

LEXICON LOHKAIT !!≈ * **@CODE@**
 nubáloh THOUSANDS ;

LEXICON TENST !!≈ * **@CODE@**
          muádlov:muád#lov   LOGIT ;
         maŋgâlov:maŋgâ#lov  LOGIT ;
 muáddilov+Use/NG:muáddi#lov LOGIT ;
         kyehtlov:kyeht#lov  LOGIT ;
         kulmâlov:kulmâ#lov  LOGIT ;
   kulmlov+Use/NG:kulm#lov   LOGIT ;
         neljilov:nelji#lov  LOGIT ;
          vittlov:vitt#lov   LOGIT ;
   vittlov+Use/NG:vittâ#lov  LOGIT ;
          kuttlov:kutt#lov   LOGIT ;
        čiččâmlov:čiččâm#lov LOGIT ;
         kähcilov:kähci#lov  LOGIT ;
          ohcelov:ohce#lov   LOGIT ;

LEXICON LOGIT !!≈ * **@CODE@**
 ohtâ:oht^RVâ THOUSANDS ;
              ONEST     ;
              THOUSANDS ;

LEXICON ONEST !!≈ * **@CODE@**
 kyeht THOUSANDS ; ! 2000-2999
 kulmâ THOUSANDS ; ! etc.
 nelji THOUSANDS ;
 vittâ THOUSANDS ;
 kuttâ THOUSANDS ;
čiččâm THOUSANDS ;
käävci THOUSANDS ;
 oovce THOUSANDS ;

LEXICON THOUSANDS !!≈ * **@CODE@**
 tuhháát THOUSAND ; ! 1000-1999


LEXICON THOUSAND !!≈ * **@CODE@**
       UNDERTHOUSAND ; ! 1100-1999
       TENS          ; ! 1020-1099
       TEENS         ; ! 1010-1019
       ONES          ; ! 1001-1009
 +Num: X_POTTAAK     ; ! 1000



!    ===========================
!! # Here starts the 999 numbers
!    ===========================

LEXICON UNDERTHOUSAND !!≈ * **@CODE@**
                                   HUNDREDS ;
                   čyeti: ONECYETI  ;
 čyeti+Use/NG+Num+Sg+Gen:čyet      K        ;


LEXICON ONECYETI
 @U.Case.SgNom@:@U.Case.SgNom@čyeti      HUNDRED            ;
 @U.Case.SgGen@:@U.Case.SgGen@čyeđe      HUNDRED            ; ! 
 @U.Case.SgCom@:@U.Case.SgCom@čuođijn    HUNDRED            ; !
   @U.Case.Ess@:@U.Case.Ess@čyettin      HUNDRED            ; !
 @U.Case.PlNom@:@U.Case.PlNom@čyeđeh     HUNDRED            ; !
 @U.Case.PlAcc@:@U.Case.PlAcc@čuođijd    HUNDRED            ; !
 @U.Case.PlGen@:@U.Case.PlGen@čuođij     HUNDRED            ; !
 @U.Case.PlIll@:@U.Case.PlIll@čuođijd    HUNDRED            ; !
 @U.Case.PlLoc@:@U.Case.PlLoc@čuođijn    HUNDRED            ; !
 @U.Case.SgGen@:@U.Case.SgGen@čyet%>     NUMERALCOMPOUNDS ;


LEXICON HUNDREDS  !!≈ * **@CODE@** 200-999
  ohtâ+Use/NG:oht       CUODI       ;
       kyehti:kyeht^RCi KYEHTICUODI ;
kyehti+Use/NG:kyeht     CUODI       ;
        kulmâ:ku^RVlmâ  KULMACUODI  ;
 kulmâ+Use/NG:kulm      CUODI       ;
        nelji:ne^RVlji  KYEHTICUODI ;
        vittâ:vi^RVtt4â VITTACUODI  ;
 vittâ+Use/NG:vitt      CUODI       ;
 vittâ+Use/NG:vit       CUODI       ;
        kuttâ:ku^RVtt4â VITTACUODI  ;
 kuttâ+Use/NG:kutt      CUODI       ;
 kuttâ+Use/NG:kut       CUODI       ;
       čiččâm:čiččâm    CICCAMCUODI ;
       käävci:käävc^RCi KYEHTICUODI ;
käävci+Use/NG:käävc     CUODI       ;
käävci+Use/NG:kävc      CUODI       ;
        oovce:oovc^RCe  KYEHTICUODI ;



LEXICON KYEHTICUODI
 @U.Case.SgNom@:@U.Case.SgNom@                  CUODI            ;
 @U.Case.SgGen@:@U.Case.SgGen@^WG^RLEN          CUODI            ; !oovtâ K ;
 @U.Case.SgIll@:@U.Case.SgIll@^EA^RLEN          CUODI            ; ! oohtân K ;
 @U.Case.SgLoc@:@U.Case.SgLoc@^WG^RLEN          CUODI            ; ! oovtâst K ;
 @U.Case.SgCom@:@U.Case.SgCom@^WG^CLEN^RVSH%>in CUODI            ; !   neljijn
   @U.Case.Ess@:@U.Case.Ess@%>n                 CUODI            ; ! ohtân	 K ;
 @U.Case.PlNom@:@U.Case.PlNom@^WG^RLEN%>h       CUODI            ; !oovtah K ;
 @U.Case.PlAcc@:@U.Case.PlAcc@^WG^RLEN%>id      CUODI            ; !oovtâid K ;
 @U.Case.PlGen@:@U.Case.PlGen@^WG^RVSH%>i       CUODI            ; !oovtâi K ;
 @U.Case.PlIll@:@U.Case.PlIll@^WG^CLEN^RVSH%>id CUODI            ; !ovttáid K ;
 @U.Case.PlLoc@:@U.Case.PlLoc@^WG^RLEN%>in      CUODI            ; !oovtâin K ;
 @U.Case.PlCom@:@U.Case.PlCom@^WG^RVSH          CUODI            ; !ovtâiguin K ;
 @U.Case.SgGen@:@U.Case.SgGen@^WG^RLEN%>        NUMERALCOMPOUNDS ;




LEXICON KULMACUODI   !!≈ * **@CODE@** maaŋgâi
 @U.Case.SgCom@:@U.Case.SgCom@^WG^SVLOW%>in CUODI            ; !  ovttáin
 @U.Case.PlIll@:@U.Case.PlIll@^WG^SVLOW%>id CUODI            ; !ovttáid
                                            KULMA_VITTACUODI ;

LEXICON VITTACUODI   !!≈ * **@CODE@**
 @U.Case.SgCom@:@U.Case.SgCom@^WG^RLEN^SVLOW%>in CUODI            ; !  viiđáin
 @U.Case.PlIll@:@U.Case.PlIll@^WG^RLEN^SVLOW%>id CUODI            ; !viiđáid
                                                 KULMA_VITTACUODI ;

LEXICON KULMA_VITTACUODI   !!≈ * **@CODE@**
  @U.Case.SgNom@:@U.Case.SgNom@             CUODI            ; !vittâ
  @U.Case.SgGen@:@U.Case.SgGen@^WG^RLEN     CUODI            ; !kuulmâ
  @U.Case.SgAcc@:@U.Case.SgAcc@^WG^RLEN     CUODI            ; !kuulmâ   oovtâ kistui
  @U.Case.SgIll@:@U.Case.SgIll@^WG^RLEN     CUODI            ; ! kuulmân
  @U.Case.SgLoc@:@U.Case.SgLoc@^WG^RLEN     CUODI            ; ! kuulmâst
    @U.Case.Ess@:@U.Case.Ess@%>n            CUODI            ; ! vittân
  @U.Case.PlNom@:@U.Case.PlNom@^WG^RLEN%>h  CUODI            ; !viiđah
  @U.Case.PlAcc@:@U.Case.PlAcc@^WG^RLEN%>id CUODI            ; ! viiđâid
  @U.Case.PlGen@:@U.Case.PlGen@^WG^RLEN%>i  CUODI            ; !viiđâi
  @U.Case.PlGen@:@U.Case.PlGen@^WG%>i       CUODI            ; !viđâi
  @U.Case.PlLoc@:@U.Case.PlLoc@^WG^RLEN%>in CUODI            ; !viiđâin
  @U.Case.PlGen@:@U.Case.PlGen@^WG%>iguin   CUODI            ; !viđâiguin
  @U.Case.SgGen@:@U.Case.SgGen@^WG^RLEN     NUMERALCOMPOUNDS ;


LEXICON CICCAMCUODI  !!≈ * **@CODE@** 700-900 with +Par, we comment Par out
 @U.Case.SgNom@:@U.Case.SgNom@         CUODI            ;
 @U.Case.SgGen@:@U.Case.SgGen@         CUODI            ; !čiččâm K ;
 @U.Case.SgAcc@:@U.Case.SgAcc@         CUODI            ; !čiččâm K ;
 @U.Case.SgIll@:@U.Case.SgIll@         CUODI            ; ! čiččâmân K ;
 @U.Case.SgLoc@:@U.Case.SgLoc@         CUODI            ; ! čiččâmist K ;
 @U.Case.SgCom@:@U.Case.SgCom@á%>in    CUODI            ; !  ovttáin K ;
   @U.Case.Ess@:@U.Case.Ess@i%>n       CUODI            ; ! ohtân	 K ;
 @U.Case.PlNom@:@U.Case.PlNom@e%>h     CUODI            ; !oovtah K ;
 @U.Case.PlAcc@:@U.Case.PlAcc@i%>id    CUODI            ; !oovtâid K ;
 @U.Case.PlGen@:@U.Case.PlGen@i%>i     CUODI            ; !oovtâi K ;
 @U.Case.PlIll@:@U.Case.PlIll@á%>id    CUODI            ; !ovttáid K ;
 @U.Case.PlLoc@:@U.Case.PlLoc@á%>in    CUODI            ; !oovtâin K ;
 @U.Case.PlGen@:@U.Case.PlGen@i%>iguin CUODI            ; !ovtâiguin K ;
 @U.Case.SgGen@:@U.Case.SgGen@%>       NUMERALCOMPOUNDS ;


LEXICON CUODI  !!≈ * **@CODE@** kept for čyet
        čyeđe: CYEDE   ;
 čyeđe+Use/NG:čyet  HUNDRED ;

LEXICON CYEDE
          @U.Case.SgNom@+Num+Sg+Nom:@U.Case.SgNom@čyeđe      HUNDRED ; ! kyeli
          @U.Case.SgGen@+Num+Sg+Gen:@U.Case.SgGen@čyeđe      HUNDRED ; ! kyele
          @U.Case.SgAcc@+Num+Sg+Acc:@U.Case.SgAcc@čyeđe      HUNDRED ; ! kyele
          @U.Case.SgIll@+Num+Sg+Ill:@U.Case.SgIll@čuátán     HUNDRED ; ! kuálán
          @U.Case.SgLoc@+Num+Sg+Loc:@U.Case.SgLoc@čyeđeest   HUNDRED ; ! kyeleest
          @U.Case.SgCom@+Num+Sg+Com:@U.Case.SgCom@čuođijn    HUNDRED ; ! kuolijn
          @U.Case.SgGen@+Num+Sg+Abe:@U.Case.SgGen@čyeđettáá  HUNDRED ; ! kyelettáá
               @U.Case.Ess@+Num+Ess:@U.Case.Ess@čyettin      HUNDRED ; ! kyellin
          @U.Case.PlNom@+Num+Pl+Nom:@U.Case.PlNom@čyeđeh     HUNDRED ; ! kyeleh
          @U.Case.PlGen@+Num+Pl+Gen:@U.Case.PlGen@čuođij     HUNDRED ; ! kuolij
          @U.Case.PlAcc@+Num+Pl+Acc:@U.Case.PlAcc@čuođijd    HUNDRED ; ! kuolijd
          @U.Case.PlIll@+Num+Pl+Ill:@U.Case.PlIll@čuođijd    HUNDRED ; ! kuolijd
          @U.Case.PlLoc@+Num+Pl+Loc:@U.Case.PlLoc@čuođijn    HUNDRED ; ! kuolijn
          @U.Case.PlGen@+Num+Pl+Com:@U.Case.PlGen@čuođijguin HUNDRED ; ! kuolijguin
 @U.Case.PlGen@+Num+Pl+Com+Err/Orth:@U.Case.PlGen@čuođijgijn HUNDRED ; ! kuolijguin
          @U.Case.PlGen@+Num+Pl+Abe:@U.Case.PlGen@čuođijttáá HUNDRED ; ! kuolijttáá
       @U.Case.SgGen@+Num+Cmp/SgGen:@U.Case.SgGen@čyet%>     NUMERALCOMPOUNDS ;

LEXICON HUNDRED  !!≈ * **@CODE@**
  K     ; ! monitor this
  TENS  ;
  TEENS ;
  ONES  ;
  CYETI ;

LEXICON TEENS  !!≈ * **@CODE@**
  TEEN ;

LEXICON TEEN  !!≈ * **@CODE@**

    @U.Case.SgNom@oht:@U.Case.SgNom@oht       LOHKAI ; !
    @U.Case.SgGen@oht:@U.Case.SgGen@oht       LOHKAI ; !
    @U.Case.SgAcc@oht:@U.Case.SgAcc@oht       LOHKAI ; !
    @U.Case.SgIll@oht:@U.Case.SgIll@oht       LOHKAI ; !
    @U.Case.SgLoc@oht:@U.Case.SgLoc@oht       LOHKAI ; !
    @U.Case.SgCom@oht:@U.Case.SgCom@ovttáin   LOHKAI ; !
      @U.Case.Ess@oht:@U.Case.Ess@ohtân       LOHKAI ; !
    @U.Case.PlNom@oht:@U.Case.PlNom@oovtah    LOHKAI ; !
    @U.Case.PlGen@oht:@U.Case.PlGen@oovtâi    LOHKAI ; !
    @U.Case.PlGen@oht:@U.Case.PlGen@ovtâi     LOHKAI ; !
    @U.Case.PlAcc@oht:@U.Case.PlAcc@oovtâid   LOHKAI ; !
    @U.Case.PlIll@oht:@U.Case.PlIll@ovttáid   LOHKAI ; !
    @U.Case.PlLoc@oht:@U.Case.PlLoc@oovtâin   LOHKAI ; !

  @U.Case.SgNom@kyeht:@U.Case.SgNom@kyeht     LOHKAI ;
  @U.Case.SgGen@kyeht:@U.Case.SgGen@kyevt     LOHKAI ;
  @U.Case.SgAcc@kyeht:@U.Case.SgAcc@kyevt     LOHKAI ;
  @U.Case.SgIll@kyeht:@U.Case.SgIll@kyevt     LOHKAI ;
  @U.Case.SgLoc@kyeht:@U.Case.SgLoc@kyevt     LOHKAI ;
  @U.Case.SgCom@kyeht:@U.Case.SgCom@kuovttijn LOHKAI ;
    @U.Case.Ess@kyeht:@U.Case.Ess@kyehtin     LOHKAI ;
  @U.Case.PlNom@kyeht:@U.Case.PlNom@kyevtih   LOHKAI ;
  @U.Case.PlGen@kyeht:@U.Case.PlGen@kuovtij   LOHKAI ;
  @U.Case.PlAcc@kyeht:@U.Case.PlAcc@kuovtijd  LOHKAI ;
  @U.Case.PlIll@kyeht:@U.Case.PlIll@kuovttijd LOHKAI ;
  @U.Case.PlLoc@kyeht:@U.Case.PlLoc@kuovttijn LOHKAI ;

  @U.Case.SgNom@kulmâ:@U.Case.SgNom@kulmâ     LOHKAI ;
  @U.Case.SgGen@kulmâ:@U.Case.SgGen@kuulmâ    LOHKAI ;
  @U.Case.SgAcc@kulmâ:@U.Case.SgAcc@kuulmâ    LOHKAI ;
  @U.Case.SgIll@kulmâ:@U.Case.SgIll@kuulmâ    LOHKAI ;
  @U.Case.SgLoc@kulmâ:@U.Case.SgLoc@kuulmâ    LOHKAI ;
  @U.Case.SgCom@kulmâ:@U.Case.SgCom@kulmáin   LOHKAI ;
    @U.Case.Ess@kulmâ:@U.Case.Ess@kulmân      LOHKAI ;
  @U.Case.PlNom@kulmâ:@U.Case.PlNom@kuulmah   LOHKAI ;
  @U.Case.PlGen@kulmâ:@U.Case.PlGen@kuulmâi   LOHKAI ;
  @U.Case.PlAcc@kulmâ:@U.Case.PlAcc@kuulmâid  LOHKAI ;
  @U.Case.PlIll@kulmâ:@U.Case.PlIll@kulmáid   LOHKAI ;
  @U.Case.PlLoc@kulmâ:@U.Case.PlLoc@kulmáin   LOHKAI ;

  @U.Case.SgNom@nelji:@U.Case.SgNom@nelji     LOHKAI ;
  @U.Case.SgGen@nelji:@U.Case.SgGen@neelji    LOHKAI ;
  @U.Case.SgAcc@nelji:@U.Case.SgAcc@neelji    LOHKAI ;
  @U.Case.SgIll@nelji:@U.Case.SgIll@neelji    LOHKAI ;
  @U.Case.SgLoc@nelji:@U.Case.SgLoc@neelji    LOHKAI ;
    @U.Case.Ess@nelji:@U.Case.Ess@neljin      LOHKAI ;
  @U.Case.PlNom@nelji:@U.Case.PlNom@neeljih   LOHKAI ;
  @U.Case.PlGen@nelji:@U.Case.PlGen@neeljij   LOHKAI ;
  @U.Case.PlGen@nelji:@U.Case.PlGen@neljij    LOHKAI ;
  @U.Case.PlAcc@nelji:@U.Case.PlAcc@neeljijd  LOHKAI ;
  @U.Case.PlIll@nelji:@U.Case.PlIll@neljijd   LOHKAI ;
  @U.Case.PlLoc@nelji:@U.Case.PlLoc@neljijn   LOHKAI ;
  @U.Case.SgCom@nelji:@U.Case.SgCom@neljijn   LOHKAI ;

   @U.Case.SgNom@vitt:@U.Case.SgNom@vitt      LOHKAI ;
   @U.Case.SgNom@vitt+Use/NG:@U.Case.SgNom@vit       LOHKAI ;
   @U.Case.PlNom@vitt:@U.Case.PlNom@viiđah    LOHKAI ;
   @U.Case.SgGen@vitt:@U.Case.SgGen@viđâ      LOHKAI ;
   @U.Case.PlGen@vitt:@U.Case.PlGen@viđâi     LOHKAI ;
   @U.Case.PlGen@vitt:@U.Case.PlGen@viiđâi    LOHKAI ;
   @U.Case.PlAcc@vitt:@U.Case.PlAcc@viiđâid   LOHKAI ;
   @U.Case.PlIll@vitt:@U.Case.PlIll@viiđâid   LOHKAI ;
   @U.Case.PlLoc@vitt:@U.Case.PlLoc@viiđáin   LOHKAI ;
   @U.Case.SgCom@vitt:@U.Case.SgCom@viiđáin   LOHKAI ;
     @U.Case.Ess@vitt:@U.Case.Ess@vittân      LOHKAI ;

   @U.Case.SgNom@kutt:@U.Case.SgNom@kutt      LOHKAI ;
   @U.Case.SgNom@kutt+Use/NG:@U.Case.SgNom@kut       LOHKAI ;
   @U.Case.SgCom@kutt:@U.Case.SgCom@kuuđáin   LOHKAI ;
   @U.Case.SgGen@kutt:@U.Case.SgGen@kuđâ      LOHKAI ;
   @U.Case.SgAcc@kutt:@U.Case.SgAcc@kuđâ      LOHKAI ;
   @U.Case.SgIll@kutt:@U.Case.SgIll@kuđâ      LOHKAI ;
   @U.Case.SgLoc@kutt:@U.Case.SgLoc@kuđâ      LOHKAI ;
     @U.Case.Ess@kutt:@U.Case.Ess@kuttân      LOHKAI ;
   @U.Case.PlNom@kutt:@U.Case.PlNom@kuuđah    LOHKAI ;
   @U.Case.PlGen@kutt:@U.Case.PlGen@kuđâi     LOHKAI ;
   @U.Case.PlGen@kutt:@U.Case.PlGen@kuuđâi    LOHKAI ;
   @U.Case.PlAcc@kutt:@U.Case.PlAcc@kuuđâid   LOHKAI ;
   @U.Case.PlIll@kutt:@U.Case.PlIll@kuuđâid   LOHKAI ;
   @U.Case.PlLoc@kutt:@U.Case.PlLoc@kuuđáin   LOHKAI ;

 @U.Case.SgNom@čiččâm:@U.Case.SgNom@čiččâm    LOHKAI ; ! čiččâmlovkuttâ
 @U.Case.SgGen@čiččâm:@U.Case.SgGen@čiččâm    LOHKAI ; ! čiččâmlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
 @U.Case.SgAcc@čiččâm:@U.Case.SgAcc@čiččâm    LOHKAI ; ! čiččâmlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
 @U.Case.SgIll@čiččâm:@U.Case.SgIll@čiččâm    LOHKAI ; ! čiččâmlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
 @U.Case.SgLoc@čiččâm:@U.Case.SgLoc@čiččâm    LOHKAI ; ! čiččâmlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
 @U.Case.SgCom@čiččâm:@U.Case.SgCom@čiččâm    LOHKAI ; ! čiččâmluvvijnkuuđáin kulmáinluvvijnovttáin
   @U.Case.Ess@čiččâm:@U.Case.Ess@čiččâm      LOHKAI ; ! čiččâmlovveenkuttân
 @U.Case.PlNom@čiččâm:@U.Case.PlNom@čiččâm    LOHKAI ; ! čiččâmlovehkuuđah
 @U.Case.PlGen@čiččâm:@U.Case.PlGen@čiččâm    LOHKAI ; ! čiččâmluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
 @U.Case.PlAcc@čiččâm:@U.Case.PlAcc@čiččâm    LOHKAI ; ! čiččâmluuvijdkuuđâid
 @U.Case.PlIll@čiččâm:@U.Case.PlIll@čiččâm    LOHKAI ; ! čiččâmluvvijdkuuđáid
 @U.Case.PlLoc@čiččâm:@U.Case.PlLoc@čiččâm    LOHKAI ; ! čiččâmluvvijnkuuđáin

 @U.Case.SgNom@käävci:@U.Case.SgNom@käävci    LOHKAI ;
 @U.Case.SgGen@käävci:@U.Case.SgGen@käävci    LOHKAI ;
 @U.Case.SgAcc@käävci:@U.Case.SgAcc@käävci    LOHKAI ;
 @U.Case.SgIll@käävci:@U.Case.SgIll@käävci    LOHKAI ;
 @U.Case.SgLoc@käävci:@U.Case.SgLoc@käävci    LOHKAI ;
 @U.Case.SgCom@käävci:@U.Case.SgCom@kavccijn  LOHKAI ;
   @U.Case.Ess@käävci:@U.Case.Ess@käävcin     LOHKAI ;
 @U.Case.PlNom@käävci:@U.Case.PlNom@käävcih   LOHKAI ;
 @U.Case.PlGen@käävci:@U.Case.PlGen@kavcij    LOHKAI ;
 @U.Case.PlGen@käävci:@U.Case.PlGen@kaavcij   LOHKAI ;
 @U.Case.PlAcc@käävci:@U.Case.PlAcc@kaavcijd  LOHKAI ;
 @U.Case.PlIll@käävci:@U.Case.PlIll@kavccijd  LOHKAI ;
 @U.Case.PlLoc@käävci:@U.Case.PlLoc@kavccijn  LOHKAI ;

  @U.Case.SgNom@oovce:@U.Case.SgNom@oovce     LOHKAI ; ! oovcenubáloh
  @U.Case.SgGen@oovce:@U.Case.SgGen@oovce     LOHKAI ; ! oovcenubálov, oovcenubálovveest, oovcenubálohán
  @U.Case.SgAcc@oovce:@U.Case.SgAcc@oovce     LOHKAI ; ! oovcenubálov, oovcenubálovveest, oovcenubálohán
  @U.Case.SgIll@oovce:@U.Case.SgIll@oovce     LOHKAI ; ! oovcenubálov, oovcenubálovveest, oovcenubálohán
  @U.Case.SgLoc@oovce:@U.Case.SgLoc@oovce     LOHKAI ; ! oovcenubálov, oovcenubálovveest, oovcenubálohán
  @U.Case.SgCom@oovce:@U.Case.SgCom@ovccijn   LOHKAI ; ! ovccijnnubáluvvijn
    @U.Case.Ess@oovce:@U.Case.Ess@ohcen       LOHKAI ; ! oovcenubálohheen
  @U.Case.PlNom@oovce:@U.Case.PlNom@oovceh    LOHKAI ; ! oovcehnubálov
  @U.Case.PlGen@oovce:@U.Case.PlGen@ovcij     LOHKAI ; ! ovcijnubáluvij
  @U.Case.PlGen@oovce:@U.Case.PlGen@oovcij    LOHKAI ; ! oovcijnubálunij
  @U.Case.PlAcc@oovce:@U.Case.PlAcc@oovcijd   LOHKAI ; ! oovcijdnubáluuvijd
  @U.Case.PlIll@oovce:@U.Case.PlIll@ovccijd   LOHKAI ; ! ovccijdnubáluvvijd
  @U.Case.PlLoc@oovce:@U.Case.PlLoc@ovccijn   LOHKAI ; ! ovccijdnubáluvvijn



LEXICON LOHKAI  !!≈ * **@CODE@**
          nubáloh:nubá# NUBALOH ;
 nubáloh+Err/Orth:nuba# NUBALOH ;
                        LOGAAT  ;

LEXICON LOGAAT  !!≈ * **@CODE@**
 nubálovváád+A+Ord:nubá#lovváád X_EEMEED ; ! to be fixed for flags


!! ## Under hundred

!! ### Numeral agreement, an overview
!! Gen' means shortened forms, lov = the ten denominator
!! - Nominative: Nom + lov + Nom
!! - Genitive: Gen' + lov + Gen
!! - Illative: Gen' + lohán + Ill
!! - Locative: Gen' + lov + Loc
!! - Comitative: Com + lov-Com + Com
!! - Essive: Ess + lov-Ess + Com

!! - Gen' =

!Kulmâlovkuttâ               Mii lii tuu kamuvnummeer? Tot lii kulmâlovkuttâ.
!kuulmâlovkuuđâ              Must lii kuulmâlovkuuđâ nummeer kaamuv.
!-
!kuulmâlovkuutân             Mon kalle kuuđii mättih kuulmâlovkuutân?
!kuulmâlovkuuđâst            Kamuvnumereh älgih taan ilddest kuulmâlovkuuđâst.
!kulmáinluvvijnkuuđáin       Mon ennuv tot lii ko viiđii kiärdu kulmáinluvvijnkuuđáin?
!-
!kulmânlovveenkuttân         Taat kirje vuábdoo kulmânlovveenkuttân pittán.

!vittâ:đ + kuttâ:đ
!
!vitlovnelji                 Mii lii tuu kamuvnummeer? Tot lii vitlovnelji.
!viđâlovneelji               Must lii viđâlovneelji nummeer kaamuv.
!-
!viđâlovniälján              Mon kalle neljii mättih viđâlovniälján?
!viđâlovneeljist             Kamuvnumereh älgih taan ilddest viđâlovneeljist.
!viiđáinluvvijnnelijn        Mon ennuv tot lii ko viiđii kiärdu viiđáinluvvijnneljijn?
!-
!vittânlovveenneljin         Taat kirje vuábdoo vittânlovveenneljin pittán.

LEXICON TENS  !!≈ * **@CODE@**
        lovváád+A+Ord:lovváád                 X_EEMEED ;

  @U.Case.SgNom@kyeht:@U.Case.SgNom@kyeht     LOGI     ; ! kyehtlovkuttâ
  @U.Case.SgGen@kyeht:@U.Case.SgGen@kyevt     LOGI     ; ! kyevtlovkuuđâ, kyevtlovkuuđâst, kyevtlovkuutân also Loc, Ill
  @U.Case.SgAcc@kyeht:@U.Case.SgAcc@kyevt     LOGI     ; ! kyevtlovkuuđâ, kyevtlovkuuđâst, kyevtlovkuutân also Loc, Ill
  @U.Case.SgIll@kyeht:@U.Case.SgIll@kyevt     LOGI     ; ! kyevtlovkuuđâ, kyevtlovkuuđâst, kyevtlovkuutân also Loc, Ill
  @U.Case.SgLoc@kyeht:@U.Case.SgLoc@kyevt     LOGI     ; ! kyevtlovkuuđâ, kyevtlovkuuđâst, kyevtlovkuutân also Loc, Ill
  @U.Case.SgCom@kyeht:@U.Case.SgCom@kuovttijn LOGI     ; ! kuovttijnluvvijnkuuđáin kuovttijnluvvijnovttáin
    @U.Case.Ess@kyeht:@U.Case.Ess@kyehtin     LOGI     ; ! kyehtinlovveenkuttân
  @U.Case.PlNom@kyeht:@U.Case.PlNom@kyevtih   LOGI     ; ! kuulmahlovehkuuđah
  @U.Case.PlGen@kyeht:@U.Case.PlGen@kuovtij   LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
  @U.Case.PlAcc@kyeht:@U.Case.PlAcc@kuovtijd  LOGI     ; ! kuulmâidluuvijdkuuđâid
  @U.Case.PlIll@kyeht:@U.Case.PlIll@kuovttijd LOGI     ; ! kulmáidluvvijdkuuđáid
  @U.Case.PlLoc@kyeht:@U.Case.PlLoc@kuovttijn LOGI     ; ! kulmáinluvvijnkuuđáin
  @U.Case.PlGen@kyeht:@U.Case.PlGen@kuovtij   LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
  @U.Case.PlCom@kyeht:@U.Case.PlCom@kuovtij   LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom

  @U.Case.SgNom@kulmâ:@U.Case.SgNom@kulmâ     LOGI     ; ! kulmâlovkuttâ
  @U.Case.SgGen@kulmâ:@U.Case.SgGen@kuulmâ    LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
  @U.Case.SgAcc@kulmâ:@U.Case.SgAcc@kuulmâ    LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
  @U.Case.SgIll@kulmâ:@U.Case.SgIll@kuulmâ    LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
  @U.Case.SgLoc@kulmâ:@U.Case.SgLoc@kuulmâ    LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
  @U.Case.SgCom@kulmâ:@U.Case.SgCom@kulmáin   LOGI     ; ! kulmáinluvvijnkuuđáin kulmáinluvvijnovttáin
    @U.Case.Ess@kulmâ:@U.Case.Ess@kulmân      LOGI     ; ! kulmânlovveenkuttân
  @U.Case.PlNom@kulmâ:@U.Case.PlNom@kuulmah   LOGI     ; ! kuulmahlovehkuuđah
  @U.Case.PlGen@kulmâ:@U.Case.PlGen@kulmâi   LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
  @U.Case.PlGen@kulmâ+Use/NG:@U.Case.PlGen@kuulmâi   LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
  @U.Case.PlAcc@kulmâ:@U.Case.PlAcc@kuulmâid  LOGI     ; ! kuulmâidluuvijdkuuđâid
  @U.Case.PlIll@kulmâ:@U.Case.PlIll@kulmáid   LOGI     ; ! kulmáidluvvijdkuuđáid
  @U.Case.PlLoc@kulmâ:@U.Case.PlLoc@kulmáin   LOGI     ; ! kulmáinluvvijnkuuđáin
  @U.Case.PlCom@kulmâ:@U.Case.PlCom@kulmâi    LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom

  @U.Case.SgNom@nelji:@U.Case.SgNom@nelji     LOGI     ; ! kulmâlovkuttâ
  @U.Case.SgGen@nelji:@U.Case.SgGen@neelji    LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
  @U.Case.SgAcc@nelji:@U.Case.SgAcc@neelji    LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
  @U.Case.SgIll@nelji:@U.Case.SgIll@neelji    LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
  @U.Case.SgLoc@nelji:@U.Case.SgLoc@neelji    LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
  @U.Case.SgCom@nelji:@U.Case.SgCom@neljijn   LOGI     ; ! kulmáinluvvijnkuuđáin kulmáinluvvijnovttáin
    @U.Case.Ess@nelji:@U.Case.Ess@neljin      LOGI     ; ! kulmânlovveenkuttân
  @U.Case.PlNom@nelji:@U.Case.PlNom@neeljih   LOGI     ; ! kuulmahlovehkuuđah
  @U.Case.PlGen@nelji:@U.Case.PlGen@neeljij   LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
  @U.Case.PlGen@nelji:@U.Case.PlGen@neljij    LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
  @U.Case.PlAcc@nelji:@U.Case.PlAcc@neeljijd  LOGI     ; ! kuulmâidluuvijdkuuđâid
  @U.Case.PlIll@nelji:@U.Case.PlIll@neljijd   LOGI     ; ! kulmáidluvvijdkuuđáid
  @U.Case.PlLoc@nelji:@U.Case.PlLoc@neljijn   LOGI     ; ! kulmáinluvvijnkuuđáin
  @U.Case.PlCom@nelji:@U.Case.PlCom@neeljij   LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom

   @U.Case.SgNom@vitt:@U.Case.SgNom@vitt      LOGI     ; ! kulmâlovkuttâ
   @U.Case.SgNom@vitt+Use/NG:@U.Case.SgNom@vit       LOGI     ; ! kulmâlovkuttâ
   @U.Case.SgGen@vitt:@U.Case.SgGen@viđâ      LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
   @U.Case.SgAcc@vitt:@U.Case.SgAcc@viđâ      LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
   @U.Case.SgIll@vitt:@U.Case.SgIll@viđâ      LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
   @U.Case.SgLoc@vitt:@U.Case.SgLoc@viđâ      LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
   @U.Case.SgCom@vitt:@U.Case.SgCom@viiđáin   LOGI     ; ! kulmáinluvvijnkuuđáin kulmáinluvvijnovttáin
     @U.Case.Ess@vitt:@U.Case.Ess@vittân      LOGI     ; ! kulmânlovveenkuttân
   @U.Case.PlNom@vitt:@U.Case.PlNom@viiđah    LOGI     ; ! kuulmahlovehkuuđah
   @U.Case.PlGen@vitt:@U.Case.PlGen@viđâi     LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
   @U.Case.PlGen@vitt+Use/NG:@U.Case.PlGen@viiđâi    LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
   @U.Case.PlAcc@vitt:@U.Case.PlAcc@viiđâid   LOGI     ; ! kuulmâidluuvijdkuuđâid
   @U.Case.PlIll@vitt:@U.Case.PlIll@viiđâid   LOGI     ; ! kulmáidluvvijdkuuđáid
   @U.Case.PlLoc@vitt:@U.Case.PlLoc@viiđáin   LOGI     ; ! kulmáinluvvijnkuuđáin
   @U.Case.PlCom@vitt:@U.Case.PlCom@viđâi     LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom

   @U.Case.SgNom@kutt:@U.Case.SgNom@kutt      LOGI     ; ! kulmâlovkuttâ
   @U.Case.SgNom@kutt:@U.Case.SgNom@kut       LOGI     ; ! kulmâlovkuttâ
   @U.Case.SgGen@kutt:@U.Case.SgGen@kuđâ      LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
   @U.Case.SgAcc@kutt:@U.Case.SgAcc@kuđâ      LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
   @U.Case.SgIll@kutt:@U.Case.SgIll@kuđâ      LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
   @U.Case.SgLoc@kutt:@U.Case.SgLoc@kuđâ      LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
   @U.Case.SgCom@kutt:@U.Case.SgCom@kuuđáin   LOGI     ; ! kulmáinluvvijnkuuđáin kulmáinluvvijnovttáin
     @U.Case.Ess@kutt:@U.Case.Ess@kuttân      LOGI     ; ! kulmânlovveenkuttân
   @U.Case.PlNom@kutt:@U.Case.PlNom@kuuđah    LOGI     ; ! kuulmahlovehkuuđah
   @U.Case.PlGen@kutt+Use/NG:@U.Case.PlGen@kuuđâi    LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
   @U.Case.PlAcc@kutt:@U.Case.PlAcc@kuuđâid   LOGI     ; ! kuulmâidluuvijdkuuđâid
   @U.Case.PlIll@kutt:@U.Case.PlIll@kuuđâid   LOGI     ; ! kulmáidluvvijdkuuđáid
   @U.Case.PlLoc@kutt:@U.Case.PlLoc@kuuđáin   LOGI     ; ! kulmáinluvvijnkuuđáin
   @U.Case.PlCom@kutt:@U.Case.PlCom@kuđâi     LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom

 @U.Case.SgNom@čiččâm:@U.Case.SgNom@čiččâm    LOGI     ; ! čiččâmlovkuttâ
 @U.Case.SgGen@čiččâm:@U.Case.SgGen@čiččâm    LOGI     ; ! čiččâmlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
 @U.Case.SgAcc@čiččâm:@U.Case.SgAcc@čiččâm    LOGI     ; ! čiččâmlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
 @U.Case.SgIll@čiččâm:@U.Case.SgIll@čiččâm    LOGI     ; ! čiččâmlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
 @U.Case.SgLoc@čiččâm:@U.Case.SgLoc@čiččâm    LOGI     ; ! čiččâmlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
 @U.Case.SgCom@čiččâm:@U.Case.SgCom@čiččâmáin LOGI     ; ! čiččâmluvvijnkuuđáin kulmáinluvvijnovttáin
   @U.Case.Ess@čiččâm:@U.Case.Ess@čiččâmin    LOGI     ; ! čiččâmlovveenkuttân
 @U.Case.PlNom@čiččâm:@U.Case.PlNom@čiččâmeh  LOGI     ; ! čiččâmlovehkuuđah
 @U.Case.PlGen@čiččâm:@U.Case.PlGen@čiččâmij  LOGI     ; ! čiččâmluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
 @U.Case.PlAcc@čiččâm:@U.Case.PlAcc@čiččâmijd LOGI     ; ! čiččâmluuvijdkuuđâid
 @U.Case.PlIll@čiččâm:@U.Case.PlIll@čiččâmáid LOGI     ; ! čiččâmluvvijdkuuđáid
 @U.Case.PlLoc@čiččâm:@U.Case.PlLoc@čiččâmijn LOGI     ; ! čiččâmluvvijnkuuđáin
 @U.Case.PlCom@čiččâm:@U.Case.PlCom@čiččâmij  LOGI     ; ! čiččâmluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom

  @U.Case.SgNom@kähci:@U.Case.SgNom@kähci     LOGI     ; ! kulmâlovkuttâ
  @U.Case.SgGen@kähci:@U.Case.SgGen@käävci    LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
  @U.Case.SgAcc@kähci:@U.Case.SgAcc@käävci    LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
  @U.Case.SgIll@kähci:@U.Case.SgIll@käävci    LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
  @U.Case.SgLoc@kähci:@U.Case.SgLoc@käävci    LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
  @U.Case.SgCom@kähci:@U.Case.SgCom@kavccijn  LOGI     ; ! kulmáinluvvijnkuuđáin kulmáinluvvijnovttáin
    @U.Case.Ess@kähci:@U.Case.Ess@kähcin      LOGI     ; ! kulmânlovveenkuttân
  @U.Case.PlNom@kähci:@U.Case.PlNom@käävcih   LOGI     ; ! kuulmahlovehkuuđah
  @U.Case.PlGen@kähci:@U.Case.PlGen@kavcij    LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
  @U.Case.PlGen@kähci+Use/NG:@U.Case.PlGen@kaavcij   LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
  @U.Case.PlAcc@kähci:@U.Case.PlAcc@kaavcijd  LOGI     ; ! kuulmâidluuvijdkuuđâid
  @U.Case.PlIll@kähci:@U.Case.PlIll@kavccijd  LOGI     ; ! kulmáidluvvijdkuuđáid
  @U.Case.PlLoc@kähci:@U.Case.PlLoc@kavccijn  LOGI     ; ! kulmáinluvvijnkuuđáin
  @U.Case.PlCom@kähci:@U.Case.PlCom@kavcij    LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom

   @U.Case.SgNom@ohce:@U.Case.SgNom@ohce      LOGI     ; ! kulmâlovkuttâ
   @U.Case.SgGen@ohce:@U.Case.SgGen@oovce     LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
   @U.Case.SgAcc@ohce:@U.Case.SgAcc@oovce     LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
   @U.Case.SgIll@ohce:@U.Case.SgIll@oovce     LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
   @U.Case.SgLoc@ohce:@U.Case.SgLoc@oovce     LOGI     ; ! kuulmâlovkuuđâ, kuulmâlovkuuđâst, kuulmâlovkuutân also Loc, Ill
   @U.Case.SgCom@ohce:@U.Case.SgCom@ovccijn   LOGI     ; ! kulmáinluvvijnkuuđáin kulmáinluvvijnovttáin
     @U.Case.Ess@ohce:@U.Case.Ess@ohcen       LOGI     ; ! kulmânlovveenkuttân
   @U.Case.PlNom@ohce:@U.Case.PlNom@oovceh    LOGI     ; ! kuulmahlovehkuuđah
   @U.Case.PlGen@ohce:@U.Case.PlGen@ovcij     LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
   @U.Case.PlGen@ohce+Use/NG:@U.Case.PlGen@oovcij    LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom
   @U.Case.PlAcc@ohce:@U.Case.PlAcc@oovcijd   LOGI     ; ! kuulmâidluuvijdkuuđâid
   @U.Case.PlIll@ohce:@U.Case.PlIll@ovccijd   LOGI     ; ! kulmáidluvvijdkuuđáid
   @U.Case.PlLoc@ohce:@U.Case.PlLoc@ovccijn   LOGI     ; ! kulmáinluvvijnkuuđáin
   @U.Case.PlCom@ohce:@U.Case.PlCom@ovcij     LOGI     ; ! kuulmâiluvijku(u)đâi, kuulmâiluvijku(u)đâiguin, also PlCom

               muáddi:muáddi                  LOV      ;




LEXICON LOGI  !!≈ * **@CODE@** pointing to tens (LOV) and numbers (ONES)
                                         LOV  ; ! For whole tens
 @U.Case.SgNom@lov:@U.Case.SgNom@lov     ONES ;
 @U.Case.SgGen@lov:@U.Case.SgGen@lov     ONES ;
 @U.Case.SgAcc@lov:@U.Case.SgAcc@lov     ONES ;
 @U.Case.SgIll@lov:@U.Case.SgIll@lov     ONES ;
 @U.Case.SgLoc@lov:@U.Case.SgLoc@lov     ONES ;
 @U.Case.SgCom@lov:@U.Case.SgCom@luvvijn ONES ;
   @U.Case.Ess@lov:@U.Case.Ess@lovveen   ONES ;
 @U.Case.PlNom@lov:@U.Case.PlNom@loveh   ONES ;
 @U.Case.PlGen@lov:@U.Case.PlGen@luvij   ONES ;
 @U.Case.PlAcc@lov:@U.Case.PlAcc@luuvijd ONES ;
 @U.Case.PlIll@lov:@U.Case.PlIll@luvvijd ONES ;
 @U.Case.PlLoc@lov:@U.Case.PlLoc@luvvijn ONES ;
 @U.Case.PlCom@lov:@U.Case.PlCom@luvij   ONES ;


LEXICON ONES  !!≈ * **@CODE@** to card and ord
  CARDINAL      ;
  SHORTCARDINAL ;
  ORDINAL       ;

LEXICON CARDINAL  !!≈ * **@CODE@** this is 1-10
   ohtâ:o^RVht^RCâ OHTA   ;
 kyehti:kyeht^RCi  KYEHTI ;
  kulmâ:ku^RVlmâ   KULMA  ;
  nelji:ne^RVlji   KYEHTI ;
  vittâ:vi^RVtt4â  VITTA  ;
  kuttâ:ku^RVtt4â  VITTA  ;
 čiččâm:čiččâm     CICCAM ;
 käävci:käävc^RCi  KYEHTI ;
  oovce:oovc^RCe   KYEHTI ;
   love:           LOVE   ;

LEXICON SHORTCARDINAL
   ohtâ+Num+Cmp/SgGen+Use/NG:oovt  NUMERALCOMPOUNDS ;
   ohtâ+Num+Cmp/SgGen+Use/NG:ovt   NUMERALCOMPOUNDS ;
 kyehti+Num+Cmp/SgGen+Use/NG:kyevt NUMERALCOMPOUNDS ;
  kulmâ+Num+Cmp/SgGen+Use/NG:kulm  NUMERALCOMPOUNDS ;
 käävci+Num+Cmp/SgGen+Use/NG:kävc  NUMERALCOMPOUNDS ;
 käävci+Num+Cmp/SgGen+Use/NG:käävc NUMERALCOMPOUNDS ;
 oovvce+Num+Cmp/SgGen+Use/NG:ovc   NUMERALCOMPOUNDS ;
 oovvce+Num+Cmp/SgGen+Use/NG:oovc  NUMERALCOMPOUNDS ;


LEXICON MISCNUM 			 !!≈ * **@CODE@**
             pelnub:pel#nub         num       ;
            lakknub:lakk#nub        num       ;
         pelnubčyet:pel#nub#čyet    num       ;
        pelnubčyeđe:pel#nub#čyeđe   num       ;
              maŋgâ:ma^RVŋgâ        KULMA     ;
          maŋgâčyet:ma^RVŋgâ#čyet   CYETI     ;
         muáddičyet:muáddi#čyet     CYETI     ;
           maŋgâlov:ma^RVŋgâ#       LOV       ;
          muáddilov:muáddi#         LOV       ;
             muáddi:muáddi          KYEHTI    ;
        miljovn+Num:miljovn         X_LITTER  ;
   čyetimiljovn+Num:čyeti#miljovn   X_LITTER  ;
 tuhháátmiljovn+Num:tuhháát#miljovn X_LITTER  ;
  muáddituhháát+Num:muáddi#tuhháát  X_POTTAAK ;



LEXICON ARABICCOMPOUNDS   !!≈ * **@CODE@**   arabic as first part,
                +Cmp/Hyph+Cmp#:»»»-  SASCont   ;
      +Cmp/Hyph+Der+Der/ahasas:-ahas ANOLASJ   ;
                     +Cmp/Hyph:»»»-  NAMAG     ;
!               +Cmp/Hyph+Cmp#:»»»-  SAS       ;
  +Der+Der/ivvaas+Sem/Dummytag:ivvá  A_VUALAAS ;

!                             :-     NAMAT     ; ! 10-jienat, etc. NAMAT derivs are
!                    ! treated as compounds and excluded from the nonrec
!                    ! variants, otherwise the PLX lexicon size explodes
!               +Cmp/Hyph+Cmp#:-»»#  SASCont   ; !6-jahkásaš
                       +Use/NG:      RHyph     ; ! 3-


LEXICON NUMERALCOMPOUNDS !!≈ * **@CODE@**: numeral as first part: duhatjienat,
                         !! logigielat, etc. 2nd part in affixes/adjectives.lexc
          NAMAG ; ! duhatjienat, logigielat, etc. NAMAG derivs are
                  ! treated as compounds and excluded from the nonrec
                  ! variants, otherwise the PLX lexicon size explodes
          SAS   ;
 +Use/NG: RHyph ; ! golmma-


LEXICON ORDINAL	  !!≈ * **@CODE@**
 vuossâmuš+A+Ord+Der1+Der/Superl+A:vuossâ   MUSj-SUF                  ; !sme has not Ord for this
                     vuosmuš+A+Ord:vuos     MUSj-SUF    "ensimmäinen" ; !¢vuosmuin, vuosmust, vuosmui, vuosmuš   !MUSj-SUF gives vuosmuust
            vuosmuš+A+Ord+Err/Orth:vuoss    MUSj-SUF    "ensimmäinen" ; !
                       nubbe+A+Ord:nu^RVbbe nubbecase                 ;
                     kuálmád+A+Ord:kuálmád  ORDINALInfl               ;
            kuálmád+A+Ord+Err/Orth:kuálmâd  SOOHAD                    ; ! D XY V
                     niäljád+A+Ord:niäljád  ORDINALInfl               ;
            niäljád+A+Ord+Err/Orth:niäljâd  ORDINALInfl               ;
                      viiđâd+A+Ord:viiđâd   ORDINALInfl               ;
                      kuuđâd+A+Ord:kuuđâd   ORDINALInfl               ;
                      čiččâd+A+Ord:čičč5âd  CICCADInfl                ;
                     káávcád+A+Ord:káávcád  ORDINALInfl               ;
            káávcád+A+Ord+Err/Orth:káávcâd  ORDINALInfl               ;
                      oovcád+A+Ord:oovcád   ORDINALInfl               ;
                     kallaad+A+Ord:kallaad  X_EEMEED                  ;
                     lovváád+A+Ord:lovváád  X_EEMEED    "kymmenes"    ;
                     čuáđáád+A+Ord:čuáđáád  X_EEMEED                  ; !D X VV


LEXICON ARABIC-COLL !!≈ * __@CODE@__
 < [1|2|3|4|5|6|7|8|9] [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOPCOLL ;
                      < [2|3|4|5|6|7|8|9]     > ARABICLOOPCOLL ;




