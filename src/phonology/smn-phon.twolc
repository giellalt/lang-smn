! Divvun & Giellatekno - open source grammars for S√°mi and other languages
! Copyright ¬© 2000-2010 The University of Troms√∏ & the Norwegian S√°mi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no



!! !!!Alphabet 

Alphabet   

!! !!The Inari Saami letters

 a √° √¢ √§ b c d e f g h i j k l m n o √µ p q !!= * @CODE@
 r s t u v w x y z √¶ √§ √∏ √∂ √•			   !!=  @CODE@
 ƒç ƒë ≈ã ≈ß ≈° ≈æ				 			   !!=  @CODE@

 A √Å √Ç √Ö B C D E F G H I J K L M N O √ï P Q !!= * @CODE@
 R S T U V W X Y Z √Ü √Ñ √ò √ñ √Ö 			   !!=  @CODE@
 ƒå ƒê ≈ä ≈¶ ≈† ≈Ω  							   !!=  @CODE@
   %- 							   !!= * @CODE@
 ':0   !!= * @CODE@ this to zero
  üò±     !!= * @CODE@ the ghost sign used in compounding, does not work in hfst

!! !Accented letters, just in case

 √â √ì √ö √ç √Ä √à √í √ô √å √ã √ú √è √ä √î √õ √é √É √ù √æ √ê   !!= * @CODE@
 √© √≥ √∫ √≠ √† √® √≤ √π √¨ √´ √º √Ø √™ √¥ √ª √Æ √£ √Ω √æ √∞   !!=  @CODE@
 √ü ¬™ √ü √ß								   !!=  @CODE@

 
 
!! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 ¬ª7     !! * ¬ª
 ¬´7     !!  ¬´
 %[%>%] !!  >
 %[%<%] !!  <



!! !Different archiphonemes


p4:p  k4:k  l4:l t4:t ƒç4:ƒç c4:c   !!= * @CODE@  these are consonants that change in cg
i4:i                    	        !!= * @CODE@  this is the underlying i consonant
i2:i u2:u √¢2:√¢        	        !!= * @CODE@  this is final i, u in contract verbs etc changing to e, o
i3:i           	        !!= * @CODE@  this is invariant i
a5:a √°5:√° i5:i u5:u y5:y o5:o  √§5:√§               !!= * @CODE@  these do not change 
c5:c ƒç5:ƒç d5:d h5:h j5:j l5:l m5:m ≈ã5:≈ã r5:r t5:t  !!= * @CODE@  these do not change 
j6:0 !!= * @CODE@ a fake consonant, to get WG rules to function, e.g. pu√°vui
i6:0 !!= * @CODE@ a fake vowel, to get WG rules to function, e.g. kyehtlov (kyehti6lov)
 '7:' !!= * @CODE@ is the real apostroph

!! !More abstract archiphonemes

!%^A:0		 = algan lod√°n
%^RC:0		!!= * @CODE@ = root cons., lengthened by ^CLEN - m√°√°n%RCu m√°√°nnun
%^RV:0	 	!!= * @CODE@ = root vowel, lengthened by ^RLEN - ta%^RVvlu taavlu 
%^SC:0      !!= * @CODE@ = stem cons, %^SC:j, pluss lengthen after earlier shortening by %^CSH, see lyeme
%^SV:0 		!!= * @CODE@ = stem vowel, lengthened by ^SLEN u:uu - t√°√°lu%^SV t√°√°loost
%^V			!!= * @CODE@ = vowel copy

!! !Triggers

%^CLEN:0	!!= * @CODE@ = Cons lengthening, with ^RC
%^CSH:0     !!= * @CODE@ = Cons shortening, laa≈ãkku - laa≈ãkun
%^RVSH:0    !!= * @CODE@ = Root vowel shortening
%^FCD:0     !!= * @CODE@ = Final consonant deletion 
%^EA:0      !!= * @CODE@ = Stem vowel: e and i to a/√° in ill, and root vowel √§>√°, e(e)>i√§, ie>i√§, ye>u√°
%^RLEN:0    !!= * @CODE@ = Root vowel lengthening
%^SLEN:0    !!= * @CODE@ = Stem vowel lengthening
%^SVLOW:0   !!= * @CODE@ = Stem vowel lowering √¢>√° and u>o - √°rppu √°rppoin, Root vowel shortening when more than one cns in consonant centre and suffix starts with i
%^SVSH:0    !!= * @CODE@ = Stem vowel shortening kappeer kaper√¢n
%^WG:0      !!= * @CODE@ = Weak grade trigger
%^√ÅE:0      !!= * @CODE@ = √° to e in 3-syll, triggered by ^WG
%^√ÅI:0      !!= * @CODE@ = √° to i in 3-syll, triggered by ^WG
%^VHIGH:0   !!= * @CODE@ = High rootvow for verbs: e>i√§, a>o, o>u, √§√§>√°√°, ee>i√§, note also: ie>i√§, ye>u√°, uo>u√°
%^VBACK:0   !!= * @CODE@ = Back rootvow for verbs: √§(√§)>a(a), √°>aa, u√°>o, ye>uo
%^BLOCK:0   !!= * @CODE@ = This symbol just to block otherwise triggering contexts


!! Trigger ordering in twolc and lexc, from left to right:

!! {{{
!! SV|√ÅE|√ÅI  EA|SC  FCD  WG|CSH  CLEN  VHIGH|VBACK  RVSH|RLEN  SLEN|SVSH  SVLOW
!! }}}


!! !!Morpheme boundaries:

!! Here we define the suffix border >
!! The other borders are not used yet, but still defined:

 ¬´  !!‚âà ;@CODE@: Derivational prefix
 ¬ª  !!‚âà ;@CODE@: Derivational suffix
 %< !!‚âà ;@CODE@: Inflectional prefx
 %> !!‚âà ;@CODE@: Inflectional suffix
 %^ !!‚âà ;@CODE@: (exceptional) soft hyphenation point
 #  !! and the symbol #, which is Word boundary for both lexicalised and dynamic compounds

!! End of alphabet definitions

;

!! !!! Sets

Sets
 Vow = a √° √¢ √§ e i o u u2 y √¶ √∏ √∂ √• 
       i2 u2 √¢2
       i3
       a5 √°5 i5 u5 y5 o5 √§5 		   !!= * @CODE@ 
       i6
       √© √≥ √∫ √≠ √† √® √≤ √π √¨ √´ √º √Ø √™ √¥ √ª √Æ √£ √Ω 	   !!= @CODE@ 
       A √Å √Ç √Ñ E I O U Y √Ü √ò √ñ √Ö			   !!= @CODE@ 
       √â √ì √ö √ç √Ä √à √í √ö √å √ã √ú √è √ä √î √õ √é √É √ù 	   !!= @CODE@ 
       %^RV %^SV ;						   !!= @CODE@ 
 RealVow = a √° √¢ √§ e i o u y √¶ √∏ √∂ √• y5	i2 i5 i3 i6	√¢2   !!= * @CODE@ 
       √© √≥ √∫ √≠ √† √® √≤ √π √¨ √´ √º √Ø √™ √¥ √ª √Æ √£ √Ω 	   !!= @CODE@ 
       A √Å √Ç √Ñ E I O U Y √Ü √ò √ñ √Ö 		   !!= @CODE@ 
       √â √ì √ö √ç √Ä √à √í √ö √å √ã √ú √è √ä √î √õ √é √É √ù ;   !!= @CODE@ 
 Cns = b c ƒç d ƒë f g h j k l m n ≈ã 			   !!= * @CODE@ 
       p q r s ≈° t ≈ß v w x z ≈æ				   !!= @CODE@ 
       B C ƒå D ƒê F G H J K L M N ≈ä			   !!= @CODE@ 
       P Q R S ≈† T ≈¶ V W X Z ≈Ω 				   !!= @CODE@ 
        g2 k2 m2 n2 							   !!= @CODE@ 
       b3 d3 g3 m3 h3 						   !!= @CODE@ 
       p4 k4 r4 l4  i4  t4 c4 ƒç4                !!= @CODE@
       h5 j5 ≈ã5 t5 c5 l5 t5 d5 r5 j6                   !!=  @CODE@
       %^RC %^SC   ;                         !!=  @CODE@ 
 Geminate = b d g p h t k c f z l m n ≈ã r s ≈° v  ;  !!= * @CODE@  
 Sgm = Vow Cns ;							   !!= * @CODE@ 
 NotSuffixBorder = Vow %^FCD Cns %^CSH %^WG %^CLEN %^SLEN %^RLEN %^SVLOW %^RVSH %^√ÅE %^√ÅI %^EA %^SVSH ; !!= * @CODE@ 
 Triggers =   %^FCD %^WG %^CSH %^CLEN %^SLEN %^RLEN %^RVSH %^SVLOW %^√ÅE %^EA %^√ÅI %^SVSH %^VHIGH %^VBACK ; !!= * @CODE@ 
 StemCns = b b3 d d3 g g2 g3 h h2 h3 		   !!= * @CODE@ 
           j l m m2 m3 m8 n n8 n2 ≈ã r s  ;     !!= @CODE@ 



           
!! !!!Definition section

!! Introducing a WG right context 
!! {{{WeakGrade =  (RealVow:) [:RealVow |i6:]  (Cns:) ([%^SV:|%^√ÅI:|%^√ÅE:]) (%^FCD:) %^WG:0   ; }}}  

Definitions

WeakGrade =  (RealVow:) [:RealVow - ƒç: |i6:] (Vow:) (Cns:+) ([%^SV:|%^√ÅI:|%^√ÅE:]) (%^FCD:) %^WG:0      ;   
ConsonantShorting =  (RealVow:) [:RealVow - ƒç: |i6:] (Vow:) (Cns:+) ([%^SV:|%^√ÅI:|%^√ÅE:]) (%^EA:) (%^FCD:) %^CSH:0 ;  
Syll = :* Cns:+ Vow: :* ; 

!! As we look at more cases, the WeakGrade definition tends to be longer. 
!! We want to keep it short, but we want the code to work Thus, we document.
!! * The second :RealVow is there for long consonant stems
!! * The (Cns:) is there for consonant stems

!! !!!Rule section

Rules

!! !!Consonant rules

!! !Consonant gradation



"x:y consonant gradation"
!! * __@RULENAME@__ \\  WG gives x:x and x:y, when there is no consonant lengthening  
Cx:Cy <=> Vow: _ (%^RC: )  WeakGrade  ([%^VHIGH:|%^VBACK:]) ([%^RVSH:|%^RLEN:]) (%^SLEN:) (%^SVLOW:)  [.#.| %>] ; !RULE RC only \CLEN makes conflict
	where Cx in ( h p c ƒç )    
          Cy in ( v v s j )    
		  matched ;

!!‚Ç¨ va^RVh%^RCo^WG^CLEN>im
!!‚Ç¨ va0vvo00>im


"Quality change of root consonant x:yy"
!! * __@RULENAME@__ \\  consonant lengthening after WG, changing h:v, p:v, c:s
Cx:Cy <=> Vow:  _ %^RC: :Vow+ Cns:* [Triggers: -  %^FCD:]* %^WG: %^CLEN: (%^VHIGH:|%^VBACK:) (%^RVSH:) ([%^SVLOW: |%^SLEN:]) %> [ i:|s|u|o|≈æ|e h | e m | √°] ; !RULE RC
	where Cx in ( h p c )
	      Cy in ( v v s ) 
		  matched ;



"t:ƒë, also tk4:ƒëh gradation, part 1" 
!! * __@RULENAME@__ \\   
t:ƒë <=> Vow: _   k4: (%^RC:) WeakGrade ; !RULE RC !! 1) tk4:ƒëk WG 
        Vow: _ (%^RC:)  WeakGrade  ; !RULE RC !! 2) ƒçyet%^RCi3:ƒçyeƒëi
        Vow: _ j  WeakGrade  ; !RULE RC !! 3) vitj√°:viƒëj√°

!!‚Ç¨ jo^RVtk4√¢^WG^RLEN>
!!‚Ç¨ jooƒëh√¢00>


"k4:h gradation for kk4, tk4, rkk4, lkk4"
!! * __@RULENAME@__ \\  
k4:h <=> Vow: [t:|k:]                    _  WeakGrade  ; !RULE RC  !! 1) WG atka:aƒëha, kk4:h
         Vow: [l | r | v | i4: | ƒë | j ]  k: _  WeakGrade ; !RULE RC !! 2) e.g. rkk:rh

"xx:yy gradation for xx4"
!! * __@RULENAME@__ \\   pu√°hhoin, sukk4√°:suhh√°√°st
Cx:Cy <=> Vow: _  Cz:Cy WeakGrade %^CLEN: ; !RULE RC 
	where Cx in ( k  c  p ) 
	      Cy in ( h  s  v ) 
	      Cz in ( k4 c4 p4  ) 
		  matched ;
         
!!‚Ç¨ m√§^RVtk4i^WG^RLEN>.#.
!!‚Ç¨ m√§√§ƒëhi00>.#.

!!‚Ç¨ su^RVkk4√°^SV^WG^CLEN^SLEN>st
!!‚Ç¨ su0hh√°√°000>st


"h:v gradation for hh"
!! * __@RULENAME@__ \\  j√°%^RVhh√°%^SVl%^√ÅI : j√§√§vvil
h:v <=> Vow:     _ h: WeakGrade  %^CLEN: ; !RULE RC  !! 1) WG hh:vh
        Vow:  h: _    WeakGrade  ; !RULE RC  !! 2) WG hh:hv
!kahhaan:kahan√¢n
!!‚Ç¨ kahhaan^CSH^SVSH>√¢n
!!‚Ç¨ ka0h0an00>√¢n



"x4:z gradation for xx4" 
!! * __@RULENAME@__ 
Cy:Cz <=> Cx: _  WeakGrade ; !RULE RC !! ƒçuott√¢lm:ƒçuoƒë√¢lm njuott√¢s 
       where Cx in ( t  c  ƒç  p   ) 
             Cy in ( t4 c4 ƒç4 p4  ) 
             Cz in ( ƒë  s  j   v  ) 
       matched ;

!!‚Ç¨ ru^RVtt4√¢^WG^RLEN>
!!‚Ç¨ ruu0ƒë√¢00>
 
!rejected:       
!!‚Ç¨ haavv√¢r^CSH^SVSH>ist
!!‚Ç¨ ha00v√¢r00>ist

!rejected:       
!!‚Ç¨ pott√°√°k^SC^CSH^SVSH>ist
!!‚Ç¨ po0t0√°kk00>ist


"xy:zy gradation"
!! * __@RULENAME@__ \\  ht:vt, we use h 
Cx:v <=>  _  Cz (%^RC:) Vow:+ (%^SV: ) (Cns:+) ([%^√ÅI:|%^√ÅE:])  (%^FCD:) %^WG: ; !RULE RC !! 1) pi%^RVht√¢s
          _  Cz t Vow (%^FCD:) %^WG: ;  !RULE RC !! 2) tekst√¢
       where Cx in ( h k4 k4 h h ) 
             Cz in ( t s  ≈° ƒç c ) 
       matched ;


!!‚Ç¨ piht√¢s^WG
!!‚Ç¨ pivt√¢s0

!!‚Ç¨ totk4os^WG
!!‚Ç¨ toƒëhos0



!!‚Ç¨ √°^RVrppu^WG^RLEN>
!!‚Ç¨ √°√°r0pu00>

!!‚Ç¨ √°^RVrppu^WG^RVSH>tt√°√°
!!‚Ç¨ √°0r0pu00>tt√°√°

!!‚Ç¨ ri√§nttus^WG>
!!‚Ç¨ ri√§n0tus0>

!kurr√¢ kuur√¢n
!!‚Ç¨ ku^RVrr√¢^CSH^RLEN>n
!!‚Ç¨ kuu0r√¢00>n

!soll√¢ soo√¢n
!!‚Ç¨ so^RVll√¢^CSH^RLEN>n
!!‚Ç¨ soo0l√¢00>n


!! !Consonant shortening

"k:0 gradation for xkk4"
!! * __@RULENAME@__ \\  for lkk4, rkk4, vkk4
k:0 <=> Vow: [l | j | r | v | i4: | ƒë ] _ k4:h  WeakGrade (%^CLEN: ) (%^VBACK:) (%^SVLOW:) ; !RULE RC !! 1) WG 
        Vow: [l | j | r | v | i4: | ƒë ] _ k4:k Vow: (%^EA:) %^CSH: (%^VBACK:) (%^RLEN:)  [.#.| %>] ; !RULE RC !! 2) CSH 



"xyy:xy gradation A"
!! * __@RULENAME@__ \\   h√°%^RVi4tt√°s%^√ÅI
Cy:0 <=> :Vow (Vow:) Cx: _ Cz: (%^RC:) WeakGrade \%^CLEN:   ;   !RULE RC !! 1) WG
                     Cx: _ Cz:  ConsonantShorting    ; !RULE RC   !! 2) CSH  !he%^RVi4ttug
      where Cx in ( j i4 i4 j j i4 i4 i4 i4 l l l l l l l m m m n n n ≈ã r r r r r r r r v v v v v ) 
            Cy in ( p c  s  s t k  l  p  t  c k p s t ƒç ≈° p ƒç s c s t k c ƒç p k s t ƒç ≈° c d k p t ) 
            Cz in ( p c  s  s t k  l  p  t  c k p s t ƒç ≈° p ƒç s c s t k c ƒç p k s t ƒç ≈° c d k p t ) 
      matched ;


"ƒç:0 in ƒçƒç4 consonant gradation when CLEN"
!! * __@RULENAME@__ \\ when ƒçƒç4
ƒç:0 <=> Vow: _ ƒç4:  WeakGrade %^CLEN: ; !RULE RC

!i≈°Ã£e:i≈°e
!!‚Ç¨ i^RV≈°^RCe^WG>
!!‚Ç¨ i0≈°0e0>


!luƒçƒç√¢:luuj√°in 
!!‚Ç¨ lu^RVƒçƒç4√¢^WG^RLEN^SVLOW>in
!!‚Ç¨ luu0j√°000>in


"From i4j to j when WG and CSH"
!! * __@RULENAME@__ \\ e.g. sai4joos:sajoos
i4:0 <=> Vow: _ j:  WeakGrade ; !RULE RC !! 1) when WG
         Vow: _ j:  ConsonantShorting ; !RULE RC !! 2) when CSH 
         Vow _ n j (%^CLEN:) (%^RLEN:) %^SVSH: [%>|.#.] ;  !RULE SV !! 3)  -i- in pieh√¢in to pieh√¢n√¢n pieh√¢in pi√§i4pp√°i4n
         



"xx:x consonant gradation and shortening and x:x shortening"
!! * __@RULENAME@__ \\   weak grade or consonant shortening 
Cx:0 <=> Vow: _ Cy: WeakGrade \%^CLEN: ; !RULE RC !! 1) when WG
         Vow: _ Cy: ConsonantShorting \%^CLEN:  ; !RULE RC !! 2) when CSH  
	where Cx in ( b c c  ƒç  ƒç d ƒë f g h j k k  l m n ≈ã p p  r s ≈° v z ≈æ ) !t in other rule
	      Cy in ( b c c4 ƒç4 ƒç d ƒë f g h j k k4 l m n ≈ã p p4 r s ≈° v z ≈æ ) 
		  matched ;


!!‚Ç¨ vi^RV≈°≈°√¢^CSH^RVSH>vettee
!!‚Ç¨ vi00≈°√¢00>vettee


!!‚Ç¨ la^RVs^RCe^SVtt^RLEN^SLEN>vettee
!!‚Ç¨ laas0ee0t00>vettee

!cuozz√¢:cuoz√¢
!!‚Ç¨ cuozz√¢^WG^RLEN>
!!‚Ç¨ cuo0z√¢00>

!v√°ddu:v√°√°dun
!!‚Ç¨ v√°^RVddu^CSH^RLEN>n
!!‚Ç¨ v√°√°0du00>n


"tt:t  - final consonant and consonant gradation"
!! * __@RULENAME@__ \\ consonant gradation, and 3syll verbs in -tt lose t in front of cns-initial suff
t:0 <=> Vow:                _ [t:|t4:] WeakGrade \%^CLEN: ; !RULE SC !! 1) WG, not CLEN
        Vow:                _ [t:|t4:] ConsonantShorting \%^CLEN:  ; !RULE SC !! 2) CSH
        [RealVow:|%^SV:|%>] _ [t:|t4:] (i6:) Triggers:* [.#.| %>] \Vow  ; !RULE SC !! 3) u√°iv√°tt : u√°iv√°t
        [RealVow:|%^SV:]    _ [t:|t4:] m Triggers:* [.#.| %>]  ; !RULE SC !! 4) ol√°≈°uttem:ol√°≈°utm√¢n
        Vow: Vow:           _  t:       %> √¢ n  ; !RULE SC !! 5) u√°iv√°√°tt>√¢n u√°iv√°√°t√¢n
       [.#.|#] Cns:* :Vow+ Cns:+ :Vow+  Cns:+ i _  t:      %> i [j|≈æ|≈°]  ; !RULE SC !! 6) u√°p√°smittijm>u√°p√°smitijm

!!‚Ç¨ kulluutt^SVSH>
!!‚Ç¨ kullu00t0>

!!‚Ç¨ las^RCe^SVtt^CLEN^SLEN>
!!‚Ç¨ lassee0t00>

"nnj to nj shortening weak grade and consonant shortening"
!! * __@RULENAME@__ \\  special rule, e.g. konnj√¢l -> konj√¢l√¢n pu√°nnj√°
n:0 <=> n _ j ConsonantShorting \%^CLEN: ;!RULE RC !! 1)  vinnjooh:vinjoh√¢n
        n _ j WeakGrade \%^CLEN: ; !RULE RC !! 2)lonnj√¢:loonj√°in 


"Word final consonant deletion"                 
!!  * __@RULENAME@__ \\ for vask√¢m:vaask√¢
Cx:0 <=> _ ([%^√ÅI:|%^√ÅE:]) %^FCD: ;  !RULE SC
          where Cx in (n m t v j s ≈°) ;

!rejected, look at this:
!!‚Ç¨ va^RVsk√¢m^FCD^RLEN
!!‚Ç¨ vaask√¢000
	
"Final m in stems, smm to sm"  
!!  * __@RULENAME@__ \\ 
m:0 <=>  s _ m [Triggers: - %^FCD:]* [.#.|%>]   ; !RULE SC !! 1) putt√°√°s_min 
         s _   [Triggers: - %^FCD:]*      %>  m ; !RULE SC !! 2) stoorg√¢s_m√¢n  sto%^RVrg√¢sm



!"Stem final geminate shortening in front of consonant-initial suffix"
!Cx:0 <=> Vow _ Cx %> Cns ;
!     where Cx in (d l ≈°);


"Wordfinal consonants part 1"
!! * __@RULENAME@__  e%^RVl√¢ƒçƒç
Cx:0 <=> Vow: _ Cy: (%^CLEN:) (%^RLEN:) (%^SLEN:|%^SVSH:) [.#.| %>] \Sgm: ; !! 1)
         Vow: _  [Cy | ƒç:≈°] [Triggers: - %^FCD:]* %> m ; !! 2)jurd√¢ƒçƒçm√¢n:jurd√¢≈°m√¢n ju%^RVrd√¢ƒçƒç%^RLEN%>m√¢n
	 Vow: _ Cy %> Cns ; !! 3)  ≈°≈°>≈° in h√°r√°vu≈°≈°>vetteƒë 
	where Cx in ( d ƒç ≈° l h ) 
	      Cy in ( d ƒç ≈° l ƒç )
		  matched ;


!!‚Ç¨ h√°r√°vu≈°≈°>vetteƒë
!!‚Ç¨ h√°r√°vu0≈°>vetteƒë

!!‚Ç¨ konnj√¢l^CSH>√¢n
!!‚Ç¨ kon0j√¢l0>√¢n

!!‚Ç¨ su√°n^RCju^CLEN>n
!!‚Ç¨ su√°nnju0>n

!rejected, look at this:
!!‚Ç¨ haamit^SC^CSH^SVSH>ist
!!‚Ç¨ h0amitt00>ist

!!‚Ç¨ su^RVkk4√°^SV^WG^CLEN^SLEN>st
!!‚Ç¨ su0hh√°√°000>st

!rejected:
!!‚Ç¨ kopp^RC√¢^WG^CLEN^SVN>in
!!‚Ç¨ ko0p0√°000>in




!! !Consonant lengthening
 
         
"Lengthening of root consonant x:yy"
!! * __@RULENAME@__ \\  consonant lengthening after WG - h:vv, p:vv, c:ss
%^RC:Cy <=> Vow: Cx:Cy _ :Vow+ Cns:* [Triggers: - %^FCD:]* %^WG: %^CLEN: (%^VHIGH:|%^VBACK:) (%^RVSH:) ([%^SVLOW: |%^SLEN:]) %> [ i:|s|u|o|≈æ |e h | e m | √°] ; !RULE RC
	where Cx in ( h p c )
	      Cy in ( v v s ) 
		  matched ;

!kassa:kassaast
!!‚Ç¨ ka^RVss^RCa^SV^WG^CLEN^SLEN>st
!!‚Ç¨ ka0ss0aa000>st

!vahe vavveest
!!‚Ç¨ va^RVh^RCe^SV^WG^CLEN^SLEN>st
!!‚Ç¨ va0vvee000>st

!vahe vahheen
!!‚Ç¨ va^RVh^RCe^SV^CLEN^SLEN>n
!!‚Ç¨ va0hhee00>n


"Lengthening of half-long root consonant x:xx"
!! * __@RULENAME@__ \\  riƒçƒç√° piiru piirrun niisu niissun
%^RC:Cy <=> Vow:  Cx _ :Vow+ Sgm:* Triggers:* %^CLEN: ; !RULE RC  !! Cx, not Cx:  ƒçeec%^RCi%^CLEN%>n
 	where Cx in ( b c ƒç d g k l r s h m n ƒë v ≈ã p ≈° z ≈æ t )  
 	      Cy in ( b c ƒç d g k l r s h m n ƒë v ≈ã p ≈° z ≈æ t ) 
 		  matched ;


"Lengthening of consonant h5"
!! * __@RULENAME@__ \\    this one does not fit into the general rule because of h5:h
%^RC:Cy <=> Vow:  Cx:Cy _ Vow:+ Cns:* Triggers:* %^CLEN: ; !RULE RC 
	where Cx in ( h5 )  
	      Cy in ( h  )   
		  matched ;

!!‚Ç¨ put^RCe^SVst^CLEN^SLEN>
!!‚Ç¨ putteest00>

!ki√§r%^RCun%^CLEN%>tt√°√°

!!‚Ç¨ ke^RVp^RCidm^CLEN^RLEN^SLEN>√¢n
!!‚Ç¨ keeppidm000>√¢n  

!kieh√¢  kievv√°in
!!‚Ç¨ kieh^RC√¢^WG^CLEN^SVLOW>in
!!‚Ç¨ kievv√°000>in

!i≈°e i≈°≈°eest
!!‚Ç¨ i^RV≈°≈°^RCe^SV^WG^CLEN^SLEN>st
!!‚Ç¨ i0≈°≈°0ee000>st

!!‚Ç¨ kol^RCe^CLEN>
!!‚Ç¨ kolle0>

!!‚Ç¨ kiel^RC√¢^CLEN>n
!!‚Ç¨ kiell√¢0>n

"ƒç:ij part 1"
!! * __@RULENAME@__ \\  
ƒç:i <=> Vow: _ %^RC:  WeakGrade %^CLEN: ; !RULE RC  !!  eeƒç%^RCi:eijijn eeƒç%^RCi%^WG%^CLEN%^SVLOW%>in

"Lengthening root consonant j:ij 1"
!! * __@RULENAME@__ \\  for saje:saijeen 
j:i <=> Vow: _ %^RC: :Vow (Cns:+) (%^SV:) Cns:* %^CLEN: ([%^RVSH:|%^RLEN:]) (%^SLEN:) [.#.| %>] ; !RULE RC !!Par Ess


"ƒç:ij and j:ij part 2"
!! * __@RULENAME@__ \\  
%^RC:j <=> Vow: ƒç:i _ WeakGrade %^CLEN: ; !RULE RC !! 1)  eeƒç%^RCi:eijijn
           Vow: j:i _ Vow:+ (%^SV:) Cns:*  %^CLEN: ; !RULE RC !! 2) syej%^RCi3%^CLEN%^RVSH%>in suoijijn

!!‚Ç¨ sa^RVj^RCe^SV^CLEN^SLEN>n
!!‚Ç¨ sa0ijee00>n

!!‚Ç¨ p√°√°j^RC√°^CLEN>n
!!‚Ç¨ p√°√°ij√°0>n

!!‚Ç¨ vaj^RCe^SVd^CLEN^SLEN>
!!‚Ç¨ vaijeed>




"nj to nnj consonant lengthening"
!! * __@RULENAME@__ \\  special rule, su√°nju : su√°nnjun for Par and Ess
%^RC:n <=> Vow: n _ j RealVow: %^CLEN: %> [ d | n ] ; !RULE RC



"Consonant lengthening after xx:x and xy:zy weak grade with x4 on upper side"
!! * __@RULENAME@__ \\   k4, ƒç, p4  kukse:kuvssijn  
%^RC:Cx <=> Vow:  Cy:  Cz: _ WeakGrade %^CLEN:  ; !RULE RC
        where Cy in ( ƒç  p  k m  m ≈ã h h  h l l l h k4 k4 r r r v v ) 
              Cz in ( ƒç  p4 s p  s k c t  ƒç c ƒç p c s  ≈° c ≈° t c ƒç ) 
              Cx in ( j  v  s p  s k c t  ƒç c ƒç p c s  ≈° c ≈° t c ƒç ) 
        matched ;


"Consonant lengthening xy:xyy"
!! * __@RULENAME@__ \\ 
%^RC:Cx <=> Vow:  Cy:  Cz: _ Vow:+ Cns:+ %^CLEN:  ;  !RULE RC
        where Cy in ( k m  m ≈ã h h  h l l l h r r r v v ) 
              Cz in ( s p  s k c t  ƒç c ƒç p c c ≈° t c ƒç ) 
              Cx in ( s p  s k c t  ƒç c ƒç p c c ≈° t c ƒç ) 
        matched ;



!! !Stem consonant and suffix consonant 


"Assimilation of consonants t d ≈°"
!! * __@RULENAME@__  delet t in front of -tt√°√°, avoid -ttt√°√°
Cy:0  <=>  Sgm:  _ (%^CSH:) (%^RLEN:) %> Cy Cy ;  !RULE SC
	where Cy in ( d t ≈°)
		  matched ;

!!‚Ç¨ haamit>tt√°√°
!!‚Ç¨ haami0>tt√°√°



"Wordfinal consonants part 2"
!! * __@RULENAME@__  e%^RVl√¢ƒçƒç cumm√¢l√¢hƒç:cumm√¢l√¢h≈°
ƒç:≈°  <=>  Vow: [ƒç:|h:] _ Triggers:* [.#.| %>] \Sgm: ; !RULE SC
          Vow:  ƒç:0    _ Triggers:* %> m ; !RULE SC jurd√¢ƒçƒçm√¢n:jurd√¢≈°m√¢n

"Final consonant x:xx"
!! * __@RULENAME@__ \\  pott√°√°k pot√°kk√¢n  
%^SC:Cx <=> Cx _ Triggers:* [%>|.#.] ; !RULE SC
        where Cx in ( k l n p t ) ;

!rejected:
!!‚Ç¨ kahhaan^SC^CSH^RVSH>ist
!!‚Ç¨ ka0ha0nj00>ist





!! !!Vowel rules
!  =============
!  =============

!! !First syllable vowel shortening


!!‚Ç¨ koonnj√¢l^CSH>√¢n
!!‚Ç¨ ko0n0j√¢l0>√¢n

!!‚Ç¨ kappeer^CSH^SVSH>ist
!!‚Ç¨ ka0p0er00>ist

!!‚Ç¨ ƒç√§√§llim^CSH>ist
!!‚Ç¨ ƒç√§00lim0>ist

!This is not correct, Check it:
!!‚Ç¨ muusik^SC^CSH^RVSH>ist
!!‚Ç¨ mu0sikk00>ist

! kahhaan^SC^CSH^RVSH>ist
! ka0ha0n0j00>ist

!aarr√¢n:ar√¢n√¢n
!!‚Ç¨ aarr√¢n^CSH>√¢n
!!‚Ç¨ a00r√¢n0>√¢n

!rejected:
!!‚Ç¨ kiirr√¢s^RVSH>√¢n
!!‚Ç¨ ki0rr√¢s0>√¢n
 
 
 
"Vowel shortening VV:V"
!! * __@RULENAME@__ \\ r√°√°vhu- : r√°vhoid, k√§√§hir%^CSH%>id kuuƒëi√¢%^RVSH%>≈æ√¢n
Vy:0 <=> Vx: _  Cns:+ [ Cns: - %^RC:0 ] [Vow: - u2:]  %^WG: (%^CLEN:) %^SVLOW: %> i: ; !RULE RV !! 1)
         Vx: _  Cns:+                   [Vow: - u2:]  %^WG:  %^CLEN:  %^RVSH:  %> [i:|e]  ; !RULE RV !! 2) ƒç√§√§ci : ƒçassijn
         Vx: _  Cns:+   [Vow: - u2:]+ Cns:* (%^CSH:) %^RVSH: (%^SVLOW:)  [%>|.#.]  ; !RULE RV !! 3) kaamuv kammust
         Vx: _ [Cns:+ - ƒç:] [Vow: - u2:]+ Cns:+ ([%^√ÅI:|%^√ÅE:]) %^CSH: (%^SVSH:) [%>|.#.] ; !RULE RV !! 4) p√°√°pp√°r : p√°p√°r√¢n p√°√°pp4√°r%^√ÅI%^WG%^RVSH%>id
         Vx: _ [Cns:+ - ƒç:] [Vow: - u2:]+ Cns:+ ([%^√ÅI:|%^√ÅE:]) (%^FCD:) %^WG: %^RVSH: [%>|.#.] ; !RULE RV !! 5) p√°√°pp√°r : p√°p√°r√¢n viijƒë√°s%^√ÅE%^WG%^RVSH> 
         Vx: _  Cns:+    [Vow: - u2:]+ (%^WG:) (%^CLEN:)  %^RVSH: (%^SVSH:) [%>|.#.] ;  !RULE RV !! 6) nj√§√§h^RCe^CLEN^RVSH> = nj√§hhe>
         [Vx: - :i] _  Cns:+      [Vow: - u2:]+ (%^WG:) (%^CLEN:) %^VHIGH: %^RVSH: (%^SVSH:) [%>|.#.] ;  !RULE RV !! 6) nj√§√§h^RCe^CLEN^RVSH> = nj√§hhe>
         Vx: _  Cns:+       Vow  Cns:* (%^WG:|%^CSH:) (%^CLEN:) %^VBACK:  %^RVSH: [%>|.#.] ; !RULE RV !! 7) nj√§√§h^RCe^CLEN^RVSH> = nj√§hhe> 
       where Vx in ( a e i o u √° √§ y A E I O U √Å √Ñ ) 
             Vy in ( a e i o u √° √§ y a e i o u √° √§ ) 
       matched ;

! ƒç√§√§llu2^VBACK^RVSH>ƒçƒç>im
"Long vowel shortening"
!! * __@RULENAME@__ \\ (example?), no ^CSH, since that gives Vy:0
Vy:0 <=> Vx: _ [Cns: - ƒç:] Cns:* (%^RC:) [Vow: - u2:]  %^WG:  %^RVSH: %^SVLOW: [%>|.#.] \e ; !RULE RV !! 2st stems
         Vx: _  Cns:+         Vow:  Cns: %^CSH: %^RVSH: %^SVSH: [%>|.#.] ; !RULE RV !! 3syll kaavpug:kavpug√¢n 
         Vx: _  Cns:+         Vow: (%^SV:) Cns:+ Vow: Cns:  %^RVSH: [%>|.#.] ; !RULE RV !! t√°√°lhudem koomerdem
       where Vx in ( a e i o u y √° √§ ) 
             Vy in ( a e i o u y √° √§ ) 
       matched ;

"e:0 in passive"
!! * __@RULENAME@__ \\
e:0 <=>   e: _  Cns:+ u2:u   ([%^WG: | %^CSH:]) (%^CLEN:) %^VBACK: %^RVSH:  [%>|.#.] ; !RULE RV 



!!‚Ç¨ m√°√°h^RCu^WG^CLEN^SVLOW>in
!!‚Ç¨ m√°0vvo000>in
 
!!‚Ç¨ mool^RC√¢^WG^RVSH>tt√°√°
!!‚Ç¨ mo0l0√¢00>tt√°√°



!"Long vowel shortening, before ƒç:j "
!! * __@RULENAME@__ \\ special rule for getting ejÃ£itt√°√° instead of ejÃ£itt√°√°.
!Vy:0 <=> Vx: _ ƒç: (%^RC:) Vow:  %^WG: %^RVSH: (%^SVLOW:) [%>|.#.] ; !RULE RV !!  2st stems
!     where Vx in ( a e i o u y √° √§ ) 
!           Vy in ( a e i o u y √° √§ ) 
!     matched ;
! Temporarily commented out due to conflict, have a look at this.

"Underlying long vowel shortening i√§:e and √§√§:√§ or a"
!! * __@RULENAME@__ \\ ƒç√§√§h%^RCu2%^CLEN%^VBACK%>um
√§:0 <=> i: _  Cns:+ Vow:  Cns: [%^√ÅI:|%^√ÅE:] %^WG: %^RVSH:  [%>|.#.] ;  !RULE RV 
        √§: _  Cns:+ u2:o  Triggers:* %^RVSH:  [%>|.#.] ; !RULE RV !%^VBACK also?
        √§: _  Cns:+ u2:u   ([%^WG: | %^CSH:]) (%^CLEN:) %^VBACK: %^RVSH:  [%>|.#.] ;
  \Vow √§:√° _  Cns:+ Vow:  %^EA: ([%^CSH:| %^CLEN:]) %^RVSH:  [%>|.#.] ; !RULE RV  !s√°m√°sis


!!  3st stems vi√§sk√°r veskirin, ƒçaskes ƒçaskes ƒça%^RVskas%^√ÅE vi√§sk√°r%^√ÅI%^WG%^RVSH

!kaavpug:kavpug√¢n
!!‚Ç¨ kaavpug^CSH^RVSH^SVSH>√¢n
!!‚Ç¨ ka0vpug000>√¢n


! mool√¢ to mool√¢:		  
		  
!!‚Ç¨ mool^RC√¢^WG>
!!‚Ç¨ mool0√¢0>


!k√§√§hir to k√§√§vir
!!‚Ç¨ k√§√§hir^WG>
!!‚Ç¨ k√§√§vir0>

!ju√°hu to ju√°vutt√°√°
!!‚Ç¨ ju√°h^RCu^WG^RVSH>tt√°√°
!!‚Ç¨ ju√°v0u00>tt√°√°





!! !First syllable vowel lengthening

"Root vowel lenghtening in many stems"
!! * __@RULENAME@__ \\  no rootvowel length if stem vowel lengthens and the consonantcentre consists of two or more consonants 
%^RV:Vy <=> Vx _ Cns:+  (Vow:0) Vow: (Vow:0)  (Cns:+)  Triggers:*  (√¢)   %^RLEN: ;  !RULE RV !! 2syll and 3syll 
            Vx _ Cns:  (Cns:0)  Vow: (:Vow) (Cns:+)  Triggers:*  (√¢)   %^RLEN: %^SLEN:  [%>|.#.] ; !RULE RV  
       where Vx in (a e i o y √° √∂ A E I O √Å √ñ ) ! √Ånn√°:√Å√°n√° u, U in separate rule Root vowel u length
             Vy in (a e i o y √° √∂ a e i o √° √∂ ) 
       matched ;

!!‚Ç¨ k√§^RVvn√¢st^WG^VBACK^RLEN>
!!‚Ç¨ kaavn√¢st000>

!!‚Ç¨ le^RVkk4√¢^WG^RLEN>
!!‚Ç¨ lee0h√¢00>

! Left context example:     e:√§, √°:√§
! Right context example:    ƒç:i p4:0 ^WG:0 ^RLEN:0

!!‚Ç¨ nju^RVne^SV^WG^RLEN^SLEN>st
!!‚Ç¨ njuunee000>st



"Root vowel o5:u "
!! * __@RULENAME@__ \\ 
o5:u <=> \Vow: _ %^RV: Cns:+   i (%^WG:) (%^CLEN:)   (%^RLEN:) [%>|.#.] ;  !RULE RV !! puhtii




"Root vowel √§:√§√§ lenghtening and diphthongisation e:i√§"
!! * __@RULENAME@__ \\ special rule, for avoiding conflict sp√§%^RVi4lih%^WG%^RLEN, 
%^RV:√§ <=> √§ _ Cns:+  Vow:+        Cns:+            %^CLEN: %^RLEN: ; !RULE RV !! 1) v√§%^RVr%^RCid
           √§ _ Cns:+  Vow: (Vow:) (Cns:+)   (%^WG: |%^CSH:)   %^RLEN:  (%^SLEN:) (%^SVSH:) [%>|.#.] ; !RULE RV !! 2)
          :√§ _ Cns:*  :i (%^SV:)   Cns %^√ÅI: %^WG: (%^CLEN:) %^RLEN: [%>|.#.] ; !RULE RV !! 3)j√°%^RVhh√°%^SVl%^√ÅI%^WG%^RLEN>
           √§ _ Cns:+  i:                                    %^WG: %^RLEN: [%>|.#.] \i: ;  !RULE RV !! 4)
         e:i _ Cns:+  i:                     %^EA: (%^CSH:) (%^RLEN:) [%>|.#.] ; !RULE RV 5) e:i√§, ergi -> i√§rg√°n
         e:i _ Cns:+ Vow: ([%^WG:|%^CSH:]) (%^CLEN:) %^VHIGH: ;  !RULE RV 6) too much here?
         e:i _ Cns:+ u i4: %^WG:  [%>|.#.] ;  !RULE RV 7) ƒçev≈æuin ƒçi√§v≈æui ƒçe%^RVv≈æui4%^WG>


"Root vowel o lengthening"
!! * __@RULENAME@__ \\  alge : oolgijn 
%^RV:o <=> [Cns|.#.|#] a:o  _  Cns:+  e:     %^WG:   %^RLEN:  %> i: ; !RULE RV !! 1)
           [Cns|.#.|#] a:o  _  Cns:+  u      %^CSH:  %^RLEN:  %> [m|ƒë|u]   ; !RULE RV !!  2)
           [Cns|.#.|#] a:o  _  Cns:+ Vow:    (%^WG:|%^CSH:) (%^CLEN:) %^VHIGH: %^RLEN: ; !RULE RV !! 3)
                \Vow:  a:o  _  Cns:+ [√¢|u2:u] (Cns:+) ([%^WG: | %^CSH:]) (%^CLEN:) %^VBACK:  %^RLEN: (%^SLEN:) [%>|.#.] ;  !RULE RV !! 5) toov√¢d moonnum
                       o5:o _  Cns:+  Vow: (Cns:+) Triggers:*   %^RLEN: ;  !RULE RV !! poovt√°m  o5o

!!‚Ç¨ a^RVlge^WG^RLEN>id
!!‚Ç¨ oolgi00>jd

!!‚Ç¨ va^RVh^RCu^CSH^RLEN>
!!‚Ç¨ vooh0u00>


"Root vowel u lengthening with vowel change before suffix i"
!! * __@RULENAME@__ \\  alge : oolgijn 
%^RV:u <=>  :u  _ Cns:+  e:     %^WG:  %^RLEN:  %> i: ; !RULE RV !! 1)
            :u  _ Cns:+ [√¢|a|i|√°|u] (%^WG:|%^CSH:) (%^CLEN:)  %^VHIGH: %^RLEN: ; !RULE RV  !! 2)
          [U|u] _ Cns:+   Vow: (Vow:) (Cns:+) (Triggers: - %^VHIGH:)* %^RLEN: ;  !RULE RV  !! 3)
    \Vow:  o5:u _ Cns:+   i (%^WG:) (%^CLEN:)  %^RLEN: [%>|.#.] ;  !RULE RV !! 4) puuvtijd
    \Vow:  o:u _ Cns:+   u2:u ([%^WG: | %^CSH:]) (%^CLEN:) %^VBACK:  %^RLEN: [%>|.#.] ;  !RULE RV !! 5) juuttu>m
    \Vow:  o:u _ Cns:+   √¢ Cns:+ ([%^CLEN:|%^WG:| %^CSH:]) %^VBACK:  %^RLEN: (%^SLEN:) [%>|.#.] ;  !RULE RV !! 5) juutt√¢l

!!‚Ç¨ lo^RVh^RC√¢^RLEN>ƒë
!!‚Ç¨ luuh0√¢0>ƒë

!!‚Ç¨ mu^RV≈°ti^WG^VHIGH^RLEN>j
!!‚Ç¨ muu≈°ti000>j

!!‚Ç¨ lo^RVh^RC√°^WG^VHIGH^RLEN>im
!!‚Ç¨ luuv0√°000im

!!‚Ç¨ ko^RVlg√¢^WG^VHIGH^RLEN>i3




!! !First syllable vowel change

!! a rules

"Root vowel change a:o before suffix i and for contract verbs"
!! * __@RULENAME@__ \\  alge : olgijn 
a:o <=> [Cns|.#.|#] _  %^RV:  Cns:+ e:   (%^WG:) ([%^RLEN: |%^CLEN:] )  %> i: ; !RULE RV
        [Cns|.#.|#] _ (%^RV:) Cns:+ [Vow: - u2:] (%^WG:|%^CSH:) (%^CLEN:) %^VHIGH: ; !RULE RV
        [Cns|.#.]   _  %^RV:  Cns:+ u    %^CSH: %^RLEN: %> [m|ƒë|u] ; !RULE RV  ! contr 
             \Vow   _ (%^RV:) Cns:+ u2:u  Triggers:*       %>   ; !RULE RV ! contr 
        [Cns|.#.]   _         Cns:+ i2:i       %> [i|i3:|Cns]         ; !RULE RV ! contr 
             \Vow: _ (%^RV:)  Cns:+  √¢ (Cns:+) ([%^CLEN:|%^CSH:|%^WG:]) %^VBACK:  ([%^RLEN:|%^RVSH:]) (%^SLEN:) [%>|.#.] ;  !RULE RV !! 5) jut√¢l√¢m juutt√¢l

"Root vowel o:u for contract verbs and Root vowel change o:u before suffix i"
!! * __@RULENAME@__ 
o:u <=> [Cns|.#.] _  (%^RV:)      Cns:+  u2:u  Triggers:*  %>   ; !RULE RV juttuum
        [Cns|.#.] _         Cns:+  i2:i %>    [i|i3:i|Cns]  ; !RULE RV
                  _  %^RV:  Cns:  (Cns:) e:i (%^WG:) ([%^RLEN: |%^CLEN:] )  %> [i:|m|v|p] ; !RULE RV !!  alge : olgijn, not ohce removed ƒë because of inf.
            \Vow: _ (%^RV:) Cns:+  [√¢|a|i|√°|u] (%^WG:|%^CSH:) (%^CLEN:) %^VHIGH: ; !RULE RV ! XXX ny
            \Vow: _ %^RV:√° Cns:+ u [i4: | j | ≈° | ≈æ]  %^WG: (%^RVSH:) [%>|.#.] ; !RULE RV ! 1) poccuu pu√°sui  
           \Vow: _ (%^RV:) Cns:+   √¢ Cns:+ ([%^WG: | %^CSH:]) (%^CLEN:) %^VBACK: ; !  ([%^RLEN:|%^RVSH:]) (%^SLEN:) [%>|.#.] ;  !RULE RV !! 5) jut√¢l√¢m juutt√¢l


!!‚Ç¨ tohhi2>ƒë
!!‚Ç¨ tuhhi>ƒë

!!‚Ç¨ lo^RVh^RC√°^WG^VHIGH^RLEN>im
!!‚Ç¨ luuv0√°000>im




!! !√§ rules

"Monophthongisation rule i√§:ee 1"
!! * __@RULENAME@__ \\  
i:e <=> _ √§: Cns:* i %^WG: %> i: ;   !! 1)
        _ √§: Cns:* Vow: Cns [%^√ÅI:|%^√ÅE:] %^WG: ([%^RLEN:|%^RVSH:]) [%>|.#.]  ; !RULE RV !! 2)vi√§sk√°r veeskir i√§bb√°r eebir 
         _  √§:e    Cns:+ u (%^CLEN:) %^VBACK: %^RLEN: [%>|.#.] ;  !RULE RV ! estuƒë

"i√§:e rule 1 and second syllable Fleeting vowel before suffix"        
!! * __@RULENAME@__ \\  Note: many rules in one
i:0 <=> \Vow: _ √§: Cns:+ i2:i %>    [i|i3:i|Cns]  ;  !RULE RV !! 1)  i:0 for iƒë C verbs 
        \Vow: _ √§: Cns:+ u2:u  %>   ;  !RULE RV !! 2)  i:0 for uƒë C verbs
          \Vow: _ √§: Cns:+ u     (%^CLEN:) %^VBACK: %^RVSH: [%>|.#.] ;  !RULE RV ! melluu
         Cns: _ i t: t: %^CLEN: (%^RLEN:) %^SLEN: %> m   ;  !RULE SV !! 4) cokkiitt%^CLEN%^RLEN%^SLEN%>m√¢n



"i√§:e and i√§:ee rule 2 and Diphthongisation i5√§ to ie"
!! * __@RULENAME@__ \\  
√§:e <=> i:  _ Cns:+ i %^WG:   %> i: ; !RULE RV 
        i:e _ Cns:+ i %^WG: %^RLEN:  %> i: ;  !RULE RV 
        i:e _ Cns:+ :i Cns: %^√ÅI: %^WG: %^RLEN: [%>|.#.] ;  !RULE RV  !vi√§sk√°r%^√ÅI%^WG%^RLEN
         i: _ Cns:+ u2:u %>  ;  !RULE RV 
        i5: _ Cns: Cns:+ √°: s [%^√ÅI:|%^√ÅE:] %^WG: [%^RVSH:|%^RLEN:] [%>|.#.] ;  !RULE RV  !vi√§kk4√°s : viehis
        [i5:|i:] _      Cns:+ u (%^CLEN:) %^VBACK: (%^RVSH:|%^RLEN:) [%>|.#.] ;  !RULE RV  ! estuƒë
        i5:   _      Cns:+ u2:u Triggers:* [%>|.#.] ;  !RULE RV  ! estuƒë ki√§5ptu2 > kieptuƒë

!!‚Ç¨ si√§mmu2>ƒë>
!!‚Ç¨ s0emmu>ƒë>




"Different rules for √§:a, first vowel (or only vowel)"
!! * __@RULENAME@__ \\  ƒç√§√§ci:ƒçassijn, l√§b≈æi:lab≈æijn
√§:a <=>      _ [√§:|%^RV:] Cns:+ [i|√¢|a|√°|u2:] (Cns:+)  ([%^WG: | %^CSH:]) (%^CLEN:) (%^CLEN:) %^VBACK: ([%^RVSH:|%^RLEN:])  (%^SLEN:) [%>|.#.]  ; !RULE RV 
             _ [√§:|%^RV:] Cns:+ i            %^WG:  ([%^RVSH:|%^RLEN:])             %> [i:|i3:] ;  !RULE RV 
             _ [√§:|%^RV:] Cns:+ i            %^WG:   [%^CLEN:|%^RLEN: ]  (%^SVLOW:) %> i: ;  !RULE RV !ƒç√§√§ci:ƒçassijn
             _     %^RV:  Cns:+ i            %^WG:            %^RLEN:               %> i: ; !RULE RV  !√§%^RVi4gi%^WG%^RLEN%>id
             _ √§:0        Cns:+ Vow:         %^WG:   %^CLEN:  %^RVSH:  %> i:  ;  !RULE RV !! ƒç√§√§ci : ƒçassijn
             _ √§:a        Cns:+ i2:i                                                %> [i|i3:i|Cns]  ;  !RULE RV  !cntrV
       \Vow: _            Cns:+ i2:i                                                %> [i|i3:i|Cns]  ; !RULE RV   !cntrV
             _ [√§:|%^RV:] Cns:+ a  (%^WG:|%^CSH:)          (%^RVSH:)              [%>|.#.]  ;  !RULE RV 
             _ √§:         Cns:+ i      (%^WG:|%^CSH:) %^VBACK: (%^RVSH:)  [%>|.#.]       ;  !RULE RV  ! OBS ^EA -> VHIGH?
       \Vow: _ √§:         Cns:+ Vow:   (%^WG:)         (%^CLEN:) %^VBACK: [%>|.#.] ; !RULE RV 

!!‚Ç¨ nj√§√§h^RCi^WG^VBACK>i3
!!‚Ç¨ njaav0i00>i



"√§√§ to aa, second vowel"
!! * __@RULENAME@__ \\  √°:o in pairs like ku√°tij -> kooƒëij, s√§√§mi -> saamij ƒç√§√§llu%^VBACK%>m
√§:a   <=> √§:  _ Cns:+ i               ( %^WG:)                  %> i: ; !RULE RV 
          √§:a _ Cns:+ i                %^WG: %^RLEN:          %> i: ; !RULE RV 
          √§:a _ Cns:+ :i   Cns: %^√ÅI: %^WG: %^RLEN: [%>|.#.] ;  !RULE RV  !vi√§sk√°r:veeskir  vi√§sk√°r%^√ÅI%^WG%^RLEN
          √§:  _ Cns:+ i2:i                                    %> [i|i3:i|Cns] Sgm:* %> ; !RULE RV 
          √§:  _ Cns:+ a             (%^WG:|%^CSH:)      [%>|.#.]    ;  !RULE RV 
          √§:  _ Cns:+ √¢    (%^WG:|%^CSH:) (%^VBACK:)    [%>|.#.]    ;  !RULE RV 
          √§:  _ Cns:+ [i|u] (%^WG:|%^CSH:) %^VBACK:        (%^RLEN:)  [%>|.#.]    ;  !RULE RV 
          √§:a _ Cns:+  [Vow: - u2:]     %^WG:          %^CLEN: %^VBACK: [%>|.#.] ; !RULE RV 
!         √§:a _ Cns:+   √¢       l   (%^CSH:)  %^RLEN:  (%^SLEN:)         %>   ;  !RULE RV  !cntrV trond
          √§:  _ Cns:+ √¢     [l|d|[s|t:][t4:|t:]]   ([%^WG:|%^CSH:|%^CLEN:]) %^VBACK: (%^RLEN:) (%^SLEN:) %> ;
          √§:a _            Cns:+ [i|i2:| u2:u]   ([%^WG: | %^CSH:]) (%^CLEN:) %^VBACK: (%^RLEN:)   [%>|.#.]       ; !RULE RV  !paaccim ƒçaallum



"√§√§ to √°√° and √°0 in Illative and Sg3"
!! * __@RULENAME@__ \\  ... for s√§√§mi > s√°√°m√°n
√§:√° <=>  \Vow      _   :√°   Cns:+ :√°  %^EA: ([%^CSH:| %^CLEN:])  (%^RLEN:) [%>|.#.]  ;  !RULE RV    !:√°  XXX
         \Vow   √§: _        Cns:+ :√°  %^EA: ([%^CSH:| %^CLEN:])  (%^RLEN:) [%>|.#.]  ; !RULE RV 
         \Vow      _   √§:  Cns:+ Vow:  %^EA:  ([%^CSH:| %^CLEN:])   %^RVSH:  [%>|.#.]  ;    !RULE RV  ! s√°m√°sis
      [.#.|Cns]    _  %^RV: Cns:+ :√°  %^EA: (%^CSH:) (%^RVSH:)   [%>|.#.]  ;  !RULE RV  ! nj√°lm√°sis
! del 2
        \Vow       _   :√°   Cns:+ :Vow     (%^CSH:|%^WG:) (%^CLEN:) %^VHIGH: (%^RLEN:) [%>|.#.]  ;  !RULE RV    !:√°  XXX
        \Vow    √§: _        Cns:+ [:Vow - u2: ]     (%^CSH:|%^WG:) (%^CLEN:) %^VHIGH: (%^RLEN:) [%>|.#.]  ; !RULE RV 
        \Vow       _   √§:0  Cns:+ :Vow (%^EA:) (%^CSH:|%^WG:) (%^CLEN:) %^VHIGH:  %^RVSH:  [%>|.#.]  ;  !RULE RV    ! ny: %%^VHIGH%^CLEN%^RVSH
      [.#.|Cns]    _  %^RV: Cns:+ :Vow      %^CSH:                  %^VHIGH:           [%>|.#.]  ;  !RULE RV  
      \Vow    _  %^RV: Cns:+ :Vow            (%^WG:) (%^CLEN:) %^VHIGH: ; ! %^RVSH:  [%>|.#.]  ;   !RULE RV 



!!‚Ç¨ s√§√§m^RCi^EA>n
!!‚Ç¨ s√°√°m0√°0>n

!!‚Ç¨ nj√§√§h^RV0^EA^CLEN^RVSH>on

"√§RV to √°√° in Illative and Sg3"
!! * __@RULENAME@__ \\  ... for t√§sni -> t√°√°sn√°n
%^RV:√° <=> √§: _  Cns:+ :√°  %^EA: ([%^CSH:| %^CLEN:]) %^RLEN: [%>|.#.]  ; !RULE RV 
           √§: _  Cns:+ :Vow (%^WG:) (%^CLEN:) %^VHIGH: %^RLEN: [%>|.#.] ;  !RULE RV  ! ƒçev≈æuin ƒçi√§v≈æui RLEN added
           √§: _  Cns:+ :Vow      (%^CSH:) %^VHIGH: %^RLEN:  [%>|.#.] ; !RULE RV 


!!‚Ç¨ t√§^RVsni^EA^CSH^RLEN>n
!!‚Ç¨ t√°√°sn√°000>n

"Vowel lengthening √§RV to aa "
!! * __@RULENAME@__ \\  √§igi aaigijd
%^RV:a <=> √§:a _  Cns:+ i: (%^WG:) (%^VBACK:) %^RLEN: %> [i:|j] ; !RULE RV 
           √§:a _  Cns:+ [√°|u]  %^WG:          %^RLEN: [%>|.#.] ; !RULE RV  ! exit VHIGH
           √§:a _  Cns:+ [√°|u|i|√¢|a] Cns:*  %^WG: %^VBACK: %^RLEN: (%^SLEN:) [%>|.#.] ; !RULE RV  !k√§%^RVlg√¢%^WG%^VBACK%^RLEN%>m
           √§:a _  Cns:+  √¢ Cns:+                         %^RLEN: ;  ! k√§%^RVvn√¢st^WG^VBACK^RLEN%>

!!‚Ç¨ s√§lkki2>ƒë>
!!‚Ç¨ salkki>ƒë>

!!‚Ç¨ s√§√§m^RCi^WG>in
!!‚Ç¨ saam0i0>jn

!!‚Ç¨ s√§√§m^RCi^WG>id
!!‚Ç¨ saam0i0>jd

! ri√§gg√°s%^√ÅI%^WG%^RLEN>  
! ƒçu√°gg√°s%^√ÅI%^WG%^RLEN> 

!!‚Ç¨ st√§√§vi2>ƒë>
!!‚Ç¨ staavi>ƒë>

!!‚Ç¨ st√§√§vi2>j>
!!‚Ç¨ staavi>j>

!!‚Ç¨ st√§√§vi2>i2>
!!‚Ç¨ staavi>i>

!!‚Ç¨ st√§√§vi2>jeh>
!!‚Ç¨ st√§√§ve>jeh>

!!‚Ç¨ k√§%^RVvn√¢^WG>st^RLEN>
!!‚Ç¨ kaavn√¢0>st0>



!! √°

"Root vowel change for √° to √§ with i in second syllable"
!! * __@RULENAME@__ \\  p√§√§vir , j√°%^RVhh√°%^SVl%^√ÅI%^WG%^RLEN>
√°:√§ <=>    _ [ √°:|%^RV:] Cns:* :i (%^SV:) Cns: %^√ÅI: %^WG: (%^CLEN:) [%^RLEN:|%^RVSH:] [%>|.#.] ;  !RULE RV !! 1) p√°√°pp√°r:p√§√§vir
        √°: _   Cns:* :i (%^SV:) [l|m|s|n:|r] %^√ÅI: %^WG: (%^CLEN:) %^RLEN: [%>|.#.] ;  !RULE RV  !! 2) p√°√°pp√°r:p√§√§vir

!rejected, look at this:

!!‚Ç¨ p√°√°pp4√°r^√ÅI^WG>
!!‚Ç¨ p√§√§0vir00>



"Root and stem vowel √°:a for verbs"
!! * __@RULENAME@__ 
√°:a <=> [Cns|.#.|#] _       Cns:+ u2:u  %>   ;  !RULE RV ! conflict when \Vow:
        [Cns|.#.]   _       Cns:+ i2:i %>    [i|i3:i|Cns]  ; !RULE RV 
        \Vow:   _ [%^RV: | √°: ] Cns:+ [u:|u2:] ([%^WG: | %^CSH:]) (%^CLEN:) %^VBACK: (%^RLEN:|%^RVSH:)  [%>|.#.]   ; !RULE RV  ! 2st verb p√°kkuƒë: pakkum/h/u
        \Vow:   √°: _  Cns:+ [u:|u2:] ([%^WG: | %^CSH:]) (%^CLEN:) %^VBACK: (%^RLEN:) [%>|.#.]   ;  !RULE RV ! n√°√°ƒëƒëu%^VBACK%^RLEN%>m naaƒëƒëu>m
        \Vow: a (%^RV:) Cns:+ _  ([%^CSH:|%^WG:])  (%^CLEN:) (%^RLEN:) [%>|.#.] ;  !RULE SV  !trying this as a phonological rule for -eƒë verbs
        \Vow: a (%^RV:) Cns:+ :a  ([%^CSH:|%^WG:]) (%^CLEN:) (%^RLEN:) %> _ \Vow: ;  !RULE SUFF  !to prevent conflict

!√°%^RVrgu2

!!‚Ç¨ p√°^RVkk4u2>m
!!‚Ç¨ pa0kku>m


!! !√° and u√° rules

"Monophthongisation rule u√°:o0 part 2: Vowel shortening for u√°" 
!! * __@RULENAME@__ \\  for u√°bbi -> obbijn  ƒçu√°l%^RCi%^WG%>in tu√°i4j√¢l%^CSH%^VBACK%^RVSH%>√¢m
√°:0 <=> u: _ Cns:+ i %^WG: %^CLEN: %^SVLOW: %> i:  ;  !RULE RV 
        u: _ Cns:+ i %^WG: %^RVSH: %> i:  ;  !RULE RV 
        u: _ Cns:+ [i|u|√¢|u2:] (Cns:+) ([%^WG:|%^CSH:|%^CLEN:]) %^VBACK: %^RVSH: [%>|.#.]   ;  !RULE RV  !sop√¢vetteƒë toj√¢d√¢m
        u: _ Cns:+ u %^VBACK: %^RVSH: [%>|.#.]  ; !RULE RV  ! u√°inu
        u: _ Cns:+ i2:i %> [i|i3:i|Cns]  ;  !RULE RV 
        u: _ Cns:+ u2:u ([%^WG: | %^CSH:]) (%^CLEN:) (%^VBACK:)  %>   ; !RULE RV  !*


"Monophthongisation rule u:o and root vowel change u:o, special rule for olmoo≈° and contract verbs"
!! * __@RULENAME@__ \\ 
u:o <=>  [.#.|#] _     Cns:+ u:o (%^SV:) [≈°|≈æ]  %^WG: (%^SLEN:)  %^SVLOW: [%>|.#.]  ;  !RULE RV !! ulmu : olmoo≈°, olmo≈æin, carefully with only initial vowel
                 _ √°:  Cns:+ u2:u             (%^CLEN:)  (%^RLEN:)          %>  ; !RULE RV 
                 _ √°:  Cns:+ i2:i                              %>    [i|i3:i|Cns]      ;  !RULE RV 
     [Cns|.#.|#] _ √°:  Cns:+ [i|√¢|a|√°|u]   (Cns:+)     (%^WG:|%^CSH:) (%^CLEN:)     %^VBACK: (%^RLEN:) (%^SLEN:) [%>|.#.]  ;  !RULE RV   ! ooƒëƒëim            
                 _ √°:0 Cns:+ [i|u|√¢] (Cns:+) ([%^WG:|%^CSH:|%^CLEN:]) %^VBACK: %^RVSH: [%>|.#.]   ; !RULE RV  !sop√¢vetteƒë toj√¢d√¢m
                 _ √°:0 Cns:* i %^WG:          %^CLEN: %^SVLOW: %> i:  ; !RULE RV 
    		     _ √°:o Cns:+ Vow: j %^√ÅI: %^WG: ; !RULE RV  !pu√°hh√°√° : poovij ! XXX
                 _ √°:0 Cns:+ i %^WG:                   %^RVSH: %> i: ; !RULE RV 
                 _ √°:  Cns:* i %^WG:                 (%^RLEN:) %> i: ;  !RULE RV 
                 _ √°:  Cns:* u2:u   ([%^WG: | %^CSH:]) (%^CLEN:) %^VBACK:          (%^RLEN:) [%>|.#.]   ;  !RULE RV 
             Cns _     Cns:+ e: %^VBACK: [%>|.#.] ;  !RULE RV !

!!‚Ç¨ u√°inu2>ƒë
!!‚Ç¨ o0inu>ƒë


"Diphthong rule u√°:uo and u√°:oo, part 2"
!! * __@RULENAME@__ \\  
√°:o <=> u5:  _ Cns:+ u2:u   ([%^WG: | %^CSH:])  (%^CLEN:) (%^VBACK:)    %>  ;  !RULE RV !u√° to uo
        u5:  _ Cns:+ u  %^VBACK:  (%^RLEN:) [%>|.#.]  ;  !RULE RV !u√° to uo
        u5:  _ Cns:* [u2:|u]  ([%^WG: | %^CSH:]) (%^CLEN:) %^VBACK: (%^RVSH:)    %> [m|h|u]   ;  !RULE RV ! should not hit pu5√°htu2%>o but ƒçu5√°rvu2 OBS
        u:   _ Cns:+ i                           %^WG:   %> i: ;  !RULE RV  ! 
        u:   _ Cns:+ [i|√¢|a|√°|u]  (Cns:+)        (%^WG:|%^CSH:)(%^CLEN:)  %^VBACK: (%^RLEN:) (%^SLEN:) [%>|.#.]  ;  !RULE RV  ! ooƒëƒëim  
        u:   _ Cns:+ Vow:      j     %^√ÅI:           %^WG: [%>|.#.] ;  !RULE RV !pu√°hh√°√° poovij 
        u    _ Cns:+ Vow:+ (Cns:) ([%^√ÅI:|%^√ÅE:]) %^FCD: √¢ [%>|.#.]  ;  !RULE RV !stu√°rr√°:stuorr√¢
        u:o  _ Cns:+ i                          %^WG: %^RLEN:  %> i: ; !RULE RV 
        u:o  _ Cns:+ :i    Cns:          %^√ÅI: %^WG: %^RLEN: [%>|.#.] ;  !RULE RV !vi√§sk√°r:veeskir  vi√§sk√°r%^√ÅI%^WG%^RLEN
        u:o  _ Cns:* u2:u   ([%^WG: | %^CSH:]) (%^CLEN:) (%^VBACK:)   %^RLEN: [%>|.#.]   ;  !RULE RV lagt til clen vback og o tt



"Diphthongisation e to i√§, part 1"
!! * __@RULENAME@__ \\   
e:i <=>  \Vow _  Vow: Cns:+ [i2:|e:|i:|i3:] %^EA:  ([%^CSH:| %^CLEN:]) (%^RLEN:)   [%>|.#.] ;  !RULE RV !! 1) P1: e > i; ergi:i√§rg√°n 1, veerdi:vi√§rd√°n
         \Vow _ %^RV: Cns:+ [u|√°|o|√¢2:] ([%^WG:|%^CSH:]) (%^CLEN:) %^VHIGH: (%^RLEN:) [%>|.#.] ;   !RULE RV 
         \Vow _ %^RV: Cns:+ u i4: %^WG:  [%>|.#.] ;  !RULE RV  ! ƒçev≈æuin ƒçi√§v≈æui ƒçe%^RVv≈æui4%^WG>
         \Vow _ e:    Cns:+ [√° | u | o | √¢2: ] Triggers:*  %> [s | o| ƒç |p |m | √° |h] ;  !RULE RV  ! pi√§vu>s pi√§hho>on pi√§v√°ƒçƒçii  - not meendu
         \Vow _ e:    Cns:+ √°   %^EA: ([%^CSH:| %^CLEN:])  [%>|.#.] \[Cns | Vow]  ;  !RULE RV  ! veerr√°%^EA%^CSH>
         \Vow _ e:    Cns:+ √°  (%^CSH:) %^VHIGH:   [%>|.#.] \[Cns | Vow]  ;  !RULE RV  ! peeh%^RC√°%^VHIGH> peeh%^RC 
         \Vow _ [%^RV:|e:]    Cns:+ u2:o Triggers:*  %> ; !RULE RV


"Root vowel aa:√°√° in C-stems with stemvowel u, part 1"
!! * __@RULENAME@__ \\  ahhu:√°hu
a:√° <=> [Cns|.#.|#] _ %^RV: Cns: Cns: u [≈°|≈æ] %^CSH: (%^RLEN:) [%>|.#.]  ; !RULE RV 


"Root vowel aa:√°√° in C-stems with stemvowel u, part 2"
!! * __@RULENAME@__ \\  ahhu:√°√°hu
%^RV:√° <=> [Cns|.#.|#] a:√° _ Cns: Cns: u [≈°|≈æ] %^CSH: %^RLEN: [%>|.#.]   ;  !RULE RV !! 1)
                \Vow: o: _ Cns:+ u [i4: | j | ≈° | ≈æ]  %^WG: (%^RVSH:) [%>|.#.] ;  !RULE RV  !! 2) poccu pu√°sui





"ie to i√§ in Illative and Sg3" 
!! * __@RULENAME@__ \\  kiem¬¥ni -> ki√§mn√°n, veerdi : vi√§rd√°n peerru2%^CLEN%^VHIGH%^RVSH%>o
e:√§ <=> [i | e: ] _  Cns:+ Vow:  %^EA: Triggers:* [%>|.#.] ;  !RULE RV 
        [i | e: ] _  Cns:+ Vow:  Triggers:* %^VHIGH:  [%>|.#.] ;  !RULE RV 
        :i        _  Cns:+ [u|√°|o|√¢2:] (%^WG:) (%^CLEN:) %^VHIGH: ([%^RLEN:|%^RVSH:]) [%>|.#.] ;  !RULE RV  ! ƒçev≈æuin ƒçi√§v≈æui
        :i        _  Cns:+ u2:o Triggers:* [%>|.#.] ;  !RULE RV  ! pi√§hhoo
         \Vow :i  _   Cns:+ √°  (%^CSH:) %^VHIGH:   [%>|.#.] \[Cns | Vow]  ;  !RULE RV  ! eell√°%^CSH%^VHIGH
         \Vow :i  _  Cns:+    √¢2:   %^VHIGH:  %> m ;   !RULE RV   !PrfPrc Err/Orth




"Diphthongisation uo:u√°"
!! * __@RULENAME@__ \\   
o:√° <=> u _ Cns:+ u: [i4: | j | ≈° | ≈æ] %^WG: ([%^RVSH:|%^RLEN:]) [%>|.#.] ;  !RULE RV !! 1) cuobbuu cu√°bui, kuoƒëhu po^RVrru^EWG>i
 		u _ Cns:+ √°       %^CSH: ; !RULE RV 
        u _ Cns:+ √¢ %^EA: %^CSH: ; !RULE RV 
        u _ Cns:+ [o|u|√°|i|u2:] %^VHIGH: (%^RVSH:) [%>|.#.] ; !RULE RV  !! 4) vuoll- > vu√°llus ru√°tt√°


"ye to u√° and uo, part 1" 
!! * __@RULENAME@__ \\  Part 1 of ye to u√° for kyeli : ku√°l√°n  
y:u <=> _ e:  Cns:+ Vow:  %^EA: ([%^CSH:| %^CLEN:]) (%^RLEN:)   [%>|.#.] ;   !RULE RV 
        _ :o  Cns:+ :i (%^WG:) (%^CLEN:) (%^VBACK:|%^VHIGH:) ([%^RVSH: |%^RLEN:] ) %> [i:|j |i3: | ≈° | ≈æ ] ; !RULE RV  !syej%^RCi%^RVSH%^CLEN%>in
        _ :o  Cns:+ :i  (%^WG:) (%^CLEN:) %^VBACK: %^RLEN: %> [m | h] ; !RULE RV  
        _ :o  Cns:+ [ √¢:|√° |u2:] (Cns:+) ([%^WG: | %^CSH:])  (%^CLEN:) %^VBACK: %^RLEN: [%>|.#.] ;  !RULE RV ! [i | Cns] ; ! ryett√¢^WG^VBACK^RLEN>m
        _ :o  Cns:+ :i %^WG: %^CLEN: %^SVLOW: %> i: ; !RULE RV 
        _ :o  Cns:  Cns: :i  %^WG: %^CLEN:  %^RVSH: %> [i:|i3:] ; !RULE RV 
        _ :o  Cns:+ i2:i %> [i|i3:i|Cns]  ;  !RULE RV 
        _ e:  Cns:+ [:Vow | i6:] (%^CSH:|%^WG:) (%^CLEN:) %^VHIGH: ([%^RVSH: |%^RLEN:] ) [%>|.#.] ;  !RULE RV 


"ye to u√°, part 2"
!! * __@RULENAME@__ \\  Part 2 of ye to u√° for kyeli : ku√°l√°n
e:√° <=> y: _  Cns:+ [Vow: - u2:]  %^EA: (%^CSH:|%^CLEN:|%^WG:) (%^RLEN:)  [%>|.#.] \[i:|j |i3:| ≈° | ≈æ ]  ; !RULE RV 
        y: _  Cns:+ [u:|√°|o|√¢2:] (%^CSH:|%^WG:) (%^CLEN:) (%^VHIGH:) (%^RLEN:) [%>|.#.] \[l|i] ;  !RULE RV  ! not ryet√°im:
        y: _  Cns:+ i6: %^VHIGH: [%>|.#.] \[l|i] ;  !RULE RV  ! not ryet√°im:
        y: _  Cns:+ u2:o Triggers:* [%>|.#.]  ;  !RULE RV  ! not ryet√°im:

!!‚Ç¨ kyel^RCi3^EA^CLEN>m
!!‚Ç¨ ku√°ll√°00>m

"ye to uo, part 2 "
!! * __@RULENAME@__ \\  Part 2 of ye to u√° for kyeli : kuolijn
e:o <=> y: _  Cns:+ :i (%^WG:) (%^CLEN:) (%^VBACK:) ([%^RVSH: |%^RLEN:] )  %> [i:|j |i3:| ≈° | ≈æ ] ; !RULE RV 
        y: _  Cns:+  :i  (%^WG:) (%^CLEN:) %^VBACK: %^RLEN:   %> [m | h] ;  !RULE RV 
        y: _  Cns:+ [ √¢:|√° ] (Cns:+)  ([%^WG: | %^CSH:]) (%^CLEN:) %^VBACK: %^RLEN:   [%>|.#.] ; ![i | Cns ] ; !RULE RV  !ryett√¢^WG^VBACK^RLEN>m
        y: _  Cns:+ :i %^WG: %^CLEN: %^SVLOW: %> i: ; !RULE RV 
        y: _  Cns: Cns: :i  %^WG: %^CLEN: %^RVSH: %>  [i:|i3:]  ; !RULE RV 
        y: _  Cns:+ i2:i %> [i|i3:i|Cns]  ;  !RULE RV 
        y: _  Cns:+  u2:u  Triggers:*   %>  ;  !RULE RV 

"u√° to ye and uo to ye, part 1" 
!! * __@RULENAME@__ \\  Part 1 lu√°mm√°n : lyeme ƒçu√°gg√°s%^√ÅI%^WG%^RLEN%>
u:y <=> _ √°: Cns:+ √°: (%^SV:) [l|m:|s|n:|r] [%^√ÅI:|%^√ÅE:] (%^FCD:) %^WG: [%^RLEN:|%^RVSH:] ([%^SVSH:|%^SLEN:]) [%>|.#.] ; !RULE RV 
        _ o: Cns:+ [e ([%^WG:|%^CLEN:]) %> e|i %^CSH: %> h] ; !RULE RV 

!!‚Ç¨ vuolle^CLEN>en
!!‚Ç¨ vyelle0>en

"u√° to ye, part 2"
!! * __@RULENAME@__ \\  Part 2 lu√°mm√°n : lyeme
√°:e <=> :y _ Cns:+ √°: (%^SV:) [l|m:|s|n:|r] [%^√ÅI:|%^√ÅE:] (%^FCD:) %^WG:  [%^RLEN:|%^RVSH:] ([%^SVSH:|%^SLEN:]) [%>|.#.] ; !RULE RV  !cu√°gg√°s : cyegis ƒçu√°gg√°s%^√ÅI%^WG%^RLEN

"uo to ye, part 2"
o:e <=> u:y _ Cns:+ [e ([%^WG:|%^CLEN:]) %> e|i %^CSH: %> h] ; !RULE RV 
!vyelleen, vyeleem,vyelih



!! !Second syllable rules

"Second syllable vowel change √° to e or i in 3syll stems"
!! * __@RULENAME@__ \\ putt√°s- > putes, triggers are ^√ÅE and ^√ÅI j√°%^RVhh√°%^SVl%^√ÅI%^WG%^RLEN%> pu5√°rr√°s%^√ÅI%^WG%>umos
Vx:Vy <=> Cns: _ (%^SV:) (Cns:)  Vz: (%^FCD:) %^WG: (%^CLEN:) ([%^RVSH:|%^RLEN:]) ([%^SVSH:|%^SLEN:])  [.#.| %> [# |üò±  |i n | i d| n |d ]]   ;  !RULE SV  !no change when the word is added a new foot  \Syll 
          Cns: Vx: _ s  Vz: %^WG: ; !lohhes
       where Vx in ( √°    √°    a   )
             Vy in ( e    i    e   )
             Vz in ( %^√ÅE %^√ÅI %^√ÅE) 
       matched ;

!!‚Ç¨ putt√°s^√ÅE^WG>
!!‚Ç¨ pu0tes00>

"Stemvowel shortening  √°√° and aa to e or i in 3syll"
!! * __@RULENAME@__ \\ putt√°s- > putes, triggers are ^√ÅE and ^√ÅI
Vx:0 <=> Cns: _ Vx: (Cns:) Vz: %^WG: ;  !RULE SV  !!!!!%^CSH:
       where Vx in ( √°    √°    a   )
             Vz in ( %^√ÅE %^√ÅI %^√ÅE) 
       matched ;

!rejected
!!‚Ç¨ nahhaar^√ÅE^WG>
!!‚Ç¨ na0v0er00>



 
"Stem vowel e to a for a root vowel."
!! * __@RULENAME@__ \\  alge to aalgan
e:a <=> a (%^RV:) Cns:+ _  (%^SV:) %^EA: ([%^CSH:| %^CLEN:]) (%^RLEN:) (%^SLEN:) [%>|.#.] ; !RULE SV 

!!‚Ç¨ a^RVlge^EA^CSH^RLEN>n
!!‚Ç¨ aalga000>n

!!‚Ç¨ va^RVh^RCe^SV^EA^CLEN^SLEN>m
!!‚Ç¨ va0hhaa000>m

"Stem vowel e to √° for other root vowel"
!! * __@RULENAME@__ \\  if e stem
e:√° <=> [o|i|u] (%^RV:) Cns:+ _ (%^SV:) %^EA: ([%^CSH:| %^CLEN: |%^WG:]) ([%^RLEN: |%^RVSH:]) (%^SLEN:)  [%>|.#.] ;  !RULE SV !njuun√°√°d

"Stem vowel i to √° for other root vowel."
!! * __@RULENAME@__ \\  e.g. s√§√§mi > Ill s√°√°m√°n 
Vx:√° <=> [:e|:i|:√§|:√°|√§:0] (%^RV:)  Cns:+ _   %^EA: ([%^CSH:| %^CLEN:]) ([%^RLEN: |%^RVSH:])  [%>|.#.] [i: | n | d | m | s |≈° ] ;  !RULE SV !u√°bb√°m i√§nn√°sis
    where Vx in ( i i2 i3 ) ;

"Stem vowel i to e in front of j"
!! * __@RULENAME@__ \\  
Vx:e <=> Cns _ %> [j [Vow - √¢]|e] ; !RULE SV  !not hit karri>j√¢n ! 
	where Vx in (i i2) ;


! kalg√° -> kalga

"Stem vowel √¢ to a in Pl Nom -h"
!! * __@RULENAME@__ \\  silb√¢ to siilbah  %^WG%^RLEN%>hlove%>h
√¢:a <=> :Cns (%^RC:) _ Triggers:* %> h [l | .#.|%>] ; !RULE SV 


!!‚Ç¨ so^RVll√¢^WG^RLEN>h
!!‚Ç¨ soo0la00>h

"Stem vowel √¢ > √° change before i"
!! * __@RULENAME@__ \\ soll√¢ to sool√°id 
√¢:√° <=> _ (%^SV:) (%^WG:) (%^RLEN:|%^CLEN:|%^SLEN:) %^SVLOW: %> [:i|:j] ;  !RULE SV 


!!‚Ç¨ so^RVll√¢^WG^RLEN^SVLOW>in 
!!‚Ç¨ soo0l√°000>in

"Stem vowel √¢ > e change before e"
√¢:e <=> _ (%^WG:) (%^RLEN:|%^CLEN:|%^SLEN:) %^SVLOW: %> e ;  !RULE SV 

"Stem vowel change e:i before suffix"
!! * __@RULENAME@__ \\ 
e:i <=> Cns: _  [Triggers: - %^EA:]*  %> i: ;  !RULE SV 
        Cns: _  %> √§ p ; !RULE SV  !! m√°nuttep : m√°nutti√§p√°n
        
!"Stem vowel i3:e"
! * __@RULENAME@__ \\ 
!i3:e <=> _   %^WG: %^RVSH:  .#.   ; !RULE SV  !kyele
!i3:e <=>  _  (%^SV:) [Triggers: - %^EA:]+  %>  [t |h|s|e]  ; !RULE SV  !hyeneest

"Stem vowel i3:0"
!! * __@RULENAME@__ \\ 
i3:0 <=> _  e Triggers:*  [.#.|#|%>] ; !RULE SV  !kyele temp l√∏sning



"Stem vowel u:o change before suffix"
!! * __@RULENAME@__ \\  
u:o <=> Cns: _  (%^SV:) ([≈°|≈æ]) (%^WG:) (%^CLEN:) (%^RLEN:|%^RVSH:) (%^SLEN:) %^SVLOW:  ;  !RULE SV !! 1) olmoo≈°, olmo≈æin
 		Cns  _  %> [ j Vow | o ] ; !! 2) 


"Stem vowel u2:o change before suffix"
!! * __@RULENAME@__ \\  
u2:o <=> Cns:  _ Triggers:* %> [ j Vow | o ] ;  !RULE SV  ! SVLOW together with u?
 		
!!‚Ç¨ u√°inu>o
!!‚Ç¨ u√°ino>o


 		
! st√°√°lu st√°√°loust

"Second vowel shortening"
!! * __@RULENAME@__ \\ eemeed : emed√¢n, savvoon : savo 
Vx:0 <=> _ Vx: (Cns:+) (%^FCD:) (%^CSH:|%^WG:) ([%^RLEN:|%^RVSH:]) %^SVSH:  ;  !RULE SV !eemeed : emed√¢n sai4joos
    where Vx in ( e o a √° u i y √§ ) ;

!eemeed:emed√¢n

!!‚Ç¨ eemeed^CSH^SVSH>√¢n
!!‚Ç¨ 0em0ed00>√¢n


!!‚Ç¨ kappeer^CSH^SVSH>√¢n
!!‚Ç¨ ka0p0er00>√¢n

"Stem vowel:0 in front of √¢"
!! * __@RULENAME@__ \\ aiguu:aaig√¢
Vx:0 <=> Cns: _  ([%^√ÅE:|%^√ÅI:]) √¢ Cns:* Triggers:*  [%>|.#.]   ;  !RULE SV 
         \%> _ (%^SV:) Cns: ([%^√ÅE:|%^√ÅI:]) %^FCD: √¢ Cns:* Triggers:*  [%>|.#.]   ;  !RULE SV 
    where Vx in ( √¢ e o a √° u i y √§) ;

! this rule should be generalised, we talk about a ban on o>u strings

"Stem √¢ deleting"
!! * __@RULENAME@__ \\ kand√¢:kandii
√¢:0 <=> \%> _ i i (%^WG:) (%^RVSH:) [%>|.#.]  ; !RULE SV 
         \%> _ i  Triggers:* %>  i: ; !RULE SV 

!! !Suffix vowel rules

"Suffix vowel lengthening"
!! * __@RULENAME@__ \\ long √° in kiss√°√°st, nuor√¢ : nuor√°√°in, hyeni:hyeneest
%^SV:Vx <=> :Vx  _ (Cns)  NotSuffixBorder:* %^SLEN:   ;  !RULE SV ! hyeneest
       where Vx in ( √° o u e a i   y √§  ) ; !nuor√¢ : nuor√°√°in



!!‚Ç¨ ki^RVss√°^SV^WG^CLEN^SLEN>st
!!‚Ç¨ ki0ss√°√°000>st

!!‚Ç¨ mi^RVllo^SV^WG^CLEN^SLEN>st
!!‚Ç¨ mi0lloo000>st

!!‚Ç¨ huksi^SVm^SLEN>ist
!!‚Ç¨ huksiim0>ist


!!‚Ç¨ t√°√°l^RCu^SV^WG^RVSH^SLEN^SVLOW>st
!!‚Ç¨ t√°√°l0oo0000>st

"Suffix i5:j for lexicon APINA"
!! * __@RULENAME@__ \\ salijn
i5:j  <=> Cns i %> _ ; !RULE SV 


"Suffix i5:0 and i5√§:e"
!! * __@RULENAME@__ \\ timotein oboen, not Chariliein
i5:0 <=> o  e %> _ ;  !RULE SUFF
		 Vow i  %> _ ;!RULE SUFF
!         Cns _ √§:e Cns: Cns:+ Vow: s %^√ÅE: %^WG: %^RVSH: [%>|.#.]  ; !pi5√§ll√°s%^√ÅE


"Suffix i:j for i and e-stems"
!! * __@RULENAME@__ \\  
i:j <=> [i: | e: | i2: | i3: ] Triggers:*   %> _ [l|d|n|m|g|t|%>|.#.];  !RULE SUFF


"Vowel copy in suffix"
%^V:Vx <=> :Vx Triggers:* %> _ ; !RULE SUFF
!! * __@RULENAME@__ \\  for imperative Sg3, at least
	where Vx in ( a √° i u o ) ;


!! Suffix consonant rules


"Adjective illative in -v√¢n after u"
j:v <=> u u:  %> _ √¢ n ; !RULE SUFF

"Shortening u for v√¢n illative"
u:0 <=> u _  %> :v ; !RULE SV

!! !!Other rules



"Potential vowel shortening for contract verbs"
!! * __@RULENAME@__ \\  -√°√°≈æep becomes -√°≈° etc.
Vx:0 <=> Vx _ %> ≈° [.#.|v|j] ; !RULE SV
	where Vx in (a √° i o u ) ;



"Insertion of pleonastic hyphen"
%#:%- <=> Vx _ :Vx ;!RULE HYPH
    where Vx in (a e i o u √§ √°) ;


