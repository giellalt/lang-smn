! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

!! !!!Alphabet 

Alphabet   

!! !!The Inari Saami letters

 a á â ä b c d e f g h i j k l m n o õ p q !!= * @CODE@
 r s t u v w x y z æ ä ø ö å			   !!= * @CODE@
 č đ ŋ ŧ š ž				 			   !!= * @CODE@

 A Á Â Å B C D E F G H I J K L M N O Õ P Q !!= * @CODE@
 R S T U V W X Y Z Æ Ä Ø Ö Å 			   !!= * @CODE@
 Č Đ Ŋ Ŧ Š Ž  							   !!= * @CODE@
 ˊ  %- 							   !!= * @CODE@
 ':0   !!= @CODE@ this to zero

!! !Accented letters, just in case

 É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Ê Ô Û Î Ã Ý þ Ð   !!= * @CODE@
 é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý þ ð   !!= * @CODE@
 ß ª ß ç								   !!= * @CODE@
 
!! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 »7     !! * »
 «7     !! * «
 %[%>%] !! * >
 %[%<%] !! * <K4



!! !Different archiphonemes

p4:p                                  !!= * @CODE@ pääpp4ir these are consonants that do not change in cg
k4:k č4:č r4:r l4:l h4:h              !!= * @CODE@  these are consonants that do not change in cg
i4:i                                  !!= * @CODE@  this is the underlying i consonant
m7:ṃ n7:ṇ ŋ7:ŋ̣ s7:ṣ r7:ṛ đ7:đ̣ l7:ḷ    !!= * @CODE@  with dots
j7:j̣ h7:ḥ š7:ṣ̌  v7:ṿ                  !!= * @CODE@  with dots
%^A:á 	           		              !!= * @CODE@ loddán but algan, default is á.


!! !More abstract archiphonemes

%^RC:0		!!= * @CODE@ = root consonant
%^RV:0	 	!!= * @CODE@ = root vowel
%^SC:0      !!= * @CODE@ = suffix consonant
%^SV:0 		!!= * @CODE@ = stem vowel

!! !Triggers

%^CLEN:0	!!= * @CODE@ = cons lengthening
%^CSH:0     !!= * @CODE@ = cons shortening
%^EE:0      !!= * @CODE@ = Final vowel deletion in ee stems
%^FCD:0     !!= * @CODE@ = Final consonant deletion
%^IA:0      !!= * @CODE@ = i to á (more)
%^ILL:0 	!!= * @CODE@ = e to a/á in ill etc
%^LLEN:0    !!= * @CODE@ = low vow lengthening
%^MONO:0    !!= * @CODE@  monophtongization of diphthongs
%^ODDC:0	!!= * @CODE@ = shorten cns in odd-syll stems before C, #
%^ODDSHV:0	!!= * @CODE@ = Cns shortening in C|# and shortvow in Inf etc
%^RLEN:0    !!= * @CODE@ = root lengthening
%^RVL:0     !!= * @CODE@ = root vow length
%^RVSH:0	!!= * @CODE@ = root vow shortening
%^SLEN:0    !!= * @CODE@ = stem vowel lengthening
%^SFV:0   	!!= * @CODE@ = Deleting fleeting vowel before suffix
%^SVF:0	    !!= * @CODE@ = stem vowel lowering â>á and u>o
%^SVLOW:0	!!= * @CODE@ = stem vowel lowering â>á and u>o
%^UU:0      !!= * @CODE@ = Final vowel deletion in uu stems
%^VLOW:0   	!!= * @CODE@ = Vowel lowering in 3rd sg of contract verbs tuhhid:tohhe
%^RVSH:0     !!= * @CODE@ = vow shortening
%^WG:0 		!!= * @CODE@ = weak grade trigger
%^ÁE:0      !!= * @CODE@ = á to e (more)
%^ÁI:0      !!= * @CODE@ = á to i (more)

!! !!Morpheme boundaries:

!! Here we define the suffix border >
!! The other borders are not used yet, but still defined:

 «  !!≈ ;@CODE@: Derivational prefix
 »  !!≈ ;@CODE@: Derivational suffix
 %< !!≈ ;@CODE@: Inflectional prefx
 %> !!≈ ;@CODE@: Inflectional suffix
 %^ !!≈ ;@CODE@: (exceptional) soft hyphenation point
 #  !! and the symbol #, which is Word boundary for both lexicalised and dynamic compounds

!! End of alphabet definitions

;

!! !!! Sets

Sets
 Vow = a á â ä e i o u y æ ø ö å			   !!= * @CODE@ 
       é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý 	   !!= @CODE@ 
       A Á Â Ä E I O U Y Æ Ø Ö Å			   !!= @CODE@ 
       É Ó Ú Í À È Ò Ú Ì Ë Ü Ï Ê Ô Û Î Ã Ý 	   !!= @CODE@ 
       ¥ %^RV %^SV ;									   !!= @CODE@ 
 Cns = b c č d đ f g h j k l m n ŋ 			   !!= * @CODE@ 
       p q r s š t ŧ v w x z ž				   !!= @CODE@ 
       B C Č D Đ F G H J K L M N Ŋ			   !!= @CODE@ 
       P Q R S Š T Ŧ V W X Z Ž 				   !!= @CODE@ 
       ht g2 k2 m2 n2 							   !!= @CODE@ 
       b3 d3 g3 m3 h3 						   !!= @CODE@ 
       p4 k4 č4 r4 l4 h4
       m7 n7 ŋ7 v7 s7 r7 đ7	l7 j7 h7 š7		   !!= @CODE@ 
       ṃ  ṇ  ŋ̣  ṿ  ṣ  ṛ  đ̣  ḷ  j̣  ḥ  ṣ̌         !!= * @CODE@
       %^RC %^SC ;
 Geminate = b d g p t k c č f z ž l m n ŋ r s š v ;
 Seg = Vow Cns ;							   !!= * @CODE@ 
 WeG = %^WG1 %^WGSH %^CPL %^WG ;		  
 StemCns = b b3 d d3 g g2 g3 h h2 h3 		   !!= * @CODE@ 
           j l m m2 m3 m8 n n8 n2 ŋ r s  ;     !!= * @CODE@ 

Arch = %^RC %^RV %^SV %^SC  ;                !!= * @CODE@ 
Len = %^RLEN %^LLEN %^SLEN %^CLEN ;    !!= * @CODE@ 


Dummy = %^CLEN %^CSH %^EE %^FCD %^IA %^ILL %^LLEN %^MONO %^ODDC %^ODDSHV 
        %^RLEN %^RVL %^RVSH %^SFV %^SLEN                     !!= * @CODE@ 
        %^SVF %^SVLOW %^UU %^VLOW %^RVSH %^WG %^ÁE %^ÁI ;  !!= * @CODE@ 

           
!! !!!Rule section

Rules

!! !!Consonant rules

!! !Consonant gradation

"X:x halflong to short gradation"
!! => __@RULENAME@__  
Cx:Cy <=> Vow: _ (%^RC:) Vow (%^SV:) (Cns) (Dummy:*) %^WG:0 ;
	where Cx in (m7 n7 ŋ7 r7 s7 š7 j7 v7 đ7 l7 )
          Cy in (m  n  ŋ  r  s  š  j  v  đ  l  )
		  matched;
!€ mool7^RCâ^WG^SVLOW
!€ moˊl0â00

"x:y gradation" 
!! => __@RULENAME@__  Note that t:đ is in a separate rule
Cx:Cy <=> Vow: _ Vow (%^SV:) (Cns) (Dummy:*) %^WG:0 ;
          Vow: _ k4:h Vow (Cns) (Dummy:*) %^WG:0 ;
	where Cx in (c č h p4 )
              Cy in (s j̣ ṿ ṿ  )
		  matched;

!€ käähir^WG
!€ kääṿir0

!€ käähir
!€ käähir

"t:đ gradation" 
!! => __@RULENAME@__  
t:đ <=> Vow: _ (k4:) Vow (%^SV:) (Cns) (Dummy:*) %^WG:0 ;

"tk:đh gradation"
!! => __@RULENAME@__  
k4:h <=> Vow: t: _ Vow (%^SV:) (Cns) (Dummy:*) %^WG: ;


"xx:x gradation 1"
!! => __@RULENAME@__  
Cx:0 <=> Vow: _ (ˊ:) Cx (%^RC:) Vow: (%^SV:) (Vow) (Cns:) (Dummy:*) %^WG:0  ;
	where Cx in ( b d g p t k c č f z ž l m n ŋ r s š v ) ;

!"xx:x gradation 2"
!!! => __@RULENAME@__  
!%^RV:ˊ <=> Vow _ Geminate: (ˊ:) Geminate (%^RC:) Vow: (Vow) (Cns:) (Dummy:*) %^WG:0  ;

!€ kopp^RCâ^WG^LLEN>st
!€ ko0p0â00>st

"xX:X gradation"
Cx:0 <=> Vow: _ Cy: (%^RC:) Vow (Vow:) (Cns:) (Dummy:*) %^WG:0 ;
	where Cx in ( l  r  s  h  m  n  đ  ) 
	      Cy in ( l7 r7 s7 h7 m7 n7 đ7 ) 
		  matched ;


"xx:y long to something else gradation 1" 
!! => __@RULENAME@__   hh:V, kk:h, pp:v, čč:J
Cx:0 <=> Vow: _ Cy: (%^RC:) Vow: (Vow:) (Cns:) (Dummy:*) %^WG:0 ;
         Vow (%^RV:) _ Cy: (%^RC:) Vow (Dummy:*) %^WG:0 ;
	where Cx in ( h   k  p  č ) 
	      Cy in ( h4  k4 p4 č4 )
		  matched ;


!€ le^RVkk^RCâ^WG^RLEN
!€ lee0hâ00

"xx:y long to something else gradation 2" 
!! => __@RULENAME@__  hh:V, kk:h, pp:v, čč:J
Cx:Cy <=> Vow Cz: _ (%^RC:) Vow (Vow:) (Cns:) (Dummy:*) %^WG:0 ;
          Vow (%^RV:) Cz: _ (%^RC:) Vow (Dummy:*) %^WG:0 ;
	where Cx in ( h4  k4 p4 č4 ) 
	      Cy in ( ṿ   h  v  j̣ ) 
	      Cz in ( h   k  p  č ) 
		  matched ;

!€ pääpp4ir^DIPH^WG
!€ pää0 vir00

!€ su^RVkk4^RCá^SV^WG^CLEN^SLEN>st
!€ su0  0 hh  áá  0  0    0    0st

! č i ä p:0 p4:v á t ^WG:0 ^FCD:0

"yx:zx gradation"
!! => __@RULENAME@__  
Cx:Cy <=> _ (ˊ:) Cz (%^RC:) Vow (%^SV:) (Cns:) (Dummy:*) %^WG: ;
       where Cx in ( h4 k4 h4 h4 ) 
             Cy in ( v  v  v  v  )
             Cz in ( t  s  č  c  ) 
       matched ;

!€ liähˊtu^SV^WG^CLEN^SLEN>st
!€ liäv0tu00000st

!€ pih4tâs^WG
!€ pi vtâs0

!€ totk4os^WG
!€ tođ hos0
 
"xyy:xy gradation"
Cy:0 <=> Cx: _ Cy: (%^RC:) Vow (%^SV:) (Cns) %^WG: ;
      where Cx in ( i i l l l l l l m m n n n r r r r r r v ) 
            Cy in ( c t c p s t č š p č c s t c p s t č š p ) 
      matched ;

!€ riänttus^WG
!€ riän0tus0

"rkk:rk gradation"
k:0 <=> r _ k4: (%^RC:) Vow (%^SV:) (Cns) %^WG: ;
 
"xxy:xy gradation for nnj"
!! => __@RULENAME@__  
Cx:0 <=> Cx: _ Cy: Vow (%^SV:) (Cns) %^WG: ;
      where Cx in ( n ) 
            Cy in ( j )
      matched ;

!€ koonnjâl^WG
!€ koon0jâl0 

"xyy:xy gradation"
!! => __@RULENAME@__  
Cy:0 <=> Vow: (%^RV:) Cx: _ Cy: Vow (%^SV:) (Cns) %^WG: ;
	where Cx in ( i4 l n r l m r v i4 l l l l n n r r r r )
          Cy in ( t  t t t p p p p c  c č s š c s c č s š ) 
	matched ;

! s š c č p t 

!€ aˆRVi4kkâ^WG
!€ a0i0há0

!€ vieltti^WG
!€ viel0ti0

"xyy:xz gradation 1"
!! => __@RULENAME@__  
Cy:0 <=> Vow: (%^RV:) Cx:Cz _ Cy: (%^RC:) Vow (%^SV:)  (Cns) %^WG: ;
	where Cx in ( i4 j l r v )
		  Cy in ( k  k k k k )
		  Cz in ( i  j l r v )
		  matched ;

"xyy:xz gradation 2" 
!! => __@RULENAME@__  
Cy:Cz <=> Vow: (%^RV:) Cx: Cy: _ (%^RC:) Vow (%^SV:) (Cns) %^WG: ;
	where Cx in ( i4 j l r v  )
		  Cy in ( k  k k k k  )
		  Cz in ( h  h h h h  )
		  matched ;

"xˊy:xy gradation"
!! => __@RULENAME@__  
ˊ:0 <=> Vow: Cx _ Cy Vow (%^SV:) (Cns) (Dummy:*) %^WG:0 ;
     where Cx in (b b b b h h h h j j j j l l l l l l l l l l l m n n n n n p p p p r r r r r r r r r r r r r r s s s s s s t t v v v v v v v v v v v v v đ đ đ đ đ ŋ š š š š )
           Cy in (d l đ ž l m t č d l m v b d g j m n s v ŋ š ž n d h j z ž c s t č b d f g h j m n r s t v ŋ š k m n p t v k h d g h k j l m n r z đ ŋ ž b f g j m g k m p t )
		  matched;

"xˊy:xy gradation for i4"
!! => __@RULENAME@__  
ˊ:0 <=> Vow: i4: _ Cx Vow (%^SV:) (Cns) (Dummy:*) %^WG:0 ;
	where Cx in (b  d  g  l  m  n  r  v  z  ŋ ) ;



  
!! !Other Consonant Rules
!  ======================
!  ======================
!  ======================

       
"Consonant shortening (not weak grade)"
!! => __@RULENAME@__ pp to p for kappeer ~ kaperist
Cy:0 <=> Cx _  (%^RC:) [ Vow:  ]* (Cns) (Cns) (%^RC:) (%^SFV:) (%^SC:) (%^ÁI:) (%^ÁE:) (%^RLEN:) %^CSH: (%^RVSH:) %>: ;
       where Cx in ( s v m n ŋ š h l p d k k  h  p  t g r č  ) 
             Cy in ( s v m n ŋ š h l p d k k4 h4 p4 t g r č4 ) 
       matched ;
 
!€ kappeer^CSH^RVSH>ist
!€ ka0p0er000ist
!€ haavvâr^CSH^RVSH>ist
!€ h0a0vâr00>ist
!€ pottáák^RC^CSH^RVSH>ist
!€ pot00ákk000ist
!€ vinnjooh^CSH^RVSH>ist
!€ vi0nj0oh000ist
!€ paakkeet^CSH>ân
!€ paa0keet00ân
!€ nahhaar^CSH>ân
!€ na0h0ar00ân

"nnj to nj shortening"
!! => __@RULENAME@__  
n:0 <=> n _ j Vow (Cns:) %^CSH: %>: ;

!€ konnjâl^CSH>ân
!€ kon0jâl00ân

 
"Lengthen root consonant after vowel shortening"
%^RC:Cx <=> Vow Cx _ %^CSH: ;
        where Cx in ( p t k ) ; 

!€ haamit^RC^CSH^RVSH>ist
!€ h0amitt000ist
!€ ruávást^RC^CSH^RVSH>ist
!€ ruávást0000ist

"Lengthening of half-long root consonant 1"
!! => __@RULENAME@__  for l-dot to ll
Cx:Cy <=> Vow _ %^RC: Vow  %^CLEN: %>: [n|d] ;
	where Cx in ( l7 r7 s7 h7 m7 n7 đ7 )
		  Cy in ( l  r  s  h  m  n  đ  ) 
		  matched ;

"Lengthening of half-long root consonant 2"
!! => __@RULENAME@__   for l-dot to ll  
%^RC:Cx <=> Vow :Cx _ Vow  %^CLEN: %>: [n|d] ;
	where Cx in ( l  r  s  h  m  n  đ  ) ;

!€ kiel7^RCâ^CLEN>n
!€ kiellâ0n


"Assimilation of root consonant and double suffix consonant"
!! => __@RULENAME@__  
t:0 <=> _ %>: t t ; 

!€ haamit>ttáá
!€ haami00ttáá



"Consonant lengthening after weak grade"
!! => __@RULENAME@__  
%^RC:Cx <=> Cy: _ Vow: (%^SV:) %^WG: (Dummy:) %^CLEN: ;
        where Cy in ( k4 č4 p4 p l d m s l7 r7 s7 h7 m7 n7 đ7) 
              Cx in ( h  j  v  p l d m s l  r  s  h  m  n  đ ) 
        matched ;

!€ kass7^RV^a^SV^WG^CLEN

"Consonant lengthening after h4 initial weak grade"
!! => __@RULENAME@__  
%^RC:Cx <=> h4: Cx _ Vow: (%^SV:) %^WG: (Dummy:) %^CLEN: ;
        where Cx in ( v  č  s  t  c  ) ;

!€ su^RVkk4^RCá^SV^WG^CLEN^SLEN>st
!€ su0  0 hh  áá  0  0    0    0st

!€ kopp^RCâ^WG^CLEN>in
!€ ko0ppá0000in


!! !Other consonant rules

"Word final consonant deletion of n m t"                 
!! => __@RULENAME@__ for vaskâm:vaaskâ
Cx:0 <=> _ (%^WG:) %^FCD: ;      
         where Cx in (n m t) ;

!€ va^RVskâm^WG^FCD
!€ vaaskâ00
	
!"Word Final m9 to n"
!m9:n <=> _ %>: [.#.|Cns] ;
!
!€ puhelim9>ntáá
!€ puhelin0ttáá
 


!! !!Vowel rules

!! !First syllable rules

"Vowel shortening with consonant shortening"
!! => __@RULENAME@__  
Vx:0 <=> _ Vx Cns: Cns:+ ([%^RC:|%^SC:]) %^CSH: (Dummy:*) %>: ;
        where Vx in ( a e o á ä u y i ) ;

!  ([%^RC:|%^SC:])  was earlier (Dummy:*), but we changed to avoid conflict
! Follow 
!€ koonnjâl^CSH>ân
!€ k0on0jâl0>ân

!€ kappeer^CSH^RVSH>ist
!€ ka0p0er000ist

!€ čäällim^CSH^RVSH>ist
!€ č0ä0lim000ist

!€ muusik^RC^CSH^RVSH>ist
!€ m0usikk000ist

!€ kahhaan^SC^CSH^RVSH>ist
!€ ka0ha0n0j000ist

"Vowel shortening (no consonant shortening) in consonant stems"
!! => __@RULENAME@__ 
Vy:0 <=> _ Vx   Cns: (Cns:) (%^RC:) Vow :Cns (%^SFV:) (%^SC:) (%^ÁI:) (%^ÁE:) %^RVSH: %>: ;
       where Vx in ( a e i o u á ä ) 
             Vy in ( a e i o u á ä ) 
       matched ;

!€ kiirrâs^RVSH>ân
!€ ki0rrâs0>ân
 
"Vowel shortening (no consonant shortening) in vowel stems"
!! => __@RULENAME@__ 
Vy:ˊ <=> Vx _  Cns: (Cns:) (%^RC:) Vow (%^WG:) (%^SFV:) (%^SC:) (%^ÁI:) (%^ÁE:) %^RVSH: %>: ;
       where Vx in ( a e i o u y á ä ) 
             Vy in ( a e i o u y á ä ) 
       matched ;

"Root vowel lenghtening in bisyllabic stems"
!! => __@RULENAME@__  
%^RV:Vx <=> Vx _ Cns:* (%^RC:) Vow: (%^SV:) (%^WG:) %^RLEN: ;
	where Vx in ( a e i o u y) ;

! s o ^RV:o l:0 l ^RC:0 â ^WG:0 ^RLEN:0
! l e ^RV:e k:0 k4:h ^RC:0 â ^WG:0 ^RLEN:0

!€ le^RVkk4^RCâ^WG^RLEN
!€ lee0h0â00

"Root vowel change for á to ä with i in second syllable"
!! => __@RULENAME@__  
á:ä <=>    _ á: Cns:* :i Cns: %^ÁI: [.#.|%>:] ;
        á: _    Cns:* :i Cns: %^ÁI: [.#.|%>:] ;

!€ páápp4ár^ÁI^WG
!€ pää0vir00

"Monophthongisation rule 1"
!Vx:Vy <=> _ Vz: Cns:* i %>: j ;
Vx:Vy <=> _ Vz: Cns:* i %^WG: %>: j ;
	where Vx in ( i u ä )
		  Vy in ( e o a )
		  Vz in ( ä á ä )
	matched ;

! säämi>j
! saami>j

!€ kuáti^WG>j
!€ koođi00j


"Monophthongisation rule 2"
Vx:Vy <=> Vz: _ Cns:* i %^WG: %>: j ;
	where Vx in ( ä á ä )
		  Vy in ( e o a )
		  Vz in ( i u ä )
		  matched ;

!nom	ill
!e^RV	iä
!ie	iä
!ä	á
!ye	uá

"e to iä in Illative and Sg3 1"
!! => __@RULENAME@__  
e:i <=> _ %^RV: Cns:+ Vow: %^ILL: ;

"e to iä in Illative and Sg3 2"
!! => __@RULENAME@__  
%^RV:ä <=> e: _ Cns:+ Vow: %^ILL: ;

"ie to iä in Illative and Sg3"
!! => __@RULENAME@__  
e:ä <=> i _  Cns:+ Vow: %^ILL: ;

"ä to á in Illative and Sg3"
!! => __@RULENAME@__  
ä:á <=> [.#.|Cns] _  Cns:+ Vow: %^ILL: ;

"ye to uá in Illative and Sg3 1"
!! => __@RULENAME@__  
y:u <=> _ e:  Cns:+ Vow: %^ILL: ;

"ye to uá in Illative and Sg3 2"
!! => __@RULENAME@__  
e:á <=> y: _  Cns:+ Vow: %^ILL: ;

!! !Second syllable rules

"Vowel change á to e or i in 3syll"
!! => __@RULENAME@__  
Cx:Cy <=> _ (Cns:) Cz: %^WG: ; 
       where Cx in ( á á )
             Cy in ( e i )
             Cz in ( %^ÁE %^ÁI ) 
       matched ;

!€ puttás^ÁE^WG
!€ pu0tes00

"Stem vowel lengthening"
!! => __@RULENAME@__ long á in kissáást
%^SV:Vx <=> Vx       _ Dummy:* (Cns)                  %^SLEN: ;
        where Vx in ( á o e a i ) ; ! pending...

!€ ki^RVss^RCá^WG^CLEN^SV^SLEN>st
!€ ki00ssá00á00st

!€ mi^RVll^RCo^WG^CLEN^SV^SLEN>st
!€ mi00llo00o00st

!€ huksi^SVm^SLEN>ist
!€ huksiim00ist





"Fleeting vowel before suffix"
!! => __@RULENAME@__  
i:0 <=> _ Cns %^SFV: (%^SC:) (%^CSH:) (%^RVSH:) %>: ;

!€ pieˊhâin^SFV^SC^CSH^RVSH>ist
!€ pie0hâ0n0j000ist



"Stem vowel e deletion before 2nd syllable ij"
!! => __@RULENAME@__  
e:0 <=> Cns: _ (Dummy:) (Dummy:) %>: i j ;
        Cns: _ Cns %^SFV: (%^SC:) (%^CSH:) (%^RVSH:) %>: ; 

!€ šoddâdem^SFV>ist
!€ šoddâd0m00ist

 
"Stem vowel e to a for a root vowel."
!! => __@RULENAME@__  
e:a <=> a (%^RV:) Cns:+ _ %^RLEN: %^CSH: %^ILL: ;

"Stem vowel e to á for other root vowel."
!! => __@RULENAME@__  
e:á <=> [o|i|u] (%^RV:) Cns:+ _ %^RLEN: %^CSH: %^ILL: %>: ;

!€ a^RVlge^RVL>^An
!€ aalg00an
!€ lo^RVdde^RVL>^An 
!€ loodde000an 


"Stem vowel â to a in Pl Nom -h"
!! => __@RULENAME@__  
â:a <=> _ (%^WG:) (%^RLEN:) %> h ;

!€ so^RVll^RCâ^WG^RLEN>h
!€ soo0l0a000h

"Stem vowel â > á and u > o change before i"
!! => __@RULENAME@__  
Vx:Vy <=> _ (Dummy:*) %^SVLOW:0 ;
	where Vx in ( â u )
		  Vy in ( á o )
		  matched ;

!€ so^RVll^RCâ^WG^CLEN^SVLOW>in
!€ so00llá0000in


"Illative suffix change in bisyllabic e stems"
!! => __@RULENAME@__  
%^A:a <=> a (%^RV:) Cns:* e: (Dummy:*) %>: _ ;


!! !!Other rules

"Inversion of in to nj"
!! => __@RULENAME@__  
%^SC:j <=> n (%^SFV:) _ (%^CSH:) (%^RVSH:) %>: ; 

!€ pieˊhâin^SFV^SC^CSH^RVSH>ist
!€ pie0hâ0n0j000ist


!€ kahhaan^SC^CSH^RVSH>ist
!€ ka0ha0n0j000ist

