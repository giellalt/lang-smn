! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

!! !!!Alphabet 

Alphabet   

!! !!The Inari Saami letters

 a á â ä b c d e f g h i j k l m n o õ p q !!= * @CODE@
! â:a â:á
 r s t u v w x y z æ ä ø ö å			   !!= * @CODE@
 č đ ŋ ŧ š ž				 			   !!= * @CODE@

 A Á Â Å B C D E F G H I J K L M N O Õ P Q !!= * @CODE@
 R S T U V W X Y Z Æ Ä Ø Ö Å 			   !!= * @CODE@
 Č Đ Ŋ Ŧ Š Ž  							   !!= * @CODE@
 ˊ  %- 							   !!= * @CODE@
 ':0   !!= * @CODE@ this to zero

!! !Accented letters, just in case

 É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Ê Ô Û Î Ã Ý þ Ð   !!= * @CODE@
 é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý þ ð   !!= * @CODE@
 ß ª ß ç								   !!= * @CODE@

 
 
!! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 »7     !! * »
 «7     !! * «
 %[%>%] !! * >
 %[%<%] !! * <



!! !Different archiphonemes


p4:p  t4:t k4:k č4:č r4:r l4:l h4:h   !!= * @CODE@  these are consonants that change in cg
i4:i                                  !!= * @CODE@  this is the underlying i consonant
m7:ṃ n7:ṇ ŋ7:ŋ̣ s7:ṣ r7:ṛ đ7:đ̣ l7:ḷ    !!= * @CODE@  with dots
j7:j̣ h7:ḥ š7:ṣ̌  v7:ṿ                  !!= * @CODE@  with dots
!h:ṿ č:j̣  
!%^A:á 	           		      loddán but algan, default is á.


!! !More abstract archiphonemes

!%^A:0		 = algan lodán
%^RC:0		!!= * @CODE@ = root cons., lengthened by ^CLEN - máán%RCu máánnun
%^RD:0		!!= * @CODE@ = root dipht., shortened by by ^DSH - vuá%^RDg'gu vuáˊguttáá
%^RV:0	 	!!= * @CODE@ = root vowel, lengthened by ^RLEN - ta%^RVvlu taavlu and ^RV:ˊ when WG and suffix: 0 | t | i
%^SC:0      !!= * @CODE@ = stem cons, %^SC:j, pluss lengthen after earlier shortening by %^CSH, see lyeme
%^SV:0 		!!= * @CODE@ = stem vowel, lengthened by ^SLEN u:uu - táálu%^SV tááloost

!! !Triggers

%^CLEN:0	!!= * @CODE@ = cons lengthening, with ^RC
%^CSH:0     !!= * @CODE@ = cons shortening, laaŋkku - laaŋkun
%^DLEN:0    !!= * @CODE@ = Diphtong lengthening (remove?)
%^DSH:0     !!= * @CODE@ = Diphtong shortening
%^EE:0      !!= * @CODE@ = Final vowel deletion in ee stems (not in use)
%^FCD:0     !!= * @CODE@ = Final consonant deletion 
%^IA:0      !!= * @CODE@ = i to á (more) (not in use)
%^ILL:0     !!= * @CODE@ = e to a/á in ill etc (change name to EA?)
%^LLEN:0    !!= * @CODE@ = low vow lengthening (not in use)
%^MONO:0    !!= * @CODE@ = monophtongization of diphthongs  (not in use)
%^RLEN:0    !!= * @CODE@ = root lengthening, with ^RD
%^RVSH:0    !!= * @CODE@ = root vow shortening - máán7%^RCu máˊnuttáá 
%^SLEN:0    !!= * @CODE@ = stem vowel lengthening
%^SFV:0     !!= * @CODE@ = Deleting fleeting vowel before suffix
%^SVLOW:0   !!= * @CODE@ = stem vowel lowering â>á and u>o - árppu árppoin
%^UU:0      !!= * @CODE@ = Final vowel deletion in uu stems
%^VLOW:0    !!= * @CODE@ = Vowel lowering in 3rd sg of contract verbs tuhhid:tohhe
%^WG:0      !!= * @CODE@ = weak grade trigger
%^ÁE:0      !!= * @CODE@ = á to e (more) in 3-syll
%^ÁI:0      !!= * @CODE@ = á to i (more) in 3-syll

!%^ODDC:0     shorten cns in odd-syll stems before C, #  (not in use)
!%^ODDSHV:0   Cns shortening in C|# and shortvow in Inf etc  (not in use)
!%^RVD:0      root vowel  diftongisation

!! !!Morpheme boundaries:

!! Here we define the suffix border >
!! The other borders are not used yet, but still defined:

 «  !!≈ ;@CODE@: Derivational prefix
 »  !!≈ ;@CODE@: Derivational suffix
 %< !!≈ ;@CODE@: Inflectional prefx
 %> !!≈ ;@CODE@: Inflectional suffix
 %^ !!≈ ;@CODE@: (exceptional) soft hyphenation point
 #  !! and the symbol #, which is Word boundary for both lexicalised and dynamic compounds

!! End of alphabet definitions

;

!! !!! Sets

Sets
 Vow = a á â ä e i o u y æ ø ö å			   !!= * @CODE@ 
       é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý 	   !!= @CODE@ 
       A Á Â Ä E I O U Y Æ Ø Ö Å			   !!= @CODE@ 
       É Ó Ú Í À È Ò Ú Ì Ë Ü Ï Ê Ô Û Î Ã Ý 	   !!= @CODE@ 
       %^RV %^RD %^SV ;						   !!= @CODE@ 
 Cns = b c č d đ f g h j k l m n ŋ 			   !!= * @CODE@ 
       p q r s š t ŧ v w x z ž				   !!= @CODE@ 
       B C Č D Đ F G H J K L M N Ŋ			   !!= @CODE@ 
       P Q R S Š T Ŧ V W X Z Ž 				   !!= @CODE@ 
       ht g2 k2 m2 n2 							   !!= @CODE@ 
       b3 d3 g3 m3 h3 						   !!= @CODE@ 
       p4 k4 č4 r4 l4 h4 t4 i4                        !!= * @CODE@
       m7 n7 ŋ7 v7 s7 r7 đ7	l7 j7 h7 š7		   !!= @CODE@ 
       ṃ  ṇ  ŋ̣  ṿ  ṣ  ṛ  đ̣  ḷ  j̣  ḥ  ṣ̌         !!= * @CODE@
       %^RC %^RD %^SC  ˊ ;
 Geminate = b d g p t k k4 c č f z ž l m n ŋ r s š v đ j l7 n7 s7 r7 ;
 Seg = Vow Cns ;							   !!= * @CODE@ 
 WeG = %^WG1 %^WGSH %^CPL %^WG ;		  !!= * @CODE@
 StemCns = b b3 d d3 g g2 g3 h h2 h3 		   !!= * @CODE@ 
           j l m m2 m3 m8 n n8 n2 ŋ r s  ;     !!= * @CODE@ 

Arch = %^RC %^RV %^SV %^SC %^RVD ;                !!= * @CODE@ 
Len = %^RLEN %^LLEN %^SLEN %^CLEN ;    			!!= * @CODE@ 


Dummy = %^CLEN %^CSH %^EE %^FCD %^IA %^ILL %^LLEN %^MONO %^ODDC %^ODDSHV    !!= * @CODE@
        %^RLEN %^RVL %^RVSH %^SFV %^SLEN                     !!=  @CODE@ 
        %^SVF %^SVLOW %^UU %^VLOW %^RVSH %^WG %^ÁE %^ÁI ;  !!=  @CODE@ 


           
!! !!!Definition section

!! Trying to introduce a WG right context, so far resulting in drop in performance.

Definitions

!WeakGrade = (%^RC:) :Vow (%^SFV:) (%^SV:) (Vow) (Cns:) (%^ÁI:|%^ÁE:) (%^DSH:) %^WG:0 (%^RLEN:|%^RVSH:)  [%>|.#.] ;
WeakGrade = (%^RC:) :Vow (%^SFV:) (%^SV:) (Vow) (Cns:) (%^ÁI:|%^ÁE:) (%^DSH:) %^WG:0 (%^RLEN:|%^RVSH:)  (%^SLEN:)   ;    
! The original list of right contexts, in the same order as below, 
! just count the instances of WeakGrade to see what it replaced. 
! (this list will be deleted when dust settles):
! 
! (%^RC:) Vow  (%^SV:) (Cns:) %^WG:0 (%^RLEN:|%^RVSH:) (%^SLEN:) (%^SVLOW:) [%>|.#.] ;
! (%^RC:) :Vow (%^SV:) (Cns) %^WG:0 ;
! (%^RC:) :Vow (%^SV:) (Cns) %^WG: ;
! (%^RC:) Vow: (%^SV:) (Vow) (Cns:) (%^ÁI:|%^ÁE:) %^WG:0 (%^RLEN:)(%^CLEN:)(%^SLEN:)(%^SVLOW:) [%>|.#.] ;
! (%^RC:) Vow: (%^SV:) (Vow) (Cns:) (%^ÁI:|%^ÁE:) %^WG:0 (%^RLEN:)(%^CLEN:)(%^SLEN:)(%^SVLOW:) [%>|.#.] ;
! (%^RC:) Vow: (%^SFV:) (%^SV:) (Vow) (Cns:) (%^DSH:) %^WG:0 (%^RLEN:) (%^CLEN:) (%^SLEN:) [%>|.#.]  ;
! (%^RC:) Vow: (%^SV:) (Vow) (Cns:) %^WG:0 (%^RVSH:0)  %>: [ t | i | .#. ] ;
! (%^RC:) Vow: (Vow:) (Cns:) %^WG:0 ; 
! (%^RC:) Vow  %^WG:0 ;
! (%^RC:) Vow: (Vow:) (Cns:)  %^WG:0 ;
! (%^RC:) Vow:  %^WG:0 ;
! (%^RC:) Vow (%^SV:) (Cns:) %^WG: ;
! (%^RC:) Vow (%^SV:) (Cns) %^WG: ;
! (%^SV:) (Cns) %^WG: ;
! (%^SV:) (Cns) %^WG: ;
! (%^RC:) Vow (%^SV:)  (Cns) %^WG: ;
! (%^RC:) Vow (%^SV:) (Cns) %^WG: ;
            
           
!! !!!Rule section

Rules

!! !!Consonant rules

!! !Consonant gradation

"X:x halflong to short gradation"
!! * __@RULENAME@__  mool7â to moolâ
Cx:Cy <=> Vow: _ WeakGrade ;
	where Cx in (m7 n7 ŋ7 r7 s7 š7 j7 v7 đ7 l7 )
          Cy in (m  n  ŋ  r  s  š  j  v  đ  l  )
		  matched;
		  
!€ mool7^RCâ^WG
!€ mool0â0


"x:y gradation" 
!! * __@RULENAME@__ c to s etc, note that t:đ is in a separate rule
! Cx:Cy <=> Vow: _ WeakGrade ;
Cx:Cy <=> Vow: _ (%^RC:0) Vow (%^SV:) (Cns:) (%^DSH:0) [ %^WG:0 | %^CSH:0 ] (%^RLEN:|%^RVSH:) (%^SLEN:) (%^SVLOW:) [%>|.#.] ;
	where Cx in (c č h )
          Cy in (s j̣ ṿ )
		  matched;

!€ käähir^WG
!€ kääṿir0

!€ käähir
!€ käähir

!€ juá%^RDh%^RCu%^DSH%^WG%>ttáá
!€ juáˊṿ0u00>ttáá

"t:đ gradation" 
!! * __@RULENAME@__   for ito : iđo
t:đ <=> Vow: _ (k4:) WeakGrade ;
!t:đ <=> Vow: _ (k4:) (%^RC:) :Vow (%^SV:) (Cns) %^WG:0 ;

!€ jo^RVtk4^RCâ^WG^RLEN
!€ joođh0â00

"k4:h gradation for kk4 and tk4"
!! * __@RULENAME@__    for atka : ađha
k4:h <=> Vow: [t:|k:] _ WeakGrade ;
!k4:h <=> Vow: [t:|k:] _ (%^RC:) :Vow (%^SV:) (Cns) %^WG: ;

!€ mä^RVtk4i^WG^RLEN
!€ määđhi00

!€ su^RVkk4^RCá^SV^WG^CLEN^SLEN>st
!€ su00hháá000>st

"t4:đ gradation for tt4" 
!! * __@RULENAME@__  ruttâ ruuđâ
t4:đ <=> t: _ Vow: %^WG: ;

!€ ru%^RVtt4â%^WG%^RLEN>
!€ ruu0đâ00>

"xx:x gradation"
!! * __@RULENAME@__  tikká:tiká, deleting the first k, etc.
!Cx:0 <=> Vow: _ (ˊ:) Cx WeakGrade ;
!Cx:0 <=> Vow: _ (ˊ:) Cx (%^RC:) Vow: (%^SV:) (Vow) (Cns:) (%^ÁI:|%^ÁE:) %^WG:0 (%^RLEN:)(%^CLEN:)(%^SLEN:)(%^SVLOW:) [%>|.#.] ; ref
Cx:0 <=> Vow: _ (ˊ:) Cx (%^RC:) :Vow (%^SV:) (Vow) (Cns:) (%^ÁI:|%^ÁE:) (%^DSH:) %^WG:0  (%^RLEN:|%^RVSH:)(%^SLEN:)(%^SVLOW:) [%>|.#.] ;
           Vow: _ (ˊ:) Cx  :Vow (%^RLEN:)  %^CSH:   ;  
	where Cx in ( b d g p t k c č f z ž l m n ŋ r s š v ) ;

!€ cuozˊz^RCâ^WG^RLEN
!€ cuo00z0â00

"ˊ to zero gradation"
ˊ:0 <=> Vow: (%^RD:) Cns: _ Cns: (Cns) (%^RC:) Vow: (%^SFV:) (%^SV:) (Vow) (Cns:) (%^DSH:) [ %^WG:0 | %^CSH:0 ] (%^RLEN:) (%^CLEN:) (%^SLEN:) [%>|.#.]  ;
 !        Vow: Cns: _ Cns Vow: %^CSH:0 %>  ; !nieidân suá%^RDi4ˊttu  puá%^RDlˊl7u

!"Consonant shortening (not weak grade) final part"
! * __@RULENAME@__ pp to p for kappeer ~ kaperist
!Cy:0 <=> Cx: (ˊ:) _  (%^RC:) [ Vow:  ]* (Cns) (Cns) (%^RC:)  (%^ÁI:|%^ÁE:) (%^SFV:) (%^SC:) (%^RLEN:) (%^DLEN:) %^CSH: (%^RVSH:) [%>|.#.] ;
!       where Cx in ( s v m n ŋ š h l p d k k  h  p  t g r č  ) 
!             Cy in ( s v m n ŋ š h l p d k k4 h4 p4 t g r č4 ) 
!       matched ;

!€ nie^RDi4ˊdâ^WG
!€ nie0i0dâ0


"Consonant shortening (not weak grade) pp:p etc"
!! * __@RULENAME@__ pp to p for kappeer ~ kaperist, ss:s, šš:š dd:d kk4:k pp4:p tt:t gg:g čč4:č zz:z cc:c tt4:t
Cy:0 <=> Cx  _  %^RC: Vow: (%^RLEN:)  %^CSH: (%^RVSH:) [%>|.#.] ;
       where Cx in (  k  p   t č  ) 
             Cy in ( k4 p4  t4 č4  ) 
       matched ;



!"Consonant shortening (not weak grade) pp:p etc"
!!! * __@RULENAME@__ pp to p for kappeer ~ kaperist, ss:s, šš:š dd:d kk4:k pp4:p tt:t gg:g čč4:č zz:z cc:c tt4:t
!Cy:0 <=> Cx (ˊ:) _  (%^RC:) [Vow:]* (Cns) (Cns) (%^ÁI:|%^ÁE:) (%^SC:) (%^SFV:) (%^RLEN:) (%^DLEN:) %^CSH: (%^RVSH:) [%>|.#.] ;
!       where Cx in ( s š p d k k  p  t t g č  z c ) 
!             Cy in ( s š p d k k4 p4 t t4 g č4 z c ) 
!       matched ;
!!ká%^RVll7u%^RLEN%^CSH%>n       
!
!!€ čiäpp4^RCát^ÁE^SC^CSH>ân
!!€ čiäp00át0t0>ân
!
!!€ ru%^RVtt4â%^RLEN%^CSH%>n
!!€ ruut0â00>n
!
!!rejected
!!€ čiäpp4^RCát^ÁE^SC^WG^FCD>ân
!!€ čiäp00át0t00>ân
!
!"Consonant shortening (not weak grade) final part (half-long)"
!!! * __@RULENAME@__ vv:v, mm:m, nn:n ŋŋ:ŋ ll:l hh4:h rr:r  
!Cy:0 <=> Cx: (ˊ:) _  (%^RC:) [ Vow:  ]* (Cns) (Cns) (%^ÁI:|%^ÁE:)  (%^DSH:) (%^SC:) (%^SFV:) (%^RLEN:) (%^DLEN:) %^CSH: (%^RVSH:) [%>|.#.] ;
!       where Cx in ( v m n ŋ h l h  r ) 
!             Cy in ( v m n ŋ h l h4 r  ) 
!       matched ;
!
!
!"Consonant shortening (not weak grade) first part (half-long)"
!!! * __@RULENAME@__ 
!Cx:Cy <=> Vow: _ (ˊ:) Cz:  (%^RC:) [ Vow:  ]* (Cns) (Cns) (%^ÁI:|%^ÁE:) (%^DSH:) (%^SC:) (%^SFV:) (%^RLEN:) (%^DLEN:) %^CSH: (%^RVSH:) [%>|.#.] ;
!       where Cx in ( v m n ŋ h l h  r ) 
!             Cy in ( ṿ ṃ ṇ ŋ̣ ḥ ḷ ḥ  ṛ ) 
!             Cz in ( v m n ŋ h l h4 r ) 
!       matched ;
!
!"Consonant shortening (not weak grade) hyphen"
!!! * __@RULENAME@__ 
!ˊ:0 <=> Cx: _ Cy: (j) (%^RC:) [ Vow:  ]* (Cns) (Cns) (%^ÁI:|%^ÁE:) (%^DSH:) (%^SC:) (%^SFV:) (%^RLEN:) (%^DLEN:) %^CSH: (%^RVSH:) %> ;
!       where Cx in ( s v m n ŋ š h l p d k k  h  p  t g r č  z l ) 
!             Cy in ( s v m n ŋ š h l p d k k4 h4 p4 t g r č4 z  l7 )  
!       matched ;
!
!!€ pie^RDgˊg^RCâ^DLEN^CSH>n
!!€ pie0g000â00>n
!

"xx:x gradation 2, RV:ˊ when WG"
!! * __@RULENAME@__ kállu káˊḷui
%^RV:ˊ <=> Vow _ [ Geminate: | i4: ] (ˊ:) [ Geminate:+ | t4: ] (%^RC:) Vow: (%^DSH:) (%^SV:) (Vow) (Cns:) %^WG:0 (%^RVSH:0)  %>  \[s | n | d | h ];
           

!pumppu puˊmputtáá
!áimu áˊimuttáá áˊimuiguin 
!á%^RVi4mu 

!€ so^RVllâ^WG^RVSH>ttáá
!€ soˊ0lâ00>ttáá

!kállu káˊḷui
!€ ká%^RVll7u%^DSH%^WG>i
!€ káˊ0ḷu00>i

"RV:ˊ in xx:x gradation 3, RV:ˊ when RVSH"
!! * __@RULENAME@__ CSH
%^RV:ˊ <=> Vow _ Geminate: Geminate: Vow: (%^DSH:) %^CSH:0 %^RVSH:0  %> ; !

"xx:y long to something else gradation 1, weak grade" 
!! * __@RULENAME@__   hh:V, kk:h, pp:v, čč:J, deleting first part, k:0 in kk4:h etc
Cx:0 <=> Vow: _  (ˊ:) Cy: (%^RC:) Vow: (Vow:) (Cns:) (%^DSH:) %^WG:0   ; !kk4%^RC - 0k4%^RC  puáˊḷui
         Vow: (%^RV: | %^RD: ) _  (ˊ:) Cy: (%^RC:) Vow:  %^WG:0 ;
         Vow: (%^RV: | %^RD: ) _ (ˊ:) Cy:  Vow: (%^RLEN:) %^CSH:0 ;
	where Cx in ( h  t k  p  č l n s r ) 
	      Cy in ( h4 t4 k4 p4 č4 l7 n7 s7 r7)
		  matched ;

!€ le^RVkk4^RCâ^WG^RLEN
!€ lee0h0â00

!€ puá%^RDlˊl7u%^DSH%^WG%>i
!€ puáˊ00ḷu00>i

"xx:y long to something else gradation 2" 
!! * __@RULENAME@__  hh:V, kk:h, pp:v, čč:J here we change second part, k4:h in kk4:h etc.
Cx:Cy <=> Vow:         Cz: (ˊ:) _  (%^RC:) Vow: (Vow:) (Cns:)  %^WG:0 ;
          Vow: (%^RV: | %^RD:) Cz: (ˊ:) _ (%^RC:) Vow:  %^WG:0 ;
	where Cx in ( h4  p4 č4 ) 
	      Cy in ( ṿ   v  j̣ ) 
	      Cz in ( h   p  č ) 
		  matched ;

!htest č i ä p:0 p4:v á t ^WG:0 ^FCD:0


"xˊx7:xˊx and xx7:xx as default when not WG nor DSH" 
!! * __@RULENAME@__  
Cx:Cy <=> Vow:  Cy: (ˊ:) _  Vow: \[%^WG:0 | %^DSH:0 | %^RLEN:0 ] ;
	where Cx in ( l7 n7 s7 r7) 
	      Cy in ( l  n s r )
		  matched ;

!€ puá%^RDlˊl7u>
!€ puá0lˊlu>

!€ puá%^RDlˊl7u%^DSH%^WG%>ttáá
!€ puáˊ00ḷu00>ttáá

!€ ká%^RVll7u%>
!€ ká0llu>

"xy:zy gradation"
!! * __@RULENAME@__  ht:vt, we use h4 
Cx:v <=> (ˊ:) _ Cz (%^RC:) Vow (%^SV:) (Cns:) %^WG: ;
! was before the change: <=> _ (´:)
       where Cx in ( h4 k4 h4 h4 ) 
             Cz in ( t  s  č  c  ) 
       matched ;


!€ pih4tâs^WG
!€ pivtâs0

!€ totk4os^WG
!€ tođhos0




"xyy:xy gradation A"
!! * __@RULENAME@__  
Cy:0 <=> :Vow (%^RV:) Cx: _ Cz: (%^RC:) Vow (%^SV:) (Cns) %^WG: (%^RLEN:) (%^RVSH:) %>  ;  
          Cx: _ Cz:  Vow (%^RLEN:)  %^CSH:   ;  
      where Cx in ( i4 i4 i4 l l l l l l l m m n n n ŋ r r r r r r r v ) 
            Cy in ( c  t  k  c k p s t č š p č c s t k c p k s t č š p ) 
            Cz in (  c  t  k  c k p s t č š p č c s t k c p k s t č š p ) 
      matched ;

!€ á%^RVrppu%^WG%^RLEN>
!€ áár0pu00>

!€ á%^RVrppu%^WG%^RVSH%>ttáá
!€ áˊr0pu00>ttáá

!€ riänttus^WG
!€ riän0tus0



!"xyy:xy gradation B"
!!! * __@RULENAME@__  this contains i4 pairs !suá%^RDi4ˊttu
!Cy:0 <=> Vow: (%^RV:) (%^RD:) Cx: (ˊ:) _ Cy: Vow (%^SV:) (Cns) [ %^WG: | %^CSH: ] ; 
!	where Cx in ( i4  i4 )
!          Cy in ( t   c  ) 
!	matched ;




!€ vieltti^WG
!€ viel0ti0

!"kk:rk gradation"
!k:0 <=> i4: _ k (%^RC:) Vow (Cns) %^WG: ;
 
"xxy:xy gradation for nnj weakgrade"
!! * __@RULENAME@__  special case
Cx:0 <=> Cx: _ Cy: Vow (%^SV:) (Cns) %^WG: ;
      where Cx in ( n ) 
            Cy in ( j )
      matched ;

!€ koonnjâl^WG
!€ koon0jâl0 



"xyy:xz gradation for k4:h, first xyy:xy"
!! * __@RULENAME@__  first k goes to 0
k:0 <=> Vow: (%^RV:) [i4:|j|l|r|v] _ k4: (%^RC:) Vow (%^SV:)  (Cns) (%^RLEN:)[ %^WG: | %^CSH:] ;

!€ pá%^RVrkk4u%^RLEN%^CSH%>n
!€ páár0ku00>n 


"xyy:xz gradation for k4:h 2" 
!! * __@RULENAME@__  second k goes to h
k4:h <=> Vow: (%^RV:) [i4:|j|l|r|v] k: _ (%^RC:) Vow (%^SV:) (Cns) %^WG: ;



  
!! !Other Consonant Rules

       
 
!€ kappeer^CSH^RVSH>ist
!€ kap00er00>ist

!€ haavvâr^CSH^RVSH>ist
!€ h0aṿ0âr00>ist

!€ pottáák^SC^CSH^RVSH>ist
!€ pot00ákk00>ist


"nnj to nj shortening not weak grade"
!! * __@RULENAME@__  special rule, e.g. konnjâl -> konjâlân
n:0 <=> n _ j Vow (Cns:) %^CSH: [%>|.#.] ;

!€ konnjâl^CSH>ân
!€ kon0jâl0>ân

! 
!"Lengthen stem consonant after earlier shortening"
!!! * __@RULENAME@__  e.g. tt in hamittist
!%^SC:Cx <=> Vow Cx (%^ÁI:|%^ÁE:) _ %^CSH: ;
!        where Cx in ( p t k ) ; 

!€ haamit^SC^CSH^RVSH>ist
!€ h0amitt00>ist


"Lengthening of half-long root consonant 1"
!! * __@RULENAME@__  for l-dot to ll, changing l7 to first l in ll
Cx:Cy <=> Vow: _ %^RC: :Vow (%^SV:) (%^RVSH:) %^CLEN: (%^SVLOW:) %>:  ;
	where Cx in ( l7 r7 s7 h7 m7 n7 đ7 v7 ŋ7 )
		  Cy in ( l  r  s  h  m  n  đ v ŋ ) 
		  matched ;

"Lengthening of half-long root consonant 2"
!! * __@RULENAME@__   for l-dot to ll, changing ^RC to second l in ll
%^RC:Cy <=> Vow: Cx: _ :Vow  (%^SV:) (%^WG:) (%^RVSH:) %^CLEN: (%^SVLOW:) [%>|.#.] ;
	where Cx in ( l7 r7 s7 h7 m7 n7 đ7 v7 ŋ7 )
		  Cy in ( l  r  s  h  m  n  đ v ŋ ) 
		  matched ;

!rejected:
!€ kol7^RCe^CLEN
!€ kolle0

"Lengthening of short root consonant 1"
!! * __@RULENAME@__  for h to vv
Cx:Cy <=> Vow: _ %^RC: :Vow (%^SV:) (%^WG:) (%^RVSH:) %^CLEN: (%^SVLOW:) %>:  ;
	where Cx in (  h )
		  Cy in (  v ) 
		  matched ;

"Lengthening of short root consonant 2"
!! * __@RULENAME@__   for h to vv
%^RC:Cy <=> Vow: Cx: _ :Vow  (%^SV:) (%^WG:) (%^RVSH:) %^CLEN: (%^SVLOW:) [%>|.#.] ;
	where Cx in (  h )  
		  Cy in (  v )   
		  matched ;

! piir %^RC u %^CLEN %> n
!€ ka^RVss^RCa^SV^WG^CLEN^SLEN>st
!€ ka00ssaa000>st

"Lengthening of consonant for essive"
!! * __@RULENAME@__   piiru piirrun niisu niissun
%^RC:Cy <=> Vow: Cx _ Vow  %^CLEN: %>: [ n | d ] ;
	where Cx in ( t r s )  
		  Cy in (  t r s )   
		  matched ;

! piir %^RC u %^CLEN %> n



!"Lengthening of half-long root consonant 2"
! * __@RULENAME@__   for l-dot to ll  
!%^RC:Cx <=> Vow :Cx _ Vow  %^CLEN: %>: [n|d] ;
!	where Cx in ( l  r  s  h  m  n  đ  ) ;

!€ kiel7^RCâ^CLEN>n
!€ kiellâ0>n


"Assimilation of root consonant and double suffix consonant"
!! * __@RULENAME@__  delet t in haamit in front of -ttáá, avoid -tttáá
t:0 <=> _ %>: t t ; 

!€ haamit>ttáá
!€ haami0>ttáá


!"Consonant lengthening after xx:x weak grade"
!!! * __@RULENAME@__  for locative kassaast, which is lengthened weak grade
!%^RC:Cx <=> Vow: Cx: Cx _ Vow: (%^SV:) %^WG: %^CLEN: ;
!        where Cx in ( p l d m s g r v ) ;

!€ ka^RVss^RCa^SV^WG^CLEN^SLEN>st
!€ ka00ssaa000>st



"Consonant lengthening after xx:x and xy:zy weak grade with x4 on upper side"
!! * __@RULENAME@__  as previous rule, but for k4, č4, p4
%^RC:Cx <=> [:Vow|%^RV:] Cy: Cz: _ Vow: (%^SV:) %^WG: %^CLEN: ;
        where Cy in ( k  č  p  k  h ) 
              Cz in ( k4 č4 p4 s  c ) 
              Cx in ( h  j  v  s  c ) 
        matched ;

"Consonant lengthening after x:y weak grade, to x:yy"
!! * __@RULENAME@__  , here we lengthen the lower y consonant, spaha -> spavvaast
%^RC:Cx <=> [:Vow|%^RV:] Cx: _ Vow: (%^SV:) %^WG: %^CLEN: ;
        where Cx in ( p l d m s r v ) ;



"Consonant lengthening after x:y weak grade with x4 on upper side"
!! * __@RULENAME@__  as previous rule, but for k4, č4, p4
%^RC:Cx <=> [:Vow|%^RV:] (Cz:) Cy: _ Vow: (%^SV:) %^WG:  %^CLEN: ;
        where Cy in ( k4 č4 p4 ) 
              Cx in ( h  j  v  ) 
              Cz in ( k  č  p  ) 
        matched ;



!€ su^RVkk4^RCá^SV^WG^CLEN^SLEN>st
!€ su00hháá000>st

!strange:

!€ kopp^RCâ^WG^CLEN^SVN>in
!€ ko0pp0á000>in


!! !Other consonant rules

"Word final consonant deletion of n m t"                 
!! * __@RULENAME@__ for vaskâm:vaaskâ
Cx:0 <=> _  (%^ÁE:) (%^SC:) (%^WG:) %^FCD: ;      
         where Cx in (n m t) ;

!rejected:
!€ va^RVskâm^RLEN^FCD
!€ vaaskâ000

!rejected:
!€ čiäpp4%^RCát%^ÁE^SC^WG^FCD
!€ č0e0v0e00000
	


!! !!Vowel rules

!! !First syllable rules
 
"Vowel shortening with consonant shortening" 
!! * __@RULENAME@__  for koonjâl -> konjâlân
Vx:0 <=> _ Vx Seg:* ([%^RC:|%^SC:]) %^CSH: (%^RVSH:|%^ILL:) [%>|.#.] ;
        where Vx in ( a e o á ä u y i ) ;

!  ([%^RC:|%^SC:])  was earlier (Dummy:*), but we changed to avoid conflict

!€ koonnjâl^CSH>ân
!€ k0on0jâl0>ân

!€ kappeer^CSH^RVSH>ist
!€ kap00er00>ist

!€ čäällim^CSH^RVSH>ist
!€ č0äḷ0im00>ist

!€ muusik^SC^CSH^RVSH>ist
!€ m0usikk00>ist

! kahhaan^SC^CSH^RVSH>ist
! ka0ha0n0j00>ist



"Vowel shortening (no consonant shortening) in consonant stems"
!! * __@RULENAME@__ kiirrâs- to kirrâsân
Vy:0 <=> _ Vx   Cns: (Cns:) (%^RC:) Vow  :Cns (%^SFV:) (%^SC:) (%^ÁI:) (%^ÁE:) (%^CLEN:) %^RVSH: [%>|.#.] ;
       where Vx in ( a e i o u á ä ) 
             Vy in ( a e i o u á ä ) 
       matched ;

!rejected:
!€ kiirrâs^RVSH>ân
!€ k0irrâs0>ân
 
"Vowel shortening in front two consonants and i-suffix"
!! * __@RULENAME@__ ráávhu- to rávhoid
Vy:0 <=> _ Vx   Cns: [ Cns: - %^RC:0 ] Vow:  %^WG: (%^CLEN:) %^SVLOW: %> [ i4: | i ] ;
       where Vx in ( a e i o u á ä ) 
             Vy in ( a e i o u á ä ) 
       matched ;

!€ mááh%^RCu%^WG%^CLEN%^SVLOW%>in
!€ m0ávvo000>in
  
 
"Vowel shortening (no consonant shortening) in vowel stems"
!! * __@RULENAME@__ (example?)
Vy:ˊ <=> Vx _  Cns: (Cns:) (%^RC:) Vow:  (%^DSH:) (%^WG:) (%^SFV:) (%^SC:) (%^ÁI:) (%^ÁE:) %^RVSH: (%^SVLOW:) [%>|.#.] ;
       where Vx in ( a e i o u y á ä ) 
             Vy in ( a e i o u y á ä ) 
       matched ;

!€ mool7^RCâ^WG^RVSH>ttáá
!€ moˊl0â00>ttáá



"Root vowel lenghtening in bisyllabic stems"
!! * __@RULENAME@__  lekkâ : leehá-
%^RV:Vx <=> Vx _ Cns:* (%^RC:) Vow: (%^SV:) (Cns:) (%^WG:) %^RLEN: ;
	where Vx in ( a e i o u y ä á ) ;

!€ le^RVkk4^RCâ^WG^RLEN
!€ lee0h0â00

"Root vowel change for á to ä with i in second syllable"
!! * __@RULENAME@__  päävir
á:ä <=>    _ á: Cns:* :i Cns: %^ÁI: [.#.|%>:] ;
        á: _    Cns:* :i Cns: %^ÁI: [.#.|%>:] ;

!rejected:
!€ páápp4ár^ÁI^WG
!€ pää0vir00

"Monophthongisation rule 1"
!! * __@RULENAME@__  u:o in pairs like kuátij -> koođij
!Vx:Vy <=> _ Vz: Cns:* i %>: j ;
Vx:Vy <=> _ Vz: Cns:* i %^WG: %>: j ;
	where Vx in ( i u ä )
		  Vy in ( e o a )
		  Vz in ( ä á ä )
	matched ;

!€ kuáti^WG>j
!€ koođi0>j

"Monophthongisation rule 2"
!! * __@RULENAME@__  á:o in pairs like kuátij -> koođij 
Vx:Vy <=> Vz: _ Cns:* i %^WG: %>: j ;
	where Vx in ( ä á ä )
		  Vy in ( e o a )
		  Vz in ( i u ä )
		  matched ;

!nom	ill
!e^RV	iä
!ie	iä
!ä	á
!ye	uá


"e to iä in Illative and Sg3 1"
!! * __@RULENAME@__  Part 1: e to i; ergi -> iärgán 1
e:i <=> _ %^RV: Cns:+ Vow: %^ILL: %> ;

"e to iä in Illative and Sg3 2"
!! * __@RULENAME@__  Part 2: ^RV to ä; ergi -> iärgán 2 (e+e(RV) -> i + ä
%^RV:ä <=> e: _ Cns:+ Vow: %^ILL: %> ;

"ie to iä in Illative and Sg3"
!! * __@RULENAME@__  ... with ^ILL as trigger; kiem´ni -> kiämnán
e:ä <=> i _  Cns:+ Vow: %^ILL: %> ;

"ä to á in Illative and Sg3"
!! * __@RULENAME@__  ... after .#. or Cns, with ^ILL as trigger; kiem´ni -> kiämnán
ä:á <=> [.#.|Cns] _  Cns:+ Vow: %^ILL: ;

"ye to uá in Illative and Sg3 1" 
!! * __@RULENAME@__  Part 1 of ye to uá for kyeli : kuálán
y:u <=> _ e:  Cns:+ Vow: %^ILL: ;

"ye to uá in Illative and Sg3 2"
!! * __@RULENAME@__  Part 2 of ye to uá for kyeli : kuálán
e:á <=> y: _  Cns:+ Vow: %^ILL: ;


!! !Diphthong rules

"Shortening diphthong"
!! * __@RULENAME@__ RD to ˊ muátu muáˊđoost
%^RD:ˊ <=> _ Cns:+ Vow: (%^SV:) %^DSH: ;


!! !Second syllable rules

"Vowel change á to e or i in 3syll"
!! * __@RULENAME@__ puttás- > putes, triggers are ^ÁE and ^ÁI
Vx:Vy <=> _ (Cns:) Vz: %^WG: (%^FCD:) ; 
       where Vx in ( á    á    a   )
             Vy in ( e    i    e   )
             Vz in ( %^ÁE %^ÁI %^ÁE) 
       matched ;

!€ puttás^ÁE^WG
!€ pu0tes00

"Vowel shortening  áá and to e or i in 3syll"
!! * __@RULENAME@__ puttás- > putes, triggers are ^ÁE and ^ÁI
Vx:0 <=> _ Vx: (Cns:) Vz: [%^WG:|%^CSH:] ; 
       where Vx in ( á    á    a   )
             Vz in ( %^ÁE %^ÁI %^ÁE) 
       matched ;

!rejected
!€ nahh4aar^ÁE^WG
!€ na0ṿ0er00

"Suffix vowel lengthening"
!! * __@RULENAME@__ long á in kissáást
%^SV:Vx <=> Vx:  _ (:Cns)  (%^DSH:) (%^WG:) (%^CLEN:) %^SLEN: (%^SVLOW: ) %>: ;
        where Vx in ( á o e a i ) ; ! pending...

!€ ki^RVss^RCá^SV^WG^CLEN^SLEN>st
!€ ki00ssáá000>st

!€ mi^RVll^RCo^SV^WG^CLEN^SLEN>st
!€ mi00lloo000>st

!€ huksi^SVm^SLEN>ist
!€ huksiim0>ist

"Suffix vowel lengthening o"
!! * __@RULENAME@__ stááḷu stááloost
%^SV:o <=> u:  _ %^DSH: %^WG: %^SLEN: %^SVLOW: %>  ; 

!€ táál7%^RCu%^SV%^DSH%^WG%^SLEN%^SVLOW%>st
!€ táál0oo0000>st

"Fleeting vowel before suffix"
!! * __@RULENAME@__ deletes -i- in piehâin to piehânjist 
i:0 <=> _ Cns %^SFV: (%^SC:) (%^CSH:) (%^RVSH:) [%>|.#.] ;

!rejected:
!€ pieˊhâin^SFV^SC^CSH^RVSH>ist
!€ pie0hâ0n0j00>ist

"Inversion of in to nj"
!! * __@RULENAME@__  adds -j-  in piehâin to piehânjist  
%^SC:j <=> n (%^SFV:) _ (%^CSH:) (%^RVSH:) [%>|.#.] ; 

!rejected:
!€ pieˊhâin^SFV^SC^CSH^RVSH>ist
!€ pie0hâ0n0j00>ist

!rejected:
!€ kahhaan^SC^CSH^RVSH>ist
!€ ka0ha0nj00>ist



"Stem vowel e deletion before 2nd syllable ij"
!! * __@RULENAME@__  hmm have a look at this
!e:0 <=> Cns: _ (Dummy:) (Dummy:) %>: i j ; unclear target !?
e:0 <=> Cns: _ Cns %^SFV: (%^SC:) (%^CSH:) (%^RVSH:) [%>|.#.] ; 

!€ šoddâdem^SFV>ist
!€ šoddâd0m0>ist

 
"Stem vowel e to a for a root vowel."
!! * __@RULENAME@__  alge to aalgan
e:a <=> a (%^RV:) Cns:+ _ (%^RLEN:) (%^CSH:) %^ILL: ;

!rejected:
!€ a^RVlge^SLEN>n
!€ aalga0>n

"Stem vowel e to á for other root vowel."
!! * __@RULENAME@__  
e:á <=> [o|i|u] (%^RV:) Cns:+ _ (%^RLEN:) (%^CSH:) %^ILL: [%>|.#.] ;




"Stem vowel â to a in Pl Nom -h"
!! * __@RULENAME@__  silbâ to siilbah
â:a <=> :Cns (%^RC:) _ (%^UU:|%^SV:) (%^WG:) (%^RLEN:|%^DLEN:) %> h ;



!€ so^RVll^RCâ^WG^RLEN>h
!€ soo0l0a00>h

"Stem vowel â > á change before i"
!! * __@RULENAME@__ sollâ to sooláid 
â:á <=> _ (%^UU:|%^SV:) (%^WG:) (%^RLEN:|%^CLEN:) %^SVLOW: %> i ; 

!€ so^RVllâ^WG^RLEN^SVLOW%>in 
!€ soo00lá000>in

!€ so^RVll^RCâ^WG^CLEN^SVLOW>in
!€ so00llá000>in



"Stem vowel u > o change before suffix"
!! * __@RULENAME@__  ... check this, add test
u:o <=> Cns: _ (%^UU:|%^SV:) (%^DSH:) (%^WG:) (%^RVSH:) (%^RLEN:|%^CLEN:) (%^SLEN:) %^SVLOW:  ; 
! stááḷu stááloust



!"Illative suffix change in bisyllabic e stems"
! * __@RULENAME@__  changes ^A to a, check this
!%^A:a <=> a (%^RV:) Cns:* e: (Dummy:*) %>: _ ;



!! !!Other rules

!! (no rules here now)
