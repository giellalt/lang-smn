! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

!! !!!Alphabet 

Alphabet   

!! !!The Inari Saami letters

 a á â ä b c d e f g h i j k l m n o õ p q !!= * @CODE@
 r s t u v w x y z æ ä ø ö å			   !!=  @CODE@
 č đ ŋ ŧ š ž				 			   !!=  @CODE@

 A Á Â Å B C D E F G H I J K L M N O Õ P Q !!= * @CODE@
 R S T U V W X Y Z Æ Ä Ø Ö Å 			   !!=  @CODE@
 Č Đ Ŋ Ŧ Š Ž  							   !!=  @CODE@
 ˊ  %- 							   !!= * @CODE@
 ':0   !!= * @CODE@ this to zero

!! !Accented letters, just in case

 É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Ê Ô Û Î Ã Ý þ Ð   !!= * @CODE@
 é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý þ ð   !!=  @CODE@
 ß ª ß ç								   !!=  @CODE@

 
 
!! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 »7     !! * »
 «7     !!  «
 %[%>%] !!  >
 %[%<%] !!  <



!! !Different archiphonemes


p4:p  t4:t k4:k č4:č r4:r l4:l h4:h   !!= * @CODE@  these are consonants that change in cg
i4:i        i2:i             	        !!= * @CODE@  this is the underlying i consonant
m7:ṃ n7:ṇ ŋ7:ŋ̣ s7:ṣ r7:ṛ đ7:đ̣ l7:ḷ    !!= * @CODE@  
j7:j̣ h7:ḥ š7:ṣ̌  v7:ṿ                  !!=  @CODE@  with dots
h5:h  y5:y i5:i                    !!= * @CODE@  these do not change 


!! !More abstract archiphonemes

!%^A:0		 = algan lodán
%^RC:0		!!= * @CODE@ = root cons., lengthened by ^CLEN - máán%RCu máánnun
%^RD:0		!!= * @CODE@ = root dipht., shortened by by ^RVSH - vuá%^RDg'gu vuáˊguttáá
%^RV:0	 	!!= * @CODE@ = root vowel, lengthened by ^RLEN - ta%^RVvlu taavlu and ^RV:ˊ when WG and suffix is 0, t, i
%^SC:0      !!= * @CODE@ = stem cons, %^SC:j, pluss lengthen after earlier shortening by %^CSH, see lyeme
%^SV:0 		!!= * @CODE@ = stem vowel, lengthened by ^SLEN u:uu - táálu%^SV tááloost

!! !Triggers

%^CLEN:0	!!= * @CODE@ = cons lengthening, with ^RC
%^CSH:0     !!= * @CODE@ = cons shortening, laaŋkku - laaŋkun
%^DLEN:0    !!= * @CODE@ = Diphtong lengthening (remove?) (not in use)
%^RVSH:0     !!= * @CODE@ = Diphtong shortening
%^EE:0      !!= * @CODE@ = Final vowel deletion in ee stems (not in use)
%^FCD:0     !!= * @CODE@ = Final consonant deletion 
%^IA:0      !!= * @CODE@ = i to á (more) (not in use)
%^EA:0     !!= * @CODE@ = e to a/á in ill etc
%^LLEN:0    !!= * @CODE@ = low vow lengthening (not in use)
%^MONO:0    !!= * @CODE@ = monophtongization of diphthongs  (not in use)
%^RLEN:0    !!= * @CODE@ = root lengthening, with ^RD
%^RVSH:0    !!= * @CODE@ = root vow shortening - máán7%^RCu máˊnuttáá 
%^SLEN:0    !!= * @CODE@ = stem vowel lengthening
%^SFV:0     !!= * @CODE@ = Deleting fleeting vowel before suffix
%^SVLOW:0   !!= * @CODE@ = stem vowel lowering â>á and u>o - árppu árppoin
%^SVSH:0   !!= * @CODE@ = stem vowel shortening kappeer kaperân
%^UU:0      !!= * @CODE@ = Final vowel deletion in uu stems
%^VLOW:0    !!= * @CODE@ = Vowel lowering in 3rd sg of contract verbs tuhhid:tohhe
%^WG:0      !!= * @CODE@ = weak grade trigger
%^ÁE:0      !!= * @CODE@ = á to e (more) in 3-syll
%^ÁI:0      !!= * @CODE@ = á to i (more) in 3-syll
%^EWG:0     !!= * @CODE@ = from extra strong to weak, e.g. pp:v
%^EI:0      !!= * @CODE@ = e to i

 
%^ODRVSHV:0     !!= * @CODE@ =  Cns shortening in C|# and shortvow in Inf etc  (not in use)
%^ODDC:0       !!= * @CODE@ =  shorten cns in odd-syll stems before C, #  (not in use)

!! !!Morpheme boundaries:

!! Here we define the suffix border >
!! The other borders are not used yet, but still defined:

 «  !!≈ ;@CODE@: Derivational prefix
 »  !!≈ ;@CODE@: Derivational suffix
 %< !!≈ ;@CODE@: Inflectional prefx
 %> !!≈ ;@CODE@: Inflectional suffix
 %^ !!≈ ;@CODE@: (exceptional) soft hyphenation point
 #  !! and the symbol #, which is Word boundary for both lexicalised and dynamic compounds

!! End of alphabet definitions

;

!! !!! Sets

Sets
 Vow = a á â ä e i o u y æ ø ö å y5	i2		   !!= * @CODE@ 
       é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý 	   !!= @CODE@ 
       A Á Â Ä E I O U Y Æ Ø Ö Å			   !!= @CODE@ 
       É Ó Ú Í À È Ò Ú Ì Ë Ü Ï Ê Ô Û Î Ã Ý 	   !!= @CODE@ 
       %^RV %^RD %^SV ;						   !!= @CODE@ 
 RealVow = a á â ä e i o u y æ ø ö å y5	i2	   !!= * @CODE@ 
       é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý 	   !!= @CODE@ 
       A Á Â Ä E I O U Y Æ Ø Ö Å			   !!= @CODE@ 
       É Ó Ú Í À È Ò Ú Ì Ë Ü Ï Ê Ô Û Î Ã Ý ;   !!= @CODE@ 
 Cns = b c č d đ f g h j k l m n ŋ 			   !!= * @CODE@ 
       p q r s š t ŧ v w x z ž				   !!= @CODE@ 
       B C Č D Đ F G H J K L M N Ŋ			   !!= @CODE@ 
       P Q R S Š T Ŧ V W X Z Ž 				   !!= @CODE@ 
       ht g2 k2 m2 n2 							   !!= @CODE@ 
       b3 d3 g3 m3 h3 						   !!= @CODE@ 
       p4 k4 č4 r4 l4 h4 t4 i4                  !!= @CODE@
       h5                                       !!=  @CODE@
       m7 n7 ŋ7 v7 s7 r7 đ7	l7 j7 h7 š7		   !!= @CODE@ 
       ṃ  ṇ  ŋ̣  ṿ  ṣ  ṛ  đ̣  ḷ  j̣  ḥ  ṣ̌         !!=  @CODE@ 
       %^RC %^SC  ˊ ;                         !!=  @CODE@ 
 Geminate = b d g p h t k c f z l m n ŋ r s š v  ;  !!= * @CODE@  
 Seg = Vow Cns ;							   !!= * @CODE@ 
 NotSuffixBorder = Vow %^FCD Cns %^SFV %^EWG %^CSH %^WG %^CLEN %^RLEN %^SVLOW %^RVSH %^ÁE %^ÁI %^EA ; !!= * @CODE@ 
 Triggers =  %^SFV %^FCD %^WG %^EWG %^CSH %^CLEN %^RLEN %^RVSH %^SVLOW %^ÁE %^EA %^ÁI ; !!= * @CODE@ 
 StemCns = b b3 d d3 g g2 g3 h h2 h3 		   !!= * @CODE@ 
           j l m m2 m3 m8 n n8 n2 ŋ r s  ;     !!= @CODE@ 



           
!! !!!Definition section

!! Introducing a WG right context 
!! {{{WeakGrade =  (RealVow:) :RealVow  (Cns:) ([%^SV:|%^ÁI:|%^ÁE:]) (%^FCD:) %^WG:0   ; }}}  

Definitions

WeakGrade =  (RealVow:) :RealVow (Vow:)  (Cns:) ([%^SV:|%^ÁI:|%^ÁE:|%^EA:]) (%^FCD:) %^WG:0     ;   
ConsonantShorting =  (RealVow:) :RealVow (Vow:)  (Cns:+) ([%^SV:|%^ÁI:|%^ÁE:|%^EA:] ) (%^FCD:) %^CSH:0     ;   


!! As we look at more cases, the WeakGrade definition tends to be longer. 
!! We want to keep it short, but we want the code to work Thus, we document.
!! * The second :RealVow is there for long consonant stems
!! * The (Cns:) is there for consonant stems

!! !!!Rule section

Rules

!! !!Consonant rules

!! !Consonant gradation

"x7:x ja x:y consonant gradation"
!! * __@RULENAME@__ \\  WG gives x7:x and x:y, when there is no consonant lengthning  
Cx:Cy <=> Vow: _ (%^RC: )  WeakGrade ([%^RVSH:|%^RLEN:]) (%^SLEN:) (%^SVLOW:) %> ;
	where Cx in ( m7 n7 ŋ7 r7 s7 š7 j7 v7 đ7 l7 h p c )   !č
          Cy in ( m  n  ŋ  r  s  š  j  v  đ  l  ṿ v s )   !j7
		  matched ;


!€ suá^RDn7^RCju^WG>
!€ suá0n0ju0>

"č:j7 consonant gradation"
!! * __@RULENAME@__ \\ 
č4:j̣ <=> Vow: _ (%^RC: )  WeakGrade \%^CLEN: ;


!iˊṣ̌e:iˊše

!€ i^RVš7^RCe^WG>
!€ iˊš0e0>

"n7j:nj consonant gradation and lengthning"
!! * __@RULENAME@__ \\   n7:n in the combinataion n7j:nj 
n7:n <=> Vow: _ (%^RC: ) j WeakGrade \%^CLEN:0 ; !! 1) weak grade
         Vow: _ %^RC:  j RealVow:  %^CLEN:0 ; !! 2) consonantlengthning
         Vow: _ %^RC:  j RealVow: %^WG:0 %^CLEN:0 %^SVLOW:0 ; !! 3) weak grade and consonant lengthning 




"xx:x consonant gradation and shortening and x:x shortening"
!! * __@RULENAME@__ \\   weak grade or consonant shortening for illativ
Cx:0 <=> Vow: _ (ˊ:) Cx: WeakGrade \%^CLEN: ;
         Vow: _ (ˊ:) Cx: ConsonantShorting \%^CLEN:  ;
	where Cx in ( b d đ g p t k c č f z ž s š v h ) ; !lnŋr kahhaanj 

!cuozˊzâ:cuozâ
!€ cuo^RDzˊzâ^WG^RLEN>
!€ cuo000zâ00>

!váddu:váádun
!€ vá^RVddu^RLEN^CSH>n
!€ váá0du00>n

!kahhaan:kahanân
!€ kahhaan^CSH^SVSH>ân
!€ ka0h0an00>ân


"xx:x consonant shortening for consonants belonging to two different patterns"
!! * __@RULENAME@__ \\   weak grade or consonant shortening for illativ
Cx:0 <=> Vow: _ (ˊ:) Cx: WeakGrade ([%^RVSH: |%^RLEN:]) ([%^SVLOW: | %^SLEN:]) %> ; !! 1) weak grade päddi : päädi tullâ:tuuláin
         Vow: _ (ˊ:) Cx: Vow: (%^EA: ) %^CSH: ( %^RVSH:) %> ; !! 2) consonant shortening for illativ, päddi : páádán RLEN?
	where Cx in ( l n ŋ r m ) ; !


!luččâ:luuj̣áin 

!€ lu^RVčč4â^WG^RLEN^SVLOW>in
!€ luu0j̣á000>in

"ˊ to zero gradation"
!! * __@RULENAME@__ \\ when weak grade or consonant shortening 
ˊ:0 <=> Vow: Cns: _ Cns:+  WeakGrade \%^CLEN:   ; !! 1) weak grade, mie%^RDlˊgâs
        Vow: Cns: _ Cns:+  Vow: (%^EA:)  %^CSH: ; !!  2) consonant shortening for illativ
        Vow: č:   _ č4: WeakGrade %^CLEN: %^SVLOW: %> ; !! 3) weak grade and consonant lengthning, kiäčˊču kiäj̣u 
        Vow: RealVow:0 %^RD:  Cns:  _ Cns:+  WeakGrade %^CLEN: %^SVLOW: %> ; !! 4) uábˊbi:obbijn



"xx:y to something else, consonant gradation and shortening, part 1" 
!! * __@RULENAME@__ \\   hh:V, kk:h, pp:v, čč:J, deleting first part, k:0 in kk4:h etc  
Cx:0 <=> Vow: _ (ˊ:) Cy: (%^RC:) WeakGrade \%^CLEN: ; !! 1) weak grade: ruttâ:ruuđáin 
         Vow: _ (ˊ:) Cy: Vow: (%^EA: ) %^CSH: (%^RLEN:)  %> ; !! 2) consonant shortening for illativ
         Vow: _ (ˊ:) Cy: Vow:+  Cns (%^ÁI:|%^ÁE:) %^CSH: ([%^RLEN: | %^RVSH:])  (%^SVSH:)  %> ; !! 3) consonant shortening for páápp4ár%^ÁI
	where Cx in ( h  h4 t 	k4 p    l  n  s  r  š  m  v  đ) 
	      Cy in ( h4 h4 t4 k4 p4  l7 n7 s7 r7 š7 m7 v7  đ7)
		  matched ;


"č:0 in čč4:j,  part 1" 
!! * __@RULENAME@__ \\   
č:0 <=> Vow: _ (ˊ:) č4: (%^RC:) WeakGrade ; !! 1) weak grade: luččâ:luučân
        Vow: _ (ˊ:) č4: Vow: (%^EA: ) %^CSH: (%^RLEN:)   %> ; !! 3) consonant shortening for illativ: luččâ:luučân



"xx:y to something else, consonant gradation, part 2" 
!! * __@RULENAME@__ \\  hh:V, kk:h, pp:v, čč:J here we change second part, 
Cx:Cy <=> Vow:  Cz: (ˊ:) _  WeakGrade  ; !! 1) weak grade: 
          Vow:  Cz: _ :i %^SV: Cns %^ÁI: %^WG: %^RVSH: %> ;
	where Cx in ( h4  č4 ) 
	      Cy in ( ṿ    j̣ ) 
	      Cz in ( h4   č ) 
		  matched ;

"RV:ˊ in weak grade or root vowel shortening"
!! * __@RULENAME@__ \\  saˊj̣e 
%^RV:ˊ <=>  _ Cns:+  WeakGrade   %> ; !
            _ Cns:+ Vow: (Cns) (Triggers: - %^RLEN:)*   %^RVSH: (%^CLEN:) (%^SVLOW:) %> ; !si%^RVjvo%^WG%^RVSH%^CLEN%>in
       Vow  _ Cns:+ (j) Vow:+ Cns [%^CSH:|%^WG: | %^EWG:] %^SVSH: %> ; !kilgool kiˊlgolân kolgooh koˊlgoohijn vinnjoo	a%^RVlmoos


"Long vowel shortening (no consonant shortening)"
!! * __@RULENAME@__ \\ (example?), no ^CSH, since that gives Vy:0
Vy:ˊ <=> Vx: _ [Cns: - č4: ] Cns:* (%^RC:) Vow:  %^WG: %^RVSH: (%^SVLOW:) [%>|.#.] \e ; !! 1) 2st stems
         Vx: _  Cns:+         Vow:  Cns: %^CSH: %^RVSH: %^SVSH: [%>|.#.] ; !! 2) 3st stems kaavpug:kaˊvpugân 
         Vx: _ Cns:+ Vow: (%^SV:) Cns:+ Vow: Cns:  %^RVSH: [%>|.#.] ; !táálhudem koomerdem
       where Vx in ( a e i o u y á ä ) 
             Vy in ( a e i o u y á ä) 
       matched ;

"From i4j7 to ˊj"
!! * __@RULENAME@__ \\ sai4j7oos 
i4:ˊ <=>  Vow: _ j:  Vow:+ Cns [%^EWG:|%^WG:] %^SVSH:  [%>|.#.] ; !! 1) saijos:saˊjos



"Long vowel shortening, č:j̣ "
!! * __@RULENAME@__ \\ special rule for getting ej̣ittáá instead of eˊj̣ittáá.
Vy:0 <=> Vx: _ č4: (%^RC:) Vow:  %^WG: %^RVSH: (%^SVLOW:) [%>|.#.] ; !! 1) 2st stems
     where Vx in ( a e i o u y á ä ) 
           Vy in ( a e i o u y á ä ) 
     matched ;


"Underlying long vowel shortening iä:eˊ"
!! * __@RULENAME@__ \\ 
Vy:ˊ <=> Vx: _  Cns:+ Vow:  Cns: [%^ÁI:|%^ÁE:] [%^WG:|%^EWG:] %^RVSH:  [%>|.#.] ; !! 1) 3st stems viäsˊkár veˊskirin, časkes čaˊskes ča%^RVskas%^ÁE viäsˊkár%^ÁI%^EWG%^RVSH
       where Vx in ( i ) 
             Vy in ( ä ) 
       matched ;


!kaavpug:kaˊvpugân
!€ kaavpug^RVSH^CSH^SVSH>ân
!€ kaˊvpug000>ân


! mool7â to moolâ:		  
		  
!€ mool7^RCâ^WG>
!€ mool0â0>


!käähir to kääṿir

!€ käähir^WG>
!€ kääṿir0>

!juáhu to juáˊṿuttáá

!€ juá^RDh^RCu^WG^RVSH>ttáá
!€ juáˊṿ0u00>ttáá


"t:đ, also tˊk4:đh gradation, part 1" 
!! * __@RULENAME@__ \\   
t:đ <=> Vow: _  (ˊ:) k4: (%^RC:) WeakGrade ; !for ito : iđo 
        Vow: _  %^RC:   j RealVow: %^WG:0 %^CLEN:0 %^SVLOW:0 ; ! Com
        Vow: _ (%^RC:) (j) WeakGrade (%^CLEN:0) ;
        Vow: _  %^RC:   j RealVow:  %^CLEN:0 ; ! Par and Ess


!€ jo^RVtk4â^WG^RLEN>
!€ joođhâ00>

"k4:h gradation for k4k4, tk4, rkk4, lkk4"
!! * __@RULENAME@__ \\    for atka : ađha 
k4:h <=> Vow: [t:|k4:] (ˊ:) _  WeakGrade  ;
         Vow: [l | r | v | i4: | đ ] (ˊ:) k: _  WeakGrade ;
         Vow: [l | r | v | i4: | đ ] (ˊ:) k: _ Vow:+ %^EWG:  ; !rˊkk	rh
         Vow: _ (ˊ:) k4: WeakGrade %^CLEN: ;
         
!€ mä^RVtk4i^WG^RLEN>.#.
!€ määđhi00>.#.

!€ su^RVk4k4á^SV^WG^CLEN^SLEN>st
!€ su0hháá000>st

"h4:v gradation for h4h4"
!! * __@RULENAME@__ \\   
h4:v <=>  _ h4: Vow:+ Cns %^ÁI:  %^EWG: %^RLEN: %> ;
        h4:  _  Vow:+  Cns %^ÁI:  %^EWG: %^RLEN: %> ;

"k:0 gradation for xkk4"
!! * __@RULENAME@__ \\  for lkk4, rkk4, vkk4
k:0 <=> Vow: [l | r | v | i4: | đ ] (ˊ:) _ k4:h  WeakGrade (%^CLEN: ) (%^SVLOW:) ; !! 1) WeakGrade 
        Vow: [l | r | v | i4: | đ ] (ˊ:) _ k4:k Vow: (%^EA:) %^CSH: (%^RLEN:)   %> ; !! 2) consonant shortening for illativ
        Vow: [l | r | v | i4: | đ ] (ˊ:) _ k4:h Vow:+ %^EWG:  ; !rˊkk	rh



"t4:đ gradation for tt4" 
!! * __@RULENAME@__ 
t4:đ <=> t: (ˊ:) _ WeakGrade ; !! 1) ruttâ ruuđâ
         t: (ˊ:) _ Vow:+ %^EWG: ; !! 2) tietˊtu tieđâ
!€ ru^RVtt4â^WG^RLEN>
!€ ruu0đâ00>

 
"t:0 foran t4" 
!! * __@RULENAME@__ 
t:0 <=> _ (ˊ:) t4: Vow:+ %^EWG: ; !! 1) tietˊtu tieđâ        

!rejected:       
!€ haavvâr^CSH^RVSH>ist
!€ h0aṿ0âr00>ist

!rejected:       
!€ pottáák^SC^CSH^SVSH>ist
!€ po0t0ákk00>ist

"xy:zy gradation"
!! * __@RULENAME@__ \\  ht:vt, we use h4 
Cx:v <=>  _ (ˊ:) Cz (%^RC:) Vow (%^SV: ) (Cns:)  (%^FCD:) %^WG: ;
         _  Cz t Vow (%^FCD:) %^WG: ; !pi%^RVh4tâs
       where Cx in ( h4 k4 h4 h4 ) 
             Cz in ( t  s  č  c  ) 
       matched ;


!€ pih4tâs^WG
!€ pivtâs0

!€ totk4os^WG
!€ tođhos0

"xyy:xy gradation A"
!! * __@RULENAME@__ \\   
Cy:0 <=> :Vow ([%^RV: | %^RD: ]) Cx: (ˊ: ) _ Cz: (%^RC:) Vow (%^SV:) (Cns)  %^WG: ( [%^RLEN: |%^RVSH:]) %>  ;  
          Cx: (ˊ:) _ Cz:  Vow (%^EA:) %^CSH: (%^RLEN:)    ;  
      where Cx in ( j i4 i4 j i4 l l l l l l l m m n n n ŋ r r r r r r r v ) 
            Cy in ( p c  t  t k  c k p s t č š p č c s t k c p k s t č š p ) 
            Cz in ( p c  t t k  c k p s t č š p č c s t k c p k s t č š p ) 
      matched ;

!€ á^RVrppu^WG^RLEN>
!€ áár0pu00>

!€ á^RVrppu^WG^RVSH>ttáá
!€ áˊr0pu00>ttáá

!€ riänttus^WG>
!€ riän0tus0>


"xx:x7 gradation for ^CSH illative 1, and x:x7 for ^CSH" 
!! * __@RULENAME@__ \\   
Cx:Cy <=> Cx: _ Vow: %^CSH: %^RLEN: %> n ; !! 1) 2st stems hellá:heeḷán
          Cx: _ Vow: (Vow:) Cns: (Cns:) %^CSH: (%^SVSH:) [%>|.#.] ; !! 2) 3st stems aarrân:aṛânân, iššeed:iṣ̌edân, but not for kiärunj:kiäˊrunjân, jiereeh:jieˊrehân
      where Cx in ( r l n ŋ m š ) 
            Cy in ( ṛ ḷ ṇ ŋ̣ ṃ ṣ̌ ) 
      matched ;

!iäláán:iäˊḷánân  
!€ iä^RDláán^CSH^SVSH>ân
!€ iäˊḷ0án00>ân



"xx:x7 gradation for ^CSH illative 2" 
!! * __@RULENAME@__ \\    
Cx:0 <=> _  Cx:Cy Vow: %^CSH: %^RLEN: %> n ; !! 1) 2st hellá:heeḷán
         _  Cx:Cy Vow: (Vow:) Cns: (Cns:) %^CSH: (%^SVSH:) [%>|.#.] ; !! 2) 3st stems aarrân:aṛânân
      where Cx in ( r l n ŋ m ) 
            Cy in ( ṛ ḷ ṇ ŋ̣ ṃ ) 
      matched ;

!kurrâ kuuṛân
!€ ku^RVrrâ^RLEN^CSH>n
!€ kuu0ṛâ00>n

!sollâ sooḷân
!€ so^RVllâ^RLEN^CSH>n
!€ soo0ḷâ00>n

!! !Consonant shortening

"Word final consonant deletion"                 
!!  * __@RULENAME@__ for vaskâm:vaaskâ
Cx:0 <=> _  %^FCD: ;      
          where Cx in (n m t v j s š) ;

!rejected, look at this:
!€ va^RVskâm^FCD^RLEN
!€ vaaskâ000
	
"Final m in stems, mn to n"  
!!  * __@RULENAME@__ for puhelim :  puhelin   
m:0 <=>  _ n  [%>|.#.] ;          


"nnj to nj shortening weak grade and consonant shortening"
!! * __@RULENAME@__ \\  special rule, e.g. konnjâl -> konjâlân puá%^RDnˊnjá
n:0 <=> n (ˊ:) _ j ConsonantShorting \%^CLEN: ; !! 1) 
        n (ˊ:) _ j WeakGrade \%^CLEN: ; !! 2)lonnjâ:loonjáin
        n7: (ˊ:) _ j Vow:+ Cns %^CSH: %^SVSH: %> ; !vinnjooh:viˊṇjohân


!€ konnjâl^CSH>ân
!€ kon0jâl0>ân



!€ suá^RDn7^RCju^CLEN>n
!€ suá0nnju0>n

!rejected, look at tis:
!€ haamit^SC^CSH^RVSH>ist
!€ h0amitt00>ist


!€ su^RVk4k4á^SV^WG^CLEN^SLEN>st
!€ su0hháá000>st

!rejected:
!€ kopp^RCâ^WG^CLEN^SVN>in
!€ ko0pp0á000>in


!! !Consonant lengthening



"Lengthening of half-long root consonant x7:x 1"
!! * __@RULENAME@__ \\  for l-dot to ll, changing l7 to first l in ll
Cx:Cy <=> Vow: _ %^RC: :Vow Seg:* Triggers:* %^CLEN: ;
	where Cx in ( l7 r7 s7 h7 m7 n7 đ7 v7 ŋ7 š7 )
	      Cy in ( l  r  s  h  m  n  đ  v  ŋ  š  ) 
		  matched ;

"Lengthening of half-long root consonant j7:j 1"
!! * __@RULENAME@__ \\  
j7:j <=> i4: _ Vow:+ Cns (%^EWG:) (%^SVSH:) [%>|.#.] ;
!         i4: _ Vow:+ Cns ( %^SVSH:) [%>|.#.] ;
 
         
"Lengthening root consonant j7:ij 1"
!! * __@RULENAME@__ \\  for saje:saijeen
j7:i <=> Vow: _ %^RC: :Vow (%^RVSH:)  %^CLEN:         %> ; !Par Ess
         Vow: _ %^RC: :Vow %^SV: %^CLEN: %^SLEN: %> ; !Par Ess



!€ sa^RVj7^RCe^SV^CLEN^SLEN>n
!€ sa0ijee00>n

!€ pááj7^RCá^CLEN>n
!€ pááijá0>n

"Lengthening of half-long root consonant x7 : xx 2"
!! * __@RULENAME@__ \\  
%^RC:Cy <=> Vow: Cx: _ :Vow+ Seg:* Triggers:* %^CLEN: ;
	where Cx in ( l7 r7 s7 h7 m7 n7 đ7 j7 v7 ŋ7  š7 )
		  Cy in ( l  r  s  h  m  n  đ  j v  ŋ   š  ) 
		  matched ;

"Lengthening of consonant, e.g. for Ess/Par"
!! * __@RULENAME@__ \\   piiru piirrun niisu niissun vahe vahheen
%^RC:Cy <=> Vow: Cx: _ Vow (%^SV:)  %^CLEN: (%^SLEN:) %> [ n | d ] ; !kiä%^RDr%^RCun%^CLEN%>ttáá
            Vow: Cx: _ Vow Cns  %^CLEN:  %>  ;
            Vow: Cx: _  Vow: Cns:+ Vow: Cns: %^SFV: (%^RLEN:) %^CLEN:  ; !! 5) keeppidmân  ke%^RVp%^RCidem%^RLEN%^CLEN%>ân
	where Cx in ( t r s h h5 c m p)  
		  Cy in ( t r s h h c m p)   
		  matched ;



!kiehâ  kievváin
!€ kie^RDh^RCâ^WG^CLEN^SVLOW>in
!€ kie0vvá000>in

!iše iššeest
!€ i^RVš7^RCe^SV^WG^CLEN^SLEN>st
!€ i0ššee000>st

!€ kol7^RCe^CLEN>
!€ kolle0>

"Lengthening of h and p to vv, for everything except Ess/Par 1"
!! * __@RULENAME@__ \\  for h to vv p to vv čääc%^RCi
Cx:Cy <=> Vow: _ (:ˊ) %^RC: :Vow (%^SV:) %^WG: (%^RVSH:) %^CLEN: ([%^SVLOW: |%^SLEN:])  %> \[ n | d ]  ;
	where Cx in ( h p c )
		  Cy in ( v v s ) 
		  matched ;

"Lengthening of h and p to vv, for everything except Ess/Par 2"
!! * __@RULENAME@__ \\   for h to vv  p to vv   
%^RC:Cy <=> Vow: Cx: (:ˊ) _ :Vow  (%^SV:) %^WG: (%^RVSH:) %^CLEN: ([%^SVLOW: |%^SLEN:]) %> \[ n | d ] ;
	where Cx in ( h p c )  
		  Cy in ( v v s )   
		  matched ;



!kassa:kassaast
!€ ka^RVss^RCa^SV^WG^CLEN^SLEN>st
!€ ka0ss0aa000>st

!vahe vavveest
!€ va^RVh^RCe^SV^WG^CLEN^SLEN>st
!€ va0vvee000>st

!vahe vahheen
!€ va^RVh^RCe^SV^CLEN^SLEN>n
!€ va0hhee00>n

!€ kiel7^RCâ^CLEN>n
!€ kiellâ0>n



"n7j to nnj consonant lengthning"
!! * __@RULENAME@__ \\  special rule, suán7ju : suánnjun for Par and Ess
%^RC:n <=> Vow: n7: _ j RealVow: %^CLEN: %> [ d | n ] ;



"Consonant lengthening after xx:x and xy:zy weak grade with x4 on upper side"
!! * __@RULENAME@__ \\  as previous rule, but for k4, č4, p4  kukse:kuvssijn  
%^RC:Cx <=> Vow:  Cy: (ˊ:) Cz: _ WeakGrade %^CLEN: \%^SLEN: ; 
        where Cy in ( k  č  p  k  h h4 h4 h4 k4 ) 
              Cz in ( k4 č4 p4 s  c t  č c s ) 
              Cx in ( h  j  v  s  c t  č c s ) 
        matched ;

"Consonant lengthening č:čč"
!! * __@RULENAME@__ \\  as previous rule, but for k4, č4, p4  kukse:kuvssijn  
%^RC:č <=> Vow:  č4:  _ Vow %^CLEN:  ; 


"Mark consonant length after diphthong for Pl Ill"
0:ˊ <=> %^RD: Cx: _ :Cy Vow: %^WG: %^RVSH: %^CLEN:  %> i: [d | n ] ; !! 
        where Cx in ( l h) 
              Cy in ( g v ) 
        matched ;


"From extra strong to weak, part 1 cc:s, rr:r"
Cy:0 <=>  Cx: (ˊ:0) _  Vow:+ (Cns:) ([%^ÁI:|%^ÁE:]) (%^FCD:) %^EWG: ; ! ([%^RVSH:|%^RLEN:|%^SVSH:|%^SLEN:]) [%>|.#.] ; !! poccuu:puásui  kiepˊpu:kiepâ iäbˊbár%^ÁI cuobˊbuu:cuábui kie%^RDllâs
       where Cx in ( b c đ d g h l l m m n r t p p  đ  n  r s) 
             Cy in ( b c đ d g h l l7 m m7 n r t p p4 đ7 n7 r7 s7 ) 
        matched ;

"From extra strong to weak, remove ˊ "
ˊ:0 <=>  Cx: _  Cy: Vow:+ (Cns) (%^ÁI:)  %^EWG: ([%^RVSH:|%^RLEN:]) [%>|.#.] ; !viäsˊkár%^ÁI kuo%^RDk4ˊsu kuo%^RDk4ˊsu
       where Cx in  ( k4 b d g l l t p h s r r n n  t) 
              Cy in ( s  b d g l g t p h k v m n n7 t4) 
        matched ;


"From extra strong to weak, part 2"
Cx:Cy <=>   _ (ˊ:) Cz:  Vow:+ (Cns) (%^ÁI:) %^EWG: ([%^RVSH:|%^RLEN:|%^SVSH:]) [%>|.#.] ; !! 1) poccuu:puásui  páápp4ár%^ÁI
        where Cx in ( c k4 h p  h4 ) 
              Cy in ( s v  ṿ v  v  ) 
              Cz in ( c s  h p4 t  ) 
        matched ;

"In front of %^EWG – i4j:j"
i4:0 <=>   _ j  Vow:+ (Cns)  %^EWG: ([%^RVSH:|%^RLEN:]) [%>|.#.] ; ! poijuu:puájui  va%^RVi4jâs


!! !Other consonant rules

"xˊx7:xˊx and xx7:xx" 
!! * __@RULENAME@__ \\  
Cx:Cy <=> Vow:  Cy (ˊ:) _  Vow:+ (Cns) (%^ÁE:) (%^CLEN:) [%>|.#.] ;
		  Vow:  Cy (ˊ:) _  Vow:+ %^WG:  %^CLEN:  ;
		  Vow:  _ (ˊ:) Cy j Vow:+ Cns (%^CSH:) (%^RLEN:)  [%>|.#.]  ; !vi%^RVn7njooh
	where Cx in ( l7 n7 s7 š7 r7 m7 v7 đ7) 
	      Cy in ( l  n  s  š  r  m  v  đ)
		  matched ;

!€ puá^RDlˊl7u>
!€ puá0lˊlu>

!€ puá^RDlˊl7u^WG^RVSH>ttáá
!€ puáˊ00ḷu00>ttáá

!€ ká^RVll7u>
!€ ká0llu>



"Assimilation of root consonant t"
!! * __@RULENAME@__  delet t in front of -ttáá, avoid -tttáá
Cx:0  <=> _ (%^CSH:) (%^RLEN:) %> Cy Cy ; 
        Vow Cy _ [â:|e:] m %^SFV: ; ! vajoidittem vajoiditmáin
	where Cx in ( d t š) 
	      Cy in ( d t š)
		  matched ;

!€ haamit>ttáá
!€ haami0>ttáá


!! !!Vowel rules

!! !First syllable vowel shortening


!€ koonnjâl^CSH>ân
!€ k0on0jâl0>ân

!€ kappeer^CSH^SVSH>ist
!€ ka0p0er00>ist

!€ čäällim^CSH>ist
!€ č0ä0ḷim0>ist

!This is not correct, Check it:
!€ muusik^SC^CSH^RVSH>ist
!€ m0usikk00>ist

! kahhaan^SC^CSH^RVSH>ist
! ka0ha0n0j00>ist

!aarrân:aṛânân
!€ aarrân^CSH>ân
!€ 0a0ṛân0>ân



"Vowel shortening (no consonant shortening) in consonant stems"
!! * __@RULENAME@__ \\ kiirrâs- to kirrâsân, 
Vy:0 <=> Seg: _ Vx Cns: (Cns:) (%^RC:) Vow  :Cns (%^SFV:) (%^SC:) (%^ÁI:) (%^ÁE:) (%^CLEN:) %^RVSH: [%>|.#.] ;
       where Vx in ( a e i o u á ä ) 
             Vy in ( a e i o u á ä ) 
       matched ;

!rejected:
!€ kiirrâs^RVSH>ân
!€ k0irrâs0>ân
 
"Vowel shortening VV:V"
!! * __@RULENAME@__ \\ ráávhu- : rávhoid, käähir%^CSH%>id
Vy:0 <=> Vx: _  [[Cns: Cns:] - [Cns: :0]] Vow: (Cns:) [Triggers: - %^RVSH:]* %> i: ;
         Vx: _  Cns:+ Vow:  %^WG: %^RVSH: %^CLEN:  %> i:  ; !! 2) čääci : čassijn
         Vx: _  Cns:+ Vow: (%^CSH:) %^RVSH:  %>  ; !! 3) kaamuv kammust
         Vx _ Cns Cns Vow: %^SV: Cns:+ Vow: Cns %^SFV: %^RLEN: (%^CLEN:) %^SLEN: %> ; !! 4) kiiško%^SVdem
		 Vx: _ [Cns:+ - č4:] Vow:+ Cns:+ (%^ÁI:) %^CSH: (%^SVSH:) [%>|.#.] ; ! pááppár : pápárân
       where Vx in ( a e i o u á ä ) 
             Vy in ( a e i o u á ä ) 
       matched ;

"Vowel shortening eej to eij"
!! * __@RULENAME@__ \\  leeji : leijijn  
e:0  <=> e _  j7: %^RC: Vow:  %^RVSH: %^CLEN:  %>  ; !!  


"Stem vowel shortening in front of"
!! * __@RULENAME@__ \\ 
Vx:0 <=> Vy _ %^EE: ;
       where Vx in ( e o u ) 
             Vy in ( e o u ) 
       matched ;



!€ mááh^RCu^WG^CLEN^SVLOW>in
!€ má0vvo000>in
  
 
!€ mool7^RCâ^WG^RVSH>ttáá
!€ moˊl0â00>ttáá


!! !First syllable vowel lengthening

"Root vowel lenghtening in many stems"
!! * __@RULENAME@__ \\  lekkâ : leehá-  meecij ko%^RVppâin
%^RV:Vx <=> Vx _ Cns:+  Vow: (Vow:)  (Cns:) (Triggers: - %^RLEN:)*   %^RLEN: ; !! 1) 2st and 3st
            Vx _ Cns: (Cns:) Vow:+ Cns:+ Vow: Cns: %^SFV: %^RLEN:  ; !! 2) 4st 
	where Vx in ( a e i o u y á ) ; !ä in a special rule
		
!€ le^RVk4k4â^WG^RLEN>
!€ lee0hâ00>

!€ nju^RVn7e^SV^WG^RLEN^SLEN>st
!€ njuunee000>st

"Root vowel ä:ää lenghtening"
!! * __@RULENAME@__ \\ special rule, for avoiding conflict
%^RV:ä <=> ä _ Cns:+ Vow:+ Cns:+ Vow: Cns: %^SFV: %^RLEN: %^CLEN: ; !! 5) vä%^RVr7%^RCid
           á:ä _ Cns:* :i (%^SV:) Cns: %^ÁI: (%^FCD:) [%^EWG:|%^WG:] %^RLEN: [%>|.#.] ;


"Root vowel lengtning with vowel change before suffix i"
!! * __@RULENAME@__ \\  alge : oolgijn 
%^RV:Vy <=> Vx:Vy  _  Cns:+  e: %^WG: %^RLEN:  %> i: ; 
       where Vx in ( a o )
             Vy in ( o u ) 
       matched ;

!€ a^RVlge^WG^RLEN>id
!€ oolgi00>jd





!! !First syllable vowel change

"Root vowel change for á to ä with i in second syllable"
!! * __@RULENAME@__ \\  päävir já%^RVh4h4á%^SVl%^ÁI%^EWG%^RLEN%>
á:ä <=>    _ [á:|%^RV:] Cns:* :i (%^SV:) Cns: %^ÁI: [%^EWG:|%^WG:] [%^RLEN:|%^RVSH:] [%>|.#.] ; !! 1) pááppár:päävir
        á:ä _    Cns:* :i (%^SV:) Cns: %^ÁI: %^EWG: %^RLEN: [%>|.#.] ;  !! 2) pááppár:päävir
         _  á: Cns:+ Vow: Cns  %^ÁI:  %^WG:  %^RVSH:  %> i  ; !! 3) pááppár:pävirin

!rejected, look at this:
!€ páápp4ár^ÁI^WG>
!€ pää0vir00>



"Root vowel change before suffix i"
!! * __@RULENAME@__ \\  alge : olgijn 
Vx:Vy <=> _  %^RV: Cns:+  e: (%^WG:) ([%^RLEN: |%^CLEN:] )  %> i: ; 
       where Vx in ( a o )
             Vy in ( o u ) 
       matched ;

"Root vowel change u:o, special rule for olmooš"
!! * __@RULENAME@__ \\  ulmu : olmooš. Waiting for more lemmas. u%^RVlmu%^SV%^WG%^SLEN%^SVLOW%>š
u:o <=>   _  %^RV: Cns:+ u:o %^SV: %^WG: %^SLEN:  %^SVLOW:  [%>|.#.]  ;
         _  %^RV: Cns:+ u:o %^WG: %^RVSH:  %^SVLOW:  [%>|.#.]  ; 



!! !First syllable diphthong rules



"Shortening diphthong"
!! * __@RULENAME@__ \\ RD to ˊ muátu muáˊđoost  mie%^RDlˊgâs čiermih vuá%^RDlá%^RVSH%>áš luá%^RDmm7á%^SVn
%^RD:ˊ <=>   _ \j7:i Seg:+ [Triggers: - %^RVSH: ]* %^RVSH:  ; !! not suoˊijijn
            Vow: _ Cns:+ Vow: (Vow:) Cns: (Cns:) (%^ÁE:) %^CSH:  (%^SVSH:) [%>|.#.] ; !! 2) iäláán : iäˊḷánân


! RV still does not give as good result as RD.

"Vowel shortening for uá" 
!! * __@RULENAME@__ \\  for uábbi -> obbijn  čuá%^RDl7%^RCi%^WG%>in
á:0 <=> u: _ %^RD: Cns:+ i %^WG: %^CLEN: %^SVLOW: %> i:  ;
        u: _ %^RD: Cns:+ i %^WG: %^RVSH: %> i:  ;


"Monophthongisation rule i:e"
!! * __@RULENAME@__ \\  
Vx:Vy <=> _ Vz: Cns:* i %^WG: %> i: ;  
          _ Vz: Cns:* Vow: Cns %^ÁI: [%^WG:|%^EWG:] ([%^RLEN:|%^RVSH:]) [%>|.#.]  ; !! 1)viäskár veeskir iäbˊbár eebir 
	where Vx in ( i )
		  Vy in ( e )
		  Vz in ( ä)
	matched ;

"Monophthongisation rule u:o"
!! * __@RULENAME@__ \\  u:o in pairs like kuátij -> koođij čuá%^RDl7%^RCi%^WG%>in
Vx:Vy <=> _ Vz: Cns:* i %^WG: %> i: ;  !Her
         _ Vz:0 %^RD: Cns:* i %^WG: %^CLEN: %^SVLOW: %> i:  ;
         _ Vz: %^RD: Cns:* i %^WG: (%^RLEN:) %> i: ; 
         _ Vz:0 %^RD: Cns:+ i %^WG: %^RVSH: %> i: ;
	where Vx in ( u )
		  Vy in ( o )
		  Vz in ( á )
	matched ;



"Monophthongisation rule ä:a"
!! * __@RULENAME@__ \\  čääci:čassijn, läbži:labžijn 
ä:a <=> _ [ä:|%^RV:] Cns:+ i %^WG: ([%^RVSH: |%^RLEN:] ) %> i: ;
        _ [ ä: |%^RV: ] Cns:+   i %^WG: [%^CLEN: |%^RLEN: ]  %^SVLOW: %> i: ; !čääci:čassijn, läbži:labžijn
        _ %^RV:  Cns:+   i %^WG: %^RLEN:  %> i: ; !ä%^RVi4gi%^WG%^RLEN%>id
        _ ä:0  Cns:+ Vow:  %^WG: %^RVSH: %^CLEN:  %> i:  ; !! čääci : čassijn


!€ sääm7^RCi^WG>in
!€ saam0i0>jn


!€ sääm7^RCi^WG>id
!€ saam0i0>jd

!!  riä%^RDgˊgás%^ÁI%^WG%^RLEN  čuá%^RDgˊgás%^ÁI%^WG%^RLEN> ye



"Monophthongisation rule 2"
!! * __@RULENAME@__ \\  á:o in pairs like kuátij -> koođij, säämi -> saamij  
Vx:Vy <=> Vz: _  Cns:+ i %^WG:   %> i: ;
          Vz:Vy _ (%^RD:) Cns:+ i %^WG: %^RLEN:  %> i: ;
          Vz:Vy  _ (%^RD:) Cns:+ :i Cns: %^ÁI: [%^WG:|%^EWG:] %^RLEN: [%>|.#.] ; !viäsˊkár:veeskir  viäsˊkár%^ÁI%^EWG%^RLEN
          Vz  _ %^RD: Cns:+ [:i|:e] Cns: [%^ÁI:|%^ÁE:] [%^WG:|%^EWG:] ([%^RVSH:|%^RLEN:]) [%>|.#.]  ; !riä%^RDgˊgás%^ÁI%^WG%^RVSH>in 
          Vz:  _ %^RD: Cns:+ Vow:  %^ÁI: %^EWG: [%>|.#.] ; !puáhˊháá pooṿij
	where Vx in ( ä á ä )
		  Vy in ( e o a )
		  Vz in ( i u ä )
		  matched ;

!nom	ill
!e^RV	iä
!ie	iä
!ä	á
!ye	uá



"Diphthongisation e to iä, part 1"
!! * __@RULENAME@__ \\  
e:i <=> \Vow: _ Vow: Cns:+ [i2:|e:|i:] %^EA: (%^CSH:) (%^RLEN:)   %> ; !! 1) Part 1: e to i; ergi -> iärgán 1, veerdi : viärdán

"č4:ij part 1"
!! * __@RULENAME@__ \\  
č4:i <=> Vow: _ %^RC:  WeakGrade %^CLEN: %^SVLOW: %> ; !! 1) eeč%^RCi:eijijn


"č:ij part 2"
!! * __@RULENAME@__ \\  
%^RC:j <=> Vow: č4:i  _   WeakGrade %^CLEN: %^SVLOW: %> ; !! 1) eeč%^RCi:eijijn



"Diphthongisation e:iä and lengthening ä:ää in Illative and Sg3 2"
!! * __@RULENAME@__ \\  Part 2: ^RV to ä; 
%^RV:ä <=> e:i _ Cns:+ i: %^EA:  (%^CSH:) (%^RLEN:)   %> ; ! 1) e:iä, ergi -> iärgán 2 (e+e(RV) -> i + ä
           ä: _ Cns:+ i:  %^WG: %^RLEN: %> \i: ; ! 2) ä:ää


"Diphthongisation o:uá for %^EWG, part 1"
!! * __@RULENAME@__ \\   
%^RV:á <=> o: _ %^RD: Cns:+ u:  %^EWG: (%^RVSH:)  %> ; ! 1) poccuu puásui


"Diphthongisation uo:uá for %^EWG"
!! * __@RULENAME@__ \\   
o:á <=> u _ %^RD: Cns:+ u: %^EWG: (%^RVSH:) %> ; ! 1) cuobˊbuu cuábui
        u _ Cns:+ á %^CSH: ;

"Diphthongisation o:uá for %^EWG, part 2"
!! * __@RULENAME@__ \\   
o:u <=>  _ %^RV:á %^RD: Cns:+ u:  %^EWG: (%^RVSH:) %> ; ! 1) poccuu puásui


"ie to iä in Illative and Sg3"
!! * __@RULENAME@__ \\  ... with ^ILL as trigger; kiem´ni -> kiämnán, veerdi : viärdán
e:ä <=> [i | e: ]  _ (%^RD:) Cns:+ Vow: %^EA: ;


"ää to áá in Illative and Sg3"
!! * __@RULENAME@__ \\  ... for säämi > sáámán
ä:á <=> _  :á Cns:+ :á   %^EA: (%^CSH:) (%^RLEN:)   %> n ;
        ä: _          Cns:+ :á %^EA: (%^CSH:) (%^RLEN:)  %> n ;

!€ sääm7^RCi^EA>n
!€ sááṃ0á0>n

"äRV to áá in Illative and Sg3"
!! * __@RULENAME@__ \\  ... for täsni -> táásnán
%^RV:á <=> ä: _  Cns:+ :á %^EA: (%^CSH:) %^RLEN:  ;

!€ tä^RVsni^RLEN^CSH^EA>n
!€ táásná000>n

"Vowel lengthening äRV to aa "
!! * __@RULENAME@__ \\  äigi aaigijd
%^RV:a <=> ä:a _  Cns:+ i: %^WG: %^RLEN: %> i: ;


"ye to uá og uo, part 1" 
!! * __@RULENAME@__ \\  Part 1 of ye to uá for kyeli : kuálán 
y:u <=> _ e: %^RD:  Cns:+ Vow: %^EA: ((%^CSH:) %^RLEN:)  %> ;
        _ :o %^RD:  Cns:+ :i (%^WG:) ([%^RVSH: |%^RLEN:] ) (%^CLEN:) %> i: ; !sye%^RDj7%^RCi%^RVSH%^CLEN%>in
        _ :o %^RD:  Cns:+ :i %^WG: %^CLEN: %^SVLOW: %> i: ;
        _ :o %^RD:  Cns: 0:ˊ  Cns: :i  %^WG: %^RVSH: %^CLEN: %> i: ;

"ye to uá, part 2"
!! * __@RULENAME@__ \\  Part 2 of ye to uá for kyeli : kuálán
e:á <=> y: _ %^RD:  Cns:+ Vow: %^EA: (%^CSH:) (%^RLEN:)  %>  ;

"ye to uo, part 2 "
!! * __@RULENAME@__ \\  Part 2 of ye to uá for kyeli : kuolijn
e:o <=> y: _ %^RD:  Cns:+ :i (%^WG:) ([%^RVSH: |%^RLEN:] ) (%^CLEN:) %> i: ;
        y:  _ %^RD:  Cns:+ :i %^WG: %^CLEN: %^SVLOW: %> i: ;
        y: _  %^RD:  Cns: 0:ˊ Cns: :i  %^WG: %^RVSH: %^CLEN: %> i: ;

"uá to ye, part 1" 
!! * __@RULENAME@__ \\  Part 1 luámmán : lyeme čuá%^RDgˊgás%^ÁI%^WG%^RLEN%>
u:y <=> _ á: %^RD: Cns:+ Vow: %^SV: Cns: %^FCD: %^EWG: (%^SLEN:)  %> ;
        _ á: %^RD: Cns:+ Vow: Cns: %^ÁI: %^EWG: [%^RLEN:|%^RVSH:] [%>|.#.] ;

"uá to ye, part 2"
!! * __@RULENAME@__ \\  Part 2 luámmán : lyeme
á:e <=> u: _ %^RD: Cns:+ Vow: %^SV: Cns: %^FCD: %^EWG: (%^SLEN:) [%>|.#.] ;
        :e  %^RD: Cns:+ _  %^SV:  Cns: %^FCD:  %^EWG:  (%^SLEN:) [%>|.#.]  ; !luá%^RDmmá%^SVn%^FCD%^WG
        :y _ %^RD: Cns:+ Vow: Cns: %^ÁI: %^EWG: [%^RLEN:|%^RVSH:] [%>|.#.] ; !cuágˊgás : cyegis čuá%^RDgˊgás%^ÁI%^WG%^RLEN


!! !Second syllable rules

"Second syllable vowel change á to e or i in 3syll stems"
!! * __@RULENAME@__ \\ puttás- > putes, triggers are ^ÁE and ^ÁI já%^RVh4h4á%^SVl%^ÁI%^EWG%^RLEN%>
Vx:Vy <=> _ (%^SV:) (Cns:)  Vz: [%^WG: |%^EWG: ]  ;  !viäsˊkár%^ÁI ča%^RVskas%^ÁE páápp4ár%^ÁI
          _  Vz: %^EWG: ; !puáhˊháá pooṿij
       where Vx in ( á    á    a   )
             Vy in ( e    i    e   )
             Vz in ( %^ÁE %^ÁI %^ÁE) 
       matched ;

!€ puttás^ÁE^WG>
!€ pu0tes00>

"Stemvowel shortening  áá and aa to e or i in 3syll"
!! * __@RULENAME@__ \\ puttás- > putes, triggers are ^ÁE and ^ÁI
Vx:0 <=> Cns: _ Vx: (Cns:) Vz: [%^WG:|%^CSH:] ; 
       where Vx in ( á    á    a   )
             Vz in ( %^ÁE %^ÁI %^ÁE) 
       matched ;

!rejected
!€ nahh4aar^ÁE^WG>
!€ na0ṿ0er00>

"Fleeting vowel before suffix" ! 
!! * __@RULENAME@__ \\ deletes -i- in piehâin to pieˊhânân pie%^RDhâin piä%^RDi4ppáin
i:0 <=> Vow _ n j ([%^CSH: |%^RLEN:] ) %^SVSH: [%>|.#.] ;





!€ šoddâdem^SFV>ist
!€ šoddâd0m0>ist

"Limes vowel deletion"
!! * __@RULENAME@__ \\ 
Vx:0 <=> Cns: _ Cns %^SFV: ; 
       where Vx in ( e â ) ;


 
"Stem vowel e to a for a root vowel."
!! * __@RULENAME@__ \\  alge to aalgan
e:a <=> a (%^RV:) Cns:+ _ %^EA: ;

!€ a^RVlge^RLEN^CSH^EA>n
!€ aalga000>n

"Stem vowel e to á for other root vowel"
!! * __@RULENAME@__ \\  
e:á <=> [o|i|u] (%^RV:) Cns:+ _ %^EA:  (%^CSH:) ([%^RLEN: |%^RVSH:] ) [%>|.#.] ;

"Stem vowel i to á for other root vowel."
!! * __@RULENAME@__ \\  e.g. säämi > Ill sáámán 
Vx:á <=> [:e|:i|:ä|:á] ([%^RV: | %^RD: ]) Cns:+ _  %^EA: (%^CSH:) ([%^RLEN: |%^RVSH:] ) [%>|.#.] [i: | n | d] ;
    where Vx in ( i i2 ) ;



"Stem vowel â to a in Pl Nom -h"
!! * __@RULENAME@__ \\  silbâ to siilbah
â:a <=> :Cns (%^RC:) _ (%^SV:) (%^WG:) (%^RLEN:) %> h ;


!€ so^RVllâ^WG^RLEN>h
!€ soo0la00>h

"Stem vowel â > á change before i"
!! * __@RULENAME@__ \\ sollâ to sooláid 
â:á <=> _ (%^SV:) (%^WG:) (%^RLEN:|%^CLEN:) %^SVLOW: %> i: ; 

!€ so^RVllâ^WG^RLEN^SVLOW>in 
!€ soo0lá000>in



"Stem vowel change e:i before suffix"
!! * __@RULENAME@__ \\ 
e:i <=> Cns:  _  [Triggers: - %^EA:]*  %> i: ; 
        Cns:  _ %> ä p ; !! mánuttep : mánuttiäpán
        
"Stem vowel i2:e"
!! * __@RULENAME@__ \\ 
i2:e <=> _ Triggers:+ %> \[i: |n | d] ;

"Stem vowel u:o change before suffix"
!! * __@RULENAME@__ \\  
u:o <=> Cns: _ ([%^EWG:|%^SV:]) (%^WG:) (%^RVSH:) (%^RLEN:|%^CLEN:) (%^SLEN:) %^SVLOW:  ; 
		_  á:o %^RD: Cns:+ Vow: %^ÁI: %^EWG: ; !puáhˊháá : pooṿij
! stááḷu stááloust

"Second vowel shortening in 3syll consonant stems"
!! * __@RULENAME@__ \\ eemeed : emedân
Vx:0 <=> _ Vx: (Cns:+) (%^CSH:|%^WG:|%^EWG:) %^SVSH:  ; !eemeed : emedân sai4j7oos
    where Vx in ( e o a á u i ) ;

!eemeed:emedân

!€ eemeed^CSH^SVSH>ân
!€ 0em0ed00>ân


!€ kappeer^CSH^SVSH>ân
!€ ka0p0er00>ân

"Stem vowel u:0 in front of â and u"
!! * __@RULENAME@__ \\ aiguu:aaigâ
u:0 <=> _ â %^WG: ([%^RVSH:|%^RLEN:])   ; 
       _ %^CSH: %> u   ;

! this rule should be generalised, we talk about a ban on o>u strings

!Rejected, look at this:    
!€ aaigâ^UU>u
!€ a0igu0>u

!! !Suffix vowel rules

"Suffix vowel lengthening"
!! * __@RULENAME@__ \\ long á in kissáást
%^SV:Vx <=> Vx:  _  [NotSuffixBorder: - %^EWG: ]* %^SLEN:  %> ;
        where Vx in ( á o u e a i ) ; !  

"Suffix vowel lengthening when SVLOW"
!! * __@RULENAME@__ \\ preventing conflict with u %^SV:o
%^SV:Vx <=> Vx:  _  [NotSuffixBorder: - %^EWG: ]* %^SLEN: %^SVLOW:  %> ;
        where Vx in ( á o e a i ) ; ! 


"Suffix vowel lengthening SV:e"
!! * __@RULENAME@__
%^SV:e  <=> :e _ Cns: %^FCD:  %^EWG: %^SLEN:  %> ;

!€ ki^RVss7á^SV^WG^CLEN^SLEN>st
!€ ki0ssáá000>st

!€ mi^RVll7o^SV^WG^CLEN^SLEN>st
!€ mi0lloo000>st

!€ huksi^SVm^SLEN>ist
!€ huksiim0>ist

"Suffix vowel lengthening o"
!! * __@RULENAME@__ \\ stááḷu stááloost
%^SV:o <=> u:  _ %^WG: (%^RVSH:) %^SLEN: %^SVLOW: %>  ; 

!€ táál7^RCu^SV^WG^RVSH^SLEN^SVLOW>st
!€ táál0oo0000>st

"Suffix i5:j for lexicon APINA"
i5:j  <=> Cns i %> _ ;
! salijn

"Suffix i5:0 for lexicon APINA"
i5:0 <=> Vow [i | e] %> _ ;
! timotein oboen


"Suffix i:j for i and e-stems"
!! * __@RULENAME@__ \\  
i:j <=> [i: | e: | i2: ] (%^EA:) (%^WG:)   ([%^RLEN:|%^RVSH:]) (%^CLEN:)  (%^SVLOW:)   %> _ [d|n|g|t|%>|.#.];  





!! !!Other rules



"Final consonant x:xx"
!! * __@RULENAME@__ \\  pottáák potákkân  
%^SC:Vx <=> Vx _ %^CSH: %^SVSH: [%>|.#.] ; 
        where Vx in ( k l n p t ) ;


!rejected:
!€ pieˊhâin^SFV^SC^CSH^RVSH>ist
!€ pie0hâ0n0j00>ist

!rejected:
!€ kahhaan^SC^CSH^RVSH>ist
!€ ka0ha0nj00>ist

