! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

!! !!!Alphabet 

Alphabet   

!! !!The Inari Saami letters

 a á â ä b c d e f g h i j k l m n o õ p q !!= * @CODE@
 r s t u v w x y z æ ä ø ö å			   !!=  @CODE@
 č đ ŋ ŧ š ž				 			   !!=  @CODE@

 A Á Â Å B C D E F G H I J K L M N O Õ P Q !!= * @CODE@
 R S T U V W X Y Z Æ Ä Ø Ö Å 			   !!=  @CODE@
 Č Đ Ŋ Ŧ Š Ž  							   !!=  @CODE@
 ˊ  %- 							   !!= * @CODE@
 ':0   !!= * @CODE@ this to zero

!! !Accented letters, just in case

 É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Ê Ô Û Î Ã Ý þ Ð   !!= * @CODE@
 é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý þ ð   !!=  @CODE@
 ß ª ß ç								   !!=  @CODE@

 
 
!! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 »7     !! * »
 «7     !!  «
 %[%>%] !!  >
 %[%<%] !!  <



!! !Different archiphonemes


p4:p  t4:t k4:k č4:č r4:r l4:l h4:h   !!= * @CODE@  these are consonants that change in cg
i4:i                     	        !!= * @CODE@  this is the underlying i consonant
m7:ṃ n7:ṇ ŋ7:ŋ̣ s7:ṣ r7:ṛ đ7:đ̣ l7:ḷ    !!= * @CODE@  
j7:j̣ h7:ḥ š7:ṣ̌  v7:ṿ                  !!=  @CODE@  with dots
h5:h  y5:y i5:i                       !!= * @CODE@  these do not change 


!! !More abstract archiphonemes

!%^A:0		 = algan lodán
%^RC:0		!!= * @CODE@ = root cons., lengthened by ^CLEN - máán%RCu máánnun
%^RD:0		!!= * @CODE@ = root dipht., shortened by by ^RVSH - vuá%^RDg'gu vuáˊguttáá
%^RV:0	 	!!= * @CODE@ = root vowel, lengthened by ^RLEN - ta%^RVvlu taavlu and ^RV:ˊ when WG and suffix is 0, t, i
%^SC:0      !!= * @CODE@ = stem cons, %^SC:j, pluss lengthen after earlier shortening by %^CSH, see lyeme
%^SV:0 		!!= * @CODE@ = stem vowel, lengthened by ^SLEN u:uu - táálu%^SV tááloost

!! !Triggers

%^CLEN:0	!!= * @CODE@ = cons lengthening, with ^RC
%^CSH:0     !!= * @CODE@ = cons shortening, laaŋkku - laaŋkun
%^DLEN:0    !!= * @CODE@ = Diphtong lengthening (remove?)
%^RVSH:0     !!= * @CODE@ = Diphtong shortening
%^EE:0      !!= * @CODE@ = Final vowel deletion in ee stems (not in use)
%^FCD:0     !!= * @CODE@ = Final consonant deletion 
%^IA:0      !!= * @CODE@ = i to á (more) (not in use)
%^EA:0     !!= * @CODE@ = e to a/á in ill etc
%^LLEN:0    !!= * @CODE@ = low vow lengthening (not in use)
%^MONO:0    !!= * @CODE@ = monophtongization of diphthongs  (not in use)
%^RLEN:0    !!= * @CODE@ = root lengthening, with ^RD
%^RVSH:0    !!= * @CODE@ = root vow shortening - máán7%^RCu máˊnuttáá 
%^SLEN:0    !!= * @CODE@ = stem vowel lengthening
%^SFV:0     !!= * @CODE@ = Deleting fleeting vowel before suffix
%^SVLOW:0   !!= * @CODE@ = stem vowel lowering â>á and u>o - árppu árppoin
%^SVSH:0   !!= * @CODE@ = stem vowel shortening kappeer kaperân
%^UU:0      !!= * @CODE@ = Final vowel deletion in uu stems
%^VLOW:0    !!= * @CODE@ = Vowel lowering in 3rd sg of contract verbs tuhhid:tohhe
%^WG:0      !!= * @CODE@ = weak grade trigger
%^ÁE:0      !!= * @CODE@ = á to e (more) in 3-syll
%^ÁI:0      !!= * @CODE@ = á to i (more) in 3-syll
%^EWG:0     !!= * @CODE@ = from extra strong to weak, e.g. pp:v

%^ODRVSHV:0     !!= * @CODE@ =  Cns shortening in C|# and shortvow in Inf etc  (not in use)
%^ODDC:0       !!= * @CODE@ =  shorten cns in odd-syll stems before C, #  (not in use)

!! !!Morpheme boundaries:

!! Here we define the suffix border >
!! The other borders are not used yet, but still defined:

 «  !!≈ ;@CODE@: Derivational prefix
 »  !!≈ ;@CODE@: Derivational suffix
 %< !!≈ ;@CODE@: Inflectional prefx
 %> !!≈ ;@CODE@: Inflectional suffix
 %^ !!≈ ;@CODE@: (exceptional) soft hyphenation point
 #  !! and the symbol #, which is Word boundary for both lexicalised and dynamic compounds

!! End of alphabet definitions

;

!! !!! Sets

Sets
 Vow = a á â ä e i o u y æ ø ö å y5			   !!= * @CODE@ 
       é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý 	   !!= @CODE@ 
       A Á Â Ä E I O U Y Æ Ø Ö Å			   !!= @CODE@ 
       É Ó Ú Í À È Ò Ú Ì Ë Ü Ï Ê Ô Û Î Ã Ý 	   !!= @CODE@ 
       %^RV %^RD %^SV ;						   !!= @CODE@ 
 RealVow = a á â ä e i o u y æ ø ö å y5		   !!= * @CODE@ 
       é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý 	   !!= @CODE@ 
       A Á Â Ä E I O U Y Æ Ø Ö Å			   !!= @CODE@ 
       É Ó Ú Í À È Ò Ú Ì Ë Ü Ï Ê Ô Û Î Ã Ý ;   !!= @CODE@ 
 Cns = b c č d đ f g h j k l m n ŋ 			   !!= * @CODE@ 
       p q r s š t ŧ v w x z ž				   !!= @CODE@ 
       B C Č D Đ F G H J K L M N Ŋ			   !!= @CODE@ 
       P Q R S Š T Ŧ V W X Z Ž 				   !!= @CODE@ 
       ht g2 k2 m2 n2 							   !!= @CODE@ 
       b3 d3 g3 m3 h3 						   !!= @CODE@ 
       p4 k4 č4 r4 l4 h4 t4 i4                  !!= @CODE@
       h5                                       !!=  @CODE@
       m7 n7 ŋ7 v7 s7 r7 đ7	l7 j7 h7 š7		   !!= @CODE@ 
       ṃ  ṇ  ŋ̣  ṿ  ṣ  ṛ  đ̣  ḷ  j̣  ḥ  ṣ̌         !!=  @CODE@ 
       %^RC %^SC  ˊ ;                         !!=  @CODE@ 
 Geminate = b d g p h t k c f z l m n ŋ r s š v  ;  !!= * @CODE@  
 Seg = Vow Cns ;							   !!= * @CODE@ 
 NotSuffixBorder = Vow Cns %^WG %^CLEN %^RLEN %^RVSH %^ÁE %^ÁI ; !!= * @CODE@ 
 StemCns = b b3 d d3 g g2 g3 h h2 h3 		   !!= * @CODE@ 
           j l m m2 m3 m8 n n8 n2 ŋ r s  ;     !!= @CODE@ 



           
!! !!!Definition section

!! Introducing a WG right context 
!! {{{WeakGrade =  (RealVow:) :RealVow  (Cns:) ([%^SV:|%^ÁI:|%^ÁE:]) %^WG:0  (%^RVSH:) ; }}}  

Definitions

WeakGrade =  (RealVow:) :RealVow  (Cns:) ([%^SV:|%^ÁI:|%^ÁE:]) %^WG:0  (%^RVSH:)   ;   


!! As we look at more cases, the WeakGrade definition tends to be longer. 
!! We want to keep it short, but we want the code to work Thus, we document.
!! * The second :RealVow is there for long consonant stems
!! * The (Cns:) is there for consonant stems

!! !!!Rule section

Rules

!! !!Consonant rules

!! !Consonant gradation

"x7:x ja x:y consonant gradation"
!! * __@RULENAME@__ \\  WG gives x7:x and x:y, when there is no consonant lengthning  
Cx:Cy <=> Vow: _ (%^RC: )  WeakGrade ([%^RVSH:|%^RLEN:|%^DLEN:]) (%^SLEN:) (%^SVLOW:) %> ;
	where Cx in ( m7 n7 ŋ7 r7 s7 š7 j7 v7 đ7 l7 h p c č )  
          Cy in ( m  n  ŋ  r  s  š  j  v  đ  l  ṿ v s j̣ )  
		  matched ;


!€ suá^RDn7^RCju^WG>
!€ suá0n0ju0>



!iˊṣ̌e:iˊše

!€ i^RVš7^RCe^WG>
!€ iˊš0e0>

"n7j:nj consonant gradation and lengthning"
!! * __@RULENAME@__ \\   n7:n in the combinataion n7j:nj 
n7:n <=> Vow: _ (%^RC: ) j WeakGrade \%^CLEN:0 ; !! 1) weak grade
         Vow: _ %^RC:  j RealVow:  %^CLEN:0 ; !! 2) consonantlengthning
         Vow: _ %^RC:  j RealVow: %^WG:0 %^CLEN:0 %^SVLOW:0 ; !! 3) weak grade and consonant lengthning 




"xx:x consonant gradation and shortening and x:x shortening"
!! * __@RULENAME@__ \\   weak grade or consonant shortening for illativ
Cx:0 <=> Vow: _ (ˊ:) Cx: RealVow:+ (%^SV:) (Cns) ([%^SV:|%^ÁI:|%^ÁE:]) %^WG:0  ([%^RVSH: |%^RLEN:]) (%^SVLOW:) %> ; !! 1) weak grade päddi:päädi tullâ:tuuláin kaccuâ:kaacâ
         Vow: _ (ˊ:) Cx: Vow: (%^RLEN:)  %^CSH: (%^EA: ) %> ; !! 2) consonant shortening for illativ, päddi:páádán
         Vow: _ (ˊ:) Cx: Vow: (Vow:) Cns: (Cns:) %^CSH: (%^SVSH:) [%>|.#.] ; !! 3) consonant shortening 3st stems kahhaan:kahanân 
         %^RD: _  Cx: Vow: Cns:+ (%^RLEN:)  %^CSH: %>  ; !piäggálm
         Vow: _  Cx: Vow: (%^SV:) Cns:  %^WG: (%^SLEN:) %^FCD: %> ; !! 3) riessâm : riesâ
	where Cx in ( b d đ g p t k c č f z ž s š v h ) ; !lnŋr kahhaanj 
	!ka%^RVccuâ%^WG%^RLEN>

!cuozˊzâ:cuozâ
!€ cuo^RDzˊzâ^WG^RLEN>
!€ cuo000zâ00>

!váddu:váádun
!€ vá^RVddu^RLEN^CSH>n
!€ váá0du00>n

!kahhaan:kahanân
!€ kahhaan^CSH^SVSH>ân
!€ ka0h0an00>ân


"xx:x consonant shortening for consonants belonging to two different patterns"
!! * __@RULENAME@__ \\   weak grade or consonant shortening for illativ
Cx:0 <=> Vow: _ (ˊ:) Cx: WeakGrade (%^RLEN:) (%^SVLOW:) %> ; !! 1) weak grade päddi : päädi tullâ:tuuláin
         Vow: _ (ˊ:) Cx: Vow:  %^CSH: ( [%^EA:|%^RVSH:]  ) %> ; !! 2) consonant shortening for illativ, päddi : páádán RLEN?
         Vow: _  Cx: Vow: (%^SV:) Cns:  %^WG: (%^SLEN:) %^FCD: %> ; !! 3) luammán : lyeme luá%^RDmmá%^SVn
	where Cx in ( l n ŋ r m ) ; !


"s:0, 1. part of ss:ṣ"
!! * __@RULENAME@__
s:0 <=>  Vow: _  s: Vow: (%^SV:) Cns:  %^SC: %^RVSH: %^CSH: %>  ; !! riessâm : rieˊṣâmân


"s:0, 2. part of ss:ṣ"
!! * __@RULENAME@__
s:ṣ <=>  Vow: s:0 _ Vow: (%^SV:) Cns:  %^SC: %^RVSH: %^CSH: %>  ; !! riessâm : rieˊṣâmân


!luččâ:luuj̣áin 

!€ lu^RVčč4â^WG^RLEN^SVLOW>in
!€ luu0j̣á000>in

"ˊ to zero gradation"
!! * __@RULENAME@__ \\ when weak grade or consonant shortening 
ˊ:0 <=> Vow: Cns: _ Cns:+  Vow:+ (Cns)  %^WG:0 ([%^RLEN: | %^RVSH:] ) [%>|.#.]  ;  !! 1) weak grade, mie%^RDlˊgâs
        Vow: Cns: _ Cns:+  Vow: (%^RLEN:) %^CSH: (%^EA:)  [%>|.#.]  ; !!  2) consonant shortening for illativ
        Vow: č:   _ č4: WeakGrade %^CLEN: %^SVLOW: %> ; !! 3) weak grade and consonant lengthning, kiäčˊču kiäj̣u 
        Vow: RealVow:0 %^RD:  Cns:  _ Cns:+  WeakGrade %^CLEN: %^SVLOW: %> ; !! 4) uábˊbi:obbijn
		Cns: _ Cns:+ Vow:+  Cns: (%^ÁI:) %^WG:  ; !! 5) viäsˊkár kuo%^RDlˊbân uái4ˊváát%^WG



"xx:y to something else, consonant gradation and shortening, part 1" 
!! * __@RULENAME@__ \\   hh:V, kk:h, pp:v, čč:J, deleting first part, k:0 in kk4:h etc  ča%^RVpp4oos%^EWG%^SVSH
Cx:0 <=> Vow: _ (ˊ:) Cy: (%^RC:) WeakGrade (%^RLEN:) (%^SVLOW:) %> ; !! 1) weak grade: ruttâ:ruuđáin
         Vow: _ (ˊ:) Cy: Vow: (%^RLEN:) %^CSH: (%^EA: ) %> ; !! 2) consonant shortening for illativ
         Vow: _ (ˊ:) Cy: Vow:+  Cns (%^ÁI:|%^ÁE:) ([%^RLEN: | %^RVSH:]) [%^CSH: |%^WG:] (%^SVSH:)  %> ; !! 3) consonant shortening for páápp4ár%^ÁI
          Vow:  _ Cy: :i %^SV: Cns %^ÁI: %^WG: %^RVSH: %> ;
	where Cx in ( h  h4 t 	k4 p    l  n  s  r  š  m  v  đ) 
	      Cy in ( h4 h4 t4 k4 p4  l7 n7 s7 r7 š7 m7 v7  đ7)
		  matched ;


"č:0 in čč4:j,  part 1" 
!! * __@RULENAME@__ \\   
č:0 <=> Vow: _ (ˊ:) č4: (%^RC:) WeakGrade (%^RLEN:) (%^SVLOW:) %> ; !! 1) weak grade: luččâ:luučân
        Vow: _ (ˊ:) č4:  WeakGrade %^CLEN: %^SVLOW: %> ; !! 2) weak grade and consonant lengthning kiäčˊču:kiäj̣u 
        Vow: _ (ˊ:) č4: Vow: (%^RLEN:)  %^CSH: (%^EA: ) %> ; !! 3) consonant shortening for illativ: luččâ:luučân



"xx:y to something else, consonant gradation, part 2" 
!! * __@RULENAME@__ \\  hh:V, kk:h, pp:v, čč:J here we change second part, 
Cx:Cy <=> Vow:  Cz: (ˊ:) _  WeakGrade (%^RLEN:) (%^SVLOW:) %> ; !! 1) weak grade:
          Vow:  Cz: (ˊ:) _  WeakGrade %^CLEN: %^SVLOW: %> ; !! 2) weak grade and consonant lengthning kiäčˊču:kiäj̣u 
          Vow:  Cz: _ :i %^SV: Cns %^ÁI: %^WG: %^RVSH: %> ;
	where Cx in ( h4  p4 č4 ) 
	      Cy in ( ṿ   v  j̣ ) 
	      Cz in ( h4   p  č ) 
		  matched ;

"RV:ˊ in weak grade or root vowel shortening"
!! * __@RULENAME@__ \\  saˊj̣e 
%^RV:ˊ <=> [Vow: - e:] _ Cns: (%^RC:)  [e: | o: | a: | á: ] (%^WG:|%^EA:)    %> ; ! eRV ->e' ? iˊṣ̌e vaˊhe
                       _ Cns:+  WeakGrade   %> ; !laŋkku laˊŋkuttáá
                  Vow: _ Cns:+  Vow:  [%^WG: | %^EWG:] %^RVSH: (%^SVLOW:) [%>|.#.] ;
                  Vow: _ Cns:+  Vow: %^SV: Cns %^EWG: %^RVSH:  [%>|.#.] ;
                  Vow: _ Cns:+  Vow:  %^SV: Cns %^RVSH: %^CSH:  %> ;
                  Vow  _ Cns:+ (j) Vow:+ Cns [%^CSH:|%^WG: | %^EWG:] %^SVSH: %> ; !kilgool kiˊlgolân kolgooh koˊlgoohijn vinnjoo	a%^RVlmoos

!                  shorter   shortest           long
! RV: aRV ->    ,  aˊ / RVSH,   a0 / default,     aa / RLEN  = saje     = %^RV:ˊ <=> [Vow: - e:] _ 
! aa> aa  ->      aˊ / RVSH,  ie0 / default  			     = saahâ    =   Vy:ˊ <=> Vx _ ...
! RD: ieRD ->  , ieˊ / RVSH,  ie0 / default     			 = kielâ    = %^RD:ˊ <=> _ ...



"Long vowel shortening (no consonant shortening)"
!! * __@RULENAME@__ \\ (example?), no ^CSH, since that gives Vy:0
Vy:ˊ <=> Vx: _ [Cns: - č: ] Cns:* (%^RC:) Vow:  %^WG: %^RVSH: (%^SVLOW:) [%>|.#.] ; !! 1) 2st stems
         Vx: _  Cns:+         Vow:  Cns: %^RVSH: %^CSH: %^SVSH: [%>|.#.] ; !! 2) 3st stems kaavpug:kaˊvpugân 
         Vx: _ Cns:+ Vow: (%^SV:) Cns:+ Vow: Cns:  %^RVSH: [%>|.#.] ; !táálhudem koomerdem
       where Vx in ( a e i o u y á ä ) 
             Vy in ( a e i o u y á ä) 
       matched ;

"From i4j7 to ˊj"
!! * __@RULENAME@__ \\ sai4j7oos 
i4:ˊ <=>  Vow: _ j7:  Vow:+ Cns [%^EWG:|%^WG:] %^SVSH:  [%>|.#.] ; !! 1) saijos:saˊjos



"Long vowel shortening, č:j̣ "
!! * __@RULENAME@__ \\ special rule for getting ej̣ittáá instead of eˊj̣ittáá.
Vy:0 <=> Vx: _ č: (%^RC:) Vow:  %^WG: %^RVSH: (%^SVLOW:) [%>|.#.] ; !! 1) 2st stems
       where Vx in ( a e i o u y á ä ) 
             Vy in ( a e i o u y á ä ) 
       matched ;


"Underlying long vowel shortening iä:eˊ"
!! * __@RULENAME@__ \\ 
Vy:ˊ <=> Vx: _  Cns:+ Vow:  Cns: [%^ÁI:|%^ÁE:] [%^WG:|%^EWG:] %^RVSH:  [%>|.#.] ; !! 1) 3st stems viäsˊkár veˊskirin, časkes čaˊskes ča%^RVskas%^ÁE viäsˊkár%^ÁI%^EWG%^RVSH
       where Vx in ( i ) 
             Vy in ( ä ) 
       matched ;


!kaavpug:kaˊvpugân
!€ kaavpug^RVSH^CSH^SVSH>ân
!€ kaˊvpug000>ân


! mool7â to moolâ:		  
		  
!€ mool7^RCâ^WG>
!€ mool0â0>


!käähir to kääṿir

!€ käähir^WG>
!€ kääṿir0>

!juáhu to juáˊṿuttáá

!€ juá^RDh^RCu^WG^RVSH>ttáá
!€ juáˊṿ0u00>ttáá


"t:đ, also tˊk4:đh gradation, part 1" 
!! * __@RULENAME@__ \\   
t:đ <=> Vow: _  (ˊ:) k4: (%^RC:) WeakGrade ; !for ito : iđo 
        Vow: _  %^RC:   j RealVow: %^WG:0 %^CLEN:0 %^SVLOW:0 ; ! Com
        Vow: _ (%^RC:) (j) WeakGrade (%^CLEN:0) ;
        Vow: _  %^RC:   j RealVow:  %^CLEN:0 ; ! Par and Ess


!€ jo^RVtk4â^WG^RLEN>
!€ joođhâ00>

"k4:h gradation for k4k4, tk4, rkk4, lkk4"
!! * __@RULENAME@__ \\    for atka : ađha 
k4:h <=> Vow: [t:|k4:] (ˊ:) _  WeakGrade ([%^RLEN: |%^CLEN:] ) (%^SVLOW:) ;
         Vow: k4: (ˊ:) _ WeakGrade %^CLEN: %^SLEN: ;
         Vow: [l | r | v | i4: | đ ] (ˊ:) k: _  WeakGrade (%^CLEN: ) (%^SVLOW:) ; !rˊkk	rh
         Vow: [l | r | v | i4: | đ ] (ˊ:) k: _ Vow:+ %^EWG:  ; !rˊkk	rh
         Vow: _ (ˊ:) k4: WeakGrade %^CLEN: ([%^SVLOW: | %^SLEN: ]) %> ;
         
!€ mä^RVtk4i^WG^RLEN>.#.
!€ määđhi00>.#.

!€ su^RVk4k4á^SV^WG^CLEN^SLEN>st
!€ su0hháá000>st

"h4:v gradation for h4h4"
!! * __@RULENAME@__ \\   já%^RVh4h4á%^SVl%^ÁI%^EWG%^RLEN%>
h4:v <=>  _ h4: Vow:+ Cns %^ÁI:  %^EWG: %^RLEN: %> ;
        h4:  _  Vow:+  Cns %^ÁI:  %^EWG: %^RLEN: %> ;

"k:0 gradation for xkk4"
!! * __@RULENAME@__ \\  for lkk4, rkk4, vkk4
k:0 <=> Vow: [l | r | v | i4: | đ ] (ˊ:) _ k4:h  WeakGrade (%^CLEN: ) (%^SVLOW:) ; !! 1) WeakGrade 
        Vow: [l | r | v | i4: | đ ] (ˊ:) _ k4:k Vow: (%^RLEN:)  %^CSH: (%^EA:) %> ; !! 2) consonant shortening for illativ
        Vow: [l | r | v | i4: | đ ] (ˊ:) _ k4:h Vow:+ %^EWG:  ; !rˊkk	rh



"t4:đ gradation for tt4" 
!! * __@RULENAME@__ 
t4:đ <=> t: (ˊ:) _ WeakGrade ; !! 1) ruttâ ruuđâ
         t: (ˊ:) _ Vow:+ %^EWG: ; !! 2) tietˊtu tieđâ
!€ ru^RVtt4â^WG^RLEN>
!€ ruu0đâ00>

 
"t:0 foran t4" 
!! * __@RULENAME@__ 
t:0 <=> _ (ˊ:) t4: Vow:+ %^EWG: ; !! 1) tietˊtu tieđâ        

!rejected:       
!€ haavvâr^CSH^RVSH>ist
!€ h0aṿ0âr00>ist

!rejected:       
!€ pottáák^SC^CSH^SVSH>ist
!€ po0t0ákk00>ist

"xy:zy gradation"
!! * __@RULENAME@__ \\  ht:vt, we use h4 
Cx:v <=>  _ (ˊ:) Cz (%^RC:) Vow (%^SV: ) (Cns:) %^WG: ;
         _  Cz t Vow %^WG: ; !pi%^RVh4tâs
       where Cx in ( h4 k4 h4 h4 ) 
             Cz in ( t  s  č  c  ) 
       matched ;


!€ pih4tâs^WG
!€ pivtâs0

!€ totk4os^WG
!€ tođhos0

"xyy:xy gradation A"
!! * __@RULENAME@__ \\   
Cy:0 <=> :Vow ([%^RV: | %^RD: ]) Cx: (ˊ: ) _ Cz: (%^RC:) Vow (%^SV:) (Cns)  %^WG: ( [%^RLEN: |%^RVSH:]) %>  ;  
          Cx: (ˊ:) _ Cz:  Vow (%^RLEN:)  %^CSH:   ;  
      where Cx in ( j i4 i4 j i4 l l l l l l l m m n n n ŋ r r r r r r r v ) 
            Cy in ( p c  t  t k  c k p s t č š p č c s t k c p k s t č š p ) 
            Cz in ( p c  t t k  c k p s t č š p č c s t k c p k s t č š p ) 
      matched ;

!€ á^RVrppu^WG^RLEN>
!€ áár0pu00>

!€ á^RVrppu^WG^RVSH>ttáá
!€ áˊr0pu00>ttáá

!€ riänttus^WG>
!€ riän0tus0>


"xx:x7 gradation for ^CSH illative 1, and x:x7 for ^CSH" 
!! * __@RULENAME@__ \\   
Cx:Cy <=> Cx: _ Vow: %^RLEN: %^CSH: %> n ; !! 1) 2st stems hellá:heeḷán
          Cx: _ Vow: (Vow:) Cns: (Cns:) %^CSH: (%^SVSH:) [%>|.#.] ; !! 2) 3st stems aarrân:aṛânân, iššeed:iṣ̌edân, but not for kiärunj:kiäˊrunjân, jiereeh:jieˊrehân
          Cx: _ Vow:  (%^SV:) Cns: %^SC: %^RVSH: %^CSH: %> ; !! 3) siämmun siäˊṃunân luá%^RDmmá%^SVn%^SC%^RVSH%^CSH%>in
!          Vow: _ Vow: Vow: Cns: %^CSH: %^SVSH: %> ; !What is this one meant for?
      where Cx in ( r l n ŋ m š ) 
            Cy in ( ṛ ḷ ṇ ŋ̣ ṃ ṣ̌ ) 
      matched ;

!iäláán:iäˊḷánân  
!€ iä^RDláán^CSH^SVSH>ân
!€ iäˊḷ0án00>ân



"xx:x7 gradation for ^CSH illative 2" 
!! * __@RULENAME@__ \\    
Cx:0 <=> _  Cx:Cy Vow: %^RLEN: %^CSH: %> n ; !! 1) 2st hellá:heeḷán
         _  Cx:Cy Vow: (Vow:) Cns: (Cns:) %^CSH: (%^SVSH:) [%>|.#.] ; !! 2) 3st stems aarrân:aṛânân
         _ Cx: Vow:  (%^SV:) Cns: %^SC: %^RVSH: %^CSH: %> ; !! 3) siämmun siäˊṃunân
      where Cx in ( r l n ŋ m ) 
            Cy in ( ṛ ḷ ṇ ŋ̣ ṃ ) 
      matched ;

!kurrâ kuuṛân
!€ ku^RVrrâ^RLEN^CSH>n
!€ kuu0ṛâ00>n

!sollâ sooḷân
!€ so^RVllâ^RLEN^CSH>n
!€ soo0ḷâ00>n

!! !Consonant shortening

"Word final consonant deletion"                 
!!  * __@RULENAME@__ for vaskâm:vaaskâ
Cx:0 <=> _  (%^ÁE:) (%^SC:) (%^WG:) (%^SLEN:) %^FCD: ;      
          where Cx in (n m t v j s š) ;

!rejected, look at this:
!€ va^RVskâm^RLEN^FCD
!€ vaaskâ000
	

"Final m in stems, mn to m"  
!!  * __@RULENAME@__ for puhelim :  puhelin   
m:0 <=> _ n  [%>|.#.] ;          

!"Final m in stems, mt to nt"  
!!!  * __@RULENAME@__ for puhelim :  puhelinttáá   
!m:n <=> Vow _ %> t  ;          


"nnj to nj shortening weak grade and consonant shortening"
!! * __@RULENAME@__ \\  special rule, e.g. konnjâl -> konjâlân puá%^RDnˊnjá
n:0 <=> n (ˊ:) _ j Vow (Cns:) (%^RLEN:) %^CSH: [%>|.#.] ; !! 1) 
        n (ˊ:) _ j WeakGrade [%>|.#.] ; !! 2)
        n (ˊ:) _ j WeakGrade %^CLEN: %^SLEN: [%>|.#.] ; !! 3)
        n7: (ˊ:) _ j Vow:+ Cns %^CSH: %^SVSH: %> ; !vinnjooh:viˊṇjohân

!"nnj to ´nj shortening 1"
!!! * __@RULENAME@__ \\  special rule, e.g. konnjâl -> konjâlân puá%^RDnˊnjá
!n:ˊ <=>  _ n j Vow (Cns:) %^CSH: %^SVSH: [%>|.#.] ;
!
!"nnj to ´nj shortening 2"
!!! * __@RULENAME@__ \\  special rule, e.g. konnjâl -> konjâlân puá%^RDnˊnjá
!n:0 <=> n: _ j Vow (Cns:) %^CSH: %^SVSH: [%>|.#.] ;

!€ konnjâl^CSH>ân
!€ kon0jâl0>ân



!€ suá^RDn7^RCju^CLEN>n
!€ suá0nnju0>n

!rejected, look at tis:
!€ haamit^SC^CSH^RVSH>ist
!€ h0amitt00>ist


!€ su^RVk4k4á^SV^WG^CLEN^SLEN>st
!€ su0hháá000>st

!rejected:
!€ kopp^RCâ^WG^CLEN^SVN>in
!€ ko0pp0á000>in


!! !Consonant lengthening



"Lengthening of half-long root consonant n7:n 1"
!! * __@RULENAME@__ \\  for l-dot to ll, changing l7 to first l in ll
Cx:Cy <=> Vow: _ %^RC: :Vow              %^CLEN:      %> ;
          Vow: _ %^RC: :Vow %^SV: %^WG:  %^CLEN: %^SLEN: %> ; !Loc  
          Vow: _ %^RC: :Vow       %^WG:  %^CLEN: (%^SVLOW:)   %> ; !Com  %^WG%^CLEN
          Vow: _ %^RC: :Vow %^SV:        %^CLEN: %^SLEN: %> ; !Par Ess
          Vow: _ %^RC:  Vow:+ Cns:+ (%^RLEN:) %^CLEN: (%^SLEN:)  %> ; !! 5) čuovvânmijn  čuo%^RDv7%^RCânm%^RLEN%^CLEN%>ân

	where Cx in ( l7 r7 s7 h7 m7 n7 đ7 v7 ŋ7 š7 )
	      Cy in ( l  r  s  h  m  n  đ  v  ŋ  š  ) 
		  matched ;

"Lengthening of half-long root consonant j7:j 1"
!! * __@RULENAME@__ \\  for l-dot to ll, changing l7 to first l in ll
j7:j <=>  Vow: _ %^RC: :Vow %^SV: %^WG:  %^CLEN: %^SLEN: %> ; !Loc  
          Vow: _ %^RC: :Vow       %^WG:  %^CLEN: (%^SVLOW:)   %> ; 
         i4: _ Vow:+ Cns %^EWG: (%^SVSH:) [%>|.#.] ;
         i4: _ Vow:+ Cns ( %^SVSH:) [%>|.#.] ;
 
         
"Lengthening root consonant j7:ij 1"
!! * __@RULENAME@__ \\  for saje:saijeen
j7:i <=> Vow: _ %^RC: :Vow (%^RVSH:)  %^CLEN:         %> ; !Par Ess
         Vow: _ %^RC: :Vow %^SV: %^CLEN: %^SLEN: %> ; !Par Ess

"Lengthening root consonant j7:ij part 2"
!! * __@RULENAME@__ \\  
%^RC:j <=> Vow: j7:i _ :Vow %^RVSH: %^CLEN:         %> ; !Par Ess


!€ sa^RVj7^RCe^SV^CLEN^SLEN>n
!€ sa0ijee00>n

!€ pááj7^RCá^CLEN>n
!€ pááijá0>n

"Lengthening of half-long root consonant n7 : nn 2"
!! * __@RULENAME@__ \\   for l-dot to ll, changing ^RC to second l in ll  muáđoin
%^RC:Cy <=> Vow: Cx: _ :Vow  %^CLEN:   %> ; !
            Vow: Cx: _ :Vow %^SV: %^WG:  %^CLEN: %^SLEN:  %> ;
            Vow: Cx: _ :Vow  %^WG:  %^CLEN: (%^SVLOW:)   %> ; !Com  %^WG%^CLEN
            Vow: Cx: _ :Vow %^SV: %^CLEN: %^SLEN:  %>  ; !Par Ess
            Vow: Cx: _  Vow:+ Cns:+ (%^RLEN:) %^CLEN: (%^SLEN:)  %> ; !! 5) čuovvânmijn 
	where Cx in ( l7 r7 s7 h7 m7 n7 đ7 v7 ŋ7 j7 š7 )
		  Cy in ( l  r  s  h  m  n  đ  v  ŋ  j  š  ) 
		  matched ;

"Lengthening of consonant, e.g. for Ess/Par"
!! * __@RULENAME@__ \\   piiru piirrun niisu niissun vahe vahheen
%^RC:Cy <=> Vow: Cx: _ Vow (%^SV:)  %^CLEN: (%^SLEN:) %> [ n | d ] ; !kiä%^RDr%^RCun%^CLEN%>ttáá
            Vow: Cx: _ Vow Cns  %^CLEN:  %>  ;
            Vow: Cx: _  Vow: Cns:+ (%^RLEN:) %^CLEN: (%^SLEN:) %> ; !! 5) keeppidmân  ke%^RVp%^RCidm%^RLEN%^CLEN%>ân
	where Cx in ( t r s h h5 c m p)  
		  Cy in ( t r s h h c m p)   
		  matched ;



!kiehâ  kievváin
!€ kie^RDh^RCâ^WG^CLEN^SVLOW>in
!€ kie0vvá000>in

!iše iššeest
!€ i^RVš7^RCe^SV^WG^CLEN^SLEN>st
!€ i0ššee000>st

!€ kol7^RCe^CLEN>
!€ kolle0>

"Lengthening of h and p to vv, for everything except Ess/Par 1"
!! * __@RULENAME@__ \\  for h to vv p to vv čääc%^RCi
Cx:Cy <=> Vow: _ (:ˊ) %^RC: :Vow (%^SV:) %^WG: (%^RVSH:) %^CLEN: ([%^SVLOW: |%^SLEN:])  %> \[ n | d ]  ;
	where Cx in ( h p c )
		  Cy in ( v v s ) 
		  matched ;

"Lengthening of h and p to vv, for everything except Ess/Par 2"
!! * __@RULENAME@__ \\   for h to vv  p to vv   
%^RC:Cy <=> Vow: Cx: (:ˊ) _ :Vow  (%^SV:) %^WG: (%^RVSH:) %^CLEN: ([%^SVLOW: |%^SLEN:]) %> \[ n | d ] ;
	where Cx in ( h p c )  
		  Cy in ( v v s )   
		  matched ;



!kassa:kassaast
!€ ka^RVss^RCa^SV^WG^CLEN^SLEN>st
!€ ka0ss0aa000>st

!vahe vavveest
!€ va^RVh^RCe^SV^WG^CLEN^SLEN>st
!€ va0vvee000>st

!vahe vahheen
!€ va^RVh^RCe^SV^CLEN^SLEN>n
!€ va0hhee00>n

!€ kiel7^RCâ^CLEN>n
!€ kiellâ0>n



"n7j to nnj consonant lengthning"
!! * __@RULENAME@__ \\  special rule, suán7ju : suánnjun for Par and Ess
%^RC:n <=> Vow: n7: _ j RealVow: %^CLEN: %> [ d | n ] ;



"Consonant lengthening after xx:x and xy:zy weak grade with x4 on upper side"
!! * __@RULENAME@__ \\  as previous rule, but for k4, č4, p4  kukse:kuvssijn  
%^RC:Cx <=> Vow:  Cy: (ˊ:) Cz: _ WeakGrade %^CLEN: \%^SLEN: ; 
        where Cy in ( k  č  p  k  h h4 h4 h4 k4 ) 
              Cz in ( k4 č4 p4 s  c t  č c s ) 
              Cx in ( h  j  v  s  c t  č c s ) 
        matched ;

"Consonant lengthening č:čč"
!! * __@RULENAME@__ \\  as previous rule, but for k4, č4, p4  kukse:kuvssijn  
%^RC:č <=> Vow:  č  _ Vow %^CLEN:  ; 


"Mark consonant length after diphthong for Pl Ill"
0:ˊ <=> %^RD: Cx: _ :Cy Vow: %^WG: %^RVSH: %^CLEN:  %> i: [d | n ] ; !! 
        where Cx in ( l h) 
              Cy in ( g v ) 
        matched ;


"From extra strong to weak, part 1 cc:s, rr:r"
Cy:0 <=>  Cx: (ˊ:0) _  Vow:+ (Cns) (%^ÁI:) %^EWG:  ([%^RVSH:|%^RLEN:|%^SVSH:]) [%>|.#.] ; !! poccuu:puásui  kiepˊpu:kiepâ iäbˊbár%^ÁI cuobˊbuu:cuábui kie%^RDllâs
       where Cx in ( b c đ d g h l l m n r t p p  đ  n  r ) 
             Cy in ( b c đ d g h l l7 m n r t p p4 đ7 n7 r7 ) 
        matched ;

"From extra strong to weak, remove ˊ "
ˊ:0 <=>  Cx: _  Cy: Vow:+ (Cns) (%^ÁI:)  %^EWG: ([%^RVSH:|%^RLEN:]) [%>|.#.] ; !viäsˊkár%^ÁI kuo%^RDk4ˊsu kuo%^RDk4ˊsu
       where Cx in  ( k4 b d l l t p h s r r n n  t) 
              Cy in ( s  b d l g t p h k v m n n7 t4) 
        matched ;


"From extra strong to weak, part 2"
Cx:Cy <=>   _ (ˊ:) Cz:  Vow:+ (Cns) (%^ÁI:) %^EWG: ([%^RVSH:|%^RLEN:|%^SVSH:]) [%>|.#.] ; !! 1) poccuu:puásui  páápp4ár%^ÁI
!            _  ˊ: s Vow:+  (%^ÁI:) %^EWG: (%^RVSH:|%^RLEN:) [%>|.#.] ; !! 2) kuo%^RDk4ˊsu:kuávsui
        where Cx in ( c k4 h p  h4 ) 
              Cy in ( s v  ṿ v  v  ) 
              Cz in ( c s  h p4 t  ) 
        matched ;

"In front of %^EWG – i4j:j"
i4:0 <=>   _ j  Vow:+ (Cns)  %^EWG: ([%^RVSH:|%^RLEN:]) [%>|.#.] ; ! poijuu:puájui  va%^RVi4jâs


!! !Other consonant rules

"xˊx7:xˊx and xx7:xx" 
!! * __@RULENAME@__ \\   
Cx:Cy <=> Vow:  Cy (ˊ:) _  Vow: (Cns) (%^CLEN:) [%>|.#.] ;
		  Vow:  Cy (ˊ:) _  Vow: %^WG:  %^CLEN:  ;
		  Vow:  Cy (ˊ:) _  Vow: %^SV: %^WG: %^CLEN: %^SLEN: [%>|.#.] ;
		  Vow:  _ (ˊ:) Cy j Vow:+ Cns (%^RLEN:) (%^CSH:)  [%>|.#.]  ; !vi%^RVn7njooh
	where Cx in ( l7 n7 s7 š7 r7 m7 v7 đ7) 
	      Cy in ( l  n  s  š  r  m  v  đ)
		  matched ;

!€ puá^RDlˊl7u>
!€ puá0lˊlu>

!€ puá^RDlˊl7u^WG^RVSH>ttáá
!€ puáˊ00ḷu00>ttáá

!€ ká^RVll7u>
!€ ká0llu>



"Assimilation of root consonant and double suffix consonant"
!! * __@RULENAME@__  delet t in front of -ttáá, avoid -tttáá
t:0  <=> _ (%^RLEN:) (%^CSH:) %> t t ; 

!€ haamit>ttáá
!€ haami0>ttáá


!! !!Vowel rules

!! !First syllable vowel shortening


 
"Root vowel shortening with consonant shortening 3st stems" 
!! * __@RULENAME@__ \\  for  aarrân:aṛânân ^ÁI
Vx:0 <=>  _ Vx: Cns:+ Vow: (Vow:) Cns: (Cns:) (%^ÁI:) %^CSH: (%^SVSH:) [%>|.#.] ; ! pááppár : pápárân
        where Vx in ( a o á ä u y i e ) ;

!€ koonnjâl^CSH>ân
!€ k0on0jâl0>ân

!€ kappeer^CSH^SVSH>ist
!€ ka0p0er00>ist

!€ čäällim^CSH>ist
!€ č0ä0ḷim0>ist

!This is not correct, Check it:
!€ muusik^SC^CSH^RVSH>ist
!€ m0usikk00>ist

! kahhaan^SC^CSH^RVSH>ist
! ka0ha0n0j00>ist

!aarrân:aṛânân
!€ aarrân^CSH>ân
!€ 0a0ṛân0>ân



"Vowel shortening (no consonant shortening) in consonant stems"
!! * __@RULENAME@__ \\ kiirrâs- to kirrâsân, 
Vy:0 <=> Seg: _ Vx Cns: (Cns:) (%^RC:) Vow  :Cns (%^SFV:) (%^SC:) (%^ÁI:) (%^ÁE:) (%^CLEN:) %^RVSH: [%>|.#.] ;
!              _ Vx (i) Cns:+ â: %^UU: %> [i|u|s] ;
       where Vx in ( a e i o u á ä ) 
             Vy in ( a e i o u á ä ) 
       matched ;

!rejected:
!€ kiirrâs^RVSH>ân
!€ k0irrâs0>ân
 
"Vowel shortening in front of two consonants and i-suffix"
!! * __@RULENAME@__ \\ ráávhu- : rávhoid, käähir%^CSH%>id
Vy:0 <=> Vx: _  Cns: [ Cns: - %^RC:0 ] Vow:  %^WG: (%^CLEN:) %^SVLOW: %> i:  ; !! 1)
         Vx: _  Cns:+ Vow:  %^WG: %^RVSH: %^CLEN:  %> i:  ; !! 2) čääci : čassijn
         Vx: _  Cns:+ Vow: (%^CSH:) %^RVSH:  %>  ; !! 3) kaamuv kammust
         Vx _ Cns Cns Vow: %^SV: Cns:+ %^RLEN: (%^CLEN:) %^SLEN: %> ; !! 4) kiiško%^SVd
         Vx: _  Cns:+ Vow: Cns (%^ÁI:)  %^WG: %^RVSH:  %> i:  ; !! 5) pááppár : pävirid
       where Vx in ( a e i o u á ä ) 
             Vy in ( a e i o u á ä ) 
       matched ;


"Vowel shortening eej to eij"
!! * __@RULENAME@__ \\  leeji : leijijn  
e:0  <=> e _  j7: %^RC: Vow:  %^RVSH: %^CLEN:  %>  ; !!  


!€ mááh^RCu^WG^CLEN^SVLOW>in
!€ má0vvo000>in
  
 
!€ mool7^RCâ^WG^RVSH>ttáá
!€ moˊl0â00>ttáá


!! !First syllable vowel lengthening

"Root vowel lenghtening in many stems"
!! * __@RULENAME@__ \\  lekkâ : leehá-  meecij ko%^RVppâin
%^RV:Vx <=> Vx _ Cns:+  :RealVow  (%^SV:) (Cns:) [%^WG:|%^EWG:]   %^RLEN: (%^SLEN: ) %>  ; !lu%^RVčč4â xxx
            Vx _ Cns: Cns: Vow:  (Cns:) %^WG:  %^RLEN: %^SVLOW:  %>  ;
            Vx _ Cns: Cns: Vow: %^RLEN: %^EA: %>  ; !nijbe niijbán
            Vx _ Cns:+ Vow:  (Cns:) %^RLEN:  %^CSH: (%^EA:) %>  ; !
            Vx _ Cns:+ Vow: Cns: %^WG: %^FCD: ; ! poovsa
            Vx _ Cns:+ Vow:+ %^WG: %^RLEN:  [%>|.#.]  ; !
            Vx _ Cns:+ Vow:+ Cns:+ %^RLEN: (%^CLEN:) ([%^SLEN:|%^SVSH:]) %> ; !! 5) keeppidmân  ke%^RVp%^RCidm%^RLEN%^CLEN%>ân
	where Vx in ( a e i o u y á ) ; !removed ä

!€ le^RVk4k4â^WG^RLEN>
!€ lee0hâ00>

!€ nju^RVn7e^SV^WG^RLEN^SLEN>st
!€ njuunee000>st

"Root vowel ä:ää lenghtening"
!! * __@RULENAME@__ \\ special rule, for avoiding conflict
%^RV:ä <=> ä _ Cns:+ Vow:+ Cns:+ %^RLEN: (%^CLEN:) (%^SLEN:) %> ; !! 5) vä%^RVr7%^RCid
           á: _ Cns:* :i (%^SV:) Cns: %^ÁI: [%^EWG:|%^WG:] %^RLEN: [%>|.#.] ;


"Root vowel lengtning with vowel change before suffix i"
!! * __@RULENAME@__ \\  alge : oolgijn 
%^RV:Vy <=> Vx:Vy  _  Cns:+  e: %^WG: %^RLEN:  %> i: ; 
       where Vx in ( a o )
             Vy in ( o u ) 
       matched ;

!€ a^RVlge^WG^RLEN>id
!€ oolgi00>jd





!! !First syllable vowel change

"Root vowel change for á to ä with i in second syllable"
!! * __@RULENAME@__ \\  päävir já%^RVh4h4á%^SVl%^ÁI%^EWG%^RLEN%>
á:ä <=>    _ [á:|%^RV:] Cns:* :i (%^SV:) Cns: %^ÁI: [%^EWG:|%^WG:] [%^RLEN:|%^RVSH:] [%>|.#.] ; !! 1) pááppár:päävir
        á: _    Cns:* :i (%^SV:) Cns: %^ÁI: %^EWG: %^RLEN: [%>|.#.] ;  !! 2) pááppár:päävir
         _  á: Cns:+ Vow: Cns  %^ÁI:  %^WG:  %^RVSH:  %> i  ; !! 3) pááppár:pävirin

!rejected, look at this:
!€ páápp4ár^ÁI^WG>
!€ pää0vir00>



"Root vowel change before suffix i"
!! * __@RULENAME@__ \\  alge : olgijn 
Vx:Vy <=> _  %^RV: Cns:+  e: (%^WG:) ([%^RLEN: |%^CLEN:] )  %> i: ; 
       where Vx in ( a o )
             Vy in ( o u ) 
       matched ;

"Root vowel change u:o, special rule for olmooš"
!! * __@RULENAME@__ \\  ulmu : olmooš. Waiting for more lemmas. u%^RVlmu%^SV%^WG%^SLEN%^SVLOW%>š
u:o <=>   _  %^RV: Cns:+ u:o %^SV: %^WG: %^SLEN:  %^SVLOW:  [%>|.#.]  ;
         _  %^RV: Cns:+ u:o %^WG: %^RVSH:  %^SVLOW:  [%>|.#.]  ; 



!! !First syllable diphthong rules



"Shortening diphthong"
!! * __@RULENAME@__ \\ RD to ˊ muátu muáˊđoost  mie%^RDlˊgâs čiermih vuá%^RDlá%^RVSH%>áš 
%^RD:ˊ <=> _ Cns:+ Vow: (Cns)([%^ÁE:|%^ÁI:]) (%^SV:) %^WG: %^RVSH: ; !! 1) riegis : rieˊgisin
            Vow: _ Cns:+ Vow: (Vow:) Cns: (Cns:) [%^CSH:|%^RVSH:]  (%^SVSH:) [%>|.#.] ; !! 2) iäláán : iäˊḷánân
            _ Cns:0 Cns Vow:+ Cns:+ %^RLEN:0 %^CSH:0 %> ; !! 3) for  l and g as part of cosonant patterns ll:l and gg:g
            _ Cns:+ Vow:  (%^SV:) Cns: %^SC: %^RVSH: %^CSH: %> ; !! 3) siämmun siäˊṃunân
            _ Cns:+ Vow: (%^EWG:) %^RVSH: %> ; !! 4) poccu : puáˊsujin 
            _ Cns:+ Vow:+ Cns:+ (Vow:) (Cns:) %^RVSH: %> ; !! 5) čuoˊṿânem  suo%^RDnjârd


! RV still does not give as good result as RD.

"Vowel shortening for uá" 
!! * __@RULENAME@__ \\  for uábbi -> obbijn  čuá%^RDl7%^RCi%^WG%>in
á:0 <=> u: _ %^RD: Cns:+ i %^WG: %^CLEN: %^SVLOW: %> i:  ;
        u: _ %^RD: Cns:+ i %^WG: %^RVSH: %> i:  ;


"Monophthongisation rule i:e"
!! * __@RULENAME@__ \\  
Vx:Vy <=> _ Vz: Cns:* i %^WG: %> i: ;  
          _ Vz: Cns:* Vow: Cns %^ÁI: [%^WG:|%^EWG:] ([%^RLEN:|%^RVSH:]) [%>|.#.]  ; !! 1)viäskár veeskir iäbˊbár eebir 
!         _ Vz:0 %^RD: Cns:* i %^WG: %^CLEN: %^SVLOW: %> i:  ; 
	where Vx in ( i )
		  Vy in ( e )
		  Vz in ( ä)
	matched ;

"Monophthongisation rule u:o"
!! * __@RULENAME@__ \\  u:o in pairs like kuátij -> koođij čuá%^RDl7%^RCi%^WG%>in
Vx:Vy <=> _ Vz: Cns:* i %^WG: %> i: ;  !Her
         _ Vz:0 %^RD: Cns:* i %^WG: %^CLEN: %^SVLOW: %> i:  ;
         _ Vz: %^RD: Cns:* i %^WG: (%^RLEN:) %> i: ; 
         _ Vz:0 %^RD: Cns:+ i %^WG: %^RVSH: %> i: ;
	where Vx in ( u )
		  Vy in ( o )
		  Vz in ( á )
	matched ;



"Monophthongisation rule ä:a"
!! * __@RULENAME@__ \\  čääci:čassijn, läbži:labžijn 
ä:a <=> _ [ä:|%^RV:] Cns:+ i %^WG: ([%^RVSH: |%^RLEN:] ) %> i: ;
        _ [ ä: |%^RV: ] Cns:+   i %^WG: [%^CLEN: |%^RLEN: ]  %^SVLOW: %> i: ; !čääci:čassijn, läbži:labžijn
        _ %^RV:  Cns:+   i %^WG: %^RLEN:  %> i: ; !ä%^RVi4gi%^WG%^RLEN%>id
        _ ä:0  Cns:+ Vow:  %^WG: %^RVSH: %^CLEN:  %> i:  ; !! čääci : čassijn


!€ sääm7^RCi^WG>in
!€ saam0i0>jn


!€ sääm7^RCi^WG>id
!€ saam0i0>jd

!!  riä%^RDgˊgás%^ÁI%^WG%^RLEN  čuá%^RDgˊgás%^ÁI%^WG%^RLEN> ye



"Monophthongisation rule 2"
!! * __@RULENAME@__ \\  á:o in pairs like kuátij -> koođij, säämi -> saamij  
Vx:Vy <=> Vz: _  Cns:+ i %^WG:   %> i: ;
          Vz:Vy _ (%^RD:) Cns:+ i %^WG: %^RLEN:  %> i: ;
          Vz:Vy  _ (%^RD:) Cns:+ :i Cns: %^ÁI: [%^WG:|%^EWG:] %^RLEN: [%>|.#.] ; !viäsˊkár:veeskir  viäsˊkár%^ÁI%^EWG%^RLEN
          Vz  _ %^RD: Cns:+ :i Cns: %^ÁI: [%^WG:|%^EWG:] [%^RVSH:|%^RLEN:] [%>|.#.]  ; !riä%^RDgˊgás%^ÁI%^WG%^RVSH>in 
          Vz:  _ %^RD: Cns:+ Vow:  %^ÁI: %^EWG: [%>|.#.] ; !puáhˊháá pooṿij
	where Vx in ( ä á ä )
		  Vy in ( e o a )
		  Vz in ( i u ä )
		  matched ;

!nom	ill
!e^RV	iä
!ie	iä
!ä	á
!ye	uá



"Diphthongisation e to iä, part 1"
!! * __@RULENAME@__ \\  
e:i <=> \Vow: _ Vow: Cns:+ i: (%^RLEN:)  (%^CSH:) %^EA: %> ; !! 1) Part 1: e to i; ergi -> iärgán 1, veerdi : viärdán

"č:ij part 1"
!! * __@RULENAME@__ \\  
č:i <=> Vow: _ %^RC:  WeakGrade %^CLEN: %^SVLOW: %> ; !! 1) eeč%^RCi:eijijn


"č:ij part 2"
!! * __@RULENAME@__ \\  
%^RC:j <=> Vow: č:i  _   WeakGrade %^CLEN: %^SVLOW: %> ; !! 1) eeč%^RCi:eijijn



"Diphthongisation e:iä and lengthening ä:ää in Illative and Sg3 2"
!! * __@RULENAME@__ \\  Part 2: ^RV to ä; 
%^RV:ä <=> e:i _ Cns:+ i: (%^RLEN:)  (%^CSH:) %^EA: %> ; ! 1) e:iä, ergi -> iärgán 2 (e+e(RV) -> i + ä
           ä: _ Cns:+ i:  %^WG: %^RLEN: %> \i: ; ! 2) ä:ää


"Diphthongisation o:uá for %^EWG, part 1"
!! * __@RULENAME@__ \\   
%^RV:á <=> o: _ %^RD: Cns:+ u:  %^EWG: (%^RVSH:) %> ; ! 1) poccuu puásui


"Diphthongisation uo:uá for %^EWG"
!! * __@RULENAME@__ \\   
o:á <=> u _ %^RD: Cns:+ u: %^EWG: (%^RVSH:) %> ; ! 1) cuobˊbuu cuábui
        u _ Cns:+ á %^CSH: ;

"Diphthongisation o:uá for %^EWG, part 2"
!! * __@RULENAME@__ \\   
o:u <=>  _ %^RV:á %^RD: Cns:+ u:  %^EWG: (%^RVSH:) %> ; ! 1) poccuu puásui


"ie to iä in Illative and Sg3"
!! * __@RULENAME@__ \\  ... with ^ILL as trigger; kiem´ni -> kiämnán, veerdi : viärdán
e:ä <=> [i | e: ]  _ (%^RD:) Cns:+ Vow: (%^RLEN:)  (%^CSH:) %^EA: %> ; !sieidi siäidán


"ää to áá in Illative and Sg3"
!! * __@RULENAME@__ \\  ... for säämi > sáámán
ä:á <=> _  :á Cns:+ :á (%^RLEN:)  (%^CSH:)  %^EA: %> n ;
        ä: _          Cns:+ :á  (%^RLEN:) (%^CSH:) %^EA: %> n ;

!€ sääm7^RCi^EA>n
!€ sááṃ0á0>n

"äRV to áá in Illative and Sg3"
!! * __@RULENAME@__ \\  ... for täsni -> táásnán
%^RV:á <=> ä: _  Cns:+ :á  %^RLEN: (%^CSH:) %^EA: ;

!€ tä^RVsni^RLEN^CSH^EA>n
!€ táásná000>n

"Vowel lengthening äRV to aa "
!! * __@RULENAME@__ \\  äigi aaigijd
%^RV:a <=> ä:a _  Cns:+ i: %^WG: %^RLEN: %> i: ;


"ye to uá og uo, part 1" 
!! * __@RULENAME@__ \\  Part 1 of ye to uá for kyeli : kuálán 
y:u <=> _ e: %^RD:  Cns:+ Vow: (%^RLEN:) (%^CSH:) %^EA: %> ;
        _ :o %^RD:  Cns:+ i: (%^WG:) ([%^RVSH: |%^RLEN:] ) (%^CLEN:) %> i: ; !sye%^RDj7%^RCi%^RVSH%^CLEN%>in
        _ :o %^RD:  Cns:+ i: %^WG: %^CLEN: %^SVLOW: %> i: ;
        _ :o %^RD:  Cns: 0:ˊ  Cns: i:  %^WG: %^RVSH: %^CLEN: %> i: ;

"ye to uá, part 2"
!! * __@RULENAME@__ \\  Part 2 of ye to uá for kyeli : kuálán
e:á <=> y: _ %^RD:  Cns:+ Vow: (%^RLEN:) (%^CSH:) %^EA: %> ;

"ye to uo, part 2 "
!! * __@RULENAME@__ \\  Part 2 of ye to uá for kyeli : kuolijn
e:o <=> y: _ %^RD:  Cns:+ i: (%^WG:) ([%^RVSH: |%^RLEN:] ) (%^CLEN:) %> i: ;
        y:  _ %^RD:  Cns:+ i: %^WG: %^CLEN: %^SVLOW: %> i: ;
        y: _  %^RD:  Cns: 0:ˊ Cns: i:  %^WG: %^RVSH: %^CLEN: %> i: ;

"uá to ye, part 1" 
!! * __@RULENAME@__ \\  Part 1 luámmán : lyeme čuá%^RDgˊgás%^ÁI%^WG%^RLEN%>
u:y <=> _ á: %^RD: Cns:+ Vow: %^SV: Cns: %^WG: (%^SLEN:) %^FCD: %> ;
        _ á: %^RD: Cns:+ Vow: Cns: %^ÁI: %^WG: [%^RLEN:|%^RVSH:] [%>|.#.] ;

"uá to ye, part 2"
!! * __@RULENAME@__ \\  Part 2 luámmán : lyeme
á:e <=> :y _ %^RD: Cns:+ Vow: %^SV: Cns: %^WG: (%^SLEN:) %^FCD: %> ;
        :e  %^RD: Cns:+ _  %^SV:  Cns: %^WG: (%^SLEN:) %^FCD:  %> ; !luá%^RDmmá%^SVn:lyeme %^WG%^FCD
        :y _ %^RD: Cns:+ Vow: Cns: %^ÁI: %^WG: [%^RLEN:|%^RVSH:] [%>|.#.] ; !cuágˊgás : cyegis čuá%^RDgˊgás%^ÁI%^WG%^RLEN


!! !Second syllable rules

"Second syllable vowel change á to e or i in 3syll stems"
!! * __@RULENAME@__ \\ puttás- > putes, triggers are ^ÁE and ^ÁI já%^RVh4h4á%^SVl%^ÁI%^EWG%^RLEN%>
Vx:Vy <=> _ (%^SV:) (Cns:)  Vz: [%^WG: |%^EWG: ] (%^FCD:) ;  !viäsˊkár%^ÁI ča%^RVskas%^ÁE páápp4ár%^ÁI
          _  Vz: %^EWG: ; !puáhˊháá pooṿij
       where Vx in ( á    á    a   )
             Vy in ( e    i    e   )
             Vz in ( %^ÁE %^ÁI %^ÁE) 
       matched ;

!€ puttás^ÁE^WG>
!€ pu0tes00>

"Stemvowel shortening  áá and aa to e or i in 3syll"
!! * __@RULENAME@__ \\ puttás- > putes, triggers are ^ÁE and ^ÁI
Vx:0 <=> Cns: _ Vx: (Cns:) Vz: [%^WG:|%^CSH:] ; 
       where Vx in ( á    á    a   )
             Vz in ( %^ÁE %^ÁI %^ÁE) 
       matched ;

!rejected
!€ nahh4aar^ÁE^WG>
!€ na0ṿ0er00>

"Fleeting vowel before suffix" ! 
!! * __@RULENAME@__ \\ deletes -i- in piehâin to pieˊhânân pie%^RDhâin piä%^RDi4ppáin
i:0 <=> Vow _ n j ([%^CSH: |%^RLEN:] ) %^SVSH: [%>|.#.] ;




"Stem vowel e deletion before 2nd syllable ij"
!! * __@RULENAME@__ \\  hmm have a look at this
e:0 <=> Cns: _ Cns %^SFV: (%^SC:) (%^CSH:) (%^RVSH:) [%>|.#.] ; 
        e _ %^EE: ;  

!€ šoddâdem^SFV>ist
!€ šoddâd0m0>ist

 
"Stem vowel e to a for a root vowel."
!! * __@RULENAME@__ \\  alge to aalgan
e:a <=> a (%^RV:) Cns:+ _ (%^RLEN:) (%^CSH:) %^EA: ;

!€ a^RVlge^RLEN^CSH^EA>n
!€ aalga000>n

"Stem vowel e to á for other root vowel."
!! * __@RULENAME@__ \\  
e:á <=> [o|i|u] (%^RV:) Cns:+ _ (%^RLEN:) (%^CSH:) %^EA: [%>|.#.] ;


"Stem vowel i to á for other root vowel."
!! * __@RULENAME@__ \\  e.g. säämi > Ill sáámán 
i:á <=> [:e|:i|:ä|:á] ([%^RV: | %^RD: ]) Cns:+ _ (%^RLEN:) (%^CSH:) %^EA: [%>|.#.] ;



"Stem vowel â to a in Pl Nom -h"
!! * __@RULENAME@__ \\  silbâ to siilbah
â:a <=> :Cns (%^RC:) _ (%^SV:) (%^WG:) (%^RLEN:|%^DLEN:) %> h ;


!€ so^RVllâ^WG^RLEN>h
!€ soo0la00>h

"Stem vowel â > á change before i"
!! * __@RULENAME@__ \\ sollâ to sooláid 
â:á <=> _ (%^SV:) (%^WG:) (%^RLEN:|%^CLEN:) %^SVLOW: %> i: ; 

!€ so^RVllâ^WG^RLEN^SVLOW>in 
!€ soo0lá000>in



"Stem vowel change e:i before suffix"
!! * __@RULENAME@__ \\ 
e:i <=> Cns:  _  (%^WG:) ([ %^RLEN: | %^CLEN: | %^RVSH: ]) (%^SVLOW:)  %> i: ; 
        Cns:  _ %> ä p ; !! mánuttep : mánuttiäpán

"Stem vowel u:o change before suffix"
!! * __@RULENAME@__ \\  
u:o <=> Cns: _ ([%^EWG:|%^SV:]) (%^WG:) (%^RVSH:) (%^RLEN:|%^CLEN:) (%^SLEN:) %^SVLOW:  ; 
		_  á:o %^RD: Cns:+ Vow: %^ÁI: %^EWG: ; !puáhˊháá : pooṿij
! stááḷu stááloust

"Second vowel shortening in 3syll consonant stems"
!! * __@RULENAME@__ \\ eemeed : emedân
Vx:0 <=> _ Vx: Cns:+ (%^CSH:|%^WG:|%^EWG:) %^SVSH: %> ; !eemeed : emedân sai4j7oos
    where Vx in ( e o a á u i ) ;

!eemeed:emedân

!€ eemeed^CSH^SVSH>ân
!€ 0em0ed00>ân


!€ kappeer^CSH^SVSH>ân
!€ ka0p0er00>ân

"Stem vowel u:0 in front of â"
!! * __@RULENAME@__ \\ aiguu:aaigâ
u:0 <=> _ â %^WG: ([%^RVSH:|%^RLEN:])   ; 

"Stem vowel o:0 in front of u"
!! * __@RULENAME@__ \\ kuččo>um to kuččum
u:0 <=> _ %^CSH: %> u   ; 
! this rule should be generalised, we talk about a ban on o>u strings

!Rejected, look at this:    
!€ aaigâ^UU>u
!€ a0igu0>u

!! !Suffix vowel rules

"Suffix vowel lengthening"
!! * __@RULENAME@__ \\ long á in kissáást
%^SV:Vx <=> Vx:  _  NotSuffixBorder:* %^SLEN:  %> ;
        where Vx in ( á o u e a i ) ; !  já%^RVm%^RCá%^SVlgm%^RLEN%^CLEN%^SLEN%>

"Suffix vowel lengthening when SVLOW"
!! * __@RULENAME@__ \\ preventing conflict with u %^SV:o
%^SV:Vx <=> Vx:  _  NotSuffixBorder:* %^SLEN: %^SVLOW:  %> ;
        where Vx in ( á o e a i ) ; ! 


"Suffix vowel lengthening SV:e"
!! * __@RULENAME@__
%^SV:e  <=> á: _ Cns:  %^WG: %^SLEN: %^FCD: %> ;

!€ ki^RVss7á^SV^WG^CLEN^SLEN>st
!€ ki0ssáá000>st

!€ mi^RVll7o^SV^WG^CLEN^SLEN>st
!€ mi0lloo000>st

!€ huksi^SVm^SLEN>ist
!€ huksiim0>ist

"Suffix vowel lengthening o"
!! * __@RULENAME@__ \\ stááḷu stááloost
%^SV:o <=> u:  _ %^WG: (%^RVSH:) %^SLEN: %^SVLOW: %>  ; 

!€ táál7^RCu^SV^WG^RVSH^SLEN^SVLOW>st
!€ táál0oo0000>st

"Suffix i5:j for lexicon APINA"
i5:j  <=> Cns i %> _ ;
! salijn

"Suffix i5:0 for lexicon APINA"
i5:0 <=> Vow [i | e] %> _ ;
! timotein oboen


"Suffix i:j for i and e-stems"
!! * __@RULENAME@__ \\  
i:j <=> [i: | e: ] (%^WG:)   ([%^RLEN:|%^RVSH:]) (%^CLEN:)  (%^SVLOW:) (%^EA:)  %> _ [d|n|g|t|%>|.#.];  



!"Illative suffix change in bisyllabic e stems"
! * __@RULENAME@__  changes ^A to a, check this
!%^A:a <=> a (%^RV:) Cns:* e: (Dummy:*) %> _ ;



!! !!Other rules

"Inversion of in to nj"
!! * __@RULENAME@__ \\  adds -j-  in piehâin to piehânjist  
%^SC:j <=> n (%^SFV:) _ (%^CSH:) (%^RVSH:) [%>|.#.] ; 


"Final consonant x:xx"
!! * __@RULENAME@__ \\  pottáák potákkân  
%^SC:Vx <=> Vx _ %^CSH: %^SVSH: [%>|.#.] ; 
        where Vx in ( k l n p t ) ;


!rejected:
!€ pieˊhâin^SFV^SC^CSH^RVSH>ist
!€ pie0hâ0n0j00>ist

!rejected:
!€ kahhaan^SC^CSH^RVSH>ist
!€ ka0ha0nj00>ist

