! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

!! !!!Alphabet

Alphabet  

!! The Inari Saami letters
 a á â ä b c d e f g h i j k l m n o õ p q !!= * @CODE@
 r s t u v w x y z æ ä ø ö å			   !!= * @CODE@
 č đ ŋ ŧ š ž							   !!= * @CODE@
 ¥:0 !!= * @CODE@ for Vowel lengthening

! č đ ŋ š ŧ ž as in Northern Sámi

 A Á Â Å B C D E F G H I J K L M N O Õ P Q !!= * @CODE@
 R S T U V W X Y Z Æ Ä Ø Ö Å 			   !!= * @CODE@
 Č Đ Ŋ Ŧ Š Ž  							   !!= * @CODE@
 ':0 %- 							   !!= * @CODE@
 
!! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 »7     !! * »
 «7     !! * «
 %[%>%] !! * >
 %[%<%] !! * <


!! Accented letters, just in case

 É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Ê Ô Û Î Ã Ý þ Ð   !!= * @CODE@
 é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý þ ð   !!= * @CODE@
 ß ª ß ç								   !!= * @CODE@

!! Different archiphonemes

 h2:h g2:g m2:m n2:n 					   !!= * @CODE@
 b3:b d3:d g3:g m3:m h3:h 				   !!= * @CODE@
 k2:k									   !!= * @CODE@

!! Different triggers, many of these are not in use

      X2:0      X4:0 X5:0 X6:0 X7:0 X8:0 X9:0  !!= * @CODE@
 Q1:0 Q2:0 Q3:0 Q4:0 Q5:0 Q6:0 Q7:0 Q8:0 Q9:0  !!= * @CODE@ 
 Y1:0 Y2:0 Y3:0 Y4:0 Y5:0 Y6:0 Y7:0 Y8:0 	   !!= * @CODE@

!! More triggers, in the new format, we prefer to use these

 %^SHV:0 				     !!= * @CODE@
 %^CPL:0 %^SCPL:0 		     !!= * @CODE@
 %^SVL:0  %^WG1:0 %^VLOW:0   !!= * @CODE@
%^ODDC:0				     !!= * @CODE@
%^ODDSHV:0				     !!= * @CODE@
%^ODDLV:0				     !!= * @CODE@
%^XST:0 %^IA:0			     !!= * @CODE@
%^ILL:0 				     !!= * @CODE@

! Morpheme boundaries:
 «  !!= * @CODE@ Derivational prefix
 »  !!= * @CODE@ Derivational suffix
 %< !!= * @CODE@ Inflectional prefx
 %> !!= * @CODE@ Inflectional suffix
 #  !! * hashtag Word boundary for both lexicalised and dynamic compounds
 %^ !!= * @CODE@ (exceptional) soft hyphenation point

!! End of alphabet definitions

;

!! !!! Sets

Sets
 Vow = a á â ä e i o u y æ ø ö å			   !!= * @CODE@ 
       é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý 	   !!= @CODE@ 
       A Á Â Ä E I O U Y Æ Ø Ö Å			   !!= @CODE@ 
       É Ó Ú Í À È Ò Ú Ì Ë Ü Ï Ê Ô Û Î Ã Ý 	   !!= @CODE@ 
       ¥ ;									   !!= @CODE@ 
 Cns = b c č d đ f g h j k l m n ŋ 			   !!= * @CODE@ 
       p q r s š t ŧ v w x z ž				   !!= @CODE@ 
       B C č D Đ F G H J K L M N Ŋ			   !!= @CODE@ 
       P Q R S Š T Ŧ V W X Z Ž 				   !!= @CODE@ 
       ht g2 m2 n2 							   !!= @CODE@ 
       b3 d3 g3 m3 h3 						   !!= @CODE@ 
       k2 ;									   !!= @CODE@ 
 WeG = %^WG1 X2 %^CPL ;						   !!= * @CODE@ 
 StemCns = b b3 d d3 g g2 g3 h h2 h3 		   !!= * @CODE@ 
           j l m m2 m3 n n2 ŋ r s ;			   !!= @CODE@ 
 Dummy = %^WG1 X2 %^SVL X4 %^VLOW %^CPL %^SCPL !!= * @CODE@ 
         %^ODDC %^ODDLV %^ODDSHV 			   !!= @CODE@ 
         %^XST %^IA ;						   !!= @CODE@ 
 ODD = %^ODDC %^ODDLV %^ODDSHV ;			   !!= * @CODE@ 


!! !!! Rule section

Rules



!! !!Root vowel shortening

"Root Vowel Shortening in EAP"
!! __@RULENAME@__  
!! {{{
Vx:0 <=> Vx _ Cns:* Vow X2:0 ; !!= @CODE@
       where Vx in ( a e o u ä ) ; !!= @CODE@
!! }}}       
       
!€ šaaluX2%>ittáá
!€ ša0lu00ittáá

!! !!Diphtong simplification


!! Not written.


!! !!Stem vowel alternations




"Adjust â to á before Com Sg and Ill Pl"
!! __@RULENAME@__  
!! {{{
 â:á <=> _ (Dummy:*) %>: %^IA: i [ n | d ] ; !!= @CODE@
!! }}}       
 	
!! Should we do the short one instead, as Matti Morottaja?

"i as j after i-stem"
!! __@RULENAME@__  
!! {{{
 i:j <=> i (Dummy:*) %>: (Dummy:*) _  ; !!= @CODE@	
!! }}}       


"e stem to a"
!! __@RULENAME@__ , this is not finished
!! {{{
 e:a <=> [a|e] Cns+: %>: _ ; !!= @CODE@ 
!! }}}       

! "Stem Vowel Change in Comitative"
!  Vx:Vy <=> _  %^SVL:0 ;
!        where Vx in ( â e )
!   	         Vy in ( á i )
! 	     matched ;
! mielgâ : mielg%^SVLáin
! alge : olgijn
! and the umlaut must be fixed as well...
	


!! !!Cns stem alternations
! - - - - - - - - - -

"Long vowel shortening in Cns stems"
!! __@RULENAME@__  
!! {{{
Vx:0 <=> Cns:* Vx _ Cns %^SHV:0 ; !!= @CODE@
     where Vx in (á i); !!= @CODE@
!! }}}       


"Vowel lowering in Contracted Stems"
!! __@RULENAME@__  
!! {{{
Vx:Vy <=> _ Cns+ Vow:* %^VLOW: ; !!= @CODE@ 
      where Vx in (u a)       !!= @CODE@
            Vy in (o á)       !!= @CODE@
            matched ; !!= @CODE@
!! }}}       






!! !!Consonant gradation rules



!! !Compensatory lengthening rule


"Compensatory lengthening"
!! __@RULENAME@__  
!! {{{
¥:Vx <=> Vx _ Cns:* Vow:* (:StemCns) [ %^CPL: | %^SCPL: ] ; !!= @CODE@
	where Vx in ( a e i o u ) ; !!= @CODE@
!! }}}       



!Cx:Vx <=> Vx _ Cx Vow %^CPL: ;
!      where Cx in ( l p p )
!      	    Vx in ( u a ä )
!	    mixed ;




!! !Deletion rules


"Gradation: Single Consonant"
!! __@RULENAME@__  
!! {{{
 Cx:Cy <=> Vow (Vow:) _  Vow: ( StemCns:) (:StemCns) (Dummy:*) WeG: ; !!= @CODE@
 	where Cx in ( p t h k c )       !!= @CODE@
 		  Cy in ( v đ v h s )       !!= @CODE@
 		  matched ; !!= @CODE@
!! }}}       

"Gradation: Double Consonant" 
!! __@RULENAME@__  as for káffe:káfes
!! {{{
  Cx:0 <=> Vow: Cy: _ Vow: ( StemCns:) (:StemCns) (Dummy:*) WeG: ; !!= @CODE@
	where Cx in (p b d g m n ŋ d s z r š ž l v )       !!= @CODE@
	      Cy in (p b d g m n ŋ d s z r š ž l v )       !!= @CODE@
	      matched ; !!= @CODE@
!! }}}

! p t k š č c h đ, 
! Note: added : to Cy to allow for pp:v subrule below

"Gradation: pp:v"
!! __@RULENAME@__  
!! {{{
  p:v <=> Vow: p: _ Vow ( StemCns:) (:StemCns) (Dummy:*) WeG: ; !!= @CODE@
!! }}}       

"Gradation: xx:y 1"
!! __@RULENAME@__  
!! {{{
 Cx:0 <=> Vow (Vow:) _ Cx:Cy Vow: ( StemCns:) (:StemCns) (Dummy:*) WeG: ; !!= @CODE@
 	where Cx in ( t k č c )       !!= @CODE@
 		  Cy in ( đ h j s )       !!= @CODE@
 		  matched ; !!= @CODE@
!! }}}       

!! p in previous rule instead. We need both pp:p and pp:v.

"Gradation: xx:y 2"
!! __@RULENAME@__  
!! {{{
 Cx:Cy <=> Vow (Vow:) Cx: _ Vow: ( StemCns:) (:StemCns) (Dummy:*) WeG: ; !!= @CODE@
 	where Cx in ( t k č c )       !!= @CODE@
 		  Cy in ( đ h j s )       !!= @CODE@
 		  matched ; !!= @CODE@
!! }}}

! tt : đ verbeissä -- toimii (?): tiettiđ : tieđ-
! tt : t nomineissa -- ei toimi hattâ : haatâ

"xkk:xk Gradation"
!! __@RULENAME@__  
!! {{{
 k:0 <=> Vow: Cx _ k Vow: ( StemCns:) (:StemCns) (Dummy:*) WeG: ; !!= @CODE@
	where Cx in ( ŋ r ) ; !!= @CODE@
!! }}}       
	
"xtt:xt Gradation"
!! __@RULENAME@__  
!! {{{
 t:0 <=> Vow: Cx _ t Vow: ( StemCns:) (:StemCns) (Dummy:*) WeG: ; !!= @CODE@
	where Cx in ( i l n r ) ; !!= @CODE@
!! }}}       
	
"xcc:xc Gradation"
!! __@RULENAME@__  
!! {{{
 c:0 <=> Vow: Cx _ c Vow: ( StemCns:) (:StemCns) (Dummy:*) WeG: ; !!= @CODE@
	where Cx in ( i l n r ) ; !!= @CODE@
!! }}}       
	
"xpp:xp Gradation"
!! __@RULENAME@__  
!! {{{
 p:0 <=> Vow: Cx _ p Vow: ( StemCns:) (:StemCns) (Dummy:*) WeG: ; !!= @CODE@
	where Cx in ( l m r v ) ; !!= @CODE@
!! }}}       
	
"xss:xs Gradation"
!! __@RULENAME@__  
!! {{{
 s:0 <=> Vow: Cx _ s Vow: ( StemCns:) (:StemCns) (Dummy:*) WeG: ; !!= @CODE@
	where Cx in ( l n r ) ; !!= @CODE@
!! }}}       
	
"xčč:xč Gradation"
!! __@RULENAME@__  
!! {{{
 č:0 <=> Vow: Cx _ č Vow: ( StemCns:) (:StemCns) (Dummy:*) WeG: ; !!= @CODE@
	where Cx in ( l r ) ; !!= @CODE@
!! }}}       
	
"xšš:xš Gradation"
!! __@RULENAME@__  
!! {{{
 š:0 <=> Vow: Cx _ s Vow: ( StemCns:) (:StemCns) (Dummy:*) WeG: ; !!= @CODE@
	where Cx in ( l r ) ; !!= @CODE@
!! }}}       
	
"nnj:nj Gradation"
!! __@RULENAME@__  
!! {{{
 n:0 <=> Vow: n _ j Vow: ( StemCns:) (:StemCns) (Dummy:*) WeG: ; !!= @CODE@
!! }}}       

	

	
!! !Change rules


	
"h:v in Front of Voiceless Dentals"
!! __@RULENAME@__  
!! {{{
 h:v <=> Vow:*  _ (s) [c|č|t] Vow: WeG: ; !!= @CODE@
!! }}}       


"Consonant Lengthening"
!! __@RULENAME@__  
!! {{{
 0:Cx <=> Vow _ Cx Vow: %^XST: ; !!= @CODE@
 	where Cx in ( t l s ) ; !!= @CODE@
!! }}}       


! "i To j in High Vowel Suffixes"
!  i:j <=> %>: ( g u ) i _ [d|n] ;
 ! oohoid
 ! kuolijd

! "kk:h" ákku
! Cannot have kk as underlying form, because of kukká.



! Turning cns into vow.

!€ tullâ^CPL
!€ tuulâ0

! Hmm, do not like this rule, but the alternative is insertion, 
! also not good, or eventually a long baseform *tuullâ or *tuu7llâ, 
! where u7 is there just to be deleted unless in weak grade contexts.

!Idas rules for verbs

!"Even syllabic verbs present sg, monophpt E I"
! Vx:Vy <=>  _ C+ (V) %>: %^1UML:0 ;
! 	where Vx in (i e u)
! 	      Vy in (æ a å)
! 	matched ;


!! !!Odd-syllabic verb rules
! - - - - - - - - - - - - 

"t Shortening in Odd-Syllabic Verbs"
!! __@RULENAME@__  
!! {{{
t:0 <=> t _ ODD:0 [#:0|%>: Cns] ; !!= @CODE@
!! }}}       

!€ cie'lâtt#
!€ cie'lât0#

"V-Context Vowel Shortening in Odd-Syllabic Verbs"
!! __@RULENAME@__  
!! {{{
Vx:0 <=> Vx _ %^ODDSHV:0 %>: Vow ; !!= @CODE@
     where Vx in (á a i e o); !!= @CODE@
!! }}}       

"C-Context Vowel Shortening in Odd-Syllabic Verbs"
!! __@RULENAME@__  
!! {{{
Vx:0 <=> Vx _ %^ODDLV:0 %>: [Cns|#:0] ; !!= @CODE@
     where Vx in (á a i e o); !!= @CODE@
!! }}}       
