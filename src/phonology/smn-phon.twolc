! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no



!! !!!Alphabet 

Alphabet   

!! !!The Inari Saami letters

 a á â ä b c d e f g h i j k l m n o õ p q !!= * @CODE@
 r s t u v w x y z æ ä ø ö å			   !!=  @CODE@
 č đ ŋ ŧ š ž				 			   !!=  @CODE@

 A Á Â Å B C D E F G H I J K L M N O Õ P Q !!= * @CODE@
 R S T U V W X Y Z Æ Ä Ø Ö Å 			   !!=  @CODE@
 Č Đ Ŋ Ŧ Š Ž  							   !!=  @CODE@
   %- 							   !!= * @CODE@
 ':0   !!= * @CODE@ this to zero

!! !Accented letters, just in case

 É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Ê Ô Û Î Ã Ý þ Ð   !!= * @CODE@
 é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý þ ð   !!=  @CODE@
 ß ª ß ç								   !!=  @CODE@

 
 
!! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 »7     !! * »
 «7     !!  «
 %[%>%] !!  >
 %[%<%] !!  <



!! !Different archiphonemes


p4:p  t4:t k4:k č4:č  l4:l h4:h   !!= * @CODE@  these are consonants that change in cg
i4:i                    	        !!= * @CODE@  this is the underlying i consonant
i2:i u2:u         	        !!= * @CODE@  this is final i, u in contract verbs etc changing to e, o
i3:i           	        !!= * @CODE@  this is invariant i
a5:a i5:i u5:u y5:y                  !!= * @CODE@  these do not change 
c5:c č5:č h5:h j5:j l5:l ŋ5:ŋ r5:r t5:t  !!= * @CODE@  these do not change 
i6:0 !!= * @CODE@ a fake vowel, to get WG rules to function, e.g. kyehtlov (kyehti6lov)
 

!! !More abstract archiphonemes

!%^A:0		 = algan lodán
%^RC:0		!!= * @CODE@ = root cons., lengthened by ^CLEN - máán%RCu máánnun
%^RD:0		!!= * @CODE@ = root dipht., shortened by by ^RVSH - vuá%^RDggu vuáguttáá
%^RV:0	 	!!= * @CODE@ = root vowel, lengthened by ^RLEN - ta%^RVvlu taavlu 
%^SC:0      !!= * @CODE@ = stem cons, %^SC:j, pluss lengthen after earlier shortening by %^CSH, see lyeme
%^SV:0 		!!= * @CODE@ = stem vowel, lengthened by ^SLEN u:uu - táálu%^SV tááloost
%^V			!!= * @CODE@ = vowel copy

!! !Triggers

%^CLEN:0	!!= * @CODE@ = cons lengthening, with ^RC
%^CSH:0     !!= * @CODE@ = cons shortening, laaŋkku - laaŋkun
%^RVSH:0    !!= * @CODE@ = Diphtong shortening
%^EE:0      !!= * @CODE@ = Final vowel deletion in ee, oo, uu, áá stems
%^FCD:0     !!= * @CODE@ = Final consonant deletion 
%^EA:0      !!= * @CODE@ = e to a/á in ill etc
%^RLEN:0    !!= * @CODE@ = root lengthening, with ^RD
%^SLEN:0    !!= * @CODE@ = stem vowel lengthening
%^SVLOW:0   !!= * @CODE@ = stem vowel lowering â>á and u>o - árppu árppoin
%^SVSH:0    !!= * @CODE@ = stem vowel shortening kappeer kaperân
%^VLOW:0    !!= * @CODE@ = Vowel lowering in 3rd sg of contract verbs tuhhid:tohhe
%^WG:0      !!= * @CODE@ = weak grade trigger
%^ÁE:0      !!= * @CODE@ = á to e (more) in 3-syll
%^ÁI:0      !!= * @CODE@ = á to i (more) in 3-syll
%^EWG:0     !!= * @CODE@ = from extra strong to weak, e.g. pp:v
%^AO:0      !!= * @CODE@ = a to o
%^VHIGH:0   !!= * @CODE@ = hightening of vowels for verbs o to uu, a to oo
%^VBACK:0   !!= * @CODE@ = back vowels for verbs, ä to a (when needed, normally 2syll a|â is enough


 
%^ODRVSHV:0    !!= * @CODE@ =  Cns shortening in C|# and shortvow in Inf etc  (not in use)
%^ODDC:0       !!= * @CODE@ =  shorten cns in odd-syll stems before C, #  (not in use)

!SV|ÁE|ÁI|EE  EA|SC FCD  EWG|WG|CSH  CLEN  RVSH|RLEN   SLEN|SVSH  SVLOW

!! !!Morpheme boundaries:

!! Here we define the suffix border >
!! The other borders are not used yet, but still defined:

 «  !!≈ ;@CODE@: Derivational prefix
 »  !!≈ ;@CODE@: Derivational suffix
 %< !!≈ ;@CODE@: Inflectional prefx
 %> !!≈ ;@CODE@: Inflectional suffix
 %^ !!≈ ;@CODE@: (exceptional) soft hyphenation point
 #  !! and the symbol #, which is Word boundary for both lexicalised and dynamic compounds

!! End of alphabet definitions

;

!! !!! Sets

Sets
 Vow = a á â ä e i o u u2 y æ ø ö å y5	i2 i5 i3 i6		   !!= * @CODE@ 
       é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý 	   !!= @CODE@ 
       A Á Â Ä E I O U Y Æ Ø Ö Å			   !!= @CODE@ 
       É Ó Ú Í À È Ò Ú Ì Ë Ü Ï Ê Ô Û Î Ã Ý 	   !!= @CODE@ 
       %^RV %^RD %^SV ;						   !!= @CODE@ 
 RealVow = a á â ä e i o u y æ ø ö å y5	i2 i5 i3 i6	   !!= * @CODE@ 
       é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý 	   !!= @CODE@ 
       A Á Â Ä E I O U Y Æ Ø Ö Å			   !!= @CODE@ 
       É Ó Ú Í À È Ò Ú Ì Ë Ü Ï Ê Ô Û Î Ã Ý ;   !!= @CODE@ 
 Cns = b c č d đ f g h j k l m n ŋ 			   !!= * @CODE@ 
       p q r s š t ŧ v w x z ž				   !!= @CODE@ 
       B C Č D Đ F G H J K L M N Ŋ			   !!= @CODE@ 
       P Q R S Š T Ŧ V W X Z Ž 				   !!= @CODE@ 
       ht g2 k2 m2 n2 							   !!= @CODE@ 
       b3 d3 g3 m3 h3 						   !!= @CODE@ 
       p4 k4 č4 r4 l4 h4 t4 i4                  !!= @CODE@
       h5 j5 ŋ5 t5 c5 l5 t5                                      !!=  @CODE@
       m7 n7 ŋ7 v7 s7 r7 đ7	l7 j7 h7 š7		   !!= @CODE@ 
       %^RC %^SC   ;                         !!=  @CODE@ 
 Geminate = b d g p h t k c f z l m n ŋ r s š v  ;  !!= * @CODE@  
 Sgm = Vow Cns ;							   !!= * @CODE@ 
 NotSuffixBorder = Vow %^FCD Cns %^EWG %^CSH %^WG %^CLEN %^SLEN %^RLEN %^SVLOW %^RVSH %^ÁE %^ÁI %^EA %^SVSH ; !!= * @CODE@ 
 Triggers =   %^FCD %^WG %^EWG %^CSH %^CLEN %^SLEN %^RLEN %^RVSH %^SVLOW %^ÁE %^EA %^ÁI %^SVSH ; !!= * @CODE@ 
 StemCns = b b3 d d3 g g2 g3 h h2 h3 		   !!= * @CODE@ 
           j l m m2 m3 m8 n n8 n2 ŋ r s  ;     !!= @CODE@ 



           
!! !!!Definition section

!! Introducing a WG right context 
!! {{{WeakGrade =  (RealVow:) :RealVow  (Cns:) ([%^SV:|%^ÁI:|%^ÁE:]) (%^FCD:) %^WG:0   ; }}}  

Definitions

WeakGrade =  (RealVow:) :RealVow (Vow:)  (Cns:+) ([%^SV:|%^ÁI:|%^ÁE:|%^EA:]) (%^FCD:) %^WG:0     ;   
ConsonantShorting =  (RealVow:) :RealVow (Vow:) (Cns:+) ([%^SV:|%^ÁI:|%^ÁE:|%^EA:]) (%^FCD:) %^CSH:0 ;   

!! As we look at more cases, the WeakGrade definition tends to be longer. 
!! We want to keep it short, but we want the code to work Thus, we document.
!! * The second :RealVow is there for long consonant stems
!! * The (Cns:) is there for consonant stems

!! !!!Rule section

Rules

!! !!Consonant rules

!! !Consonant gradation



"x:y consonant gradation"
!! * __@RULENAME@__ \\  WG gives x:x and x:y, when there is no consonant lengthning  
Cx:Cy <=> Vow: _ (%^RC: )  WeakGrade (%^VHIGH:) ([%^RVSH:|%^RLEN:]) (%^SLEN:) (%^SVLOW:) %> ;
	where Cx in ( h p c )   !č
          Cy in ( v v s  )   !j
		  matched ;


!€ suá^RDn^RCju^WG>
!€ suá0n0ju0>

!€ va^RVh%^RCo^WG^CLEN>im
!€ va0v0o00>im

"č:j consonant gradation"
!! * __@RULENAME@__ \\ 
č4:j <=> Vow: _ (%^RC: )  WeakGrade \%^CLEN: ;


!iṣ̌e:iše

!€ i^RVš^RCe^WG>
!€ i0š0e0>




"xx:x consonant gradation and shortening and x:x shortening"
!! * __@RULENAME@__ \\   weak grade or consonant shortening for illativ
Cx:0 <=> Vow: _  Cx: WeakGrade \%^CLEN: ;
         Vow: _  Cx: ConsonantShorting \%^CLEN:  ;
	where Cx in ( b c č d đ f g h k l m n ŋ p r s š v z ž ) ; !lnŋr kahhaanj 

!€ vi^RVššâ^RVSH^CSH>vettee
!€ vi00šâ00>vettee

!€ las%^RCe%^SVtt^CLEN^SLEN>vettee
!€ lasseet000>vettee

!cuozzâ:cuozâ
!€ cuo^RDzzâ^WG^RLEN>
!€ cuo00zâ00>

!váddu:váádun
!€ vá^RVddu^RLEN^CSH>n
!€ váá0du00>n

!kahhaan:kahanân
!€ kahhaan^CSH^SVSH>ân
!€ ka0h0an00>ân

"tt:t  - final consonant and consonant gradation"
!! * __@RULENAME@__ \\ 3syll verbs in -tt lose t in front of cns-initial suff
t:0 <=> [Vow:|%^SV:] _ t: WeakGrade \%^CLEN: ; !! 1)
        [Vow:|%^SV:] _ t: ConsonantShorting \%^CLEN:  ; !! 2)
        [RealVow:|%^SV:] _ t: Triggers:* %> \Vow: ; !! 3) uáivátt : uáivát
        Vow: Vow: _ t: %> â n  ; !! 4) uáiváátt>ân uáiváátân

!€ kulluutt^SVSH>
!€ kullu00t0>

!€ las^RCe^SVtt^CLEN^SLEN>
!€ lassee0t00>
!
!"xx:x consonant shortening for consonants belonging to two different patterns"
! * __@RULENAME@__ \\   weak grade or consonant shortening for illativ
!Cx:0 <=> Vow: _  Cx: WeakGrade ([%^RVSH: |%^RLEN:]) ([%^SVLOW: | %^SLEN:]) %> ; !! 1) weak grade päddi : päädi tullâ:tuuláin
!         Vow: _  Cx: Vow: (%^EA: ) %^CSH: ( %^RVSH:) %> ; !! 2) consonant shortening for illativ, päddi : páádán RLEN?
!	where Cx in ( l n ŋ r m ) ; !


!luččâ:luujáin 

!€ lu^RVčč4â^WG^RLEN^SVLOW>in
!€ luu0já000>in



"xx:y to something else, consonant gradation and shortening, part 1" 
!! * __@RULENAME@__ \\   hh:V, kk:h, pp:v, čč:J, deleting first part, k:0 in kk4:h etc  
Cx:0 <=> Vow: _  Cy: (%^RC:) WeakGrade \%^CLEN: ; !! 1) weak grade: ruttâ:ruuđáin 
         Vow: _  Cy: Vow: (%^EA: ) %^CSH: (%^RLEN:)  %> ; !! 2) consonant shortening for illativ
         Vow: _  Cy: Vow:+  Cns ([%^ÁI:|%^ÁE:]) %^CSH: ([%^RLEN: | %^RVSH:])  (%^SVSH:)  %> ; !! 3) consonant shortening for páápp4ár%^ÁI viäkk4ás%^ÁI
	where Cx in ( h   t  k p   ) 
	      Cy in ( h4  t4 k4 p4 )
		  matched ;


"č:0 in čč4:j,  part 1" 
!! * __@RULENAME@__ \\   
č:0 <=> Vow: _  č4: (%^RC:) WeakGrade ; !! 1) weak grade: luččâ:luučân
        Vow: _  č4: Vow: Cns: %^EWG: ; 
        Vow: _  č4: Vow: (%^EA: ) %^CSH: (%^RLEN:)   %> ; !! 3) consonant shortening for illativ: luččâ:luučân



"xx:y to something else, consonant gradation, part 2" 
!! * __@RULENAME@__ \\  hh:V, kk:h, pp:v, čč:J here we change second part, 
Cx:Cy <=> Vow:  Cz:  _  WeakGrade  ; !! 1) weak grade: 
          Vow: Cz:  _ Vow: Cns: %^EWG: ; 
          Vow:  Cz: _ :i %^SV: Cns %^ÁI: %^WG: %^RVSH: %> ;
	where Cx in ( h4  č4 ) 
	      Cy in ( v    j ) 
	      Cz in ( h   č ) 
		  matched ;


"Long vowel shortening (no consonant shortening)"
!! * __@RULENAME@__ \\ (example?), no ^CSH, since that gives Vy:0
Vy:0 <=> Vx: _ [Cns: - č4:] Cns:* (%^RC:) Vow:  %^WG: %^RVSH: (%^SVLOW:) [%>|.#.] \e ; !! 1) 2st stems
         Vx: _  Cns:+         Vow:  Cns: %^CSH: %^RVSH: %^SVSH: [%>|.#.] ; !! 2) 3syll kaavpug:kavpugân 
         Vx: _  Cns:+         Vow: (%^SV:) Cns:+ Vow: Cns:  %^RVSH: [%>|.#.] ; !táálhudem koomerdem
       where Vx in ( a e i o u y á ä ) 
             Vy in ( a e i o u y á ä) 
       matched ;

"From i4j to j"
!! * __@RULENAME@__ \\ sai4joos 
i4:0 <=>  Vow: _ j:  Vow:+ (Cns) [%^EWG:|%^WG:|%^CSH:] ; ! %^SVSH:  [%>|.#.] ; !! 1) saijos:sajos po%^RVi4ju%^EWG%>i

!vuái4jus

"Long vowel shortening, č:j "
!! * __@RULENAME@__ \\ special rule for getting ej̣ittáá instead of ej̣ittáá.
Vy:0 <=> Vx: _ č4: (%^RC:) Vow:  %^WG: %^RVSH: (%^SVLOW:) [%>|.#.] ; !! 1) 2st stems
     where Vx in ( a e i o u y á ä ) 
           Vy in ( a e i o u y á ä ) 
     matched ;


"Underlying long vowel shortening iä:e"
!! * __@RULENAME@__ \\ 
Vy:0 <=> Vx: _  Cns:+ Vow:  Cns: [%^ÁI:|%^ÁE:] [%^WG:|%^EWG:] %^RVSH:  [%>|.#.] ; !! 1) 3st stems viäskár veskirin, časkes časkes ča%^RVskas%^ÁE viäskár%^ÁI%^EWG%^RVSH
       where Vx in ( i ) 
             Vy in ( ä ) 
       matched ;


!kaavpug:kavpugân
!€ kaavpug^RVSH^CSH^SVSH>ân
!€ ka0vpug000>ân


! moolâ to moolâ:		  
		  
!€ mool^RCâ^WG>
!€ mool0â0>


!käähir to käävir

!€ käähir^WG>
!€ käävir0>

!juáhu to juávuttáá

!€ juá^RDh^RCu^WG^RVSH>ttáá
!€ juá0v0u00>ttáá


"t:đ, also tk4:đh gradation, part 1" 
!! * __@RULENAME@__ \\   
t:đ <=> Vow: _   k4: (%^RC:) WeakGrade ; !for ito : iđo 
        Vow: _  %^RC:   j RealVow: %^WG:0 %^CLEN:0 %^SVLOW:0 ; ! Com
        Vow: _ (%^RC:) (j) WeakGrade (%^CLEN:0) ;
        Vow: _  %^RC:   j RealVow:  %^CLEN:0 ; ! Par and Ess
        Vow: _  (k4:) Vow:+ (Cns:) ([%^ÁI:|%^ÁE:]) %^EWG: ;

!€ jo^RVtk4â^WG^RLEN>
!€ joođhâ00>

"k4:h gradation for kk4, tk4, rkk4, lkk4"
!! * __@RULENAME@__ \\    for atka : ađha 
k4:h <=> Vow: [t:|k:]  _  WeakGrade  ;
         Vow: [t:|k:]  _  Vow:+ (Cns:) ([%^ÁI:|%^ÁE:]) %^EWG: ; !viäkk4ás%^ÁI
         Vow: [l | r | v | i4: | đ ]  k: _  WeakGrade ;
         Vow: [l | r | v | i4: | đ ]  k: _ Vow:+ (Cns:) ([%^ÁI:|%^ÁE:]) %^EWG:  ; !rkk	rh

"k:h gradation for kk4"
!! * __@RULENAME@__ \\   
k:h <=> Vow: _  k4:h WeakGrade %^CLEN: ; !puáhhoin

         
!€ mä^RVtk4i^WG^RLEN>.#.
!€ määđhi00>.#.

!€ su^RVkk4á^SV^WG^CLEN^SLEN>st
!€ su0hháá000>st

"h4:v gradation for hh4"
!! * __@RULENAME@__ \\   
h4:v <=> Vow: :Vow h:  _  Vow:+  Cns [%^ÁI:|%^ÁE:]  %^EWG: %^RLEN: ; ! jäävvil siävus but loves
		 Vow: Vow: _ Vow: Cns %^EWG: [%^RLEN:|%^RVSH:] ;!			
         
"h:v gradation for hh4"
!! * __@RULENAME@__ \\   
h:v <=> Vow:  _ h4: Vow:+ Cns [%^ÁI:|%^ÁE:]  %^EWG: ; !jäävvil and loves


"h4:0 for hh4"
!! * __@RULENAME@__ \\   
h4:0 <=>  Cns Vow: (Vow:0) h:  _  Vow:+  Cns [%^ÁI:|%^ÁE:]  %^EWG:  ; !  loves jävilid but jäävvil 



"k:0 gradation for xkk4"
!! * __@RULENAME@__ \\  for lkk4, rkk4, vkk4
k:0 <=> Vow: [l | r | v | i4: | đ ]  _ k4:h  WeakGrade (%^CLEN: ) (%^SVLOW:) ; !! 1) WeakGrade 
        Vow: [l | r | v | i4: | đ ]  _ k4:k Vow: (%^EA:) %^CSH: (%^RLEN:)   %> ; !! 2) consonant shortening for illativ
        Vow: [l | r | v | i4: | đ  ]  _ k4:h Vow:+ (Cns:) ([%^ÁI:|%^ÁE:]) %^EWG:  ; !rkk	rh
        Vow: :Vow  _ k4:h Vow:+ (Cns:) ([%^ÁI:|%^ÁE:]) %^EWG: %^RLEN:  ; !kuuhâs rá%^RVkk4ás%^ÁI 
        Vow:  _ k4:h Vow:+ (Cns:) ([%^ÁI:|%^ÁE:]) %^EWG: %^RVSH:  ; ! rá%^RVkk4ás%^ÁI%^EWG%^RVSH




"t4:đ gradation for tt4" 
!! * __@RULENAME@__ 
t4:đ <=> t:  _ WeakGrade ; !! 1) ruttâ ruuđâ
         t:  _ Vow:+ (Cns:+) ([%^ÁI:|%^ÁE:]) %^EWG: ; !! 2) tiettu:tieđâ čuottâlm:čuođâlm
!€ ru^RVtt4â^WG^RLEN>
!€ ruu0đâ00>

 
"t:0 foran t4" 
!! * __@RULENAME@__ 
t:0 <=> _  t4: Vow:+  (Cns:+) ([%^ÁI:|%^ÁE:]) %^EWG: ; !! 1) tiettu tieđâ        

!rejected:       
!€ haavvâr^CSH^RVSH>ist
!€ h0av0âr00>ist

!rejected:       
!€ pottáák^SC^CSH^SVSH>ist
!€ po0t0ákk00>ist

"xy:zy gradation"
!! * __@RULENAME@__ \\  ht:vt, we use h4 
Cx:v <=>  _  Cz (%^RC:) Vow:+ (%^SV: ) (Cns:)  (%^FCD:) %^WG: ;
         _  Cz t Vow (%^FCD:) %^WG: ; !pi%^RVh4tâs
       where Cx in ( h4 k4 h4 h4 ) 
             Cz in ( t  s  č  c  ) 
       matched ;


!€ pih4tâs^WG
!€ pivtâs0

!€ totk4os^WG
!€ tođhos0

"xyy:xy gradation A"
!! * __@RULENAME@__ \\   
Cy:0 <=> :Vow (Vow:) Cx: _ Cz: (%^RC:) WeakGrade \%^CLEN:   ;  
          Cx:  _ Cz:  Vow (%^EA:) %^CSH:    ;  
      where Cx in ( j i4 i4 j j i4 i4 l l l l l l l m m n n n ŋ r r r r r r r r v v v ) 
            Cy in ( p c  t  s t k  p  c k p s t č š p č c s t k c č p k s t č š c p t ) 
            Cz in ( p c  t  s t k  p  c k p s t č š p č c s t k c č p k s t č š c p t) 
      matched ;

!€ á^RVrppu^WG^RLEN>
!€ áár0pu00>

!€ á^RVrppu^WG^RVSH>ttáá
!€ á0r0pu00>ttáá

!€ riänttus^WG>
!€ riän0tus0>





!"xx:x gradation for ^CSH illative 2" 
! * __@RULENAME@__ \\    
!Cx:0 <=> _  Cx Vow: %^CSH: %^RLEN: %> n ; !! 1) 2st hellá:heeḷán
!         _  Cx Vow: (Vow:) Cns: (Cns:) %^CSH: (%^SVSH:) [%>|.#.] ; !! 2) 3st stems aarrân:arânân
!      where Cx in ( r l n ŋ m )  ;

!kurrâ kuurân
!€ ku^RVrrâ^RLEN^CSH>n
!€ kuu0râ00>n

!sollâ sooân
!€ so^RVllâ^RLEN^CSH>n
!€ soo0lâ00>n

!! !Consonant shortening

"Word final consonant deletion"                 
!!  * __@RULENAME@__ \\ for vaskâm:vaaskâ
Cx:0 <=> _ ([%^ÁI:|%^ÁE:]) %^FCD: ;      
          where Cx in (n m t v j s š) ;

!rejected, look at this:
!€ va^RVskâm^FCD^RLEN
!€ vaaskâ000
	
"Final m in stems, mn to n"  
!!  * __@RULENAME@__ \\ for puhelim :  puhelin   
m:0 <=>  _ n  [%>|.#.] ;          


"nnj to nj shortening weak grade and consonant shortening"
!! * __@RULENAME@__ \\  special rule, e.g. konnjâl -> konjâlân puánnjá
n:0 <=> n  _ j ConsonantShorting \%^CLEN: ; !! 1) 
        n  _ j WeakGrade \%^CLEN: ; !! 2)lonnjâ:loonjáin
        n  _ j Vow:+ Cns ([%^ÁI:|%^ÁE:]) [%^CSH:|%^EWG:] ; !vinnjooh:vinjohân


!€ konnjâl^CSH>ân
!€ kon0jâl0>ân



!€ suá^RDn^RCju^CLEN>n
!€ suá0nnju0>n

!rejected, look at tis:
!€ haamit^SC^CSH^RVSH>ist
!€ h0amitt00>ist


!€ su^RVkk4á^SV^WG^CLEN^SLEN>st
!€ su0hháá000>st

!rejected:
!€ kopp^RCâ^WG^CLEN^SVN>in
!€ ko0pp0á000>in


"Final consonant x:xx"
!! * __@RULENAME@__ \\  pottáák potákkân  
%^SC:Cx <=> Cx _ %^CSH: %^SVSH: [%>|.#.] ; 
        where Cx in ( k l n p t ) ;



!rejected:
!€ kahhaan^SC^CSH^RVSH>ist
!€ ka0ha0nj00>ist




!! !Consonant lengthening
 
         
"Lengthening root consonant j:ij 1"
!! * __@RULENAME@__ \\  for saje:saijeen
j:i <=> Vow: _ %^RC: :Vow   %^CLEN:  (%^RVSH:)       %> ; !Par Ess
         Vow: _ %^RC: :Vow %^SV: %^CLEN: %^SLEN: %> ; !Par Ess



!€ sa^RVj^RCe^SV^CLEN^SLEN>n
!€ sa0ijee00>n

!€ pááj^RCá^CLEN>n
!€ pááijá0>n




"Lengthening of half-long root consonant x:xx"
!! * __@RULENAME@__ \\  
%^RC:Cy <=> Vow:  Cx: _ :Vow+ Sgm:* Triggers:* %^CLEN: ;
 	where Cx in ( č g k l r s h m n đ j v ŋ  š )
 		  Cy in ( č g k l r s h m n đ j v ŋ š ) 
 		  matched ;

"Lengthening of consonant, e.g. for Ess/Par"
!! * __@RULENAME@__ \\   piiru piirrun niisu niissun vahe vahheen suop%^RCâinj%^CLEN%^RLEN%^SVSH
%^RC:Cy <=> Vow: Cx:   _ Vow (%^SV:)  Cns:* [%^CLEN: (%^SLEN:)|%^SVSH:] %> \[Vow: | s ] ; 
            Vow: Cx:   _ Vow           Cns         %^CLEN:  %> \[ i:|s]  ;
            Vow: (i4:) Cx:Cy _ Vow: (i:) Cns:+   %^CLEN: (%^RLEN:) ([%^SLEN:|%^SVSH:]) %>   ; !! 3) kepidem : keeppidmân
	where Cx in ( v t h5 c p)  
	      Cy in ( v t h  c p)   
		  matched ;

!kiär%^RCun%^CLEN%>ttáá

!€ ke%^RVp%^RCidem%^RLEN%^CLEN%>ân
!€ keeppid0m00>ân  

!kiehâ  kievváin
!€ kie^RDh^RCâ^WG^CLEN^SVLOW>in
!€ kie0vvá000>in

!iše iššeest
!€ i^RVšš^RCe^SV^WG^CLEN^SLEN>st
!€ i0šš0ee000>st

!€ kol^RCe^CLEN>
!€ kolle0>


"Lengthening of root consonant x:yy 1"
!! * __@RULENAME@__ \\  
Cx:Cy <=> Vow:  _ %^RC: :Vow+ Cns:* [Triggers: -  %^FCD:]* %^CLEN: (%^RVSH:) ([%^SVLOW: |%^SLEN:]) %> [ i:|s] ;
	where Cx in ( h p c )
		  Cy in ( v v s ) 
		  matched ;

"Lengthening of root consonant x:yy 2"
!! * __@RULENAME@__ \\  
%^RC:Cy <=> Vow: Cx:Cy _ :Vow+ Cns:* [Triggers: - %^FCD:]* %^CLEN:  (%^RVSH:) ([%^SVLOW: |%^SLEN:]) %> [ i:|s] ;
	where Cx in ( h p c )
		  Cy in ( v v s ) 
		  matched ;


!kassa:kassaast
!€ ka^RVss^RCa^SV^WG^CLEN^SLEN>st
!€ ka0ss0aa000>st

!vahe vavveest
!€ va^RVh^RCe^SV^WG^CLEN^SLEN>st
!€ va0vvee000>st

!vahe vahheen
!€ va^RVh^RCe^SV^CLEN^SLEN>n
!€ va0hhee00>n

!€ kiel^RCâ^CLEN>n
!€ kiellâ0>n



"nj to nnj consonant lengthning"
!! * __@RULENAME@__ \\  special rule, suánju : suánnjun for Par and Ess
%^RC:n <=> Vow: n _ j RealVow: %^CLEN: %> [ d | n ] ;



"Consonant lengthening after xx:x and xy:zy weak grade with x4 on upper side"
!! * __@RULENAME@__ \\  as previous rule, but for k4, č4, p4  kukse:kuvssijn  
%^RC:Cx <=> Vow:  Cy:  Cz: _ WeakGrade %^CLEN: \%^SLEN: ; 
        where Cy in (   č  p  k  h h4 h4 h4 k4 v ) 
              Cz in (  č4 p4 s  c t  č c s c ) 
              Cx in (   j  v  s  c t  č c s c ) 
        matched ;

"Consonant lengthening č:čč"
!! * __@RULENAME@__ \\  as previous rule, but for k4, č4, p4  kukse:kuvssijn  
%^RC:č <=> Vow:  č4:  _ Vow %^CLEN:  ; 



"From extra strong to weak, part 1 cc:s, rr:r"
!! * __@RULENAME@__ \\  
Cy:0 <=>  [Cx: | .#. | # ]  _  Vow:+ (Cns:+) ([%^ÁI:|%^ÁE:]) (%^FCD:) %^EWG: ; !  !! poccuu:puásui  kieppu:kiepâ iäbbár%^ÁI cuobbuu:cuábui kiellâs
       where Cx in ( b c č đ d g h k l m n ŋ r t p  p  r s š v z ž) 
             Cy in ( b c č đ d g h k l m  n ŋ r t p p4 r s š v z ž) 
        matched ;



"From extra strong to weak, part 2"
!! * __@RULENAME@__ \\  
Cx:Cy <=>   _  Cz:  Vow:+ (Cns:) ([%^ÁI:|%^ÁE:]) (%^FCD:) %^EWG: ([%^RVSH:|%^RLEN:|%^SVSH:]) [%>|.#.] ; !! 1) poccuu:puásui  páápp4ár%^ÁI
        where Cx in ( c k4 h p  h4 h4 h4 ) 
              Cy in ( s v  v v  v  v  v ) 
              Cz in ( c s  h p4 t  č  c ) 
        matched ;

"From extra strong to weak, p4:v"
!! * __@RULENAME@__ \\  
p4:v <=> Vow: _ Vow: (Cns:) %^EWG: ;

!! !Other consonant rules





"Assimilation of consonants t d š"
!! * __@RULENAME@__  delet t in front of -ttáá, avoid -tttáá
Cx:0  <=>  Sgm: (Cy:) _ (%^CSH:) (%^RLEN:) %> Cy Cy ; 
           Vow: Cx _ %> m ;
	where Cx in ( d t š) 
	      Cy in ( d t š)
		  matched ;

!€ haamit>ttáá
!€ haami0>ttáá


!! !!Vowel rules
!  =============
!  =============

!! !First syllable vowel shortening


!€ koonnjâl^CSH>ân
!€ k0on0jâl0>ân

!€ kappeer^CSH^SVSH>ist
!€ ka0p0er00>ist

!€ čäällim^CSH>ist
!€ č0ä0lim0>ist

!This is not correct, Check it:
!€ muusik^SC^CSH^RVSH>ist
!€ m0usikk00>ist

! kahhaan^SC^CSH^RVSH>ist
! ka0ha0n0j00>ist

!aarrân:arânân
!€ aarrân^CSH>ân
!€ 0a0rân0>ân



!rejected:
!€ kiirrâs^RVSH>ân
!€ k0irrâs0>ân
 
"Vowel shortening VV:V"
!! * __@RULENAME@__ \\ ráávhu- : rávhoid, käähir%^CSH%>id kuuđiâ%^RVSH%>žân
Vy:0 <=> Vx: _  Cns:+ [ Cns: - %^RC:0 ] Vow:  %^WG: (%^CLEN:) %^SVLOW: %> i: ; !! 1)
         Vx: _  Cns:+ Vow:  %^WG: %^CLEN:  %^RVSH:  %> i:  ; !! 2) čääci : čassijn
         Vx: _  Cns:+ Vow:+ (Cns) (%^CSH:) %^RVSH: (%^SVLOW:)  %>  ; !! 3) kaamuv kammust
		 Vx: _ [Cns:+ - č4:] Vow:+ Cns:+ (%^ÁI:) %^CSH: (%^SVSH:) [%>|.#.] ; ! pááppár : pápárân páápp4ár%^ÁI%^EWG%^RVSH%>id
		 Vx: _ [Cns:+ - č4:] Vow:+ Cns:+ (%^ÁI:) %^EWG: %^RVSH: [%>|.#.] ; ! pááppár : pápárân páápp4ár%^ÁI%^EWG%^RVSH%>id
       where Vx in ( a e i o u á ä y A E I O U Á Ä ) 
             Vy in ( a e i o u á ä y a e i o u á ä ) 
       matched ;

"Vowel shortening eej to eij"
!! * __@RULENAME@__ \\  leeji : leijijn  
e:0  <=> e _  j: %^RC: Vow:  %^CLEN:  %^RVSH:  %>  ; !!  


"Stem vowel shortening in front of ^EE"
!! * __@RULENAME@__ \\ 
Vx:0 <=> Vx _ %^EE: ;
       where Vx in ( e o u á i a)  ;



!€ mááh^RCu^WG^CLEN^SVLOW>in
!€ má0vvo000>in
  
 
!€ mool^RCâ^WG^RVSH>ttáá
!€ mo0l0â00>ttáá


!! !First syllable vowel lengthening

"Root vowel lenghtening in many stems"
!! * __@RULENAME@__ \\  lekkâ : leehá-  meecij ko%^RVppâin
%^RV:Vy <=> Vx _ [Cns:+ - [h: h ]]  Vow: (Vow:)  (Cns:+) (Triggers: - %^RLEN:)*   %^RLEN: ; !! 1) 2st and 3st
!            Vx: _ Cns: (Cns:) Vow:+ Cns:+ Vow: Cns:  %^RLEN:  ; !! 2) 4st 
       where Vx in (a e i o y á A E I O Á ) ! Ánná:Ááná u, U in separate rule Root vowel u length
             Vy in (a e i o y á a e i o á) 
       matched ;

		
!€ le^RVkk4â^WG^RLEN>
!€ lee0hâ00>

!€ nju^RVne^SV^WG^RLEN^SLEN>st
!€ njuunee000>st

"Root vowel ä:ää lenghtening"
!! * __@RULENAME@__ \\ special rule, for avoiding conflict spä%^RVi4lih%^EWG%^RLEN
%^RV:ä <=> ä _ Cns:+ Vow:+ Cns:+  %^CLEN: %^RLEN: ; !! 5) vä%^RVr%^RCid
          :ä _ Cns:* :i (%^SV:) Cns: (%^ÁI:) (%^FCD:) [%^EWG:|%^WG:] %^RLEN: [%>|.#.] ; !spääilih


"Root vowel o lengthening with vowel change before suffix i"
!! * __@RULENAME@__ \\  alge : oolgijn 
%^RV:o <=> a:o  _  Cns:+  e:     %^WG:  %^RLEN:  %> i: ; 
           a:o  _  Cns:+  u      %^CSH: %^RLEN:  %> [m|đ|u]   ;

!€ a^RVlge^WG^RLEN>id
!€ oolgi00>jd

!€ va^RVh^RCu^CSH^RLEN>
!€ vooh0u00>

"Root vowel u lengtning with vowel change before suffix i"
!! * __@RULENAME@__ \\  alge : oolgijn 
%^RV:u <=> o:u  _  Cns:+  e:     %^WG:  %^RLEN:  %> i: ; 
           o:u  _  Cns:+  [â|a|i|á|u] (%^WG:|%^CSH:) (%^CLEN:)  %^VHIGH: %^RLEN: ;
          [U|u] _ [Cns:+ - [h: h ]]  Vow: (Vow:) (Cns:+) (Triggers: - %^VHIGH:)* %^RLEN: ; 
          
!€ lo^RVh^RCâ^RLEN>đ
!€ luuh0â0>đ

!€ lo^RVh^RCá^WG^VHIGH^RLEN>im
!€ luuv0á000im



!! !First syllable vowel change

"Root vowel change for á to ä with i in second syllable"
!! * __@RULENAME@__ \\  päävir 
á:ä <=>    _ [ á:|%^RV:] Cns:* :i (%^SV:) Cns: %^ÁI: [%^EWG:|%^WG:] [%^RLEN:|%^RVSH:] [%>|.#.] ; !! 1) pááppár:päävir
        á: _   Cns:* :i (%^SV:) Cns: %^ÁI: %^EWG: %^RLEN: [%>|.#.] ;  !! 2) pááppár:päävir
!         _ [ á:|%^RV:] Cns:+ :i         Cns  %^ÁI:  %^EWG:  %^RVSH:  %> ; !i  ; !! 3) pááppár:pävirin páápp4ár%^ÁI%^EWG%^RVSH%>id

!rejected, look at this:

!€ páápp4ár^ÁI^WG>
!€ pää0vir00>


"Root vowel change a:o before suffix i"
!! * __@RULENAME@__ \\  alge : olgijn 
a:o <=> _  %^RV: Cns:+  e: (%^WG:) ([%^RLEN: |%^CLEN:] )  %> i: ; 



"Root vowel á:a for verbs"
!! * __@RULENAME@__ 
á:a <=> [Cns|.#.]    _    Cns:+ u2:  %> [:u|i|i2:i|Cns - j] Sgm:* %> ;
        [Cns|.#.]    _    Cns:+ i2:i %>    [i|i2:i|Cns] Sgm:* %> ;
        [Cns|.#.]    _ á: Cns:+ %^VLOW: [â|a] %> ;
        [Cns|.#.] á: _    Cns:+ %^VLOW: [â|a] %> ;
        [Cns|.#.]    _  %^RV:   Cns:+ u %>    [m|h|u] ; ! 2st verb pákkuđ: pakkum/h/u

"Root vowel a:o for contract verbs"
!! * __@RULENAME@__ 
a:o <=> [Cns|.#.] _       Cns:+ u2:  %> [:u|i|i2:i|Cns - j] Sgm:* %> ;
        [Cns|.#.] _       Cns:+ i2:i %>    [i|i2:i|Cns] Sgm:* %> ;
        [Cns|.#.] _ %^RV: Cns:+ u %^CSH: %^RLEN: %> [m|đ|u] ;

"Root vowel o:u for contract verbs and Root vowel change o:u before suffix i"
!! * __@RULENAME@__ 
o:u <=> [Cns|.#.] _ Cns:+ u2:  %> [:u|i|i2:i|Cns - j] Sgm:* %> ;
        [Cns|.#.] _ Cns:+ i2:i %>    [i|i2:i|Cns] Sgm:* %> ;
                  _ %^RV: Cns: (Cns:) e: (%^WG:) ([%^RLEN: |%^CLEN:] )  %> [i:|đ|m|v|p] ; !!  alge : olgijn, not ohce 
        [Cns|.#.] _ %^RV: Cns:+  [â|a|i|á|u] (%^WG:|%^CSH:) (%^CLEN:) %^VHIGH: ; ! XXX ny

!€ tohhi2>đ
!€ tuhhi>đ

!€ lo^RVh^RCá^WG^VHIGH^RLEN>im
!€ luuv0á000>im

!! !First syllable diphthong rules


! RV still does not give as good result as RD.


!! !ä rules

"Monophthongisation rule iä:ee 1"
!! * __@RULENAME@__ \\  
i:e <=> _ ä: Cns:* i %^WG: %> i: ;  
        _ ä: Cns:* Vow: Cns [%^ÁI:|%^ÁE:] [%^WG:|%^EWG:] ([%^RLEN:|%^RVSH:]) [%>|.#.]  ; !! 1)viäskár veeskir iäbbár eebir 

"iä:e rule 1"        
!! * __@RULENAME@__ \\  
i:0 <=> [.#.|Cns] _ ä: Cns:+ i2:i %>    [i|i2:i|Cns] Sgm:* %> ;
        [.#.|Cns] _ ä: Cns:+ u2:  %> [:u|i|i2:i|Cns - j] Sgm:* %> ; 


"iä:e and iä:ee rule 2"
!! * __@RULENAME@__ \\  
ä:e <=> i:  _ Cns:+ i %^WG:   %> i: ;
        i:e _ Cns:+ i %^WG: %^RLEN:  %> i: ;
        i:e _ Cns:+ :i Cns: %^ÁI: [%^WG:|%^EWG:] %^RLEN: [%>|.#.] ; !viäskár%^ÁI%^EWG%^RLEN
        i:  _ Cns:+ Vow:  %^ÁI: %^EWG: [%>|.#.] ; !puáhháá poovij
!       i: _  Cns:+ u2: %> [o|j Vow] ;
        i: _  Cns:+ u2: %> [:u|i|i2:i|Cns - j] Sgm:* %> ;


!€ siämmu2>đ>
!€ s0emmu>đ>

"Different rules for ä:a, first vowel (or only vowel)"
!! * __@RULENAME@__ \\  čääci:čassijn, läbži:labžijn 
ä:a <=>      _ [ä:|%^RV:] Cns:+ i    %^WG: ([%^RVSH:|%^RLEN:]) %> i: ;
             _ [ä:|%^RV:] Cns:+ i    %^WG:  [%^CLEN:|%^RLEN: ]  (%^SVLOW:) %> i: ; !čääci:čassijn
             _     %^RV:  Cns:+ i    %^WG:   %^RLEN:           %> i: ; !ä%^RVi4gi%^WG%^RLEN%>id
             _ ä:         Cns:+ Vow: %^WG:   %^CLEN:  %^RVSH:  %> i:  ; !! čääci : čassijn
             _ ä:a        Cns:+ i2:i                           %> [i|i2:i|Cns] Sgm:* %> ;
   [.#.|Cns] _            Cns:+ i2:i                           %> [i|i2:i|Cns] Sgm:* %> ;
   [.#.|Cns] _ ä:         Cns:+ [a|â|i %^VBACK:] (%^EA:) (%^WG:|%^CSH:)   %>        ; ! to be written
   

"ää to aa, second vowel"
!! * __@RULENAME@__ \\  á:o in pairs like kuátij -> koođij, säämi -> saamij  
ä:a   <=> ä:  _ Cns:+ i                %^WG:                  %> i: ;
          ä:  _ Cns:+ i                %^WG: %^RLEN:          %> i: ;
          ä:  _ Cns:+ :i   Cns: %^ÁI: [%^WG:|%^EWG:] %^RLEN: [%>|.#.] ; !viäskár:veeskir  viäskár%^ÁI%^EWG%^RLEN
          ä:  _ Cns:+ Vow:      %^ÁI:  %^EWG:                [%>|.#.] ; !puáhháá poovij
          ä:  _ Cns:+ i2:i                                    %> [i|i2:i|Cns] Sgm:* %> ;
          ä:  _ Cns:+ [a|â|i %^VBACK:]  (%^EA:) (%^WG:|%^CSH:)   %>        ; ! to be written

!conflict Vz  _ Cns:+ [:i|:e] Cns: [%^ÁI:|%^ÁE:] [%^WG:|%^EWG:] ([%^RVSH:|%^RLEN:]) [%>|.#.]  ; !riäggás%^ÁI%^WG%^RVSH>in 


"ää to áá in Illative and Sg3"
!! * __@RULENAME@__ \\  ... for säämi > sáámán
ä:á <=> _  :á Cns:+ [:á|u]  %^EA: (%^CSH:|%^WG:) (%^RLEN:) %>  ;    
        ä: _  Cns:+ [:á|u]  %^EA: (%^CSH:|%^WG:) (%^RLEN:) %>  ;
        _  ä: Cns:+ [:á|u]  %^EA: (%^CSH:|%^WG:) %^RVSH:   %>  ;  

!€ sääm^RCi^EA>n
!€ sáám0á0>n

! "ää to áá in Illative and Sg3"
! !! * __@RULENAME@__ \\  ... for säämi > sáámán
! ä:á <=> _  :á Cns:+ :á   %^EA: (%^CSH:|%^WG:) (%^RLEN:)   %> n ;
!         ä: _          Cns:+ :á %^EA: (%^CSH:|%^WG:) (%^RLEN:)  %> n ;


"äRV to áá in Illative and Sg3"
!! * __@RULENAME@__ \\  ... for täsni -> táásnán
%^RV:á <=> ä: _  Cns:+ :á %^EA: (%^CSH:) %^RLEN:  ;

!€ tä^RVsni^RLEN^EA^CSH>n
!€ táásná000>n

"Vowel lengthening äRV to aa "
!! * __@RULENAME@__ \\  äigi aaigijd
%^RV:a <=> ä:a _  Cns:+ i: %^WG: %^RLEN: %> i: ;



!€ sälkki2>đ>
!€ salkki>đ>

!€ sääm^RCi^WG>in
!€ saam0i0>jn

!€ sääm^RCi^WG>id
!€ saam0i0>jd

! riäggás%^ÁI%^WG%^RLEN>  
! čuággás%^ÁI%^WG%^RLEN> 

!€ stäävi2>đ>
!€ staavi>đ>

!€ stäävi2>j>
!€ staavi>j>

!€ stäävi2>i2>
!€ staavi>i>

!€ stäävi2>jeh>
!€ stääve>jeh>


!! !á and uá rules

"Monophthongisation rule uá:o0 part 2: Vowel shortening for uá" 
!! * __@RULENAME@__ \\  for uábbi -> obbijn  čuál%^RCi%^WG%>in
á:0 <=> u: _ Cns:+ i %^WG: %^CLEN: %^SVLOW: %> i:  ;
        u: _ Cns:+ i %^WG: %^RVSH: %> i:  ;
        u: _ Cns:+ u %^RVSH: %>  ; ! uáinu
        u: _ Cns:+ i2:i %> [i|i2:i|Cns] Sgm:* %> ; 
        u: _ Cns:+ u2:  %> [:u|i|i2:i|Cns - j] Sgm:* %> ; !*

"Monophthongisation rule u:o and root vowel change u:o, special rule for olmooš and contract verbs"
!! * __@RULENAME@__ \\  ulmu : olmooš. Waiting for more lemmas. u%^RVlmu%^SV%^WG%^SLEN%^SVLOW%>š
u:o <=>  [.#.|#] _     Cns:+ u:o %^SV: %^WG: %^SLEN:  %^SVLOW: [%>|.#.]  ; !%^SV%^WG%^SLEN%^SVLOW%>š
         [.#.|#] _     Cns:+ u:o       %^WG:          %^SVLOW: [%>|.#.]  ; 
                 _ á:  Cns:+ u2:                               %> [:u|i|i2:i|Cns - j] Sgm:* %> ;
                 _ á:  Cns:+ i2:i                              %>    [i|i2:i|Cns]     Sgm:* %> ; 
                 _ á:  Cns:* i %^WG:                           %> i: ; 
                 _ á:0 Cns:* i %^WG:          %^CLEN: %^SVLOW: %> i:  ;
                 _ á:  Cns:* i %^WG:                 (%^RLEN:) %> i: ; 
                 _ á:0 Cns:+ i %^WG:                   %^RVSH: %> i: ;

!€ uáinu2>đ
!€ o0inu>đ


"Diphthong rule uá to uo"
!! * __@RULENAME@__ \\  
á:o <=> u5:  _ Cns:+ u2:u %> [i|:u|Cns - j] Sgm:* %> ; !uá to uo
!       u    _ Cns:+ u2:u %> [i|:u|Cns - j] Sgm:* %> ; ! needed?
        u:   _ Cns:+ i %^WG:   %> i: ;
        u:o  _ Cns:+ i %^WG: %^RLEN:  %> i: ;
        u:o  _ Cns:+ :i Cns: %^ÁI: [%^WG:|%^EWG:] %^RLEN: [%>|.#.] ; !viäskár:veeskir  viäskár%^ÁI%^EWG%^RLEN
        u:   _ Cns:+ Vow:  %^ÁI: %^EWG: [%>|.#.] ; !puáhháá poovij 
        u    _ Cns:+ Vow:+ (Cns:) ([%^ÁI:|%^ÁE:]) (%^FCD:) â %> ; !stuárrá:stuorrâ



"Diphthongisation i5ä to ie"
!! * __@RULENAME@__ \\  
ä:e <=> i5: _ Cns: Cns:+ á: s [%^ÁI:|%^ÁE:] %^EWG: [%^RVSH:|%^RLEN:] %> ; !viäkk4ás : viehis

"Diphthongisation e to iä, part 1"
!! * __@RULENAME@__ \\  
e:i <=> \Vow: _ Vow: Cns:+ [i2:|e:|i:|i3:] %^EA: (%^CSH:) (%^RLEN:)   %> ; !! 1) Part 1: e to i; ergi -> iärgán 1, veerdi : viärdán
       \Vow: _ %^RV: Cns:+ u %^EWG: ;  ! čevžuin čiävžui

"Root vowel ahhu to áhu in C-stems"
!! * __@RULENAME@__ \\  ahhu:áhu
a:á <=> \Vow: _ %^RV: h: h u %^WG: (%^RLEN:) %> ;

"Root vowel ahhu to ááhu in C-stems"
!! * __@RULENAME@__ \\  ahhu:ááhu
%^RV:á <=> \Vow: a: _ h: h u %^WG: %^RLEN: %> ;



"č4:ij part 1"
!! * __@RULENAME@__ \\  
č4:i <=> Vow: _ %^RC:  WeakGrade %^CLEN: %^SVLOW: %> ; !! 1) eeč%^RCi:eijijn


"č:ij part 2"
!! * __@RULENAME@__ \\  
%^RC:j <=> Vow: č4:i  _   WeakGrade %^CLEN: %^SVLOW: %> ; !! 1) eeč%^RCi:eijijn



"Diphthongisation e:iä and lengthening ä:ää in Illative and Sg3 2"
!! * __@RULENAME@__ \\  Part 2: ^RV to ä; ke%^RVrttu%^WG%^RLEN%>š
%^RV:ä <=> e:i _ Cns:+ i: %^EA:  (%^CSH:) (%^RLEN:)   %> ; ! 1) e:iä, ergi -> iärgán 2 (e+e(RV) -> i + ä
           ä: _ Cns:+ i:  %^WG: %^RLEN: %> \i: ; ! 2) ä:ää
           e:i _ Cns:+ u: %^EWG:   ; !kiärtuš čevžuin čiävžui


"Diphthongisation o:uá for %^EWG, part 1"
!! * __@RULENAME@__ \\   
%^RV:á <=> o: _ Cns:+ u:  %^EWG: (%^RVSH:)  %> ; ! 1) poccuu puásui


"Diphthongisation uo:uá for %^EWG"
!! * __@RULENAME@__ \\   
o:á <=> u _ Cns:+ u: [%^EWG:|%^WG:] ([%^RVSH:|%^RLEN:]) %> ; ! 1) cuobbuu cuábui, kuođhu
        u _ Cns:+ á %^CSH: ;
        u _ Cns:+ â %^EA: %^CSH: ;
        u _ Cns:+ [o|u] %^VHIGH: %> ; !! 4) vuoll- > vuállus

"Diphthongisation o:uá for %^EWG, part 2"
!! * __@RULENAME@__ \\   
o:u <=>  _ %^RV:á Cns:+ u:  %^EWG: (%^RVSH:) %> ; ! 1) poccuu puásui


"ie to iä in Illative and Sg3"
!! * __@RULENAME@__ \\  ... with ^ILL as trigger; kiem´ni -> kiämnán, veerdi : viärdán
e:ä <=> [i | e: ]  _  Cns:+ Vow:  %^EA: (%^CSH:) ;




"ye to uá og uo, part 1" 
!! * __@RULENAME@__ \\  Part 1 of ye to uá for kyeli : kuálán 
y:u <=> _ e:  Cns:+ Vow: %^EA: ((%^CSH:) %^RLEN:)  %> ;
        _ :o  Cns:+ :i (%^WG:) (%^CLEN:)  ([%^RVSH: |%^RLEN:] ) %> i: ; !syej%^RCi%^RVSH%^CLEN%>in
        _ :o  Cns:+ :i %^WG: %^CLEN: %^SVLOW: %> i: ;
        _ :o  Cns:  Cns: :i  %^WG: %^CLEN:  %^RVSH: %> i: ;
        _ :o  Cns:+ i2:i %> [i|i2:i|Cns] Sgm:* %> ; 

"ye to uá, part 2"
!! * __@RULENAME@__ \\  Part 2 of ye to uá for kyeli : kuálán
e:á <=> y: _  Cns:+ Vow: %^EA: (%^CSH:) (%^RLEN:)  %>  ;

"ye to uo, part 2 "
!! * __@RULENAME@__ \\  Part 2 of ye to uá for kyeli : kuolijn
e:o <=> y: _  Cns:+ :i (%^WG:) (%^CLEN:) ([%^RVSH: |%^RLEN:] )  %> i: ;
        y: _  Cns:+ :i %^WG: %^CLEN: %^SVLOW: %> i: ;
        y: _  Cns: Cns: :i  %^WG: %^CLEN: %^RVSH: %> i: ;
        y: _  Cns:+ i2:i %> [i|i2:i|Cns] Sgm:* %> ; 

"uá to ye and uo to ye, part 1" 
!! * __@RULENAME@__ \\  Part 1 luámmán : lyeme čuággás%^ÁI%^WG%^RLEN%>
u:y <=> _ á: Cns:+ Vow:+ Cns: [%^ÁI:|%^ÁE:] (%^FCD:) %^EWG: ; ! [%^RLEN:|%^RVSH:] [%>|.#.] ;
        _ o: Cns:+ [e ([%^WG:|%^CLEN:]) %> e|i %^CSH: %> h] ;
!       _ o: Cns:+ e ([%^CSH:|%^WG:|%^CLEN:]) %> [e|h] ;

!€ vuolle^CLEN>en
!€ vyelle0>en

"uá to ye, part 2"
!! * __@RULENAME@__ \\  Part 2 luámmán : lyeme
á:e <=> u: _ Cns:+ Vow: %^SV: Cns: %^FCD: %^EWG: (%^SLEN:) [%>|.#.] ;
        :y _ Cns:+ Vow:+ Cns: [%^ÁI:|%^ÁE:] (%^FCD:) %^EWG: ; ! [%^RLEN:|%^RVSH:] [%>|.#.] ; !cuággás : cyegis čuággás%^ÁI%^WG%^RLEN

"uo to ye, part 2"
o:e <=> u:y _ Cns:+ [e ([%^WG:|%^CLEN:]) %> e|i %^CSH: %> h] ;
!vyelleen, vyeleem,vyelih

!! !Second syllable rules

"Second syllable vowel change á to e or i in 3syll stems"
!! * __@RULENAME@__ \\ puttás- > putes, triggers are ^ÁE and ^ÁI já%^RVh4h4á%^SVl%^ÁI%^EWG%^RLEN%>
Vx:Vy <=> Cns: _ (%^SV:) (Cns:)  Vz: (%^FCD:) %^EWG:   ;  !viäskár%^ÁI ča%^RVskas%^ÁE páápp4ár%^ÁI
          Cns: _  Vz: %^EWG: ; !puáhháá poovij
          Cns: Vx: _ s  Vz: %^EWG: ; !lohhes
       where Vx in ( á    á    a   )
             Vy in ( e    i    e   )
             Vz in ( %^ÁE %^ÁI %^ÁE) 
       matched ;

!€ puttás^ÁE^WG>
!€ pu0tes00>

"Stemvowel shortening  áá and aa to e or i in 3syll"
!! * __@RULENAME@__ \\ puttás- > putes, triggers are ^ÁE and ^ÁI
Vx:0 <=> Cns: _ Vx: (Cns:) Vz: [%^EWG:|%^CSH:] ; 
       where Vx in ( á    á    a   )
             Vz in ( %^ÁE %^ÁI %^ÁE) 
       matched ;

!rejected
!€ nahh4aar^ÁE^WG>
!€ na0v0er00>

"Fleeting vowel before suffix" ! 
!! * __@RULENAME@__ \\ deletes -i- in piehâin to piehânân piehâin piäi4ppáin
i:0 <=> Vow _ n j (%^CLEN:) (%^RLEN:) %^SVSH: [%>|.#.] ;


 
"Stem vowel e to a for a root vowel."
!! * __@RULENAME@__ \\  alge to aalgan
e:a <=> a (%^RV:) Cns:+ _ %^EA: ;

!€ a^RVlge^RLEN^EA^CSH>n
!€ aalga000>n

"Stem vowel e to á for other root vowel"
!! * __@RULENAME@__ \\  if e stem
e:á <=> [o|i|u] (%^RV:) Cns:+ _ %^EA:  (%^CSH:) ([%^RLEN: |%^RVSH:] ) [%>|.#.] ;

"Stem vowel i to á for other root vowel."
!! * __@RULENAME@__ \\  e.g. säämi > Ill sáámán 
Vx:á <=> [:e|:i|:ä|:á] (%^RV:)  Cns:+ _  %^EA: (%^CSH:) ([%^RLEN: |%^RVSH:] ) [%>|.#.] [i: | n | d] ;
    where Vx in ( i i2 i3 ) ;

"Stem vowel i to e in front of j"
!! * __@RULENAME@__ \\  
Vx:e <=> Cns _ (%^VLOW:) %> [j Vow|e] ;
	where Vx in (i i2) ;


"Stem vowel á to a for a root vowel"
!! * __@RULENAME@__ \\  trying this as a phonological rule for -eđ verbs
á:a <=> a (%^RV:) Cns:+ _  %^CSH: %> ;


"Stem vowel â to a in Pl Nom -h"
!! * __@RULENAME@__ \\  silbâ to siilbah  %^WG%^RLEN%>hlove%>h
â:a <=> :Cns (%^RC:) _ (%^SV:) (%^WG:) (%^RLEN:) %> h [l | .#.|%>] ;


!€ so^RVllâ^WG^RLEN>h
!€ soo0la00>h

"Stem vowel â > á change before i"
!! * __@RULENAME@__ \\ sollâ to sooláid 
â:á <=> _ (%^SV:) (%^WG:) (%^RLEN:|%^CLEN:|%^SLEN:) %^SVLOW: %> i: ; 

!€ so^RVllâ^WG^RLEN^SVLOW>in 
!€ soo0lá000>in



"Stem vowel change e:i before suffix"
!! * __@RULENAME@__ \\ 
e:i <=> Cns: _  [Triggers: - %^EA:]*  %> i: ; 
        Cns: _  %> ä p ; !! mánuttep : mánuttiäpán
        
"Stem vowel i3:e"
!! * __@RULENAME@__ \\ 
i3:e <=> _ (%^SV:) Triggers:+ %> \[i: |n|d|j] ; !hyeneest



"Stem vowel u:o change before suffix"
!! * __@RULENAME@__ \\  
u:o <=> Cns: _ ([%^EWG:|%^SV:]) (%^WG:) (%^CLEN:) (%^RLEN:|%^RVSH:) (%^SLEN:) %^SVLOW:  ; 
		     _  á:o Cns:+ Vow: %^ÁI: %^EWG: ; !puáhháá : poovij
 		Cns  _  %> [ j Vow | o ] ;


"Stem vowel u2:o change before suffix"
!! * __@RULENAME@__ \\  
u2:o <=> Cns  _  %> [ j Vow | o ] ;
 		
!€ uáinu>o
!€ uáino>o


 		
! stáálu stááloust

"Second vowel shortening in 3syll consonant stems"
!! * __@RULENAME@__ \\ eemeed : emedân
Vx:0 <=> _ Vx: (Cns:+) (%^CSH:|%^WG:|%^EWG:) %^SVSH:  ; !eemeed : emedân sai4joos
    where Vx in ( e o a á u i y ä ) ;

!eemeed:emedân

!€ eemeed^CSH^SVSH>ân
!€ 0em0ed00>ân


!€ kappeer^CSH^SVSH>ân
!€ ka0p0er00>ân

"Stem vowel:0 in front of â"
!! * __@RULENAME@__ \\ aiguu:aaigâ
Vx:0 <=> _ â Triggers:*  %>   ; 
         _ (%^SV:) (Cns:0) ([%^ÁE:|%^ÁI:]) %^FCD: â Triggers:*  %> ; !stuárrá%^SVs%^ÁE
    where Vx in ( â e o a á u i y ä) ;

! this rule should be generalised, we talk about a ban on o>u strings


!! !Suffix vowel rules

"Suffix vowel lengthening"
!! * __@RULENAME@__ \\ long á in kissáást, nuorâ : nuorááin, hyeni:hyeneest
%^SV:Vx <=> :Vx  _  NotSuffixBorder:* %^SLEN:   ; ! hyeneest
       where Vx in ( á o u e a i   y ä  ) ; !nuorâ : nuorááin



!€ ki^RVssá^SV^WG^CLEN^SLEN>st
!€ ki0ssáá000>st

!€ mi^RVllo^SV^WG^CLEN^SLEN>st
!€ mi0lloo000>st

!€ huksi^SVm^SLEN>ist
!€ huksiim0>ist


!€ táál^RCu^SV^WG^RVSH^SLEN^SVLOW>st
!€ táál0oo0000>st

"Suffix i5:j for lexicon APINA"
!! * __@RULENAME@__ \\ salijn
i5:j  <=> Cns i %> _ ;


"Suffix i5:0 and i5ä:e"
!! * __@RULENAME@__ \\ timotein oboen
i5:0 <=> Vow [i | e] %> _ ;
         Cns _ ä:e Cns: Cns:+ Vow: Cns %^ÁE: %^EWG: %^RVSH: %>  ; !pi5ällás%^ÁE


"Suffix i:j for i and e-stems"
!! * __@RULENAME@__ \\  
i:j <=> [i: | e: | i2: | i3: ] (%^EA:) (%^WG:)  (%^CLEN:)  ([%^RLEN:|%^RVSH:])  (%^SVLOW:)   %> _ [l|d|n|g|t|%>|.#.];  


"Vowel copy in suffix"
%^V:Vx <=> :Vx %> _ ;
!! * __@RULENAME@__ \\  for imperative Sg3, at least
	where Vx in ( a á i u o ) ;



!! !!Other rules



"Potential vowel shortening for contract verbs"
!! * __@RULENAME@__ \\  -áážep becomes -áš etc.
Vx:0 <=> Vx _ %> š [%>|v|j] ;
	where Vx in (a á i o u ) ;


