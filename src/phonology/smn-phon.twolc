! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

!! !!!Alphabet 

Alphabet   

!! !!The Inari Saami letters

 a á â ä b c d e f g h i j k l m n o õ p q !!= * @CODE@
 r s t u v w x y z æ ä ø ö å			   !!= * @CODE@
 č đ ŋ ŧ š ž				 			   !!= * @CODE@

 A Á Â Å B C D E F G H I J K L M N O Õ P Q !!= * @CODE@
 R S T U V W X Y Z Æ Ä Ø Ö Å 			   !!= * @CODE@
 Č Đ Ŋ Ŧ Š Ž  							   !!= * @CODE@
 ˊ  %- 							   !!= * @CODE@
 ':0   !!= * @CODE@ this to zero

!! !Accented letters, just in case

 É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Ê Ô Û Î Ã Ý þ Ð   !!= * @CODE@
 é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý þ ð   !!= * @CODE@
 ß ª ß ç								   !!= * @CODE@

 
 
!! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 »7     !! * »
 «7     !! * «
 %[%>%] !! * >
 %[%<%] !! * <



!! !Different archiphonemes


p4:p  t4:t k4:k č4:č r4:r l4:l h4:h   !!= * @CODE@  these are consonants that change in cg
i4:i                                  !!= * @CODE@  this is the underlying i consonant
m7:ṃ n7:ṇ ŋ7:ŋ̣ s7:ṣ r7:ṛ đ7:đ̣ l7:ḷ    !!= * @CODE@  with dots
j7:j̣ h7:ḥ š7:ṣ̌  v7:ṿ                  !!= * @CODE@  with dots
h5:h                                  !!= * @CODE@  these consonants do not change in WG


!! !More abstract archiphonemes

!%^A:0		 = algan lodán
%^RC:0		!!= * @CODE@ = root cons., lengthened by ^CLEN - máán%RCu máánnun
%^RD:0		!!= * @CODE@ = root dipht., shortened by by ^RVSH - vuá%^RDg'gu vuáˊguttáá
%^RV:0	 	!!= * @CODE@ = root vowel, lengthened by ^RLEN - ta%^RVvlu taavlu and ^RV:ˊ when WG and suffix: 0 | t | i
%^SC:0      !!= * @CODE@ = stem cons, %^SC:j, pluss lengthen after earlier shortening by %^CSH, see lyeme
%^SV:0 		!!= * @CODE@ = stem vowel, lengthened by ^SLEN u:uu - táálu%^SV tááloost

!! !Triggers

%^CLEN:0	!!= * @CODE@ = cons lengthening, with ^RC
%^CSH:0     !!= * @CODE@ = cons shortening, laaŋkku - laaŋkun
%^DLEN:0    !!= * @CODE@ = Diphtong lengthening (remove?)
%^RVSH:0     !!= * @CODE@ = Diphtong shortening
%^EE:0      !!= * @CODE@ = Final vowel deletion in ee stems (not in use)
%^FCD:0     !!= * @CODE@ = Final consonant deletion 
%^IA:0      !!= * @CODE@ = i to á (more) (not in use)
%^EA:0     !!= * @CODE@ = e to a/á in ill etc
%^LLEN:0    !!= * @CODE@ = low vow lengthening (not in use)
%^MONO:0    !!= * @CODE@ = monophtongization of diphthongs  (not in use)
%^RLEN:0    !!= * @CODE@ = root lengthening, with ^RD
%^RVSH:0    !!= * @CODE@ = root vow shortening - máán7%^RCu máˊnuttáá 
%^SLEN:0    !!= * @CODE@ = stem vowel lengthening
%^SFV:0     !!= * @CODE@ = Deleting fleeting vowel before suffix
%^SVLOW:0   !!= * @CODE@ = stem vowel lowering â>á and u>o - árppu árppoin
%^UU:0      !!= * @CODE@ = Final vowel deletion in uu stems
%^VLOW:0    !!= * @CODE@ = Vowel lowering in 3rd sg of contract verbs tuhhid:tohhe
%^WG:0      !!= * @CODE@ = weak grade trigger
%^ÁE:0      !!= * @CODE@ = á to e (more) in 3-syll
%^ÁI:0      !!= * @CODE@ = á to i (more) in 3-syll

%^ODRVSHV:0     !!= * @CODE@ =  Cns shortening in C|# and shortvow in Inf etc  (not in use)
%^ODDC:0       !!= * @CODE@ =  shorten cns in odd-syll stems before C, #  (not in use)
!%^RVD:0      root vowel  diftongisation

!! !!Morpheme boundaries:

!! Here we define the suffix border >
!! The other borders are not used yet, but still defined:

 «  !!≈ ;@CODE@: Derivational prefix
 »  !!≈ ;@CODE@: Derivational suffix
 %< !!≈ ;@CODE@: Inflectional prefx
 %> !!≈ ;@CODE@: Inflectional suffix
 %^ !!≈ ;@CODE@: (exceptional) soft hyphenation point
 #  !! and the symbol #, which is Word boundary for both lexicalised and dynamic compounds

!! End of alphabet definitions

;

!! !!! Sets

Sets
 Vow = a á â ä e i o u y æ ø ö å			   !!= * @CODE@ 
       é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý 	   !!= @CODE@ 
       A Á Â Ä E I O U Y Æ Ø Ö Å			   !!= @CODE@ 
       É Ó Ú Í À È Ò Ú Ì Ë Ü Ï Ê Ô Û Î Ã Ý 	   !!= @CODE@ 
       %^RV %^RD %^SV ;						   !!= @CODE@ 
 RealVow = a á â ä e i o u y æ ø ö å		   !!= * @CODE@ 
       é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý 	   !!= @CODE@ 
       A Á Â Ä E I O U Y Æ Ø Ö Å			   !!= @CODE@ 
       É Ó Ú Í À È Ò Ú Ì Ë Ü Ï Ê Ô Û Î Ã Ý ;   !!= @CODE@ 
 Cns = b c č d đ f g h j k l m n ŋ 			   !!= * @CODE@ 
       p q r s š t ŧ v w x z ž				   !!= @CODE@ 
       B C Č D Đ F G H J K L M N Ŋ			   !!= @CODE@ 
       P Q R S Š T Ŧ V W X Z Ž 				   !!= @CODE@ 
       ht g2 k2 m2 n2 							   !!= @CODE@ 
       b3 d3 g3 m3 h3 						   !!= @CODE@ 
       p4 k4 č4 r4 l4 h4 t4 i4                        !!= * @CODE@
       m7 n7 ŋ7 v7 s7 r7 đ7	l7 j7 h7 š7		   !!= @CODE@ 
       ṃ  ṇ  ŋ̣  ṿ  ṣ  ṛ  đ̣  ḷ  j̣  ḥ  ṣ̌         !!= * @CODE@
       %^RC %^SC  ˊ ;
 Geminate = b d g p h t k c č f z l m n ŋ r s š v  ; !check these
 Seg = Vow Cns ;							   !!= * @CODE@ 
 StemCns = b b3 d d3 g g2 g3 h h2 h3 		   !!= * @CODE@ 
           j l m m2 m3 m8 n n8 n2 ŋ r s  ;     !!= * @CODE@ 



           
!! !!!Definition section

!! Trying to introduce a WG right context, so far resulting in drop in performance.

Definitions

WeakGrade =  :RealVow (Cns:) (%^SV:) %^WG:0  (%^RVSH:)   ;    !!= @CODE@ the general definition


!! !!!Rule section

Rules

!! !!Consonant rules

!! !Consonant gradation

"x7:x ja x:y WG gradation, also for n7j"
!! * __@RULENAME@__  WG gives x7:x and x:y, except when CLEN  
Cx:Cy <=> Vow: _ (%^RC: ) (j) WeakGrade \%^CLEN:0 ;
          Vow: _ %^RC:  j RealVow:  %^CLEN:0 ; !n7j: Par and Ess
          Vow: _ %^RC:  j RealVow: %^WG:0 %^CLEN:0 %^SVLOW:0 ; !n7j: Com
	where Cx in ( m7 n7 ŋ7 r7 s7 š7 j7 v7 đ7 l7 h p c č )  
          Cy in ( m  n  ŋ  r  s  š  j  v  đ  l  ṿ v s j̣ )  
		  matched ;

!€ suá%^RDn7%^RCju^WG>
!€ suá0n0ju0>


!iše:i%^RVš7%^RCe VAHE ;  %^SV%^WG%^CLEN%^SLEN%>st

"RV:ˊ in x:y gradation and root vowel shortening"
!! * __@RULENAME@__  saˊj̣e (now line 2 is justa copy from Vx:ˊ. Shorten the context !
%^RV:ˊ <=> Vow: _ Cns: (%^RC:) [e: | o: | a: | á: ] ( [%^EA: | %^WG: ] )  %> ; 
!%^RV:ˊ <=> Vow: _ Cns: (%^RC:) [e: | o: | a: | á: ] (%^WG:)  %> ;  ! reverted to EA -- why exclude? include gives 
           Vow: _ Cns: (Cns:) (%^RC:) Vow:  (%^WG:) (%^RVSH:) (%^SFV:) (%^SC:) (%^ÁI:) (%^ÁE:) %^RVSH: (%^SVLOW:) [%>|.#.] ;

! For reference: copied in to be edited:
   !Vy:ˊ <=> Vx _  Cns: (Cns:) (%^RC:) Vow:  (%^RVSH:) (%^WG:) (%^SFV:) (%^SC:) (%^ÁI:) (%^ÁE:) %^RVSH: (%^SVLOW:) [%>|.#.] ;


!                 shorter   shortest           long
! RV: aRV ->    , aˊ / ? ,   a0 / default,     aa / RLEN  = saje
! ---
! aa> aa  ->      aˊ & RVSH  ie0 / default  			  = saahâ
! RD: ieRD ->  , ieˊ / RVSH,  ie0 / default     			  = kielâ

!! !Diphthong rules

"Shortening diphthong"
!! * __@RULENAME@__ RD to ˊ muátu muáˊđoost
%^RD:ˊ <=> _ Cns:+ Vow: (%^SV:) %^WG: %^RVSH: ;


"Long vowel shortening (no consonant shortening) in vowel stems"
!! * __@RULENAME@__ (example?)
Vy:ˊ <=> Vx: _  Cns: (Cns:) (%^RC:) Vow:  (%^WG:) (%^RVSH:) (%^SFV:) (%^SC:) (%^ÁI:) (%^ÁE:) %^RVSH: (%^SVLOW:) [%>|.#.] ;
       where Vx in ( a e i o u y á ä ) 
             Vy in ( a e i o u y á ä ) 
       matched ;


! mool7â to moolâ:		  
		  
!€ mool7^RCâ^WG>
!€ mool0â0>

!€ käähir^WG>
!€ kääṿir0>

!€ juá%^RDh%^RCu%^WG%^RVSH%>ttáá
!€ juáˊṿ0u00>ttáá



"t:đ, also tˊk4:đh gradation, part 1" 
!! * __@RULENAME@__   for ito : iđo riä%^RDtˊk4á  utke+N+Sg+Gen:  uˊđhe  riätká+N+Sg+Gen:  riäđhá  riä%^RDtˊk4á
t:đ <=> Vow: _  (ˊ:) k4: (%^RC:) WeakGrade ;
        Vow: _  %^RC:   j RealVow: %^WG:0 %^CLEN:0 %^SVLOW:0 ; ! Com
        Vow: _ (%^RC:) (j) WeakGrade (%^CLEN:0) ;
        Vow: _  %^RC:   j RealVow:  %^CLEN:0 ; ! Par and Ess


!€ jo^RVtk4^RCâ^WG^RLEN
!€ joođh0â00

"k4:h gradation for kk4 and tk4"
!! * __@RULENAME@__    for atka : ađha
k4:h <=> Vow: [t:|k:] (ˊ:) _ (%^RC:) WeakGrade (%^RLEN:) ;
!k4:h <=> Vow: [t:|k:] _ (%^RC:) :Vow (%^SV:) (Cns) %^WG: ;

!€ mä^RVtk4i^WG^RLEN
!€ määđhi00

!€ su^RVkk4^RCá^SV^WG^CLEN^SLEN>st
!€ su00hháá000>st

"t4:đ gradation for tt4" 
!! * __@RULENAME@__  ruttâ ruuđâ
t4:đ <=> t: _ Vow: %^WG: ;

!€ ru%^RVtt4â%^WG%^RLEN>
!€ ruu0đâ00>

         

!€ suá%^RDn7%^RCju  
!€ suá0nj0u

!! !Other Consonant Rules
       
!€ kappeer^CSH^RVSH>ist
!€ kap00er00>ist

!€ haavvâr^CSH^RVSH>ist
!€ h0aṿ0âr00>ist

!€ pottáák^SC^CSH^RVSH>ist
!€ pot00ákk00>ist



! ============================================================
! ============================================================
! ============================================================
! ============================================================



! ============================================================
! ============================================================
! ============================================================
! ============================================================



!! !Consonant shortening

! "Word final consonant deletion of n m t"                 
! !! * RULENAME for vaskâm:vaaskâ
! Cx:0 <=> _  (%^ÁE:) (%^SC:) (%^WG:) %^FCD: ;      
!          where Cx in (n m t) ;

!rejected:
!€ va^RVskâm^RLEN^FCD
!€ vaaskâ000

!rejected:
!€ čiäpp4%^RCát%^ÁE^SC^WG^FCD
!€ č0e0v0e00000
	



"nnj to nj shortening not weak grade"
!! * __@RULENAME@__  special rule, e.g. konnjâl -> konjâlân
n:0 <=> n _ j Vow (Cns:) %^CSH: [%>|.#.] ;

!€ konnjâl^CSH>ân
!€ kon0jâl0>ân



!€ suá^RDn7^RCju^CLEN>n
!€ suá0nnju0>n
!€ haamit^SC^CSH^RVSH>ist
!€ h0amitt00>ist


!€ su^RVkk4^RCá^SV^WG^CLEN^SLEN>st
!€ su00hháá000>st

!strange:

!€ kopp^RCâ^WG^CLEN^SVN>in
!€ ko0pp0á000>in


!! !Consonant lengthening



"Lengthening of half-long root consonant n7:n 1"
!! * __@RULENAME@__  for l-dot to ll, changing l7 to first l in ll
Cx:Cy <=> Vow: _ %^RC: :Vow              %^CLEN:      %> ;
          Vow: _ %^RC: :Vow %^SV: %^WG:  %^CLEN: %^SLEN: %> ; !Loc  
          Vow: _ %^RC: :Vow       %^WG:  %^CLEN: (%^SVLOW:)   %> ; !Com  %^WG%^CLEN
          Vow: _ %^RC: :Vow %^SV:        %^CLEN: %^SLEN: %> ; !Par Ess
	where Cx in ( l7 r7 s7 h7 m7 n7 đ7 v7 ŋ7 š7 j7 )
		  Cy in ( l  r  s  h  m  n  đ  v  ŋ  š  j ) 
		  matched ;


"Lengthening root consonant j7:ij 1"
!! * __@RULENAME@__  for saje:sa%^RVj7%^RCe NJUNE "sija" ;
j7:i <=> Vow: _ %^RC: :Vow %^SV: %^CLEN: %^SLEN: %> ; !Par Ess

!€ sa^RVj7^RCe^SV^CLEN^SLEN>n
!€ sa0ijee00>n

"Lengthening of half-long root consonant n7 : nn 2"
!! * __@RULENAME@__   for l-dot to ll, changing ^RC to second l in ll  muáđoin
%^RC:Cy <=> Vow: Cx: _ :Vow  %^CLEN:   %> ;
            Vow: Cx: _ :Vow %^SV: %^WG:  %^CLEN: %^SLEN:  %> ;
            Vow: Cx: _ :Vow  %^WG:  %^CLEN:   %> ; !Com  %^WG%^CLEN
            Vow: Cx: _ :Vow %^SV: %^CLEN: %^SLEN:  %>  ; !Par Ess
	where Cx in ( l7 r7 s7 h7 m7 n7 đ7 v7 ŋ7 j7 š7 )
		  Cy in ( l  r  s  h  m  n  đ  v  ŋ  j  š  ) 
		  matched ;

"Lengthening of consonant for Ess/Par"
!! * __@RULENAME@__   piiru piirrun niisu niissun vahe vahheen
%^RC:Cy <=> Vow: Cx _ Vow (%^SV:)  %^CLEN: (%^SLEN:) %> [ n | d ] ;
	where Cx in ( t r s h c m p)  
		  Cy in ( t r s h c m p)   
		  matched ;


!iše:i%^RVš7%^RCe VAHE ;  %^SV%^WG%^CLEN%^SLEN%>st
!vahe:va%^RVh%^RCe VAHE ; ! sääm%^RCi

!kiehâ:kie%^RDh%^RCâ KIEHA ; ! SM9 +N+Sg+Com:%^WG%^CLEN%^SVLOW%>in K ; ! kievváin




!rejected:
!€ kol7^RCe^CLEN
!€ kolle0

"Lengthening of h and p to vv, for everything except Ess/Par 1"
!! * __@RULENAME@__  for h to vv p to vv čääc%^RCi
Cx:Cy <=> Vow: _ %^RC: :Vow (%^SV:) %^WG: %^CLEN: ([%^SVLOW: |%^SLEN: | %^RVSH:])  %> \[ n | d ]  ;
	where Cx in ( h p c )
		  Cy in ( v v s ) 
		  matched ;

"Lengthening of h and p to vv, for everything except Ess/Par 2"
!! * __@RULENAME@__   for h to vv  p to vv   
%^RC:Cy <=> Vow: Cx: _ :Vow  (%^SV:) %^WG: %^CLEN: ([%^SVLOW: |%^SLEN: | %^RVSH:]) %> \[ n | d ] ;
	where Cx in ( h p c )  
		  Cy in ( v v s )   
		  matched ;



! äšši:ä%^RVšši SAAMI "asia" ;

! piir %^RC u %^CLEN %> n
!€ ka^RVss^RCa^SV^WG^CLEN^SLEN>st
!€ ka00ssaa000>st


!€ va%^RVh%^RCe%^SV%^WG%^CLEN%^SLEN%>st
!€ va0vvee000>st

!YAML test 19: generator-dict-gt-norm.xfst + dict-gt-yamls/N-lex_kyeli_dict-gt-norm.gen.yaml - 3/13/16 FAIL - ye > uá/uo, i > e
!YAML test 34: generator-dict-gt-norm.xfst + dict-gt-yamls/N-lex_saaha1_dict-gt-norm.gen.yaml - 11/5/16 FAIL  MAANU, ii heive
!YAML test 35: generator-dict-gt-norm.xfst + dict-gt-yamls/N-lex_saaha_dict-gt-norm.gen.yaml - 14/3/17 FAIL

!€ va%^RVh%^RCe%^SV%^CLEN%^SLEN%>n
!€ va0hhee00>n

!€ kiel7^RCâ^CLEN>n
!€ kiellâ0>n



"n7j to nnj consonant lengthning"
!! * __@RULENAME@__  special rule, suán7ju : suánnjun for Par and Ess
%^RC:n <=> Vow: n7: _ j RealVow: %^CLEN: %> [ d | n ] ;



!"Consonant lengthening after xx:x and xy:zy weak grade with x4 on upper side"
!  * __@RULENAME@__  as previous rule, but for k4, č4, p4  
!%^RC:Cx <=> [:Vow|%^RV: | %^RD:] Cy: (ˊ:) Cz: _ Vow: (%^SV:) %^WG: %^CLEN: ;
!        where Cy in ( k  č  p  k  h ) 
!              Cz in ( k4 č4 p4 s  c ) 
!              Cx in ( h  j  v  s  c ) 
!        matched ;
!
!"Consonant lengthening after xy:zy weak grade with k4ss and h4čč"
! * __@RULENAME@__  as previous rule, but for  čuhčá čuvččáin kuá%^RDh4ˊč%^RCá%^WG%^CLEN%>in
!%^RC:Cx <=> [:Vow|%^RV: | %^RD:] Cy: (ˊ:) Cz: _ Vow:  %^WG: %^CLEN: ;
!        where Cy in ( k4 h4) 
!              Cz in ( s  č) 
!              Cx in (  s č ) 
!        matched ;
!
!
!"Consonant lengthening after x:y weak grade, to x:yy, with %^SV"
! * __@RULENAME@__  , here we lengthen the lower y consonant, spaha -> spavvaast  ko%^RVp%^RCe%^SV%^CLEN%^SLEN%>n
!%^RC:Cx <=> [:Vow|%^RV:] Cx: _ Vow: %^SV: (%^WG: ) %^CLEN: ;
!        where Cx in ( p l d m s r v ) ;
!
!
!
!"Consonant lengthening after x:y weak grade with x4 on upper side"
! * __@RULENAME@__  as previous rule, but for k4, č4, p4
!%^RC:Cx <=> [:Vow|%^RV:] (Cz:) Cy: _ Vow: (%^SV:) %^WG:  %^CLEN: ;
!        where Cy in ( k4 č4 p4 ) 
!              Cx in ( h  j  v  ) 
!              Cz in ( k  č  p  ) 
!        matched ;
!


!! !Other consonant rules

"xˊx7:xˊx and xx7:xx as default when not WG | RVSH | RLEN" 
!! * __@RULENAME@__  
Cx:Cy <=> Vow:  Cy: (ˊ:) _  Vow: \[%^WG:0 | %^RVSH:0 | %^RLEN:0 ] ;
		  Vow:  Cy: (ˊ:) _  Vow: %^WG:0  %^CLEN:0  ;
	where Cx in ( l7 n7 s7 š7 r7 m7 v7) 
	      Cy in ( l  n s š r m v)
		  matched ;

!€ puá%^RDlˊl7u>
!€ puá0lˊlu>

!€ puá%^RDlˊl7u%^WG%^RVSH%>ttáá
!€ puáˊ00ḷu00>ttáá

!€ ká%^RVll7u%>
!€ ká0llu>


!! (outcommented)


!"Assimilation of root consonant and double suffix consonant"
! * __@RULENAME@__  delet t in haamit in front of -ttáá, avoid -tttáá
!t:0 <=> _ %> t t ; 

!€ haamit>ttáá
!€ haami0>ttáá

!€ ka^RVss^RCa^SV^WG^CLEN^SLEN>st
!€ ka00ssaa000>st








!! !!Vowel rules

!! !First syllable vowel shortening
 
"Vowel shortening with consonant shortening" 
!! * __@RULENAME@__  for koonjâl -> konjâlân
Vx:0 <=> [Seg:|.#.] _ Vx Seg:* ([%^RC:|%^SC:]) %^CSH: (%^RVSH:|%^EA:) [%>|.#.] ;
        where Vx in ( a e o á ä u y i ) ;

!€ koonnjâl^CSH>ân
!€ k0on0jâl0>ân

!€ kappeer^CSH^RVSH>ist
!€ kap00er00>ist

!€ čäällim^CSH^RVSH>ist
!€ č0äḷ0im00>ist

!€ muusik^SC^CSH^RVSH>ist
!€ m0usikk00>ist

! kahhaan^SC^CSH^RVSH>ist
! ka0ha0n0j00>ist




"Vowel shortening (no consonant shortening) in consonant stems"
!! * __@RULENAME@__ kiirrâs- to kirrâsân
Vy:0 <=> Seg: _ Vx   Cns: (Cns:) (%^RC:) Vow  :Cns (%^SFV:) (%^SC:) (%^ÁI:) (%^ÁE:) (%^CLEN:) %^RVSH: [%>|.#.] ;
       where Vx in ( a e i o u á ä ) 
             Vy in ( a e i o u á ä ) 
       matched ;

!rejected:
!€ kiirrâs^RVSH>ân
!€ k0irrâs0>ân
 
"Vowel shortening in front two consonants and i-suffix"
!! * __@RULENAME@__ ráávhu- : rávhoid, čääci : čassijn
Vy:0 <=> Vx: _  Cns: [ Cns: - %^RC:0 ] Vow:  %^WG: (%^CLEN:) %^SVLOW: %> i:  ;
       where Vx in ( a e i o u á ä ) 
             Vy in ( a e i o u á ä ) 
       matched ;

!€ mááh%^RCu%^WG%^CLEN%^SVLOW%>in
!€ m0ávvo000>in
  
 

!This one doesn't exist in yaml:
!€ mool7^RCâ^WG^RVSH>ttáá
!€ moˊl0â00>ttáá



!! !First syllable vowel lengthening

"Root vowel lenghtening in bisyllabic stems"
!! * __@RULENAME@__  lekkâ : leehá- 
%^RV:Vx <=> Vx: _ Cns:* (%^RC:) Vow: (%^SV:) (Cns:) (%^WG:) %^RLEN: ;
	where Vx in ( a e i o u y ä á ) ;

!€ le^RVkk4^RCâ^WG^RLEN
!€ lee0h0â00

"Root vowel lengtning with vowel change before suffix i"
!! * __@RULENAME@__  alge : oolgijn 
%^RV:Vy <=> Vx:  _  Cns:+  e: %^WG: %^RLEN:  %> i: ; 
       where Vx in ( a o )
             Vy in ( o u ) 
       matched ;

!€ a%^RVlge%^RLEN%>jd
!€ oolgi>jd





!! !First syllable vowel change

"Root vowel change for á to ä with i in second syllable"
!! * __@RULENAME@__  päävir
á:ä <=>    _ á: Cns:* :i Cns: %^ÁI: [.#.|%>] ;
        á: _    Cns:* :i Cns: %^ÁI: [.#.|%>] ;

!rejected:
!€ páápp4ár^ÁI^WG>
!€ pää0vir00>

"Monophthongisation rule 1"
!! * __@RULENAME@__  u:o in pairs like kuátij -> koođij
Vx:Vy <=> _ Vz: Cns:* i %^WG: %> :j ;
	where Vx in ( i u )
		  Vy in ( e o )
		  Vz in ( ä á )
	matched ;

"Monophthongisation rule ä:a"
!! * __@RULENAME@__  čääci:čassijn, läbži:labžijn
ä:a <=> _ [ä:|%^RV:] Cns:+ i %^WG: (%^RVSH:) (%^RVSH:) %> i: ;
        _ [ ä: |%^RV: ] Cns: Cns:   i %^WG: %^CLEN: %^SVLOW: %> i: ; !čääci:čassijn, läbži:labžijn

!€ sääm7^RCi^WG^RVSH>in
!€ saam0i00>jn


!€ sääm7^RCi^WG>id
!€ saam0i0>jd

!€ kuáti^WG>j
!€ koođi0>j

"Monophthongisation rule 2"
!! * __@RULENAME@__  á:o in pairs like kuátij -> koođij 
Vx:Vy <=> Vz: _ Cns:* i %^WG: %> i: ;
	where Vx in ( ä á ä )
		  Vy in ( e o a )
		  Vz in ( i u ä )
		  matched ;

!nom	ill
!e^RV	iä
!ie	iä
!ä	á
!ye	uá

"Root vowel change before suffix i"
!! * __@RULENAME@__  alge : olgijn ko%^RVv7%^RCe%^WG%^CLEN%>jn
Vx:Vy <=> _  %^RV: Cns:+  e: (%^WG:) ([%^RLEN: |%^CLEN:] )  %> i: ; 
       where Vx in ( a o )
             Vy in ( o u) 
       matched ;

!€ ko%^RVv7%^RCe%^WG%^CLEN%>jn
!€ ku0vvi00>jn



"e to iä in Illative and Sg3 1"
!! * __@RULENAME@__  Part 1: e to i; ergi -> iärgán 1
e:i <=> _ %^RV: Cns:+ Vow: %^EA: %> ;

"e to iä in Illative and Sg3 2"
!! * __@RULENAME@__  Part 2: ^RV to ä; ergi -> iärgán 2 (e+e(RV) -> i + ä
%^RV:ä <=> e: _ Cns:+ Vow: %^EA: %> ;

"ie to iä in Illative and Sg3"
!! * __@RULENAME@__  ... with ^ILL as trigger; kiem´ni -> kiämnán
e:ä <=> i _  Cns:+ Vow: %^EA: %> ;

!"ä to á in Illative and Sg3"
!!! * __@RULENAME@__  ... after .#. or Cns, with ^ILL as trigger; kiem´ni -> kiämnán
!ä:á <=> [.#.|Cns] _  Cns:+ Vow: %^EA: ;

"ää to áá in Illative and Sg3"
!! * __@RULENAME@__  ... for säämi > sáámán
ä:á <=> _  [ä:|%^RV:] Cns:+ :á %^EA: ;
        ä: _          Cns:+ :á %^EA: ;

!€ sääm7^RCi^EA>n
!€ sááṃ0á0>n

"äRV to áá in Illative and Sg3"
!! * __@RULENAME@__  ... for täsni -> táásnán
%^RV:á <=> ä: _  Cns:+ :á %^EA: ;

!€ tä^RVsni^EA>n
!€ táásná0>n

"ye to uá og uo, part 1" 
!! * __@RULENAME@__  Part 1 of ye to uá for kyeli : kuálán kye%^RDl7%^RC
y:u <=> _ e: %^RD:  Cns:+ Vow: %^EA: ;
        _ :o %^RD:  Cns:+ i: %^WG: (%^RVSH:) %> i: ;

"ye to uá, part 2"
!! * __@RULENAME@__  Part 2 of ye to uá for kyeli : kuálán
e:á <=> y: _ %^RD:  Cns:+ Vow: %^EA: ;

"ye to uo, part 2 "
!! * __@RULENAME@__  Part 2 of ye to uá for kyeli : kuolijn
e:o <=> y: _ %^RD:  Cns:+ i: %^WG: (%^RVSH:) %> i: ;



!! !Second syllable rules

"Vowel change á to e or i in 3syll"
!! * __@RULENAME@__ puttás- > putes, triggers are ^ÁE and ^ÁI
Vx:Vy <=> _ (Cns:) Vz: %^WG: (%^FCD:) ; 
       where Vx in ( á    á    a   )
             Vy in ( e    i    e   )
             Vz in ( %^ÁE %^ÁI %^ÁE) 
       matched ;

!€ puttás^ÁE^WG>
!€ pu0tes00>

"Vowel shortening  áá and to e or i in 3syll"
!! * __@RULENAME@__ puttás- > putes, triggers are ^ÁE and ^ÁI
Vx:0 <=> _ Vx: (Cns:) Vz: [%^WG:|%^CSH:] ; 
       where Vx in ( á    á    a   )
             Vz in ( %^ÁE %^ÁI %^ÁE) 
       matched ;

!rejected
!€ nahh4aar^ÁE^WG>
!€ na0ṿ0er00>

"Suffix vowel lengthening"
!! * __@RULENAME@__ long á in kissáást
%^SV:Vx <=> Vx:  _ (:Cns)  (%^WG:) (%^RVSH:) (%^CLEN:) (%^RLEN:) %^SLEN: (%^SVLOW: ) %> ;
        where Vx in ( á o e a i ) ; ! pending...

!€ ki^RVss^RCá^SV^WG^CLEN^SLEN>st
!€ ki00ssáá000>st

!€ mi^RVll^RCo^SV^WG^CLEN^SLEN>st
!€ mi00lloo000>st

!€ huksi^SVm^SLEN>ist
!€ huksiim0>ist

"Suffix vowel lengthening o"
!! * __@RULENAME@__ stááḷu stááloost
%^SV:o <=> u:  _ %^WG: %^RVSH: %^SLEN: %^SVLOW: %>  ; 

!€ táál7%^RCu%^SV%^WG%^RVSH%^SLEN%^SVLOW%>st
!€ táál0oo0000>st

"Fleeting vowel before suffix"
!! * __@RULENAME@__ deletes -i- in piehâin to piehânjist 
i:0 <=> Seg: _ Cns %^SFV: (%^SC:) (%^CSH:) (%^RVSH:) %> ;

!rejected:
!€ pieˊhâin^SFV^SC^CSH^RVSH>ist
!€ pie0hâ0n0j00>ist

"Inversion of in to nj"
!! * __@RULENAME@__  adds -j-  in piehâin to piehânjist  
%^SC:j <=> n (%^SFV:) _ (%^CSH:) (%^RVSH:) [%>|.#.] ; 

!rejected:
!€ pieˊhâin^SFV^SC^CSH^RVSH>ist
!€ pie0hâ0n0j00>ist

!rejected:
!€ kahhaan^SC^CSH^RVSH>ist
!€ ka0ha0nj00>ist



"Stem vowel e deletion before 2nd syllable ij"
!! * __@RULENAME@__  hmm have a look at this
!e:0 <=> Cns: _ (Dummy:) (Dummy:) %> i: j ; unclear target !?
e:0 <=> Cns: _ Cns %^SFV: (%^SC:) (%^CSH:) (%^RVSH:) [%>|.#.] ; 

!€ šoddâdem^SFV>ist
!€ šoddâd0m0>ist

 
"Stem vowel e to a for a root vowel."
!! * __@RULENAME@__  alge to aalgan
e:a <=> a (%^RV:) Cns:+ _ (%^RLEN:) (%^CSH:) %^EA: ;

!rejected:
!€ a^RVlge^SLEN>n
!€ aalga0>n

"Stem vowel e to á for other root vowel."
!! * __@RULENAME@__  
e:á <=> [o|i|u] (%^RV:) Cns:+ _ (%^RLEN:) (%^CSH:) %^EA: [%>|.#.] ;


"Stem vowel i to á for other root vowel."
!! * __@RULENAME@__  e.g. säämi > Ill sáámán kye%^RDl7%^RC
i:á <=> [:e|:i|:ä|:á] ([%^RV: | %^RD: ]) Cns:+ _ (%^RLEN:) (%^CSH:) %^EA: [%>|.#.] ;



"Stem vowel â to a in Pl Nom -h"
!! * __@RULENAME@__  silbâ to siilbah
â:a <=> :Cns (%^RC:) _ (%^UU:|%^SV:) (%^WG:) (%^RLEN:|%^DLEN:) %> h ;



!€ so^RVll^RCâ^WG^RLEN>h
!€ soo0l0a00>h

"Stem vowel â > á change before i"
!! * __@RULENAME@__ sollâ to sooláid 
â:á <=> _ (%^UU:|%^SV:) (%^WG:) (%^RLEN:|%^CLEN:) %^SVLOW: %> i: ; 

!€ so^RVllâ^WG^RLEN^SVLOW%>in 
!€ soo00lá000>in

!€ so^RVll^RCâ^WG^CLEN^SVLOW>in
!€ so00llá000>in


"Stem vowel change e:i before suffix i"
!! * __@RULENAME@__ 
e:i <=> Cns:  _  (%^WG:) ([ %^RLEN: | %^CLEN: ])  %> i: ; 

"Stem vowel u > o change before suffix"
!! * __@RULENAME@__  ... check this, add test
u:o <=> Cns: _ (%^UU:|%^SV:) (%^WG:) (%^RVSH:) (%^RVSH:) (%^RLEN:|%^CLEN:) (%^SLEN:) %^SVLOW:  ; 
! stááḷu stááloust


"Suffix i:j for i and e-stems"
!! * __@RULENAME@__  
i:j <=> [i: | e: ] (%^WG:) (%^RVSH:) ([%^CLEN: |%^RLEN:|%^RVSH:]) (%^SVLOW:) (%^EA:)  %> _ [d|n|g|t|%>|.#.]; 


!"Illative suffix change in bisyllabic e stems"
! * __@RULENAME@__  changes ^A to a, check this
!%^A:a <=> a (%^RV:) Cns:* e: (Dummy:*) %> _ ;



!! !!Other rules

!! (no rules here now)
