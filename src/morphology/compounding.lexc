
LEXICON  Rnoun
               :#  Rreal ; ! pessijâšmoonijd
 +Attr+Err/Orth: K ; ! pessijâš
                 
LEXICON RAlmostReal    ! lexicalising the 3-part compounds, with the tag ShCmp
         RrealAfterCmpNFlags       ; ! Business as usual: Normal compounds right on to Rreal
 +Cmp#:# MiddleNouns ; ! These nouns should not return to themselves, to avoid -jotjotjot-

LEXICON Rreal !!≈ @CODE@ - This initial recursive/compounding lexicon adds flags to 
          !! control compounding, before going to the real compounding lexicon This is the former R lexicon, renamed to avoid the MiddleNouns loop
 @P.CmpFrst.FALSE@@P.CmpPref.FALSE@@D.CmpLast.TRUE@@D.CmpNone.TRUE@@U.CmpNone.FALSE@@P.CmpOnly.TRUE@ RrealAfterCmpNFlags ; ! Flags to control compounding

LEXICON R
 @P.CmpFrst.FALSE@@P.CmpPref.FALSE@@D.CmpLast.TRUE@@D.CmpNone.TRUE@@U.CmpNone.FALSE@@P.CmpOnly.TRUE@ RAlmostReal ; ! Flags to control compounding

LEXICON RrealAfterCmpNFlags
!          +Cmp#:#  #              ; ! For turning off compounding during testing
          +Cmp#:#  nounflag       ;
 +Cmp/Hyph+Cmp#:#- nounflag       ;
         +Cmp-#:#- ProperNoun-smn ;
         +Cmp-#:#- ProperNoun     ;
    +Cmp/SplitR:-  #              ;

LEXICON nounflag
                 NounRoot      ;
 @P.NeedNoun.ON@ VerbRoot      ; !means that the only allowed when @C.NeedNoun@, because of ENDLEX @D.NeedNoun.ON@
 @P.NeedNoun.ON@ AdjectiveRoot ;

LEXICON Rhyph !!≈ @CODE@ - This initial recursive/compounding lexicon adds flags to
          !! control compounding, before going to the real compounding lexicon
 @P.CmpFrst.FALSE@@P.CmpPref.FALSE@@D.CmpLast.TRUE@@D.CmpNone.TRUE@@U.CmpNone.FALSE@@P.CmpOnly.TRUE@ Rhyph_after_cmp_flags ;

LEXICON Rhyph_after_cmp_flags
      +Cmp-#:-# NounRoot ;
      +Cmp-#:-# Numeral  ; ! NRK-2
 +Cmp/SplitR:-  #        ;

LEXICON RProp !!≈ @CODE@ - This initial recursive/compounding lexicon adds flags to
          !! control compounding, before going to the real compounding lexicon
 @P.CmpFrst.FALSE@@P.CmpPref.FALSE@@D.CmpLast.TRUE@@D.CmpNone.TRUE@@U.CmpNone.FALSE@@P.CmpOnly.TRUE@ RProp_after_cmp_flags ;

LEXICON RProp_after_cmp_flags                  !!≈ !!!@CODE@
      +Cmp-#nommâ:»»»-nommâs  DER-SAS        ; ! Jovnâ-nommâsâš
     +Cmp-#merkkâ:»»»-merkkâs DER-SAS        ; ! Saab-merkkâsâš
           +Cmp-#:-#          ProperNoun     ;
           +Cmp-#:-#          ProperNoun-smn ;
           +Cmp-#:-#          NounRoot       ;
 @D.CmpHyph.TRUE@+Use/-Spell+Cmp#:@D.CmpHyph.TRUE@#         NounRoot       ; ! We don't want Proper-Noun compounds without hyphen in the speller.
      +Cmp/SplitR:-           #              ;

LEXICON RNum !!≈ @CODE@ - This initial recursive/compounding lexicon adds flags to
          !! control compounding, before going to the real compounding lexicon
 @P.CmpFrst.FALSE@@P.CmpPref.FALSE@@D.CmpLast.TRUE@@D.CmpNone.TRUE@@U.CmpNone.FALSE@@P.CmpOnly.TRUE@ RNum_after_cmp_flags ;

LEXICON RNum_after_cmp_flags
 +Cmp-#:-# Noun ; ! For Num Cmp Noun, vi vil ikke ha Num Cmp Num

