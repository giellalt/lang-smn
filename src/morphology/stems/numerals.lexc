! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no


!! !!!Inari Saami number <-> text


LEXICON num
+Num: numsg ;

LEXICON numsg
+Sg+Nom: # ;


LEXICON Numeral
HUNDREDSM ;		! 200M
čyeti HUNDREDM ;	! 100M
TENSM ;			! 20-99M
TEENSM ;		! 10-19M
ONESM ;			! 1-9M
HUNDREDST ;		! 200000-999999
HUNDREDST ;		! 200000-999999
čyeti  HUNDREDT ;	! 100000-100999
TENST ;			! 20000-99999,10000-10999
TEENST ;		! 11000-19999
ONEST ;			! 2000-9999
tuhháát  THOUSAND ;	! 1000-1999
UNDERTHOUSAND ;		! 100-999
TENS ;			! 20-99
TEENS ;			! 10-19
ONES ;			! 1-9
ARABIC          ; ! for the arabic numerals !
ROMAN           ; ! for the roman numerals  !
NUM-PREFIXES    ; ! for §34 etc.            !
ISOLATED-NUMEXP ; ! for ½ etc.                      !



LEXICON HUNDREDSM
kyehti   CUODIM ;
kulmâ  CUODIM ;
nelji  CUODIM ;
vittâ  CUODIM ;
kuttâ  CUODIM ;
čiččâm  CUODIM ;
käävci  CUODIM ;
oovce  CUODIM ;

LEXICON CUODIM
čyeti HUNDREDM ;

LEXICON HUNDREDM
TENSM ;
TEENSM ;
  ONESM ;
 MILJON ;

LEXICON TEENSM
  TEENM ;

LEXICON TEENM
ohtâ  LOHKAIM ;
kyehti  LOHKAIM ;
kulmâ  LOHKAIM ;
nelji  LOHKAIM ;
vittâ+Use/NG:vittâ  LOHKAIM ;
kuttâ+Use/NG:kuttâ  LOHKAIM ;
vitt  LOHKAIM ;
kutt  LOHKAIM ;
čiččâm  LOHKAIM ;
käävci  LOHKAIM ;
oovce  LOHKAIM ;

LEXICON LOHKAIM
nubáloh MILJON ;

LEXICON TENSM
love  MILJON ;
kyehti  LOGIM ;
kulmâ  LOGIM ;
nelji  LOGIM ;
vittâ  LOGIM ;
kuttâ  LOGIM ;
čiččâm  LOGIM ;
käävci  LOGIM ;
oovce  LOGIM ;

LEXICON LOGIM
love  MILJON ;
love ONESM ;

LEXICON ONESM
miljon%   OVERTHOUSANDS ;
kyehti  MILJON ;
kulmâ  MILJON ;
nelji  MILJON ;
vittâ  MILJON ;
kuttâ  MILJON ;
čiččâm  MILJON ;
käävci  MILJON ;
oovce  MILJON ; 

LEXICON MILJON
% miljona%  OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERTHOUSANDS
HUNDREDST ;		! x00.000.000
čyeđe  HUNDREDT ;	! 100.000.000
  TENST ;		!  20.000.000
  TEENST ;		!  10.000.000
  TWOST ;		!   2.000.000
  SEVENST ;		!   7.000.000
tuhháát THOUSAND ;	!   1.000.000
 UNDERTHOUSAND ;	
  TENS ;
  TEENS ;
  ONES ;
  num ;	!  x000000


LEXICON HUNDREDST
kyehti   TWOCUODIT ;	! 200000-299999
kulmâ  TWOCUODIT ;	! 300000-399999
nelji  TWOCUODIT ;	! 400000-499999
vittâ  TWOCUODIT ;	! 500000-599999
kuttâ  TWOCUODIT ;	! 600000-699999
čiččâm  SEVENCUODIT ;	! 700000-799999
käävci  SEVENCUODIT ;	! 800000-899999
oovce  SEVENCUODIT ;	! 900000-999999

LEXICON TWOCUODIT
čyeđe  HUNDREDT ;	! 100000-199999

LEXICON SEVENCUODIT
čyeđe HUNDREDT ;

LEXICON HUNDREDT	! X = 1-9, Y = 0-9
TENST ;			! X2XYYY, X10YYY
TEENST ;		! X1XYYY
ohtâ TWOTHOUSANDS ; ! Same tuhháát as 2000
  ONEST ;		! XX0YYY
 THOUSANDS ;	! X00YYY

LEXICON TEENST
  TEENT ;

LEXICON TEENT
love  THOUSANDS ;
ohtâ  LOHKAIT ;
kyehti  LOHKAIT ;
kulmâ  LOHKAIT ;
nelji  LOHKAIT ;
vitt  LOHKAIT ;
vittâ+Use/NG:vittâ  LOHKAIT ;
kutt  LOHKAIT ;
kuttâ+Use/NG:kuttâ  LOHKAIT ;
čiččâm  LOHKAIT ;
käävci  LOHKAIT ;
oovce  LOHKAIT ;

LEXICON LOHKAIT
nubáloh THOUSANDS ;

LEXICON TENST
kyehtlov  LOGIT ;
kulmâlov  LOGIT ;
kulmlov+Use/NG:kulmlov  LOGIT ;
neljilov  LOGIT ;
vittlov  LOGIT ;
kuttlov  LOGIT ;
čiččâmlov  LOGIT ;
kähcilov  LOGIT ;
ohcelov  LOGIT ;

LEXICON LOGIT
  THOUSANDS ;
ohtâ  THOUSANDS ;
ONEST ;

LEXICON ONEST
TWOST ;			! 2000-6999
SEVENST ;               ! 7000-9999

LEXICON THOUSANDS
tuhháát  THOUSAND ;    ! 1000-1999

LEXICON TWOST
kyeht  TWOTHOUSANDS ;
kulmâ  TWOTHOUSANDS ;
nelji  TWOTHOUSANDS ;
vittâ  TWOTHOUSANDS ;
kuttâ  TWOTHOUSANDS ;

LEXICON SEVENST
čiččâm  SEVENTHOUSANDS ;
käävci  SEVENTHOUSANDS ;
oovce  SEVENTHOUSANDS ;

LEXICON TWOTHOUSANDS      ! 2000 - 6999
tuhháát THOUSAND ;

LEXICON SEVENTHOUSANDS ! 7000 - 9999
tuhháát THOUSAND ;


LEXICON THOUSAND
UNDERTHOUSAND ;	   ! 1100-1999
  TENS ;	   ! 1020-1099
  TEENS ;	   ! 1010-1019
  ONES ;	   ! 1001-1009
  num ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
SEVENHUNDREDS ;
TWOHUNDREDS ;
čyeti  HUNDRED ;


LEXICON SEVENHUNDREDS ! 700-900 with +Par
čiččâm  SEVENCUODI ;
käävci  SEVENCUODI ;
ohce  SEVENCUODI ;

LEXICON TWOHUNDREDS ! 200-699 with +Acc
kyehti  CUODI ;
kyeht+Use/NG:kyeht  CUODI ;
kulmâ  CUODI ;
kulm+Use/NG:kulm  CUODI ;
nelji  CUODI ;
vittâ  CUODI ;
vitt+Use/NG:vitt  CUODI ;
vit+Use/NG:vit  CUODI ;
kuttâ  CUODI ;
kutt+Use/NG:kutt  CUODI ;
kut+Use/NG:kut  CUODI ;

LEXICON SEVENCUODI ! 700-900 with +Par
čyeđe HUNDRED ;
čyet HUNDRED ;

LEXICON CUODI ! 200-699 with +Acc
čyeđe HUNDRED ;
čyet HUNDRED ;

LEXICON HUNDRED
TENS ;
TEENS ;
  ONES ;
 num ;

LEXICON TEENS
  TEEN ;

LEXICON TEEN
oht  LOHKAI ;
kyeht  LOHKAI ;
kulmâ  LOHKAI ;
kulm+Use/NG:kulm LOHKAI ;
nelji  LOHKAI ;
vitt  LOHKAI ;
vit+Use/NG:vit LOHKAI ;
kutt  LOHKAI ;
kut+Use/NG:kut LOHKAI ;
čiččâm  LOHKAI ;
käävci  LOHKAI ;
oovce  LOHKAI ;

LEXICON LOHKAI
nubáloh num ;
 LOGAAT ;

LEXICON LOGAAT
nubálovváád  num ; 

LEXICON TENS
love  num ;
lovváád   num ; 
kyeht  LOGI ;
kulm+Use/NG:kulm  LOGI ;
kulmâ  LOGI ;
nelji  LOGI ;
vit+Use/NG:vit  LOGI ;
vitt  LOGI ;
kut+Use/NG:kut  LOGI ;
kutt  LOGI ;
čiččâm  LOGI ;
kähci  LOGI ;
ohce  LOGI ;

LEXICON LOGI
lov  num ;
lov ONES ;

LEXICON ONES
CARDINAL ;
ORDINAL ;

LEXICON CARDINAL
ohtâ  num ;
kyehti  num ;
kulmâ  num ;
nelji  num ;
vittâ  num ;
kuttâ  num ;
čiččâm  num ;
käävci  num ;
oovce  num ;


LEXICON ORDINAL		   
vuossâmuš      num ;   
nubbe  num ;		   
kuálmád   num ;	   
niäljád num ;	   
viiđâd   num ;		   
kuuđâd   num ;		   
čiččâd    num ;	   
káávcád   num ;	   
oovcád    num ;	   


!From the original numeral file
!käävci num "kahdeksan" ;
!kähcilov num "kahdeksankymmentä" ;
!káávcád num "kahdeksas" ;
!kävcinubáloh num ;
!kuávttáá:kuávˊttáá num "kahden" ;
!kyehti:kyehˊti num "kaksi" ;
!kyehtlov:kyehˊtlov num "kaksikymmentä" ;
!kyehti:kyehˊti num "kaksitoista" ;
!nubbe num ;
!kyehtnubáloh:kyehˊtnubáloh num ;
!kuálmád num "kolmas" ;
!kulmâ num "kolme" ;
!kulmâlov num "kolmekymmentä" ;
!kulmânubáloh num "kolmetoista" ;
!kuuđâd num "kuudes" ;
!kutlov num "kuusikymmentä" ;
!kuttâ num ;
!kutnubáloh num ;
!love:lov7e num "kymmenen" ;
!lovvááh num "kymmenet" ;
!lovváád num ;
!nelji num "neljä" ;
!neljilov num "neljäkymmentä" ;
!niäljád num "neljäs" ;
!neljinubáloh num "neljätoista" ;
!čuáđáád:čuáˊđáád num "sadas" ;
!čiččâm num "seitsemän" ;
!čiččâmlov num "seitsemänkymmentä" ;
!čiččâmčyeđe:čiččâmčyeˊđe num "seitsemänsataa" ;
!čiččâmnubáloh num "seitsemäntoista" ;
!čiččâd num "seitsemäs" ;
!nubáloh num "toistakymmentä" ;
!viiđâd num "viides" ;
!vittâ num "viisi" ;
!vitlov num "viisikymmentä" ;
!vitnubáloh num ;
!vittâčyeđe:vittâčyeˊđe num "viisisataa" ;
!vitnubáloh num "viisitoista" ;
!oovce num "yhdeksän" ;
!ohcelov num "yhdeksänkymmentä" ;
!oovcenubáloh num "yhdeksäntoista" ;
!oovcád num "yhdeksäs" ;
!ohtâ num "yksi" ;
!ohtnubáloh num "yksitoista" ;

! Arabic numerals
! ---------------


LEXICON NUM-PREFIXES 
 §:§       ARABIC ; ! §24
 §§:§§     ARABIC ; ! §§24
 §% :§%    ARABIC ; ! § 24
 §§% :§§%  ARABIC ; ! §§ 24
 %-:%- ARABIC ; ! -24
 %‒:%‒ ARABIC ; ! -24 U+2012
 %–:%– ARABIC ; ! -24 U+2013
 %—:%— ARABIC ; ! -24 U+2014
 %―:%― ARABIC ; ! -24 U+2015
 %+:%+ ARABIC ; ! <plus>24
 %-% :%-%  ARABIC ; ! - 24
 %‒% :%‒%  ARABIC ; ! - 24 U+2012
 %–% :%–%  ARABIC ; ! - 24 U+2013
 %—% :%—%  ARABIC ; ! - 24 U+2014
 %―% :%―%  ARABIC ; ! - 24 U+2015
 %+% :%+%  ARABIC ; ! <plus> 24
 %*:%* ARABIC ; ! *24
 %$:%$ ARABIC ; ! $24
 %€:%€ ARABIC ; ! €24
 %<:%[%<%] ARABIC ; ! <24
 %>:%[%>%] ARABIC ; ! >24
 s%.:s%. ARABIC ; ! s.24
	


LEXICON ARABIC
 1 ARABICLOOP ;
 2 ARABICLOOP ;
 3 ARABICLOOP ;
 4 ARABICLOOP ;
 5 ARABICLOOP ;
 6 ARABICLOOP ;
 7 ARABICLOOP ;
 8 ARABICLOOP ;
 9 ARABICLOOP ;
%0 ARABICLOOP ;
 1 ARABICLOOPORD ;
 2 ARABICLOOPORD ;
 3 ARABICLOOPORD ;
 4 ARABICLOOPORD ;
 5 ARABICLOOPORD ;
 6 ARABICLOOPORD ;
 7 ARABICLOOPORD ;
 8 ARABICLOOPORD ;
 9 ARABICLOOPORD ;
%0 ARABICLOOPORD ;
 1 ARABICLOOPCOLL ;
 2 ARABICLOOPCOLL ;
 3 ARABICLOOPCOLL ;
 4 ARABICLOOPCOLL ;
 5 ARABICLOOPCOLL ;
 6 ARABICLOOPCOLL ;
 7 ARABICLOOPCOLL ;
 8 ARABICLOOPCOLL ;
 9 ARABICLOOPCOLL ;
%0 ARABICLOOPCOLL ;

LEXICON ARABICLOOP
 : ARABIC ;					!
 %-:%- ARABIC;	      ! 1-2    multipart numbers	!
 %–:%– ARABIC;	      ! 1–2    multipart numbers	!
 %—:%— ARABIC;	      ! 1—2    multipart numbers	!
 %.%-:%.%- ARABIC;	      ! 1.-2   multipart numbers	!
 %.%–:%.%– ARABIC;	      ! 1.–2   multipart numbers	!
 %.%—:%.%— ARABIC;	      ! 1.—2   multipart numbers	!
 %-% :%-%  ARABIC;	      ! 1- 2   multipart numbers	!
 %–% :%–%  ARABIC;	      ! 1– 2   multipart numbers	!
 %—% :%—%  ARABIC;	      ! 1— 2   multipart numbers	!
 % %-:% %- ARABIC;	      ! 1 -2   multipart numbers	!
 % %–:% %– ARABIC;	      ! 1 –2   multipart numbers	!
 % %—:% %— ARABIC;	      ! 1 —2   multipart numbers	!
 % %-% :% %-%  ARABIC;	      ! 1 - 2  multipart numbers	!
 % %–% :% %–%  ARABIC;	      ! 1 – 2  multipart numbers	!
 % %—% :% %—%  ARABIC;	      ! 1 — 2  multipart numbers	!
 % %-%-% :% %-%-%  ARABIC;	      ! 1 -- 2 multipart numbers	!
 % %–%–% :% %–%–%  ARABIC;	      ! 1 –– 2 multipart numbers	!
 % %—%—% :% %—%—%  ARABIC;	      ! 1 —— 2 multipart numbers	!
 % %-:% %- ARABICDELIMITER;   ! 1 -   multipart numbers		!
 % %–:% %– ARABICDELIMITER;   ! 1 –   multipart numbers		!
 % %—:% %— ARABICDELIMITER;   ! 1 —   multipart numbers		!
 % %-%-:% %-%- ARABICDELIMITER;   ! 1 --  multipart numbers		!
 % %–%–:% %–%– ARABICDELIMITER;   ! 1 ––  multipart numbers		!
 % %—%—:% %—%— ARABICDELIMITER;   ! 1 ——  multipart numbers		!
 %.:%. ARABIC;							!
 %,:%, ARABIC;							!
 %: ARABIC ;							!
 % :%  ARABIC;	   ! Allowing for "23 500". !			
 : ARABIC ;	   ! Allowing for "23 500" w/nbsp (initial char is alt-space)	!
 %/:%/ ARABIC;	   ! 24/12			!		
!%' ARABIC ;	   ! Since ' denotes cons.grad. This is not optimal either. 
!+A+Ord:%. # ;
 ,%-+Num:,%-       # ; ! 10,-
 ,%–+Num:,%–       # ; ! 10,–
 ,%—+Num:,%—       # ; ! 10,—
 %%+Num+Nom:%%     # ; ! 50%. It is wrong, but 75 % of us write "75%".
 % %%+Num+Nom:% %% # ;! We must analyse the correct ones as well... 50 % etc.
 +Num: ARABICDELIMITER ; ! list of number-case delimiters
! +Num: RHyph  ;          ! Hyphen and  added appropriately in RHyph
! +Num:%-# NUMERALCOMPOUNDS  ;

LEXICON ARABICLOOPORD
 +A+Ord:%. # ;
 +A+Ord: ARABICDELIMITERORD ;

LEXICON ARABICLOOPCOLL
 +N+Coll: ARABICDELIMITERCOLL ;


LEXICON ARABICDELIMITER
             ARABICCASE0 ; ! This is the 1984 case.
 :%:         ARABICCASE  ; ! This is the 1984:s case.
 +Err/Orth:   ARABICCASE  ; ! This is the 1984s case.
 +Err/Orth:%' ARABICCASE  ; ! This is the 1984's case.
 +Err/Orth:%- ARABICCASE  ; ! This is the 1984-s case.

LEXICON ARABICDELIMITERORD
! :          ARABICCASE0   ; ! This is the 1984:s case.
 :%:         ARABICCASEORD ; ! This is the 1984:s case.
 +Err/Orth:   ARABICCASEORD ; ! This is the 1984s case.
 +Err/Orth:%' ARABICCASEORD ; ! This is the 1984's case.
 +Err/Orth:%- ARABICCASEORD ; ! This is the 1984-s case.

LEXICON ARABICDELIMITERCOLL
!            ARABICCASE0    ; ! This is the 1984 case.
 :%:         ARABICCASECOLL ; ! This is the 1984:s case.
 +Err/Orth:   ARABICCASECOLL ; ! This is the 1984s case.
 +Err/Orth:%' ARABICCASECOLL ; ! This is the 1984's case.
 +Err/Orth:%- ARABICCASECOLL ; ! This is the 1984-s case.

                     		
LEXICON ARABICCASE0
 +Sg+Nom:%> # ;
 +Sg+Gen:%> # ;
 +Pl+Nom:%> # ;


LEXICON ARABICCASE
  f+Sg+Nom+Err/Orth:f  # ; ! s. 123f.
 ff+Sg+Nom+Err/Orth:ff # ; ! s. 123ff.
  b+Sg+Nom+Err/Orth:b  # ; ! § 123b
  c+Sg+Nom+Err/Orth:c  # ; ! § 123c
  d+Sg+Nom+Err/Orth:d  # ; ! § 123d
  e+Sg+Nom+Err/Orth:e  # ; ! § 123e

 +Sg+Nom+Err/Orth:    # ;
 +Sg+Gen+Err/Orth:    # ;
!+Sg+Acc:            # ;
 +Sg+Gen+Err/Orth:a   # ; !But here 123a 123
 +Sg+Acc+Err/Orth:av  # ;
 +Sg+Acc:%>v         # ;
 +Sg+Ill:%>j         # ;
 +Sg+Ill+Err/Orth:aj  # ;
 +Sg+Ela:%>s         # ;
 +Sg+Ela+Err/Orth:is  # ;
 +Sg+Ela+Err/Orth:as  # ;
 +Sg+Ine:%>n         # ;
 +Sg+Ine+Err/Orth:in  # ;
 +Sg+Ine+Err/Orth:an  # ;
 +Sg+Com:%>jn        # ;
 +Sg+Com+Err/Orth:ajn # ;
 +Ess:%>n            # ;
 +Ess+Err/Orth:an     # ;
 +Pl+Gen:%>j   K ; !
 +Pl+Acc:%>jt  K ; !
 +Pl+Ill:%>jda K ; ! 100:iide čuđiide
 +Pl+Ine:%>jn  K ; !
 +Pl+Ela:%>js  K ; !
 +Pl+Com:%>j   K ; !
!ordinals

LEXICON ARABICCASEORD
 +Sg+Gen:%>da    K ;
 +Sg+Acc:%>dav   K ;
 +Sg+Ill:%>dij   K ;
 +Sg+Ine:%>din   K ;
 +Sg+Ela:%>dis   K ;
 +Sg+Com:%>dijn  K ;
 +Sg+Ess:%>din   K ;
 +Pl+Nom:%>da    K ;
 +Pl+Gen:%>dij   K ;
 +Pl+Acc:%>dijt  K ;
 +Pl+Ill:%>dijda K ;
 +Pl+Ine:%>dijn  K ;
 +Pl+Ela:%>dijs  K ;
 +Pl+Com:%>dij   K ;

LEXICON ARABICCASECOLL
 +Sg+Gen:%>sa   K ;
 +Sg+Acc:%>sav  K ;
 +Sg+Ill:%>sij  K ;
 +Sg+Loc:%>sin  K ;
 +Sg+Loc:%>sis  K ;
 +Sg+Com:%>sijn K ;

! The Roman numerals !
! ------------------ !

LEXICON ROMAN						!^C^
 : ROM-THOUSAND ;						!
 : ROM-HUNDRED ;						!
 : ROM-TEN ;							!
 : ROM-ONE ;							!

LEXICON ROM-THOUSAND              	!^C^
 M:M ROM-THOUSAND-TAG;				!
 MM:MM ROM-THOUSAND-TAG;				!
 MMM:MMM ROM-THOUSAND-TAG;				!
 MMMM:MMMM ROM-THOUSAND-TAG;			!
 MMMMM:MMMMM ROM-THOUSAND-TAG;			!

LEXICON ROM-THOUSAND-TAG			!^C^
 : ROMNUMTAG ;						!
 : ROM-HUNDRED ;						!
 : ROM-SPLIT	;						!

LEXICON ROM-HUNDRED					!^C^
 C:C ROM-HUNDRED-TAG;				!
 CC:CC ROM-HUNDRED-TAG;				!
 CCC:CCC ROM-HUNDRED-TAG;				!
 CD:CD ROM-HUNDRED-TAG;				!
 D:D ROM-HUNDRED-TAG;				!
 DC:DC ROM-HUNDRED-TAG;				!
 DCC:DCC ROM-HUNDRED-TAG;				!
 DCCC:DCCC ROM-HUNDRED-TAG;				!
 CM:CM ROM-HUNDRED-TAG;				!

LEXICON ROM-HUNDRED-TAG				!^C^
 : ROMNUMTAG ;						!
 : ROM-TEN ;							!
 : ROM-SPLIT ;						!

LEXICON ROM-TEN						!^C^
 X:X ROM-TEN-TAG;					!
 XX:XX ROM-TEN-TAG;					!
 XXX:XXX ROM-TEN-TAG;					!
 XL:XL ROM-TEN-TAG;					!
 L:L ROM-TEN-TAG;					!
 LX:LX ROM-TEN-TAG;					!
 LXX:LXX ROM-TEN-TAG;					!
 LXXX:LXXX ROM-TEN-TAG;					!
 XC:XC ROM-TEN-TAG;					!

LEXICON ROM-TEN-TAG					!^C^
 : ROMNUMTAG ;						!
 : ROM-ONE ;							!
 : ROM-SPLIT ;						!

LEXICON ROM-ONE						!^C^
 I:I ROM-ONE-TAG;					!
 II:II ROM-ONE-TAG;					!
 III:III ROM-ONE-TAG;					!
 IV:IV ROM-ONE-TAG;					!
 V:V ROM-ONE-TAG;					!
 VI:VI ROM-ONE-TAG;					!
 VII:VII ROM-ONE-TAG;					!
 VIII:VIII ROM-ONE-TAG;					!
 IX:IX ROM-ONE-TAG;					!

LEXICON ROM-ONE-TAG					!^C^
 : ROMNUMTAG ;						!
! +N: ROMNUMTAG ;					!^C^ !The Olav viđeš fix (Roman V now gets A)
 : ROM-SPLIT ;						!

! Here, we split the Roman numerals, in order to account for cases like "Kapihtal II-IV".
! We may send this first part directly to ROM-TAG below (and get the +Num tag), or we may
! send them through a second loop, identical to the first one, but marked wit "2" (the lexica
! are called 2ROMAN, etc. We do this instead of making a loop, since we do not want cases
! like "II-IV-VI-VII-IX". If that should turn out to be a good idea, a loop would do the 
! trick.

LEXICON ROM-SPLIT						!^C^
 :%- 2ROMAN ; ! II-VI, etc.				!


! Here goes loop 2.

LEXICON 2ROMAN							!^C^
 : 2ROM-THOUSAND ;						!
 : 2ROM-HUNDRED ;							!
 : 2ROM-TEN ;								!
 : 2ROM-ONE ;								!

LEXICON 2ROM-THOUSAND              		!^C^
 M:M 2ROM-THOUSAND-TAG;					!
 MM:MM 2ROM-THOUSAND-TAG;					!
 MMM:MMM 2ROM-THOUSAND-TAG;				!
 MMMM:MMMM 2ROM-THOUSAND-TAG;				!
 MMMMM:MMMMM 2ROM-THOUSAND-TAG;				!

LEXICON 2ROM-THOUSAND-TAG				!^C^
 : ROMNUMTAG ;							!
 : 2ROM-HUNDRED ;							!

LEXICON 2ROM-HUNDRED					!^C^
 C:C 2ROM-HUNDRED-TAG;					!
 CC:CC 2ROM-HUNDRED-TAG;					!
 CCC:CCC 2ROM-HUNDRED-TAG;					!
 CD:CD 2ROM-HUNDRED-TAG;					!
 D:D 2ROM-HUNDRED-TAG;					!
 DC:DC 2ROM-HUNDRED-TAG;					!
 DCC:DCC 2ROM-HUNDRED-TAG;					!
 DCCC:DCCC 2ROM-HUNDRED-TAG;				!
 CM:CM 2ROM-HUNDRED-TAG;					!

LEXICON 2ROM-HUNDRED-TAG				!^C^
 : ROMNUMTAG ;							!
 : 2ROM-TEN ;								!

LEXICON 2ROM-TEN						!^C^
 X:X 2ROM-TEN-TAG;						!
 XX:XX 2ROM-TEN-TAG;						!
 XXX:XXX 2ROM-TEN-TAG;						!
 XL:XL 2ROM-TEN-TAG;						!
 L:L 2ROM-TEN-TAG;						!
 LX:LX 2ROM-TEN-TAG;						!
 LXX:LXX 2ROM-TEN-TAG;						!
 LXXX:LXXX 2ROM-TEN-TAG;					!
 XC:XC 2ROM-TEN-TAG;						!

LEXICON 2ROM-TEN-TAG					!^C^
 : ROMNUMTAG ;							!
 : 2ROM-ONE ;								!

LEXICON 2ROM-ONE						!^C^
 I:I 2ROM-ONE-TAG;						!
 II:II 2ROM-ONE-TAG;						!
 III:III 2ROM-ONE-TAG;						!
 IV:IV 2ROM-ONE-TAG;						!
 V:V 2ROM-ONE-TAG;						!
 VI:VI 2ROM-ONE-TAG;						!
 VII:VII 2ROM-ONE-TAG;						!
 VIII:VIII 2ROM-ONE-TAG;					!
 IX:IX 2ROM-ONE-TAG;						!

LEXICON 2ROM-ONE-TAG
 ROMNUMTAG ;
 
LEXICON ROMNUMTAG
 +Num+Nom: # ;
 +Num+Gen: # ;
!+Num+Acc: # ;
 +A+Ord:%. # ; !III. as ordinal.
 +A+Ord:   # ; !Looking for trouble...
 +Num:%: ROMNUMTAGOBL  ;
 +Num:%'7 ROMNUMTAGOBL ;
 +Num:%- ROMNUMTAGOBL  ;
 +Num:%´ ROMNUMTAGOBL  ;
! +Num:   ROMNUMTAGOBL ;

LEXICON ROMNUMTAGOBL
 +Gen+Err/Orth:a  # ; ! But here 123a 123
 +Acc+Err/Orth:av # ;
 +Ill:%>j        # ;
 +Ill+Err/Orth:ij # ;
 +Ill+Err/Orth:áj # ;
 +Ela:%>s        # ;
 +Ine:%>n        # ;
 +Com:%>jn       # ;
 +Ess:%>n        # ;
 +Acc:%>v        # ;




! A final section with some isolated numeral expresssions !
! ------------------------------------------------------- !

      
LEXICON ISOLATED-NUMEXP					!^C^
 ½+Num:½ # ;						!
 ¹+Num:¹ # ;						!
 ²+Num:² # ;						!
 ³+Num:³ # ;						!
 ¼+Num:¼ # ;						!





