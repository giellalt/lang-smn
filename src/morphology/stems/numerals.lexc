! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no


!! !!!Inari Saami number <-> text


LEXICON num
+Num: numsg ;

LEXICON numsg
+Sg+Nom: K ;
+Sg+Acc: K ;
+Sg+Gen: K ;

LEXICON Numeral
HUNDREDSM ;		! 200M
čyeti HUNDREDM ;	! 100M
TENSM ;			! 20-99M
TEENSM ;		! 10-19M
ONESM ;			! 1-9M
HUNDREDST ;		! 200000-999999
HUNDREDST ;		! 200000-999999
čyeti  HUNDREDT ;	! 100000-100999
TENST ;			! 20000-99999,10000-10999
TEENST ;		! 11000-19999
ONEST ;			! 2000-9999
tuhháát  THOUSAND ;	! 1000-1999
UNDERTHOUSAND ;		! 100-999
TENS ;			! 20-99
TEENS ;			! 10-19
ONES ;			! 1-9
num-imprecise ;         ! kalle, ...
MISCNUM ;			! pelnub
ARABIC          ; ! for the arabic numerals !
ROMAN           ; ! for the roman numerals  !
NUM-PREFIXES    ; ! for §34 etc.            !
ISOLATED-NUMEXP ; ! for ½ etc.                      !
LOHKAI ;


LEXICON HUNDREDSM
kyehti   CUODIM ;
kulmâ  CUODIM ;
nelji  CUODIM ;
vittâ  CUODIM ;
kuttâ  CUODIM ;
čiččâm  CUODIM ;
käävci  CUODIM ;
oovce  CUODIM ;

LEXICON CUODIM
čyeti HUNDREDM ;

LEXICON HUNDREDM
TENSM ;
TEENSM ;
  ONESM ;
 MILJON ;

LEXICON TEENSM
  TEENM ;

LEXICON TEENM
ohtâ:oht^RVâ  LOHKAIM ;
kyehti  LOHKAIM ;
kulmâ  LOHKAIM ;
nelji  LOHKAIM ;
vittâ+Use/NG:vittâ  LOHKAIM ;
kuttâ+Use/NG:kuttâ  LOHKAIM ;
vitt  LOHKAIM ;
kutt  LOHKAIM ;
čiččâm  LOHKAIM ;
käävci  LOHKAIM ;
oovce  LOHKAIM ;

LEXICON LOHKAIM
nubáloh MILJON ;

LEXICON TENSM
love  MILJON ;
kyehti  LOGIM ;
kulmâ  LOGIM ;
nelji  LOGIM ;
vittâ  LOGIM ;
kuttâ  LOGIM ;
čiččâm  LOGIM ;
käävci  LOGIM ;
oovce  LOGIM ;

LEXICON LOGIM
love  MILJON ;
love ONESM ;

LEXICON ONESM
miljon%   OVERTHOUSANDS ;
kyehti  MILJON ;
kulmâ  MILJON ;
nelji  MILJON ;
vittâ  MILJON ;
kuttâ  MILJON ;
čiččâm  MILJON ;
käävci  MILJON ;
oovce  MILJON ; 

LEXICON MILJON
% miljona%  OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERTHOUSANDS
HUNDREDST ;		! x00.000.000
čyeđe  HUNDREDT ;	! 100.000.000
  TENST ;		!  20.000.000
  TEENST ;		!  10.000.000
  TWOST ;		!   2.000.000
  SEVENST ;		!   7.000.000
tuhháát THOUSAND ;	!   1.000.000
 UNDERTHOUSAND ;	
  TENS ;
  TEENS ;
  ONES ;
  num ;	!  x000000


LEXICON HUNDREDST
kyehti   TWOCUODIT ;	! 200000-299999
kulmâ  TWOCUODIT ;	! 300000-399999
nelji  TWOCUODIT ;	! 400000-499999
vittâ  TWOCUODIT ;	! 500000-599999
kuttâ  TWOCUODIT ;	! 600000-699999
čiččâm  SEVENCUODIT ;	! 700000-799999
käävci  SEVENCUODIT ;	! 800000-899999
oovce  SEVENCUODIT ;	! 900000-999999

LEXICON TWOCUODIT
čyeđe  HUNDREDT ;	! 100000-199999

LEXICON SEVENCUODIT
čyeđe HUNDREDT ;

LEXICON HUNDREDT	! X = 1-9, Y = 0-9
TENST ;			! X2XYYY, X10YYY
TEENST ;		! X1XYYY
ohtâ:oht^RVâ TWOTHOUSANDS ; ! Same tuhháát as 2000
  ONEST ;		! XX0YYY
 THOUSANDS ;	! X00YYY

LEXICON TEENST
  TEENT ;

LEXICON TEENT
love  THOUSANDS ;
ohtâ:oht^RVâ  LOHKAIT ;
kyehti  LOHKAIT ;
kulmâ  LOHKAIT ;
nelji  LOHKAIT ;
vitt  LOHKAIT ;
vittâ+Use/NG:vittâ  LOHKAIT ;
kutt  LOHKAIT ;
kuttâ+Use/NG:kuttâ  LOHKAIT ;
čiččâm  LOHKAIT ;
käävci  LOHKAIT ;
oovce  LOHKAIT ;

LEXICON LOHKAIT
nubáloh THOUSANDS ;

LEXICON TENST
muádlov:muád#lov  LOGIT ;
maŋgâlov:maŋgâ#lov LOGIT ;
muáddilov+Use/NG:muáddi#lov  LOGIT ;
kyehtlov:kyeht#lov  LOGIT ;
kulmâlov:kulmâ#lov  LOGIT ;
kulmlov+Use/NG:kulm#lov  LOGIT ;
neljilov:nelji#lov  LOGIT ;
vittlov:vitt#lov  LOGIT ;
vittlov+Use/NG:vittâ#lov  LOGIT ;
kuttlov:kutt#lov  LOGIT ;
čiččâmlov:čiččâm#lov  LOGIT ;
kähcilov:kähci#lov  LOGIT ;
ohcelov:ohce#lov  LOGIT ;

LEXICON LOGIT
  THOUSANDS ;
ohtâ:oht^RVâ  THOUSANDS ;
ONEST ;

LEXICON ONEST
TWOST ;			! 2000-6999
SEVENST ;               ! 7000-9999

LEXICON THOUSANDS
tuhháát  THOUSAND ;    ! 1000-1999

LEXICON TWOST
kyeht  TWOTHOUSANDS ;
kulmâ  TWOTHOUSANDS ;
nelji  TWOTHOUSANDS ;
vittâ  TWOTHOUSANDS ;
kuttâ  TWOTHOUSANDS ;

LEXICON SEVENST
čiččâm  SEVENTHOUSANDS ;
käävci  SEVENTHOUSANDS ;
oovce  SEVENTHOUSANDS ;

LEXICON TWOTHOUSANDS      ! 2000 - 6999
tuhháát THOUSAND ;

LEXICON SEVENTHOUSANDS ! 7000 - 9999
tuhháát THOUSAND ;


LEXICON THOUSAND
UNDERTHOUSAND ;	   ! 1100-1999
  TENS ;	   ! 1020-1099
  TEENS ;	   ! 1010-1019
  ONES ;	   ! 1001-1009
+Num:  X_POTTAAK ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
SEVENHUNDREDS ;
TWOHUNDREDS ;
čyeti:čyet^RCi3  HUNDRED ;
čyeti+Use/NG+Num+Sg+Gen:čyet  K ;


LEXICON SEVENHUNDREDS ! 700-900 with +Par
čiččâm  SEVENCUODI ;
käävci  SEVENCUODI ;
ohce  SEVENCUODI ;

LEXICON TWOHUNDREDS ! 200-699 with +Acc
kyehti  CUODI ;
kyeht+Use/NG:kyeht  CUODI ;
kulmâ  CUODI ;
kulm+Use/NG:kulm  CUODI ;
nelji  CUODI ;
vittâ  CUODI ;
vitt+Use/NG:vitt  CUODI ;
vit+Use/NG:vit  CUODI ;
kuttâ  CUODI ;
kutt+Use/NG:kutt  CUODI ;
kut+Use/NG:kut  CUODI ;

LEXICON SEVENCUODI ! 700-900 with +Par
čyeđe HUNDRED ;
čyet HUNDRED ;

LEXICON CUODI ! 200-699 with +Acc
čyeđe HUNDRED ;
čyet HUNDRED ;

LEXICON HUNDRED
TENS ;
TEENS ;
  ONES ;
 CYETI ;

LEXICON TEENS
  TEEN ;

LEXICON TEEN
oht LOHKAI ;
ohtâ LOHKAI ;
kyeht LOHKAI ;
kyehti LOHKAI ;
kulmâ LOHKAI ;
kulm:kulm LOHKAI ;
nelji LOHKAI ;
vitt LOHKAI ;
vitt+Use/NG:vittâ LOHKAI ;
vit:vit LOHKAI ;
kutt LOHKAI ;
kuttâ LOHKAI ;
kut:kut LOHKAI ;
čiččâm LOHKAI ;
käävci LOHKAI ;
oovce LOHKAI ;


LEXICON LOHKAI
nubáloh:nubá# NUBALOH ;
nubáloh+Err/Orth:nuba# NUBALOH ;
 LOGAAT ;

LEXICON LOGAAT
nubálovváád+A+Ord:nubá#lovváád  X_EEMEED ; 

LEXICON TENS
lovváád+A+Ord:lovváád X_EEMEED ; 
kyeht:kyeht^RC LOGIi ;
!kyehti LOGI ;
!kulm+Use/NG:kulm LOGI ;
kulmâ:ku^RVlmâ LOGI ;
nelji:ne^RVlji LOGI ;
vitt:vi LOGItt ;
kutt:ku LOGItt ;
čiččâm:čičč5âm LOGIm ;
kähci:kä^RVhc^RCi LOGIhc ;
ohce:o^RVhc^RCe LOGIhc ;
ohce:ohc^RCe oohceLOGIhc ;
muádi:muádi LOGI ;
muádi:muádd LOGI ;
muádlov:muád#  iLOV ;

LEXICON LOGIi
love: iLOVE ;
lov: iLOV ;
lov ONES ;

LEXICON LOGIhc
love: hcLOVE ;
lov: hcLOV ;
lov ONES ;

LEXICON oohceLOGIhc
lov+Num+Sg+Ill:^WG^RLEN%>lohá%>n K ; ! ovcelohán
lov+Num+Sg+Loc:^WG^RLEN%>lovve%>est K ; ! ovcelovveest



LEXICON LOGItt
love: ttLOVE ;
lov: ttLOV ;
lov ONES ;


LEXICON LOGI
love: plussLOVE ;
lov: LOV ;
lov ONES ;

LEXICON LOGIm
love: mLOVE ;
lov: mLOV ;
lov ONES ;


LEXICON ONES
CARDINAL ;
ORDINAL ;

LEXICON CARDINAL
ohtâ:o^RVht^RCâ OHTA ;
kyehti:kyeht^RCi KYEHTI ;
kulmâ:ku^RVlmâ KULMA ;
nelji:ne^RVlji KYEHTI ;
vittâ:vi^RVtt4â VITTA ;
kuttâ:ku^RVtt4â VITTA ;
čiččâm:čiččâm CICCAM ;
käävci:käävc^RCi KYEHTI ;
oovce:oovc^RCe KYEHTI ;
love: LOVE ;



LEXICON KYEHTI 
+Num+Sg+Nom: K ;
+Num+Sg+Acc: K ;
+Num+Sg+Gen:^WG^RLEN K ; !oovtâ K ;
+Num+Sg+Ill+Attr:^WG^RLEN K ; !oovtâ K ;
+Num+Sg+Loc+Attr:^WG^RLEN K ; !oovtâ K ;
+Num+Sg+Abe+Attr:^WG^RLEN K ; !oovtâ K ;
+Num+Sg+Ill:^EA^RLEN%>n K ; ! oohtân K ;
+Num+Sg+Loc:^WG^RLEN%>st K ; ! oovtâst K ;
+Num+Sg+Com:^WG^CLEN^RVSH%>in K ; !   neljijn
+Num+Sg+Abe:^WG^RVSH%>ttáá K ; ! ovtâttáá K ;
+Num+Ess:%>n  K ; ! ohtân	 K ;
+Num+Pl+Nom:^WG^RLEN%>h K ; !oovtah K ;
+Num+Pl+Acc:^WG^RLEN%>id K ; !oovtâid K ;
+Num+Pl+Gen:^WG^RLEN%>i K ; !oovtâi K ;
+Num+Pl+Gen:^WG^RVSH%>i K ; !oovtâi K ;
+Num+Pl+Ill:^WG^CLEN^RVSH%>id K ; !ovttáid K ;
+Num+Pl+Loc:^WG^RLEN%>in K ; !oovtâin K ;
+Num+Pl+Com:^WG^RVSH%>iguin K ; !ovtâiguin K ;
+Num+Pl+Abe:^WG^RVSH%>ittáá K ; !ovtâittáá K ;
+Num+Cmp/SgGen:^WG^RLEN%> NUM_DER ;


LEXICON OHTA
+Num+Sg+Acc:^WG^RLEN K ; !kuulmâ
+Num+Sg+Com:^WG^CLEN^SVLOW%>in K ; !  ovttáin  
+Num+Pl+Ill:^WG^CLEN^SVLOW%>id K ; !ovttáid  
KULMA_VITTA ;

LEXICON KULMA  !maaŋgâi
+Num+Sg+Acc: K ; !kulmâ
+Num+Sg+Com:^WG^SVLOW%>in K ; !  ovttáin  
+Num+Pl+Ill:^WG^SVLOW%>id K ; !ovttáid  
KULMA_VITTA ;

LEXICON VITTA 
+Num+Sg+Acc: K ; !vittâ
+Num+Sg+Com:^WG^RLEN^SVLOW%>in K ; !  viiđáin  
+Num+Pl+Ill:^WG^RLEN^SVLOW%>id K ; !viiđáid  
KULMA_VITTA ;

LEXICON KULMA_VITTA 
+Num+Sg+Nom: K ; !vittâ
+Num+Sg+Gen:^WG^RLEN K ; !kuulmâ  
+Num+Sg+Ill+Attr:^WG^RLEN K ; !kuulmâ   oovtâ kistui
+Num+Sg+Loc+Attr:^WG^RLEN K ; !kuulmâ  oovtâ kistust
+Num+Sg+Abe+Attr:^WG^RLEN K ; !kuulmâ  oovtâ kistuuttáá
+Num+Sg+Ill:^CSH^RLEN%>n K ; ! kuulmân  
+Num+Sg+Loc:^WG^RLEN%>st K ; ! kuulmâst 
+Num+Sg+Abe:^WG%>ttáá K ; ! viđâttáá  
+Num+Ess:%>n  K ; ! vittân	 
+Num+Pl+Nom:^WG^RLEN%>h K ; !viiđah 
+Num+Pl+Acc:^WG^RLEN%>id K ; ! viiđâid  
+Num+Pl+Gen:^WG^RLEN%>i K ; !viiđâi  
+Num+Pl+Gen+Use/NG:^WG%>i K ; !viđâi  
+Num+Pl+Loc:^WG^RLEN%>in K ; !viiđâin  
+Num+Pl+Com:^WG%>iguin K ; !viđâiguin  
+Num+Pl+Abe:^WG%>ittáá K ; !viđâittáá  
+Num+Cmp/SgGen:^WG^RLEN%> NUM_DER ;



LEXICON CICCAM !to be checked
+Num+Sg+Nom: K ;
+Num+Sg+Acc: K ;
+Num+Sg+Gen: K ; !čiččâm K ;
+Num+Sg+Ill+Attr: K ; !čiččâm K ;
+Num+Sg+Loc+Attr: K ; !čiččâm K ;
+Num+Sg+Abe+Attr: K ; !čiččâm K ;
+Num+Sg+Ill:%>ân K ; ! oohtân K ;
+Num+Sg+Loc:i%>st K ; ! oovtâst K ;
+Num+Sg+Com:á%>in K ; !  ovttáin K ;
+Num+Sg+Abe:%>ttáá K ; ! ovtâttáá K ;
+Num+Ess:i%>n  K ; ! ohtân	 K ;
+Num+Pl+Nom:e%>h K ; !oovtah K ;
+Num+Pl+Acc:i%>id K ; !oovtâid K ;
+Num+Pl+Gen:i%>i K ; !oovtâi K ;
+Num+Pl+Ill:á%>id K ; !ovttáid K ;
+Num+Pl+Loc:á%>in K ; !oovtâin K ;
+Num+Pl+Com:i%>iguin K ; !ovtâiguin K ;
+Num+Pl+Abe:i%>ittáá K ; !ovtâittáá K ;
+Num+Cmp/SgGen:%> NUM_DER ;


LEXICON iLOV 
+Num+Sg+Nom:lov K ; !love kyehtlov
+Num+Sg+Acc:lov K ; !love kyehtlov
+Num+Sg+Gen:i6^WG%>lov K ; !love K ; kyehtlov
+Num+Cmp/SgGen:i6^WG%>lov%> NUM_DER ;

LOV_OBLi ;

LEXICON iLOVE 
+Num+Sg+Nom:love K ; !love 
+Num+Sg+Acc:love K ; !love
+Num+Sg+Gen:i6^WG%>love K ; !love K ;
LOV_OBLi ;

LEXICON LOV_OBLi 
+Num+Sg+Ill+Err/Orth:i6^WG%>lová%>n K ; ! lován K ;
+Num+Sg+Ill:i6^WG^RLEN%>lohá%>n K ; ! kyevtlohán K ; kyevtlohán
+Num+Sg+Loc:i6^WG^RLEN%>lovve%>est K ; ! lovveest K ; kyevtlovveest
+Num+Sg+Abe:i6^WG%>love%>ttáá K ; ! lovettáá K ; kyevtlovettáá
+Num+Sg+Com:i^WG^CLEN%>inluvvi%>jn K ; !   luvvijn kuovttijnluvvijn
+Num+Ess:in%>lovve%>en  K ; ! lovveen	 K ; kyehtinlovveen
+Num+Pl+Nom:i^WGh%>love%>h K ; !loveh K ; kyevtihloveh
+Num+Pl+Acc:i^WG%>idluuvi%>jd K ; !luuvijd K ; kuovtijdluuvijd
!+Num+Pl+Gen:i%>iluuvi%>j K ; !luuvij K ;
+Num+Pl+Gen:i^WG%>iluvi%>j K ; !luvij K ; kuovtijluvij
+Num+Pl+Com:i^WG%>iluvi%>jguin K ; !luvijguin K ; kuovtijluvijguin
+Num+Pl+Abe:i^WG%>iluvi%>jttáá K ; !luvijttáá K ; kuovtijluvijttáá
+Num+Pl+Ill:i^WG^CLEN%>idluvvi%>jd K ; !luvvijd K ; kuovttijdluvvijd
+Num+Pl+Loc:i^WG%>inluuvi%>jn K ; !luuvijn K ; kuovtijnluuvijn

LEXICON LOV 
+Num+Sg+Nom:^RVSH%>lov K ; !love kyehtlov
+Num+Sg+Acc:^WG%>lov K ; !love kyehtlov neljilov
+Num+Sg+Gen:^WG^RLEN%>lov K ; !love K ; kuulmâlov
+Num+Cmp/SgGen:^WG^RLEN%>lov%> NUM_DER ;
LOV_OBL ;

LEXICON plussLOVE 
+Num+Sg+Nom:love K ; !love 
+Num+Sg+Acc:love K ; !love
+Num+Sg+Gen:^WG%>love K ; !love K ;
LOV_OBL ;

LEXICON LOV_OBL 
+Num+Sg+Ill+Err/Orth:^WG%>lová%>n K ; ! lován K ;
+Num+Sg+Ill:^WG^RLEN%>lohá%>n K ; ! kyevtlohán K ; kyevtlohán
+Num+Sg+Loc:^WG^RLEN%>lovve%>est K ; ! lovveest K ; kyevtlovveest
+Num+Sg+Abe:^WG^RLEN%>love%>ttáá K ; ! lovettáá K ; kyevtlovettáá
+Num+Sg+Com:^WG^SVLOW%>inluvvi%>jn K ; !   luvvijn kuovttijnluvvijn
+Num+Ess:%>nlovve%>en  K ; ! lovveen	 K ; kyehtinlovveen
+Num+Pl+Nom:^WG^RLEN%>hlove%>h K ; !loveh K ; kyevtihloveh
+Num+Pl+Acc:^WG^RLEN%>idluuvi%>jd K ; !luuvijd K ; kuovtijdluuvijd
+Num+Pl+Gen:^RLEN%>iluvi%>j K ; !luuvij K ;
+Num+Pl+Gen:^WG%>iluvi%>j K ; !luvij K ; kuovtijluvij
+Num+Pl+Ill:^WG^SVLOW%>idluvvi%>jd K ; !luvvijd K ; kuovttijdluvvijd
+Num+Pl+Loc:^WG^RLEN%>inluuvi%>jn K ; !luuvijn K ; kuovtijnluuvijn
+Num+Pl+Com:^WG%>iluvi%>jguin K ; !luvijguin K ; kuovtijluvijguin
+Num+Pl+Abe:^WG%>iluvi%>jttáá K ; !luvijttáá K ; kuovtijluvijttáá



LEXICON hcLOV 
+Num+Sg+Nom:lov K ; ! kähcilov
+Num+Sg+Acc:lov K ; ! kähcilov
+Num+Sg+Gen:^WG^RLEN%>lov K ; ! käävcilov
+Num+Cmp/SgGen:^WG^RLEN%>lov%> NUM_DER ;

hcLOV_OBL ;

LEXICON hcLOVE 
+Num+Sg+Nom:love K ; !love 
+Num+Sg+Acc:love K ; !love
+Num+Sg+Gen:^WG%>love K ; !love K ;
+Num+Cmp/SgGen:^WG%>love%> NUM_DER ;

hcLOV_OBL ;

LEXICON hcLOV_OBL 
+Num+Sg+Ill:^WG%>lohá%>n K ; ! kävcilohán K ;
+Num+Sg+Ill:^WG^RLEN%>lohá%>n K ; ! käävcilohán
+Num+Sg+Loc:^WG^RLEN%>lovve%>est K ; ! käävcilovveest
+Num+Sg+Loc:^WG%>lovve%>est K ; ! kävcilovveest
+Num+Sg+Com:^WG^CLEN%>inluvvi%>jn K ; ! kavccijnluvvijn
+Num+Sg+Abe:^WG%>love%>ttáá K ; ! kävcilovettáá
+Num+Ess:%>lovve%>en  K ; ! kähcilovveen
+Num+Pl+Nom:^WG^RLEN%>hlove%>h K ; !käävcihloveh
+Num+Pl+Acc:^WG^RLEN%>idluuvi%>jd K ; !kaavcijdluuvijd
!+Num+Pl+Gen:^RLEN%>iluvi%>j K ; !luuvij K ;
+Num+Pl+Gen:^WG%>iluvi%>j K ; !kavcijluvij
+Num+Pl+Ill:^WG^CLEN^SVLOW%>idluvvi%>jd K ; !kavccijdluvvijd
+Num+Pl+Loc:^WG^RLEN%>inluuvi%>jn K ; !kaavcijnluuvijn
+Num+Pl+Com:^WG%>iluvi%>jguin K ; !kavcijluvijguin
+Num+Pl+Abe:^WG%>iluvi%>jttáá K ; !kavcijluvijttáá


LEXICON ttLOV 
+Num+Sg+Nom+Use/NG:tlov K ; !vitlov
+Num+Sg+Nom:ttlov K ; !vittlov
+Num+Sg+Acc+Use/NG:tlov K ; !vitlov
+Num+Sg+Acc:ttlov K ; !vittlov
+Num+Sg+Gen+Use/NG:đâlov K ; !love kyehtlov
 :^RV LOV_OBLtt ;

LEXICON ttLOVE 
+Num+Sg+Nom+Use/NG:tlove K ; !love 
+Num+Sg+Nom:ttlove K ; !love 
+Num+Sg+Nom+Use/NG:ttâlove K ; !love 
+Num+Sg+Acc+Use/NG:tlove K ; !love 
+Num+Sg+Acc:ttlove K ; !love 
+Num+Sg+Gen+Use/NG:đâlove K ; !love
 :^RV LOV_OBLtt ;



LEXICON LOV_OBLtt
+Num+Sg+Ill+Err/Orth:đâ%>lová%>n K ; ! lován K ;
+Num+Sg+Ill:đâ%>lohá%>n K ; ! kyevtlohán K ; kyevtlohán
+Num+Sg+Loc:đâ%>lovve%>est K ; ! lovveest K ; kyevtlovveest
+Num+Sg+Com:đá^RLEN%>inluvvi%>jn K ; !   luvvijn kuovttijnluvvijn
+Num+Sg+Abe:đâ%>love%>ttáá K ; ! lovettáá K ; kyevtlovettáá
+Num+Ess:ttân%>lovve%>en  K ; ! lovveen	 K ; kyehtinlovveen
+Num+Pl+Nom:đa^RLEN%>hlove%>h K ; !loveh K ; kyevtihloveh
+Num+Pl+Gen:đâ%>iluvi%>j K ; !luuvij K ;
+Num+Pl+Acc:đâ^RLEN%>idluuvi%>jd K ; !luuvijd K ; kuovtijdluuvijd
+Num+Pl+Ill:đá^RLEN%>idluvvi%>jd K ; !luvvijd K ; kuovttijdluvvijd
+Num+Pl+Loc:đâ^RLEN%>inluuvi%>jn K ; !luuvijn K ; kuovtijnluuvijn
+Num+Pl+Com:đâ%>iluvi%>jguin K ; !luvijguin K ; kuovtijluvijguin
+Num+Pl+Abe:đâ%>iluvi%>jttáá K ; !luvijttáá K ; kuovtijluvijttáá

LEXICON mLOV
+Num+Sg+Nom:%>lov K ; !love kyehtlov
+Num+Sg+Acc:%>lov K ; !love kyehtlov
+Num+Sg+Gen:%>lov K ; !love K ; kuulmâlov
mLOV_OBL ;

LEXICON mLOVE
+Num+Sg+Nom:%>love K ; !love 
+Num+Sg+Acc:%>love K ; !love
+Num+Sg+Gen:%>love K ; !love K ;
mLOV_OBL ;



LEXICON mLOV_OBL
+Num+Sg+Ill+Err/Orth:%>lová%>n K ; ! lován K ;
+Num+Sg+Ill:%>lohá%>n K ; ! kyevtlohán K ; kyevtlohán
+Num+Sg+Loc:%>lovve%>est K ; ! lovveest K ; kyevtlovveest
+Num+Sg+Abe:%>love%>ttáá K ; ! lovettáá K ; kyevtlovettáá
+Num+Sg+Com:%>áinluvvi%>jn K ; !   luvvijn kuovttijnluvvijn
+Num+Ess:%>lovve%>en  K ; ! lovveen	 K ; kyehtinlovveen
+Num+Pl+Nom:e%>hlove%>h K ; !loveh K ; kyevtihloveh
+Num+Pl+Acc:i%>idluuvi%>jd K ; !luuvijd K ; kuovtijdluuvijd
+Num+Pl+Gen:i%>iluvi%>j K ; !luuvij K ;
+Num+Pl+Gen:i%>iluvi%>j K ; !luvij K ; kuovtijluvij
+Num+Pl+Ill:á%>idluvvi%>jd K ; !luvvijd K ; kuovttijdluvvijd
+Num+Pl+Loc:i%>inluuvi%>jn K ; !luuvijn K ; kuovtijnluuvijn
+Num+Pl+Com:i%>iluvi%>jguin K ; !luvijguin K ; kuovtijluvijguin
+Num+Pl+Abe:i%>iluvi%>jttáá K ; !luvijttáá K ; kuovtijluvijttáá

LEXICON NUBALOH 
+Num+Sg+Nom+Use/NG:lov K ; 
+Num+Sg+Nom:loh K ; 
+Num+Sg+Acc+Use/NG:lov K ; 
+Num+Sg+Acc:loh K ; 
+Num+Sg+Gen+Use/NG:lov K ; 
+Num+Sg+Gen:loh K ; 
LOVE-COMMON ;

LEXICON LOVE 
+Num+Sg+Nom:love K ; !love 
+Num+Sg+Acc:love K ; !love
+Num+Sg+Gen:love K ; !love K ;
+Num+Sg+Ill+Attr:love K ; !love K ;
+Num+Sg+Loc+Attr:love K ; !love K ;
+Num+Sg+Abe+Attr:love K ; !love K ;
LOVE-COMMON ;

LEXICON LOVE-COMMON
+Num+Sg+Ill:lohá%>n K ; ! kyevtlohán K ; kyevtlohán
+Num+Sg+Loc:lovve%>est K ; ! lovveest K ; kyevtlovveest
+Num+Sg+Abe:love%>ttáá K ; ! lovettáá K ; kyevtlovettáá
+Num+Sg+Com:luvvi%>jn K ; !   luvvijn kuovttijnluvvijn
+Num+Ess:lovve%>en  K ; ! lovveen	 K ; kyehtinlovveen
+Num+Pl+Nom:love%>h K ; !loveh K ; kyevtihloveh
+Num+Pl+Acc:luuvi%>jd K ; !luuvijd K ; kuovtijdluuvijd
+Num+Pl+Gen:luuvi%>j K ; !luuvij K ;
+Num+Pl+Gen:luvi%>j K ; !luvij K ; kuovtijluvij
+Num+Pl+Ill:luvvi%>jd K ; !luvvijd K ; kuovttijdluvvijd
+Num+Pl+Loc:luuvi%>jn K ; !luuvijn K ; kuovtijnluuvijn
+Num+Pl+Com:luvi%>jguin K ; !luvijguin K ; kuovtijluvijguin
+Num+Pl+Abe:luvi%>jttáá K ; !luvijttáá K ; kuovtijluvijttáá

LEXICON CYETI
+Num+Sg+Nom: K ;                 ! kyeli
+Num+Sg+Acc: K ;         ! kyele
+Num+Sg+Gen:e^WG^RVSH K ;         ! kyele
+Num+Sg+Com:^WG%>in K ;         ! kuolijn
+Num+Pl+Ill:^WG%>id K ; 				! kuolijd
+Num+Sg+Ill:^EA^RLEN%>n K ;     ! kuálán
+Num+Sg+Loc:e^WG^RVSH%>est K ;   ! kyeleest
+Num+Sg+Abe:e^WG^RVSH%>ttáá K ;  ! kyelettáá
+Num+Ess:^CLEN%>n K ;     ! kyellin
+Num+Pl+Nom:e^WG^RVSH%>h K ; 				! kyeleh
+Num+Pl+Gen:^WG^RVSH%>i K ; 				! kuolij
+Num+Pl+Gen:^WG%>i K ; 					! kuolij
+Num+Pl+Acc:^WG%>id K ; 					! kuolijd
+Num+Pl+Loc:^WG%>in K ; 					! kuolijn
+Num+Pl+Com:^WG^RVSH%>iguin K ;	 		! kuolijguin
+Num+Pl+Com+Err/Orth:^WG^RVSH%>igijn K ;	 		! kuolijguin
+Num+Pl+Abe:^WG^RVSH%>ittáá K ;			! kuolijttáá
+Num+Cmp/SgGen:e^WG^RVSH%> NUM_DER ;

LEXICON MISCNUM 			
pelnub:pel#nub num ;
lakknub:lakk#nub num ;
pelnubčyet:pel#nub#čyet num ;
pelnubčyeđe:pel#nub#čyeđe num ;
maŋgâ:ma^RVŋgâ KULMA ;
maŋgâčyet:ma^RVŋgâ#čyet CYETI ;
muáddičyet:muáddi#čyet CYETI ;
maŋgâlov:ma^RVŋgâ# LOV ;
muáddilov:muáddi# LOV ;
muáddi:muáddi KYEHTI ;
miljovn+Num:miljovn X_LITTER ;
muáddituhháát+Num:muáddi#tuhháát X_POTTAAK ;

LEXICON ORDINAL		   
vuossâmuš+A+Superl:vuossâ MUSj-SUF ; !sme has not Ord for this
vuosmuš+A+Ord:vuos MUSj-SUF "ensimmäinen" ;	!¢vuosmuin, vuosmust, vuosmui, vuosmuš   !MUSj-SUF gives vuosmuust
vuosmuš+A+Ord+Err/Orth:vuoss MUSj-SUF "ensimmäinen" ;	!
nubbe+A+Ord:nu^RVbbe  nubbecase ;		   
kuálmád+A+Ord:kuálmád ORDINALInfl ;	   
niäljád+A+Ord:niäljád ORDINALInfl ;	   
viiđâd+A+Ord:viiđâd ORDINALInfl ;		   
kuuđâd+A+Ord:kuuđâd ORDINALInfl ;		   
čiččâd+A+Ord:čičč5âd CICCADInfl ;	   
káávcád+A+Ord:káávcád ORDINALInfl ;	   
oovcád+A+Ord:oovcád ORDINALInfl ;	
kallaad+A+Ord:kallaad X_EEMEED ;
   

LEXICON ORDINALInfl
 X_PAAVIR_not_Nom_Par ;
+Sg+Nom:^WG^RLEN K ;	! päävir
+Attr: # ;
 ORD-SAS ;
 
LEXICON CICCADInfl
 X_LITTER_not_Par ;
 ORD-SAS ;

LEXICON ORD-SAS
+Cmp#luokkâ:»»»#luokkal DER-SAS ;

LEXICON nubbecase
X_ALGE ;
+Sg+Ill+Attr:^WG K ; 					! nube
+Sg+Loc+Attr:^WG K ; 					! nube

 ORD-SAS ;


LEXICON num-imprecise
kalle  num-imprecise-case ;

LEXICON num-imprecise-case
+Num+Sg+Nom: K ;
+Num+Sg+Gen: K ;
+Num+Sg+Acc: K ;
+Num+Sg+Loc+Attr: # ; ! Kalle visteest
+Num+Sg+Ill+Attr: # ;

!+Sg+Nom:nubbe K ;
!+Sg+Acc:nube K ;
!+Sg+Gen:nube K ;
!+Sg+Ill:nuubán K ;
!+Sg+Loc:nubbeest K ;
!+Sg+Com:nubbijn K ;
!+Sg+Abe:nubettáá K ;
!+Ess:nubben K ;
!!+Par:nubbed K ; ! XXX probably not-existing
!+Pl+Nom:nubeh K ;
!+Pl+Acc:nuubijd K ;
!+Pl+Gen:nuubij K ;
!+Pl+Gen+Use/NG:nubij K ;
!+Pl+Ill:nubbijd K ;
!+Pl+Loc:nuubijn K ;
!+Pl+Com:nubijguin K ;
!+Pl+Abe:nubijttáá K ;
 
!From the original numeral file
!kävcinubáloh num ;
!kuávttáá:kuávˊttáá num "kahden" ;
!lovvááh num "kymmenet" ;
!čuáđáád:čuáđáád num "sadas" ;
!nubáloh num "toistakymmentä" ;

! Arabic numerals
! ---------------
LEXICON NUM-PREFIXES   !!= * __@CODE@__
    §:§      ARABIC ; ! §24
   §§:§§     ARABIC ; ! §§24
  §% :§%     ARABIC ; ! § 24
 §§% :§§%    ARABIC ; ! §§ 24
   %-:%-     ARABIC ; ! -24
   %‒:%‒     ARABIC ; ! -24 U+2012
   %–:%–     ARABIC ; ! -24 U+2013
   %—:%—     ARABIC ; ! -24 U+2014
   %―:%―     ARABIC ; ! -24 U+2015
   %+:%+     ARABIC ; ! <plus>24
 %-% :%-%    ARABIC ; ! - 24
 %‒% :%‒%    ARABIC ; ! - 24 U+2012
 %–% :%–%    ARABIC ; ! - 24 U+2013
 %—% :%—%    ARABIC ; ! - 24 U+2014
 %―% :%―%    ARABIC ; ! - 24 U+2015
 %+% :%+%    ARABIC ; ! <plus> 24
   %*:%*     ARABIC ; ! *24
   %$:%$     ARABIC ; ! $24
   %€:%€     ARABIC ; ! €24
   %<:%[%<%] ARABIC ; ! <24
   %>:%[%>%] ARABIC ; ! >24
  s%.:s%.    ARABIC ; ! s.24


LEXICON ARABIC   !!= * __@CODE@__ arabic numerals
< [1|2|3|4|5|6|7|8|9|%0] > ARABICS      ;
                           ARABICYEAR ;

LEXICON ARABICS
 ARABICLOOP      ;
 ARABICLOOPORD   ;
 ARABICLOOPCOLL  ;
 ARABICLOOPphone ;

LEXICON ARABICYEAR
< [1|2] [1|2|3|4|5|6|7|8|9|%0] [1|2|3|4|5|6|7|8|9|%0] [1|2|3|4|5|6|7|8|9|%0] > numyear ;

LEXICON numyear
 yeartag ;
 %/:%/ moreyear ;
 %-:%- moreyear ;

LEXICON moreyear 
< [1|2|3|4|5|6|7|8|9|%0] [1|2|3|4|5|6|7|8|9|%0] > yeartagpl ;

LEXICON yeartag
+Num+Sem/Year:  yearcase ;

LEXICON yeartagpl
 +Num+Sem/Year+Pl+Nom:       #      ;
 +Num+Sem/Year+Pl+Gen:       #      ;
 +Num+Sem/Year+Pl+Acc:       #      ;


LEXICON yearcase
 +Sg+Nom:       #      ;
 +Sg+Gen:       #      ;
 +Sg+Acc:       #      ;
 +Sg+Loc:%:st       #      ;
 +Sg+Ill:%:n       #      ;
 +Sg+Ill+Attr:  #      ;
 +Sg+Loc+Attr:  #      ;
 +Ess: # ;
 +Par: # ;
 +Cmp/SplitR+Cmp:%-    ENDLEX ;
 +Cmp/SplitR+Cmp:%–    ENDLEX ;

LEXICON ARABICLOOPphone   !!= * __@CODE@__ +358(0)16671254
< [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOPphone ;
!  :              ARABICLOOPphone ; ! Infinite loop on the surface side
  (:(             ARABICLOOPphone ;
  ):)             ARABICLOOPphone ;
 %—:%—            ARABICLOOPphone ; 
                           ARABICCASEphone ;

LEXICON ARABICCASEphone   !!= * __@CODE@__  
 +Num+Arab+Sg+Nom: # ;
 +Num+Arab+Sg+Acc: # ;
 +Num+Arab+Sg+Gen: # ;

LEXICON ARABICLOOP   !!= * __@CODE@__
         :         ARABIC ;
       %-:%-       ARABIC ; ! 1-2 multipart numbers
       %–:%–       ARABIC ; ! 1–2 multipart numbers
       %—:%—       ARABIC ; ! 1—2 multipart numbers
     %.%-:%.%-     ARABIC ; ! 1.-2 multipart numbers ! !
     %.%–:%.%–     ARABIC ; ! 1.–2 multipart numbers ! !
     %.%—:%.%—     ARABIC ; ! 1.—2 multipart numbers ! !
     %-% :%-%      ARABIC ; ! 1- 2 multipart numbers
     %–% :%–%      ARABIC ; ! 1– 2 multipart numbers
     %—% :%—%      ARABIC ; ! 1— 2 multipart numbers
     % %-:% %-     ARABIC ; ! 1 -2 multipart numbers
     % %–:% %–     ARABIC ; ! 1 –2 multipart numbers
     % %—:% %—     ARABIC ; ! 1 —2 multipart numbers
   % %-% :% %-%    ARABIC ; ! 1 - 2 multipart numbers
   % %–% :% %–%    ARABIC ; ! 1 – 2 multipart numbers
   % %—% :% %—%    ARABIC ; ! 1 — 2 multipart numbers
 % %-%-% :% %-%-%  ARABIC ; ! 1 -- 2 multipart numbers
 % %–%–% :% %–%–%  ARABIC ; ! 1 –– 2 multipart numbers
 % %—%—% :% %—%—%  ARABIC ; ! 1 —— 2 multipart numbers
       %.:%.       ARABIC ;
       %::%:       ARABIC ;
       %,:%,       ARABIC ;
       % :%        ARABIC ; ! Allowing for "23 500".
         :         ARABIC ; ! Allowing for "23 500" w/nbsp (init char = alt-space)
       %/:%/       ARABIC ; ! 24/12
     %/% :%/%      ARABIC ; ! 24/ 12
     % %/:% %/     ARABIC ; ! 24 /12
        ':'       ARABIC ; ! Since ' denotes cons.grad. This is not optimal either.

     +Num:         RNum   ; ! Num Cmp Noun, with hyphen

         ,%-+Num:,%-    ARABICCASES     ; ! 10,-
,%-+Num+Err/Orth:.%-    ARABICCASES     ; ! 10.- It is wrong, but written.
   % %-+Err/Orth:% %-   ARABICDELIMITER ; ! 1 - multipart numbers - "1 -:s"?
 % %-%-+Err/Orth:% %-%- ARABICDELIMITER ; ! 1 -- multipart numbers
              %%:%%     ARABICDELIMITER ; ! 50%. It is wrong, but 75 % of us write "75%".
            % %%:% %%   ARABICDELIMITER ; ! the correct ones as well... 50 % etc.
                                 ARABICDELIMITER ; ! list of number-case delimiters

LEXICON ARABICLOOPORD   !!= * __@CODE@__ ordinals
 +A+Ord+Attr:%. # ;
 +A+Ord: ARABICDELIMITERORD ;
 < [a|b|c|d|e|f|f %  f] > ACASETAG ;

LEXICON ACASETAG   !!= * __@CODE@__  is for 1e+Num+Sg+Num:1e
+Num: ARABICCASES ;

LEXICON ARABICLOOPCOLL   !!= * __@CODE@__ collectives
 +N+Coll: ARABICDELIMITERCOLL ;


LEXICON ARABICDELIMITER   !!= * __@CODE@__  blank + 3 delim, what does the lg counc prefer?
 +Num:            ARABICCASE0      ; ! This is the 1984 case, ie NGA
 +Num:%:          ARABICCASE       ; ! This is the 1984:s case.
 +Num+Err/Orth:   ARABICCASE       ; ! This is the 1984s case.
 +Num+Err/Orth:' ARABICCASE       ; ! This is the 1984's case.
 +Num+Err/Orth:’  ARABICCASE       ; ! optio-B07 = ’
 +Num+Err/Orth:%- ARABICCASE       ; ! This is the 1984-s case.
 +Num+Err/Orth:´  ARABICCASE       ; ! This is the 1984´s case.
 +Num:            ARAB_DER ; ! 6-jáhkasaš, 
 +Num: Rhyph ; ! 3-


LEXICON ARABICDELIMITERORD   !!= * __@CODE@__ ordinals
             ARABICCASE0   ; ! This is the 1984 case, ie NGA
!          :%: ARABICCASEORD ; ! This is the 1984:s case.
! +Err/Orth:   ARABICCASEORD ; ! This is the 1984s case.
! +Err/Orth:' ARABICCASEORD ; ! This is the 1984's case.
! +Err/Orth:%- ARABICCASEORD ; ! The 1984-s case.

LEXICON ARABICDELIMITERCOLL   !!= * __@CODE@__ collectives
!             ARABICCASE0    ; ! The 1984 case, ie NGA
          :%: ARABICCASECOLL ; ! The 1984:s case.
 +Err/Orth:   ARABICCASECOLL ; ! The 1984s case.
 +Err/Orth:' ARABICCASECOLL ; ! The 1984's case.
 +Err/Orth:%- ARABICCASECOLL ; ! The 1984-s case.

LEXICON ARABICCASES   !!= * __@CODE@__  to distinguish between 0 and oblique
ARABICCASE0 ;
ARABICCASE ;

LEXICON ARABICCASE0   !!= * __@CODE@__
 +Arab+Sg+Nom:      #      ;
 +Arab+Sg+Gen:      #      ;
 +Arab+Sg+Acc:      #      ;
 +Arab+Sg+Ill+Attr: #      ;
 +Arab+Sg+Loc+Attr: #      ;
 +Arab+Cmp/SplitR+Cmp:%-    ENDLEX ;
 +Arab+Cmp/SplitR+Cmp:%–    ENDLEX ;


LEXICON ARABICCASE
!  f+Sg+Nom+Err/Orth:f  # ; ! s. 123f.
! ff+Sg+Nom+Err/Orth:ff # ; ! s. 123ff.
!  b+Sg+Nom+Err/Orth:b  # ; ! § 123b
!  c+Sg+Nom+Err/Orth:c  # ; ! § 123c
!  d+Sg+Nom+Err/Orth:d  # ; ! § 123d
!  e+Sg+Nom+Err/Orth:e  # ; ! § 123e
 +Arab+Sg+Ill:%>ân        # ;
 +Arab+Sg+Loc:%>st        # ;
 +Arab+Sg+Com:%>áin       # ;
 +Arab+Ess:%>n        # ;
 +Arab+Par:%>d        # ;
 +Arab+Pl+Gen:%>i   K ; !
 +Arab+Pl+Acc:%>id  K ; !
 +Arab+Pl+Ill:%>id K ; ! 100:iide čuđiide
 +Arab+Pl+Loc:%>in  K ; !
 +Arab+Pl+Com:%>iguin   K ; !
!ordinals

!LEXICON ARABICCASEORD
! +Arab+Sg+Gen:%>da    K ;
! +Arab+Sg+Acc:%>dav   K ;
! +Arab+Sg+Ill:%>dij   K ;
! +Arab+Sg+Com:%>dijn  K ;
! +Arab+Sg+Ess:%>din   K ;
! +Arab+Pl+Nom:%>da    K ;
! +Arab+Pl+Gen:%>dij   K ;
! +Arab+Pl+Acc:%>dijt  K ;
! +Arab+Pl+Ill:%>dijda K ;
! +Arab+Pl+Com:%>dij   K ;

LEXICON ARABICCASECOLL
 +Arab+Sg+Nom:%>s   K ;
! +Arab+Sg+Gen:%>sa   K ;
! +Arab+Sg+Acc:%>sav  K ;
! +Arab+Sg+Ill:%>sij  K ;
! +Arab+Sg+Loc:%>sin  K ;
! +Arab+Sg+Loc:%>sis  K ;
! +Arab+Sg+Com:%>sijn K ;

! The Roman numerals !
! ------------------ !

LEXICON ROMAN						!^C^
 : ROM-THOUSAND ;						!
 : ROM-HUNDRED ;						!
 : ROM-TEN ;							!
 : ROM-ONE ;							!

LEXICON ROM-THOUSAND              	!^C^
 M:M ROM-THOUSAND-TAG;				!
 MM:MM ROM-THOUSAND-TAG;				!
 MMM:MMM ROM-THOUSAND-TAG;				!
 MMMM:MMMM ROM-THOUSAND-TAG;			!
 MMMMM:MMMMM ROM-THOUSAND-TAG;			!

LEXICON ROM-THOUSAND-TAG			!^C^
 : ROMNUMTAG ;						!
 : ROM-HUNDRED ;						!
 : ROM-SPLIT	;						!

LEXICON ROM-HUNDRED					!^C^
 C:C ROM-HUNDRED-TAG;				!
 CC:CC ROM-HUNDRED-TAG;				!
 CCC:CCC ROM-HUNDRED-TAG;				!
 CD:CD ROM-HUNDRED-TAG;				!
 D:D ROM-HUNDRED-TAG;				!
 DC:DC ROM-HUNDRED-TAG;				!
 DCC:DCC ROM-HUNDRED-TAG;				!
 DCCC:DCCC ROM-HUNDRED-TAG;				!
 CM:CM ROM-HUNDRED-TAG;				!

LEXICON ROM-HUNDRED-TAG				!^C^
 : ROMNUMTAG ;						!
 : ROM-TEN ;							!
 : ROM-SPLIT ;						!

LEXICON ROM-TEN						!^C^
 X:X ROM-TEN-TAG;					!
 XX:XX ROM-TEN-TAG;					!
 XXX:XXX ROM-TEN-TAG;					!
 XL:XL ROM-TEN-TAG;					!
 L:L ROM-TEN-TAG;					!
 LX:LX ROM-TEN-TAG;					!
 LXX:LXX ROM-TEN-TAG;					!
 LXXX:LXXX ROM-TEN-TAG;					!
 XC:XC ROM-TEN-TAG;					!

LEXICON ROM-TEN-TAG					!^C^
 : ROMNUMTAG ;						!
 : ROM-ONE ;							!
 : ROM-SPLIT ;						!

LEXICON ROM-ONE						!^C^
 I:I ROM-ONE-TAG;					!
 II:II ROM-ONE-TAG;					!
 III:III ROM-ONE-TAG;					!
 IV:IV ROM-ONE-TAG;					!
 V:V ROM-ONE-TAG;					!
 VI:VI ROM-ONE-TAG;					!
 VII:VII ROM-ONE-TAG;					!
 VIII:VIII ROM-ONE-TAG;					!
 IX:IX ROM-ONE-TAG;					!

LEXICON ROM-ONE-TAG					!^C^
 : ROMNUMTAG ;						!
! +N: ROMNUMTAG ;					!^C^ !The Olav viđeš fix (Roman V now gets A)
%-: ROM-SPLIT ;						!

! Here, we split the Roman numerals, in order to account for cases like "Kapihtal II-IV".
! We may send this first part directly to ROM-TAG below (and get the +Num tag), or we may
! send them through a second loop, identical to the first one, but marked wit "2" (the lexica
! are called 2ROMAN, etc. We do this instead of making a loop, since we do not want cases
! like "II-IV-VI-VII-IX". If that should turn out to be a good idea, a loop would do the 
! trick.

LEXICON ROM-SPLIT						!^C^
 :%- 2ROMAN ; ! II-VI, etc.				!


! Here goes loop 2.

LEXICON 2ROMAN							!^C^
 : 2ROM-THOUSAND ;						!
 : 2ROM-HUNDRED ;							!
 : 2ROM-TEN ;								!
 : 2ROM-ONE ;								!

LEXICON 2ROM-THOUSAND              		!^C^
 M:M 2ROM-THOUSAND-TAG;					!
 MM:MM 2ROM-THOUSAND-TAG;					!
 MMM:MMM 2ROM-THOUSAND-TAG;				!
 MMMM:MMMM 2ROM-THOUSAND-TAG;				!
 MMMMM:MMMMM 2ROM-THOUSAND-TAG;				!

LEXICON 2ROM-THOUSAND-TAG				!^C^
 : ROMNUMTAG ;							!
 : 2ROM-HUNDRED ;							!

LEXICON 2ROM-HUNDRED					!^C^
 C:C 2ROM-HUNDRED-TAG;					!
 CC:CC 2ROM-HUNDRED-TAG;					!
 CCC:CCC 2ROM-HUNDRED-TAG;					!
 CD:CD 2ROM-HUNDRED-TAG;					!
 D:D 2ROM-HUNDRED-TAG;					!
 DC:DC 2ROM-HUNDRED-TAG;					!
 DCC:DCC 2ROM-HUNDRED-TAG;					!
 DCCC:DCCC 2ROM-HUNDRED-TAG;				!
 CM:CM 2ROM-HUNDRED-TAG;					!

LEXICON 2ROM-HUNDRED-TAG				!^C^
 : ROMNUMTAG ;							!
 : 2ROM-TEN ;								!

LEXICON 2ROM-TEN						!^C^
 X:X 2ROM-TEN-TAG;						!
 XX:XX 2ROM-TEN-TAG;						!
 XXX:XXX 2ROM-TEN-TAG;						!
 XL:XL 2ROM-TEN-TAG;						!
 L:L 2ROM-TEN-TAG;						!
 LX:LX 2ROM-TEN-TAG;						!
 LXX:LXX 2ROM-TEN-TAG;						!
 LXXX:LXXX 2ROM-TEN-TAG;					!
 XC:XC 2ROM-TEN-TAG;						!

LEXICON 2ROM-TEN-TAG					!^C^
 : ROMNUMTAG ;							!
 : 2ROM-ONE ;								!

LEXICON 2ROM-ONE						!^C^
 I:I 2ROM-ONE-TAG;						!
 II:II 2ROM-ONE-TAG;						!
 III:III 2ROM-ONE-TAG;						!
 IV:IV 2ROM-ONE-TAG;						!
 V:V 2ROM-ONE-TAG;						!
 VI:VI 2ROM-ONE-TAG;						!
 VII:VII 2ROM-ONE-TAG;						!
 VIII:VIII 2ROM-ONE-TAG;					!
 IX:IX 2ROM-ONE-TAG;						!

LEXICON 2ROM-ONE-TAG
 ROMNUMTAG ;
 
LEXICON ROMNUMTAG
 +Num+Rom+Sg+Nom: # ;
 +Num+Rom+Sg+Gen: # ;
!+Num+Acc: # ;
 +A+Rom+Ord+Sg:%. # ; !III. as ordinal.
 +A+Rom+Ord+Sg:   # ; !Looking for trouble...
 +Num:%: ROMNUMTAGOBL  ;
 +Num:%' ROMNUMTAGOBL ;
 +Num:%- ROMNUMTAGOBL  ;
 +Num:%´ ROMNUMTAGOBL  ;
! +Num:   ROMNUMTAGOBL ;

LEXICON ROMNUMTAGOBL
 +Rom+Sg+Ill:%>hán        # ;
 +Rom+Sg+Ill:%>n        # ;
 +Rom+Sg+Loc:%>st        # ;
 +Rom+Sg+Com:%>jn       # ;
 +Rom+Ess:%>n        # ;
 +Rom+Sg+Par:%>d        # ;

LEXICON ARAB_DER ! only for arabics
 +Cmp-#nummeer:»»»-nummeers DER-SAS ;
 +Cmp-#sijđo:»»»-sijđos DER-SAS ;
 +Cmp-#suormâráigá:»»»-suormâráigás DER-SAS ;
 +Cmp-#stuáru:»»»-stuárus DER-SAS ;
 +Cmp-#sijđo:»»»-sijđos DER-SAS ;
 +Cmp-#kiärdá:»»»-kiärdás DER-SAS ;
 +Cmp-#kilu:»»»-kilus DER-SAS ;
 +Cmp-#ihá:»»»-ihás DER-SAS ;
 +Cmp-#sentti:»»»-senttis DER-SAS ;
 +Der+Der/ahasas:-ahas ANOLASJ  ;
 +Cmp-#:»»»- CMP_NAMAG ;
 +Cmp-#:»»»- CMP_IHASAS ;


LEXICON NUM_DER ! 
! +Der+Der/ihasas:ihás ANOLASJ  ;
+Cmp#:»»»# CMP_NAMAG ;
+Cmp#:»»»# CMP_IHASAS ;
 

! A final section with some isolated numeral expresssions !
! ------------------------------------------------------- !

      
LEXICON ISOLATED-NUMEXP					!^C^
 ½+Num:½ # ;						!
 ¹+Num:¹ # ;						!
 ²+Num:² # ;						!
 ³+Num:³ # ;						!
 ¼+Num:¼ # ;						!





