! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

!! !!!Pronouns for Inari Saami
!     ========

! Pointing to all the pronominal subgroups

LEXICON Pronoun !!= * __@CODE@__
Personal ;
Demonstrative ;
Reflexive ;
! Reciprocal ;
Interrogative ;
! Relative ;
Indefinite ;


!! !!Personal pronouns
! -----------------

LEXICON Personal !!= * __@CODE@__
mun+Pron+Pers+Sg1:mu perspronsg ;
tun+Pron+Pers+Sg2:tu perspronsg ;
sun+Pron+Pers+Sg3:su perspronsg ;
mun+Pron+Pers+Du1:mu persprondu ;
tun+Pron+Pers+Du2:tu persprondu ;
sun+Pron+Pers+Du3:su persprondu ;
mun+Pron+Pers+Pl1:mi perspronpl ;
tun+Pron+Pers+Pl2:ti perspronpl ;
sun+Pron+Pers+Pl3:si perspronpl ;


LEXICON perspronsg !!= * __@CODE@__
+Nom:n	K ;
+Gen:u	      K ;
+Acc:u	      K ;
+Ill:njin	      K ;
+Loc:st	          K ;
+Loc:ste	      K ;
+Com:in	          K ;
+Com:uin	      K ;
+Abe:uttáá	      K ;
+Ess:nen	      K ;
+Par:đe	      K ;

LEXICON persprondu !!= * __@CODE@__
+Nom:oi	K ;
+Nom:ái	K ;
+Gen:nnuu	      K ;
+Acc:nnuu	      K ;
+Ill:nnui	      K ;
+Loc:nnust	      K ;
+Com:nnuin	      K ;
+Com:nnuuin	      K ; !^NG^
+Abe:nnuuttáá      K ;
+Ess:nnun	      K ;
+Par:nnud	      K ;

LEXICON perspronpl !!= * __@CODE@__
+Nom:j		K ;
+Gen:i	      K ;
+Acc:j	      K ;
+Ill:jjân	      K ;
+Loc:st	      K ;
+Loc:ste	      K ;
+Com:iguin	      K ;
+Abe:ittáá	      K ;
+Ess:nen	      K ;
+Par:đe	      K ;



!! !! Demonstrative pronouns
! ----------------------

LEXICON Demonstrative !!= * __@CODE@__
tot+Pron+Dem:	TOT ;
tuot+Pron+Dem:	TUOT ;
tot+Pron+Dem+Sg+Ill:toos K ;
tuot+Pron+Dem+Sg+Ill:tuos K ;
!tot+Pron+Dem+Pl+Ill:tooid K ;
taat+Pron+Dem: TAAT ;

LEXICON TOT !!= * __@CODE@__
:to	TO-TUO ;
:ta	TA ;

LEXICON TUOT !!= * __@CODE@__
:tuo	TO-TUO ;
:tye	TYE ;


LEXICON TO-TUO !!= * __@CODE@__
+Sg+Nom:t	K ;
+Sg+Gen:n	K ;
+Sg+Acc:m	K ;
+Sg+Com:oin	K ;
+Sg+Com:in K ;
+Sg+Abe:nttáá	K ;
+Pl+Nom:h	K ;
+Pl+Gen:i	K ;
+Pl+Ill:id	K ;
+Pl+Ill:oid	K ;
+Pl+Com:iguin	K ;
+Pl+Abe:ittáá	K ;

LEXICON TA !!= * __@CODE@__
+Sg+Loc:st	K ;
+Ess:nen	K ;
+Par:đe	K ;
+Pl+Acc:id	K ;
+Pl+Loc:in	K ;

LEXICON TYE !!= * __@CODE@__
+Sg+Loc:st	K ;
+Sg+Ess:nen	K ;
+Sg+Par:đe	K ;
+Pl+Acc:id	K ;
+Pl+Loc:in	K ;


LEXICON TAAT
+Sg+Nom:taat	K ;
+Sg+Gen:taan	K ;
+Sg+Acc:taam	K ;
+Sg+Ill:taas K ;
+Sg+Loc:täst K ;
+Sg+Com:tain	K ;
+Sg+Abe:taanttáá	K ;
+Ess:tänen K ;
+Par:täđe K ;
+Pl+Nom:taah	K ;
+Pl+Gen:tai	K ;
+Pl+Acc:täid K ;
+Pl+Ill:taid	K ;
+Pl+Loc:täin K ;
+Pl+Com:taiguin	K ;
+Pl+Abe:taittáá	K ;



!! !!Reflexive pronouns
! ------------------

LEXICON Reflexive !!= * __@CODE@__
jieš+Pron+Refl+Sg+Nom:jieš K ; 

jieš+Pron+Refl+Sg+Gen+PxSg1:jieččân K ; 
jieš+Pron+Refl+Sg+Acc+PxSg1:jieččân K ; 
jieš+Pron+Refl+Sg+Ill+PxSg1:olssân K ; 
jieš+Pron+Refl+Sg+Loc+PxSg1:alnaan K ; 

jieš+Pron+Refl+Sg+Gen+PxSg2:jieijâd K ; 
jieš+Pron+Refl+Sg+Acc+PxSg2:jieijâd K ; 
jieš+Pron+Refl+Sg+Ill+PxSg2:olssâd K ; 
jieš+Pron+Refl+Sg+Loc+PxSg2:alnaad K ; 

jieš+Pron+Refl+Sg+Gen+PxSg3:jieijâs K ; 
jieš+Pron+Refl+Sg+Acc+PxSg3:jieijâs K ; 
jieš+Pron+Refl+Sg+Ill+PxPl3:olssis K ; 
jieš+Pron+Refl+Sg+Loc+PxPl3:alneestis K ; 

!! !!Interrogative pronouns
! ----------------------

LEXICON Interrogative !!= * __@CODE@__
mii+Pron+Interr:m	MK ;
mii+Pron+Interr:moo	MK-long ;
mii+Pron+Interr:mo	MK-short ;
mii+Pron+Interr:ma	MK-low ;
mii+Pron+Interr:ma	MK-front ;

kii+Pron+Interr:k	MK ;
kii+Pron+Interr:kiä	MK-long ;
kii+Pron+Interr:kiä	MK-short ;
kii+Pron+Interr:kiä	MK-low ;
kii+Pron+Interr:ke	MK-front ;

mii+Pron+Interr+Sg+Acc:maid    K ; ! The exceptions
mii+Pron+Interr+Sg+Acc:mon     K ;
mii+Pron+Interr+Sg+Com:mooin   K ;
mii+Pron+Interr+Pl+Acc:mooi    K ;
!mii+Pron+Interr+Pl+Com:mooiguim	 K ; ! already in MK-long
mii+Pron+Interr+Pl+Abe:mooittáá	 K ;

kii+Pron+Interr+Sg+Acc:kiäm    K ;
kii+Pron+Interr+Sg+Loc:kest    K ; 
kii+Pron+Interr+Sg+Loc:kiäst    K ; 
kii+Pron+Interr+Pl+Loc:kein    K ; 
kii+Pron+Interr+Pl+Loc:kiäin    K ; 

kote+Pron+Interr+Sg+Nom:kote K ;
kote+Pron+Interr+Sg+Acc:kođe K ;
kote+Pron+Interr+Sg+Loc:koođeest K ;
kote+Pron+Interr+Sg+Com:kuuđijn K ;



LEXICON MK !!= * __@CODE@__
+Sg+Nom:ii	K ;

LEXICON MK-long !!= * __@CODE@__
+Sg+Ill:s	K ;
+Pl+Ill:id K ;
+Pl+Com:iguin	K ;
+Pl+Abe:ittáá K ;

LEXICON MK-short !!= * __@CODE@__
+Sg+Gen:n	K ;
+Sg+Com:in	K ;
+Sg+Abe:nttáá	K ;
+Pl+Nom:h	K ;
+Pl+Gen:i	K ;
+Pl+Abe:ittáá	K ;

LEXICON MK-low !!= * __@CODE@__
+Sg+Loc:st	K ;
+Pl+Acc:id	K ;
+Pl+Loc:in	K ;

LEXICON MK-front !!= * __@CODE@__
+Ess:nen	K ;
+Par:đe	K ;
+Pl+Acc:id	K ;



!! !!Indefinite pronouns
! ----------------------

LEXICON Indefinite

eres iind ; ! iärrás%^ÁE
maggaar iind ;
tággáár iind ;
taggaar:taggaar iind "semmoinen" ;
mottoom iind "eräs" ;
kuus iind "johon" ;
jyehi iind "jokainen" ;
miinii iind "jokin" ;
mottoom iind "jokunen" ;
kuábbášnii iind "jompikumpi" ;
kost iind "jossa" ;
kost iind "josta" ;
puohnâssân iind "kaiken" ;
puoh iind "kaikki" ;
kii iind "kuka" ;
kihheen iind "kukaan" ;
kuábbáš iind "kumpi" ;
kuábbáškin iind "kumpikaan" ;
kuábbáš iind "kumpikin" ;
mađe iind "kuta" ;
mii iind "mikä" ;
maggaar iind "millainen" ;
kallaad iind "monesko" ;
maŋgâ iind "moni" ;
kalle iind "montako" ;
maacij iind "mutkin" ;
muáddi:muáddi iind "muutama" ;
siämmáš:siämmáš iind "sama" ;
maŋgâ iind "usea" ;
iänááš iind "useimmat" ;
maŋgii iind "usein" ;
kales:kallaas%^ÁE NAVER ; ! V X V	!&kallasáin, kallasist, kallasân, kallas
nubijdis:nubijdis iind "toisiaan" ;	!&nubijdisáin, nubijdisist, nubijdisân, nubijdis


LEXICON iind
 +Pron+Indef:%>0 K ;


