! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no




! ======================
!! !!!Sublexica for VerbRoot for Inari Saami
! ======================

!! !!Even-syllable stems
!    -------------------

LEXICON VUOLLAD  !!= * __@CODE@__
+V:%>â  VINF ;
+V: EVEN-I ;

!! !!Odd-syllable stems
! ------------------

LEXICON LEKKASID  !!= * __@CODE@__ SM51, no length change in stem
+V:       ODDVLEX ;


LEXICON CIELATTID  !!= * __@CODE@__ SM57, consonant change only
+V:%^ODDC ODDVLEX ; ! Dia to shorten C before C|#

LEXICON VYEJETTID  !!= * __@CODE@__ SM53. short V in Inf
+V:%^ODDSHV ODDVLEX ; ! Dia to shorten V 

LEXICON IALLAATTID  !!= * __@CODE@__ SM52, long V in Inf
+V:%^ODDLV ODDVLEX ;

LEXICON ODDVLEX  !!= * __@CODE@__
+Ind+Prs:%>â VSUF_SG1 ;
+Ind+Prs:%>â VSUF_SG2 ;
+Ind+Prs:    VSUF_SG3 ;
+Ind+Prs:    VSUF_DU1 ;
+Ind+Prs:    VSUF_DU2 ;
+Ind+Prs:    VSUF_DU3 ;
+Ind+Prs:    VSUF_PL1 ;
+Ind+Prs:%>á VSUF_PL2 ;
+Ind+Prs:%>je VSUF_PL3 ;
+Ind+Prt:%>i ODD-II ;
:%>i         VINF		   ;
:%>ijj       VPRSPRC	   ;
:%>â         VPRFPRCACT	   ;
:%>u         VPRFPRCPASS   ;
:            VNEGPRSINDIMP ;
:%>i         VNEGCOND	   ;
:%>i         VNEGPOT	   ;
             VABE		   ;
             VGER		   ;
             VGERII		   ;
             VGERIII	   ;
             VACTIO		   ;



!! !!Contracted stems
! ----------------


LEXICON COKKAD  !!= * __@CODE@__
+V:%>  VINF ;
+V+Ind+Prs:%>á VSUF_SG1 ;
+V+Ind+Prs:%>á VSUF_SG2 ;
+V+Ind+Prs:%>á VSUF_SG3 ;
+V+Ind+Prs:%>jee VSUF_DU1 ;
+V+Ind+Prs:%>á VSUF_DU2 ;
+V+Ind+Prs: VSUF_DU3 ;
+V+Ind+Prs: VSUF_PL1 ;
+V+Ind+Prs:%>á VSUF_PL2 ;
+V+Ind+Prs:%je VSUF_PL3 ;


LEXICON PALLAD !!= * __@CODE@__

+V:  VINF ;
+V+Ind+Prs:%>a VSUF_SG1 ;
+V+Ind+Prs:%>a VSUF_SG2 ;
+V+Ind+Prs:%>a VSUF_SG3 ;
+V+Ind+Prs:%>jee VSUF_DU1 ;
+V+Ind+Prs:%>a VSUF_DU2 ;
+V+Ind+Prs:%> VSUF_DU3 ;
+V+Ind+Prs:%> VSUF_PL1 ;
+V+Ind+Prs:%>a VSUF_PL2 ;
+V+Ind+Prs:%je VSUF_PL3 ;

LEXICON TUHHID !!= * __@CODE@__

+V:%>i  VINF ;
+V+Ind+Prs:%>ii VSUF_SG1 ;
+V+Ind+Prs:%>ii VSUF_SG2 ;
+V+Ind+Prs:%^VLOW%>ee VSUF_SG3 ;
+V+Ind+Prs:%^VLOW%>ejee VSUF_DU1 ;
+V+Ind+Prs:%>ii VSUF_DU2 ;
+V+Ind+Prs:%>i VSUF_DU3 ;
+V+Ind+Prs:%>i VSUF_PL1 ;
+V+Ind+Prs:%>ii VSUF_PL2 ;
+V+Ind+Prs:%^VLOW%>eje VSUF_PL3 ;

LEXICON RAMMUD !!= * __@CODE@__

+V:  VINF ;
+V+Ind+Prs:%>uu VSUF_SG1 ;
+V+Ind+Prs:%>uu VSUF_SG2 ;
+V+Ind+Prs:%^VLOW%>oo VSUF_SG3 ;
+V+Ind+Prs:%^VLOW%>ojee VSUF_DU1 ;
+V+Ind+Prs:%>uu VSUF_DU2 ;
+V+Ind+Prs:%>u VSUF_DU3 ;
+V+Ind+Prs:%>u VSUF_PL1 ;
+V+Ind+Prs:%>uu VSUF_PL2 ;
+V+Ind+Prs:%^VLOW%>oje VSUF_PL3 ;


!! !!Intermediate suffix lexica
! --------------------------

!! Nothing so far.

!! !!Final suffix lexica
!    -------------------


LEXICON VINF !!= * __@CODE@__
+Inf:đ K ;

LEXICON VPRSPRC !!= * __@CODE@__
+PrsPrc:ee K ;

LEXICON VPRFPRCACT !!= * __@CODE@__
+PrfPrc:m K ;

LEXICON VPRFPRCPASS !!= * __@CODE@__
+PrfPrc+Pass:m K ;

LEXICON VNEGPRSINDIMP !!= * __@CODE@__
+ConNeg+Ind+Prs: K ;
+ConNeg+Imp+Prs: K ;

LEXICON VNEGCOND !!= * __@CODE@__
+ConNeg+Cond:ččii K ;

LEXICON VNEGPOT !!= * __@CODE@__
+ConNeg+Pot:žžii K ;

LEXICON VABE !!= * __@CODE@__
+Abe:%>hánnáá K ;
+Abe:%>hinnáá K ;
+Abe:%>hennáá K ;

LEXICON VGER !!= * __@CODE@__
+Ger:%>dijn K ;
+Ger+PxSg1:%>dijnán K ;
+Ger+PxSg1:%>dijnân K ;
+Ger+PxSg2:%>dijnád K ;
+Ger+PxSg2:%>dijnâd K ;
+Ger+PxSg3:%>dijnes K ;

LEXICON VGERII !!= * __@CODE@__
+GerII:%>min K ;

LEXICON VGERIII !!= * __@CODE@__
+GerIII:%>mist K ;

LEXICON VACTIO !!= * __@CODE@__
+Actio:%>im K ;


!! !!Suffix series I
! - - - - - - - - 

LEXICON EVEN-I !!= * __@CODE@__
+Sg1:%>m K ;
+Sg2:%>h K ;
+Sg3:%>%^WG1 K ;
+Du1:%>een K ;
+Du2:%>vettee K ;
+Du3:%>v K ;
+Pl1:%>jm K ;
+Pl2:%>jd K ;
+Pl3:%>i K ;

LEXICON POT-I !!= * __@CODE@__
+Sg1:%>eem K ;
+Sg2:%>eeh K ;
+Sg3: K ;
+Du1:%>een K ;
+Du2:%>vettee K ;
+Du3:%>uv K ;
+Pl1:%>ep K ;
+Pl2:%>vetteđ K ;
+Pl3:%>eh K ;


!! !!Suffix series II
! - - - - - - - - 

LEXICON ODD-II !!= * __@CODE@__
+Sg1:%>m K ;
+Sg2:%>h K ;
+Sg3:%>j K ;
+Du1:%>jm K ;
+Du2:%>jd K ;
+Du3:%>jn K ;
+Pl1:%>jm K ;
+Pl2:%>jd K ;
+Pl3:%>i K ;

LEXICON ODD-II-DU !!= * __@CODE@__
+Du1:%>jm K ;
+Du2:%>jd K ;
+Du3:%>jn K ;

!! !!Person-number suffixes

LEXICON V_SG12 !!= * __@CODE@__
VSUF_SG2 ;
VSUF_SG2 ;


LEXICON V_DU !!= * __@CODE@__
+Du1:%>een K ;
+Du2:%>vettee K ;
+Du3:%>v K ;

LEXICON V_PL !!= * __@CODE@__
+Pl1:%>jm K ;
+Pl2:%>jd K ;
+Pl3:%>i K ;



!! !!Other series
! - - - - - - 


!! !Auxiliary verbs
! ---------------

LEXICON NEG !!= * __@CODE@__
+Prs+Sg1:jiem K ;
+Prs+Sg2:jieh K ;
+Prs+Sg3:ij K ;
+Prs+Du1:iän K ;
+Prs+Du1:eän K ;
+Prs+Du2:eppee K ;
+Prs+Du3:iävá K ;
+Prs+Du3:eävá K ;
+Prs+Pl1:ep K ;
+Prs+Pl2:eppeđ K ;
+Prs+Pl3:iä K ;
+Prs+Pl3:eävá K ;

+Imprt+Sg1:iällum  K ;
+Imprt+Sg1:eällum  K ;
+Imprt+Sg2:ele K ;
+Imprt+Sg3:iällus K ;
+Imprt+Sg3:eällus K ;
+Imprt+Du1:iälloon K ;
+Imprt+Du1:eälloon K ;
+Imprt+Du2:ellee K ;
+Imprt+Du3:iällus K ;
+Imprt+Du3:eällus K ;
+Imprt+Pl1:iällup K ;
+Imprt+Pl1:eällup K ;
+Imprt+Pl2:elleđ K ;
+Imprt+Pl3:iällus K ;
+Imprt+Pl3:eällus K ;

+Opt+Sg2:eälu K ;
+Opt+Sg2:iälu K ;
+Opt+Du2:eällu K ;
+Opt+Du2:iällu K ;
+Opt+Pl2:eälluđ K ;
+Opt+Pl2:iälluđ K ;

+Sup:amâs K ;
+Sup:emis K ;

LEXICON LEDE-COMMON !!= * __@CODE@__
+Inf:leđe K ;

! Present
+Prs+Sg1:lam K ;
+Prs+Sg2:lah K ;
+Prs+Sg3:lii K ;
+Prs+Du1:leen K ;
+Prs+Du1:laan K ;
+Prs+Du1:láán K ;
+Prs+Du2:leppee K ;
+Prs+Du3:lava K ;
+Prs+Du3:lává K ;
+Prs+Pl1:lep K ;
+Prs+Pl2:leppeđ K ;
+Prs+Pl3:laa K ;
+Prs+Pl3:láá K ;

! Past
+Prt+Sg1:lijjim K ;
+Prt+Sg2:lijjih K ;
+Prt+Sg3:lai K ;
+Prt+Sg3:lei K ;
+Prt+Du1:láim K ;
+Prt+Du1:lááim K ;
+Prt+Du1:leim K ;
+Prt+Du2:láid K ;
+Prt+Du2:lááid K ;
+Prt+Du2:leid K ;
+Prt+Du3:láin K ;
+Prt+Du3:lááin K ;
+Prt+Du3:lein K ;
+Prt+Pl1:leim K ;
+Prt+Pl2:leid K ;
+Prt+Pl3:lijjii K ;

! Conditional
+Cond+Prs:ličč ODD-II ;
+Cond+Prs:ličči ODD-II-DU ;

!Potential
+Pot+Prs:ležž POT-I ;

LEXICON LEDE !!= * __@CODE@__

+IV: LEDE-COMMON ;

! Person suffixes

LEXICON VSUF_SG1 !!= * __@CODE@__
+Sg1:%>m K ;

LEXICON VSUF_SG2 !!= * __@CODE@__
+Sg2:%>h K ;

LEXICON VSUF_SG3 !!= * __@CODE@__
+Sg3:%> K ;

LEXICON VSUF_DU1 !!= * __@CODE@__
+Du1:%>n K ;

LEXICON VSUF_DU2 !!= * __@CODE@__
+Du2:%>vettee K ;

LEXICON VSUF_DU3 !!= * __@CODE@__
+Du3:%>v K ;

LEXICON VSUF_PL1 !!= * __@CODE@__
+Pl1:%>p K ;

LEXICON VSUF_PL2 !!= * __@CODE@__
+Pl2:%>vetteđ K ;

LEXICON VSUF_PL3 !!= * __@CODE@__
+Pl3:%>h K ;

