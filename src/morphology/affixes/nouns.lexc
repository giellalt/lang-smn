! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no


! ==========================
!! !!!Sublexica for NounRoot
! ==========================

!! Order of triggers:
!! {{{
!! SV|ÁE|ÁI|EE  AO|EA|SC  FCD  WG|CSH  CLEN  VHIGH|VBACK  RVSH|RLEN  SLEN|SVSH  SVLOW
!! }}}


!! !!Table of content
!! * 0. Unclassified lexica
!! * 1. Even-syllable stems
!! ** Two-syllabic
!! ** Four-syllabic even stems
!! * 2. Odd-syllabic stems
!! * 3. Contract stems


!! !!!The lexica themselves


!! !!0. Unclassified lexica


LEXICON KOLME !!= @CODE@ ad hoc three syllable stem > 
3LITTER ; !!= @CODE@



LEXICON nounstems !!= @CODE@: these words should be classified > 
3LITTER ;  !!= @CODE@



!! !!1. Even-syllable stem lexica

LEXICON 2ALGA  !!= __@CODE@__ 2VADDU has CLEN for Sg+Com and Pl+Ill , rlen svlow, ess:- 
+N: X_ALGA  ; 
+N+Cmp/SgNom: R ; !áimupoccee
+N+Cmp/SgGen:%^WG%^RLEN%> R ; !


LEXICON X_ALGA  !!= __@CODE@__ 2VADDU has CLEN for Sg+Com and Pl+Ill , rlen svlow, ess:- 
+Sg+Com:%^WG%^CLEN%^SVLOW%>in  K ;			! algáin
+Par:%>d K ;						! algâd
!+Pl+Ill:%^WG%^CLEN%^SVLOW%>id K ; ! algáid
+Sg+Nom: K;				 		! sollâ
+Sg+Gen:%^WG%^RLEN K ;     		! aalgâ
+Sg+Acc:%^WG%^RLEN K ;     		! aalgâ
+Sg+Ill:%^CSH%^RLEN%>n K ;		! aalgân puuksân k4s removed CSH
+Sg+Loc:%^WG%^RLEN%>st K ; 		! aalgâst
+Sg+Abe:%^WG%>ttáá K ;		! algâttáá
+Sg+Gen:%^WG VPXsuff ;  ! 
+Sg+Acc:%^WG VPXsuff ;  ! 
+Sg+Com:%^WG%^CLEN%>in CPXsuff ;  ! viljâinân 
X_PUUVSAH ;

LEXICON 2PUUVSAH
+N+Der/Dimin: S_KAANDAS ; !kaandâš nieidâš
+N:%^WG%^RLEN%> Gen_DER ;
+N:%> Nom_DER ;
+N+Cmp/SgNom: R ; !
+N: X_PUUVSAH ;

LEXICON X_PUUVSAH
+Pl+Nom:%^WG%^RLEN%>h K ;  		! puuvsah
+Pl+Gen:%^WG%^RLEN%>i K ;			! puuvsâi 
+Pl+Gen+Use/NG:%^WG%>i K ;  		! puvsâi
+Pl+Acc:%^WG%^RLEN%>id K ; 		! puuvsâid
+Pl+Ill:%^WG%^CLEN%^SVLOW%>id K ; ! puvssáid
+Pl+Loc:%^WG%^RLEN%>in K ; 		! puuvsâin
+Pl+Com:%^WG%>iguin K ;		! puvsâiguin
+Pl+Com+Err/Orth:%^WG%>igijn K ;		! 
+Pl+Abe:%^WG%>ittáá K ;		! puvsâittáá
+Pl+Gen:%^WG%>id CPXsuff ;  !
+Pl+Acc:%^WG%>id CPXsuff ;  !
+Pl+Com:%^WG%>id CPXsuffCOM ;  !
+Ess:%>n K ;						! algân


LEXICON 2SOLLA  !!= __@CODE@__ SM1, RLEN, earlier LLEN !rlen svlow, sgcom/plill:rlen  ess:clen
+N+Sg+Com:%^WG%^RLEN%^SVLOW%>in K ; ! sooláin
+N+Ess:%^CLEN%>n K ;				! sollân
+N+Par:%^CLEN%>d K ;				! sollâd
+N+Pl+Ill:%^WG%^RLEN%^SVLOW%>id K ; ! sooláid
+N+Sg+Nom: K;				 		! sollâ
+N+Cmp/SgNom: R ; !áimupoccee
+N+Cmp/SgGen:%^WG%^RLEN%> R ; !
+N+Sg+Gen:%^WG%^RLEN K ;     		! soolâ
+N+Sg+Acc:%^WG%^RLEN K ;     		! soolâ
+N+Sg+Ill:%^CSH%^RLEN%>n K ;		! soolân
+N+Sg+Loc:%^WG%^RLEN%>st K ; 		! soolâst
+N+Sg+Abe:%^WG%>ttáá K ;		! solâttáá
+N+Pl+Nom:%^WG%^RLEN%>h K ;  		! soolah
+N+Pl+Gen:%^WG%^RLEN%>i K ;			! soolâi 
+N+Pl+Gen+Use/NG:%^WG%>i K ;  		! solâi
+N+Pl+Acc:%^WG%^RLEN%>id K ; 		! soolâid
+N+Pl+Loc:%^WG%^RLEN%>in K ; 		! soolâin
+N+Pl+Com:%^WG%>iguin K ;		! solâiguin
+N+Pl+Com+Err/Orth:%^WG%>igijn K ;		! 
+N+Pl+Abe:%^WG%>ittáá K ;		! solâittáá
+N+Sg+Gen:%^WG VPXsuff ;  ! 
+N+Sg+Acc:%^WG VPXsuff ;  ! 
+N+Sg+Com:%^WG%^CLEN%^SVLOW%>in CPXsuff ;  ! 
+N+Pl+Gen:%^WG%>id CPXsuff ;  !
+N+Pl+Acc:%^WG%>id CPXsuff ;  !
+N+Pl+Com:%^WG%>id CPXsuffCOM ;  !
+N+Der/Dimin: S_KAANDAS ; !kaandâš nieidâš
+N:%^WG%^RLEN%> Gen_DER ;
+N:%> Nom_DER ;


LEXICON 2KIEHA !!= __@CODE@__
+N: X_KIEHA ;
+N+Cmp/SgNom: R ;

LEXICON 2KIEHA_PL !!= __@CODE@__
+N:  X_KIEHA_PL ;
+N:  X_KIEHA_KIELA_PL ;

LEXICON X_KIEHA !!= __@CODE@__
+Sg+Com:%^WG%^CLEN%^SVLOW%>in K ; ! kievváin
+Par:%>d K ;						! kiehâd 
KIEHA_KIELA ;
X_KIEHA_PL ;

LEXICON X_KIEHA_PL
+Pl+Ill:%^WG%^CLEN%^SVLOW%>id K ; ! kievváid
+Ess:%>n K ;						! kiehân 

LEXICON 2KIELA !!= __@CODE@__ WG, blank Ill, DLEN, RVSH, SVLOW 
+N: X_KIELA ;
+N+Cmp/SgNom:%^RVSH%> R ;
+N+Cmp/SgGen:%^WG%> R ;
+N+Sg+Gen:%^WG VPXsuff ;  !sajes
+N+Sg+Acc:%^WG VPXsuff ;  !sajes
+N+Sg+Ill:%>s CPXsuff ;  ! sajasis ill kirkesvuotâsis
+N+Sg+Loc:%>st CPXsuff ;  !sajestis sajasis ill
+N+Sg+Com:%^WG%^SVLOW%>in CPXsuff ;  ! 
2KUOLAH ;

LEXICON 2KUOLAH
+N+Pl+Gen:%^WG%^RLEN%>id CPXsuff ;  !kieđâidis
+N+Pl+Acc:%^WG%^RLEN%>id CPXsuff ;  !kieđâidis
+N+Pl+Com:%^WG%^RLEN%>id CPXsuffCOM ;  !kieđâidiskuin
+N+Pl+Ill:%^WG%^SVLOW%>id K ; ! kieláid
+N:%^WG%> Gen_DER ;
+N:%> Nom_DER ;
+N:  X_KIEHA_KIELA_PL ;
+N+Ess:%^CLEN%>n K ;				! kiellân # puolipitkä > pitkä
+N: KIELA_PL ;

LEXICON X_KIELA !!= __@CODE@__ WG, blank Ill, DLEN, RVSH, SVLOW 
+Sg+Com:%^WG%^SVLOW%>in K ; 		! kieláin
+Par:%^CLEN%>d K ;				! kiellâd # puolipitkä > pitkä
+Ess:%^CLEN%>n K ;				! kiellân # puolipitkä > pitkä
KIEHA_KIELA ;
KIELA_PL ;

LEXICON KIELA_PL
+Pl+Ill:%^WG%^SVLOW%>id K ; ! kieláid


LEXICON KIEHA_KIELA !!= __@CODE@__
+Sg+Nom: K;				 		! kiehâ
+Sg+Gen:%^WG K ;     				! kievâ
+Sg+Acc:%^WG K ;     				! kievâ
+Sg+Ill:%^CSH%>n K ;					! kiehân
+Sg+Loc:%^WG%>st K ; 				! kievâst
+Sg+Abe:%^WG%^RVSH%>ttáá K ;		! kievâttáá
+Der/Dimin: S_KAANDAS ; !kieđâš
X_KIEHA_KIELA_PL ;

LEXICON X_KIEHA_KIELA_PL
+Pl+Nom:%^WG%>h K ;  				! kievah
+Pl+Acc:%^WG%>id K ; 				! kievâid
+Pl+Gen:%^WG%>i K ;				! kievâi 
+Pl+Gen+Use/NG:%^WG%^RVSH%>i K ;  		! kievâi
+Pl+Loc:%^WG%>in K ; 				! kievâin
+Pl+Com:%^WG%^RVSH%>iguin K ;		! kievâiguin
+Pl+Com+Err/Orth:%^WG%^RVSH%>igijn K ;		! kievâiguin
+Pl+Abe:%^WG%^RVSH%>ittáá K ;		! kievâittáá



LEXICON 2TELTTA   !!= __@CODE@__
+N+Sg+Nom: K ;    			 	
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%> R ;
+N+Sg+Gen:%^WG K ; 				! telta    		
+N+Sg+Acc:%^WG K ;     			! telta
+N+Sg+Ill:%^CSH%^RLEN%>n K ;	! teeltan
+N+Sg+Loc:%^SV%^SLEN%>st K ; 	! telttaast
+N+Sg+Com:%>in K; 				! telttain
+N+Sg+Abe:%^WG%>ttáá K ;		! teltattáá
+N+Ess:%>n K ;					! telttan		
+N+Par:%>d K ;					! telttad	
+N+Pl+Nom:%^WG%>h K ; 			! teltah		
+N+Pl+Gen:%^WG%^RLEN%>i K ; 	! teeltai
+N+Pl+Gen+Use/NG:%^WG%>i K ; 	! teltai
+N+Pl+Acc:%^WG%^RLEN%>id K ; 	! teeltaid
+N+Pl+Ill:%>id K ;				! telttaid
+N+Pl+Loc:%^WG%>in K ;   		! teltain
+N+Pl+Com:%^WG%>iguin K ;		! teltaiguin
+N+Pl+Com+Err/Orth:%^WG%>igijn K ;		! teltaiguin
+N+Pl+Abe:%^WG%>ittáá K ; 		! teltaittáá
+N:%^WG%> Gen_DER ;
+N:%> Nom_DER ;



LEXICON 2FADDA  !!= __@CODE@__:  This might be the standard even syllabi lexicon
+N+Sg+Nom: K ;               ! kissá
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%^RLEN%> R ;
+N+Sg+Gen:%^WG%^RLEN K ;     ! fáádá, ráávsá
+N+Sg+Acc:%^WG%^RLEN K ;     ! fáádá, ráávsá
+N+Sg+Ill:%^CSH%^RLEN%>n K ;      ! kiisán CSH?
+N+Sg+Loc:%^WG%^RLEN%>st K ; ! fáádást, ráávsást
+N+Sg+Com:%^WG%^CLEN%>in K ; ! kissáin, rávssáin, fáddáin DIFFERS FROM KUTKA
+N+Sg+Abe:%^WG%>ttáá K ;     ! kisáttáá 
+N+Ess:%>n K ;               ! kissán
+N+Par:%>d K ;               ! kissád
+N+Pl+Nom:%^WG%^RLEN%>h K ;  ! fáádáh, ráávsáh
+N+Pl+Gen+Use/NG:%^WG%>i K ;     ! fáádá, ráávsá
+N+Pl+Gen:%^WG%^RLEN%>i K ;  ! fáádái, ráávsái
+N+Pl+Acc:%^WG%^RLEN%>id K ; ! fáádáid, ráávsáid
+N+Pl+Ill:%^WG%^CLEN%>id K ; ! kissáid, rávssáid, fáddáid
+N+Pl+Loc:%^WG%^RLEN%>in K ; ! fáádáin, ráávsáin
+N+Pl+Com:%^WG%>iguin K ;    ! kisáiguin 
+N+Pl+Com+Err/Orth:%^WG%>igijn K ;    ! kisáiguin 
+N+Pl+Abe:%^WG%>ittáá K ;    ! kisáittáá  
+N:%^WG%> Gen_DER ;
+N:%> Nom_DER ;


LEXICON 2KISSA !!= __@CODE@__  No root vowel lengthening
+N: X_KISSA ;
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%> R ;

LEXICON X_KISSA !!= __@CODE@__  
+Sg+Nom: K ;               ! kissá
+Sg+Gen:%^WG K ;           ! kiṣá
+Sg+Acc:%^WG K ;           ! kiṣá
+Sg+Ill:%^CSH%^RLEN%>n K ;      ! kiiṣán
+Sg+Loc:%^SV%^WG%^CLEN%^SLEN%>st K ; ! kissáást
+Sg+Com:%^WG%^CLEN%>in K ; ! kissáin
+Sg+Abe:%^WG%>ttáá K ;     ! kiṣáttáá 
+Ess:%^CLEN%>n K ;         ! kissán
+Par:%^CLEN%>d K ;         ! kissád
+Pl+Nom:%^WG%>h K ;        ! kiṣáh
+Pl+Gen:%^WG%>i K ;        ! kiṣái
+Pl+Acc:%^WG%^CLEN%>id K ; ! kissáid
+Pl+Ill:%^WG%^CLEN%>id K ; ! kissáid
+Pl+Loc:%^WG%>in K ; ! kiṣáin
+Pl+Com:%^WG%>iguin K ;    ! kiṣáiguin 
+Pl+Com+Err/Orth:%^WG%>igijn K ;    ! kiṣáiguin 
+Pl+Abe:%^WG%>ittáá K ;    ! kiṣáittáá  
+Sg+Gen: VPXsuff ;  !
+Sg+Acc: VPXsuff ;  !
+Sg+Ill:%>s CPXsuff ;  !  juksosis ill
+Sg+Loc:%>sti VPXsuff ;  !  juksosis ill
+Pl+Gen:%^WG%>id CPXsuff ;  ! 
+Pl+Acc:%^WG%>id CPXsuff ;  ! 
+Pl+Com:%^WG%>id CPXsuffCOM ;  ! 
+Sg+Com:%^WG%>in CPXsuff ;  ! 
+N:%^WG%> Gen_DER ;
+N:%> Nom_DER ;

LEXICON 2TAHO !!= __@CODE@__ only for taho, similar to 2LAJO except from Pl+Ill, almost similar to 2SPAHA
+N+Sg+Com:%^WG%^CLEN%>in K ;  !tavoin yaml says: tavvoin
+N+Sg+Loc:%^SV%^WG%^CLEN%^SLEN%>st K ;  !tavvoost+N+Pl+Ill:%^WG%^CLEN%>id K ;  !tavvoid
+N+Pl+Ill:%^WG%^CLEN%>id K ;  !tavvoid
+N: LAJO_TAHO ;
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%> R ;
+N:%^WG%> Gen_DER ;
+N:%> Nom_DER ;


LEXICON 2LAJO  !!= __@CODE@__
+N:  X_LAJO ;
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%> R ;
+N:%^WG%> Gen_DER ;
+N:%> Nom_DER ;

LEXICON X_LAJO  !!= __@CODE@__
+Sg+Com:%^WG%>in K ;  !lajoin
+Sg+Loc:%^SV%^WG%^RLEN%^SLEN%>st K ;  !laajoost
+Sg+Com:%^WG%>in K ;  !lajoin
+Pl+Ill:%^WG%>id K ;  !lajoid
+Pl+Com:%^WG%>iguin K ;  !lajoiguin
LAJO_TAHO ;

LEXICON LAJO_TAHO
+Sg+Nom: K ;  !laj̣o
+Sg+Gen:%^WG K ;  !lajo
+Sg+Acc:%^WG K ;  !lajo
+Sg+Ill:%^CSH%>n    K ;   !laj̣on
+Sg+Abe:%^WG%>ttáá K ;  !lajottáá
+Ess:%^SV%^CLEN%^SLEN%>n    K ;  !laijoon
+Par:%^SV%^CLEN%^SLEN%>d    K ; !laijood
+Pl+Nom:%^WG%>h K ;  !lajoh
+Pl+Gen:%^WG%>i K ;  !lajoi
+Pl+Acc:%^WG%>id K ;  !lajoid
+Pl+Loc:%^WG%>in K ; !lajoin
+Pl+Com:%^WG%>iguin K ;  !lajoiguin
+Pl+Com+Err/Orth:%^WG%>igijn K ;  !lajoiguin
+Pl+Abe:%^WG%>ittáá K ;!lajoittáá

LEXICON 2NJUNE !!= __@CODE@__ almost similar to 2LAJO
+N: X_NJUNE ;
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen: R ;
+N+Sg+Gen: VPXsuff ;  !sajes
+N+Sg+Acc: VPXsuff ;  !sajes
+N+Sg+Loc:%>sti VPXsuff ;  !sajestis sajasis ill
+N+Pl+Gen:%^WG%^RLEN%>id CPXsuff ;  !párnáidis
+N+Pl+Acc:%^WG%^RLEN%>id CPXsuff ;  !párnáidis
+N+Pl+Com:%^WG%^RLEN%>id CPXsuffCOM ;  !párnáidiskuin
+N+Sg+Com:%^WG%^RLEN%>in CPXsuff ;  ! 
+N:%^WG%> Gen_DER ;
+N:%> Nom_DER ;

LEXICON X_NJUNE !!= __@CODE@__ almost similar to 2LAJO
+Sg+Nom: K ;						! njuṇe
+Sg+Gen:%^WG K ;					! njune
+Sg+Acc:%^WG K ;					! njune
+Sg+Ill:%^EA%^CSH%>n K ;				! njuṇán # stem e>á
+Sg+Loc:%^SV%^WG%^RLEN%^SLEN%>st K ;	! njuuneest # earlier also%^RVL
!+Sg+Com:%^WG%^CLEN%>in K ;		! njunnijn # MM + MLO hardcording
+Sg+Com:%^WG%^RLEN%>in K ;		! njuunijn # KORP, MLO+MM
+Sg+Abe:%^WG%>ttáá K ;		! njunettáá
+Ess:%^SV%^CLEN%^SLEN%>n K ;			! njunneen
+Par:%^SV%^CLEN%^SLEN%>d K ;			! njunneed
+Pl+Nom:%^WG%>h K ;					! njuneh
+Pl+Gen:%^WG%^RLEN%>i K ;			! njuunij  #stem e > i suff i > j
+Pl+Gen+Use/NG:%^WG%>i K ;			! njunij 	# stem e > i in > jn
+Pl+Acc:%^WG%^RLEN%>id K ;		! njuunijd  # stem e > i in > jn
+Pl+Ill:%^WG%^RLEN%>id K ;		! njuunijd  # stem e > i in > jn
+Pl+Loc:%^WG%^RLEN%>in K ;		! njuunijn  # stem e > i in > jn
+Pl+Com:%^WG%>iguin	K ;		! njunijguin # stem e > i in > jn
+Pl+Com+Err/Orth:%^WG%>igijn	K ;		! njunijguin # stem e > i in > jn
+Pl+Abe:%^WG%>ittáá K ;		! njunijttáá # stem e > i in > jn

LEXICON 2ALME !!= __@CODE@__
+N+Pl+Ill:%^WG%^RLEN%>id K ; 		! oolmijd
2ALGE ;

LEXICON 2ALGE !!= __@CODE@__
+N: X_ALGE ;
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%> R ;
+N+Sg+Gen: VPXsuff ;  !sajes
+N+Sg+Acc: VPXsuff ;  !sajes
+N+Sg+Com:%^WG%^CLEN%>in CPXsuff ;  ! 
+N+Pl+Gen:%^WG%^CLEN%>id CPXsuff ;  !párnáidis čolmijdis
+N+Pl+Acc:%^WG%^CLEN%>id CPXsuff ;  !párnáidis
+N+Pl+Com:%^WG%^CLEN%>id CPXsuffCOM ;  !párnáidiskuin
+N:%^EA%^WG%> Gen_DER ; !CSH?
+N:%> Nom_DER ;


LEXICON X_ALGE !!= __@CODE@__
+Sg+Nom: K ;						! alge
+Sg+Gen:%^WG K ; 					! alge
+Sg+Acc:%^WG K ; 					! alge
+Sg+Ill:%^EA%^CSH%^RLEN%>n K ; 	! aalgan
+Sg+Loc:%^SV%^WG%^CLEN%^SLEN%>st K ; 	! algeest
+Sg+Com:%^WG%^CLEN%>in K ; 		! olgijn 
+Sg+Abe:%^WG%>ttáá K ; 		! algettáá
+Ess:%>n K ; 			! algen
+Par:%>d K ; 			! alged
+Pl+Nom:%^WG%>h K ; 				! algeh
+Pl+Gen+Use/NG:%^WG%>i K ; 				! olgij 
+Pl+Gen:%^WG%^RLEN%>i K ; 		! oolgij
+Pl+Acc:%^WG%^RLEN%>id K ; 		! oolgijd 
+Pl+Ill:%^WG%^CLEN%>id K ; 		! olgijd
+Pl+Loc:%^WG%^RLEN%>in K ; 		! oolgijn
+Pl+Com:%^WG%>iguin K ; 		! olgijguin
+Pl+Com+Err/Orth:%^WG%>igijn K ; 		! olgijguin
+Pl+Abe:%^WG%>ittáá K ;		! olgijttáá



LEXICON 2NIJBE !!= __@CODE@__ 
+N+Sg+Nom: K ;						! nijbe 
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%^RLEN%> R ;
+N+Sg+Gen:%^WG%^RLEN K ; 			! niijbe
+N+Sg+Acc:%^WG%^RLEN K ; 			! niijbe
+N+Sg+Ill:%^EA%^CSH%^RLEN%>n K ; 	! niijbán
+N+Sg+Loc:%^WG%^RLEN%>st K ; 	! niijbest
+N+Sg+Com:%^WG%^CLEN%>in K ; 			! nijbijn 
+N+Sg+Abe:%^WG%>ttáá K ; 		! nijbettáá
+N+Ess:%>n K ; 			! nijben
+N+Par:%>d K ; 			! nijbed
+N+Pl+Nom:%^WG%^RLEN%>h K ; 				! niijbeh
+N+Pl+Gen:%^WG%^RLEN%>i K ; 		! niijbij
+N+Pl+Acc:%^WG%^RLEN%>id K ; 		! niijbijd 
+N+Pl+Ill:%^WG%^CLEN%>id K ; 		! nijbijd
+N+Pl+Loc:%^WG%^RLEN%>in K ; 		! niijbijn
+N+Pl+Com:%^WG%>iguin K ; 		! nijbijguin
+N+Pl+Com+Err/Orth:%^WG%>igijn K ; 		! nijbijguin
+N+Pl+Abe:%^WG%>ittáá K ;		! nijbijttáá
+N:%^WG%^RLEN%> Gen_DER ;
+N:%> Nom_DER ;


LEXICON 2VAHE !!= __@CODE@__
+N+Sg+Nom: K ; 						! vahe
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%> R ;
+N+Sg+Gen:%^WG K ;					! vave
+N+Sg+Acc:%^WG K ;					! vave
+N+Sg+Ill:%^EA%^CSH%>n K ; 				! vahan
+N+Sg+Loc:%^SV%^WG%^CLEN%^SLEN%>st K ; 	! vavveest
+N+Sg+Com:%^WG%^CLEN%>in K ; 		! vovvijn
+N+Sg+Abe:%^WG%>ttáá K ; 			! vavettáá
+N+Ess:%^SV%^CLEN%^SLEN%>n K ; 			! vahheen
+N+Par:%^SV%^CLEN%^SLEN%>d K ; 		! vahheed
+N+Pl+Nom:%^WG%>h K ; 				! vaveh
+N+Pl+Gen:%^WG%^RLEN%>i K ; 		! voovij
+N+Pl+Gen+Use/NG:%^WG%>i K ; 				! vovij
+N+Pl+Acc:%^WG%^RLEN%>id K ; 		! voovijd
+N+Pl+Ill:%^WG%^CLEN%>id K ; 		! vovvijd
+N+Pl+Loc:%^WG%^RLEN%>in K ; 		! voovijn
+N+Pl+Com:%^WG%>iguin K ; 		! vovijguin
+N+Pl+Com+Err/Orth:%^WG%>igijn K ; 		! vovijguin
+N+Pl+Abe:%^WG%>ittáá K ; 		! vovijttáá
+N:%^WG%> Gen_DER ;
+N:%> Nom_DER ;


LEXICON 2KUMPPI  !!= __@CODE@__ i-i-i > 2SOLLA (?); MLO: no, difference in sg Com and pl Ill
+N+Sg+Nom: K ;							! kumppi
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%^RLEN%> R ;
+N+Sg+Gen:%^WG%^RLEN K ; 				! kuumpi
+N+Sg+Acc:%^WG%^RLEN K ; 				! kuumpi
+N+Sg+Ill:%^CSH%^RLEN%>n K ; 				! kuumpin
+N+Sg+Loc:%^WG%^RLEN%>st K ; 			! kuumpist
+N+Sg+Com:%^WG%^CLEN%>in K ; 			! kumppijn 
+N+Sg+Abe:%^WG%>ttáá K ; 			! kumpittáá
+N+Ess:%>n K ; 							! kumppin
+N+Par:%>d K ; 							! kumppid
+N+Pl+Nom:%^WG%^RLEN%>h K ; 			! kuumpih
+N+Pl+Gen+Use/NG:%^WG%>i K ; 			! kumpij 
+N+Pl+Gen:%^WG%^RLEN%>i K ; 			! kuumpij
+N+Pl+Acc:%^WG%^RLEN%>id K ; 			! kuumpijd 
+N+Pl+Ill:%^WG%^CLEN%>id K ; 			! kumppijd
+N+Pl+Loc:%^WG%^RLEN%>in K ; 			! kuumpijn
+N+Pl+Com:%^WG%>iguin K ; 		! kumpijguin
+N+Pl+Com+Err/Orth:%^WG%>igijn K ; 		! kumpijguin
+N+Pl+Abe:%^WG%>ittáá K ;			! kumpijttáá
+N:%^WG%^RLEN%> Gen_DER ;
+N:%> Nom_DER ;

LEXICON 2SAAMI   !!= __@CODE@__ to 2LAAVU after fix in twolc?  saämijn > saamijn when %^WG%^CLEN%^SVLOW
+N: X_SAAMI ;
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%^RVSH%> R ;
+N+Sg+Nom: VPXsuff ;  !uábbám uábbád
+N+Sg+Gen:%^WG VPXsuff ;  !
+N+Sg+Acc:%^WG VPXsuff ;  !
+N+Sg+Ill:%^EA%>s CPXsuff ; !
+N+Sg+Loc:%^WG%>st CPXsuff ;  !pargostis
+N+Sg+Com:%^WG%>in CPXsuff ;  !
+N:%^WG%> Gen_DER ; !hámálâš
+N:%> Nom_DER ;

LEXICON X_SAAMI   !!= __@CODE@__ to 2LAAVU after fix in twolc?  saämijn > saamijn when %^WG%^CLEN%^SVLOW
+Sg+Nom: K ;                 ! säämi,
+Sg+Gen:%^WG K ;          ! säämi,
+Sg+Acc:%^WG K ;      ! säämi,
+Sg+Ill:%^EA%^CSH%>n K ;          ! sáámán ä>á !, 
+Sg+Loc:%^WG%>st K ;         ! säämist
+Sg+Com:%^WG%^RLEN%>in   K ;       ! saamijn  # please add ä > a _ ij (twolc)
+Sg+Abe:%^WG%^RVSH%>ttáá  K ; ! sämittáá
+Ess:%^CLEN%>n  K ;          ! säämmin
+Par:%^CLEN%>d K ;           ! säämmid
X_SAANIH ;

LEXICON 2SAANIH   !!= __@CODE@__ plurals for 2SAAMI
+N: X_SAANIH ;


LEXICON X_SAANIH   !!= __@CODE@__ plurals for 2SAAMI
+Pl+Nom:%^WG%>h K ;          ! säämih
+Pl+Gen:%^WG%>i K ;          ! saamij
+Pl+Gen+Use/NG:%^WG%^RVSH%>i K ;    ! samij
+Pl+Acc:%^WG%>id K ;         ! saamijd
+Pl+Ill:%^WG%>id K ;         ! saamijd
+Pl+Loc:%^WG%>in K ;         ! saamijn
+Pl+Com:%^WG%^RVSH%>iguin K ; ! samijguin
+Pl+Com+Err/Orth:%^WG%^RVSH%>igijn K ; ! samijguin
+Pl+Abe:%^WG%^RVSH%>ittáá K ; ! samijttáá
+Pl+Gen:%^WG%>id CPXsuff ;  !
+Pl+Acc:%^WG%>id CPXsuff ;  !
+Pl+Com:%^WG%>id CPXsuffCOM ;  !


LEXICON 2CAACI !!= __@CODE@__ WG, Ill EA, Com CLEN SVLOW, Ess CLEN
+N+Ess:%^CLEN%>n K ;               ! čääccin
+N+Par:%^CLEN%>d K ;               ! čääccid 
CAACI_VYEHI ;

LEXICON 2VYEHI !!= __@CODE@__ WG, Ill EA, Com CLEN SVLOW, Ess without CLEN
+N+Ess:%>n K ;               ! vyehin
+N+Par:%>d K ;               ! vyehid 
CAACI_VYEHI ;

LEXICON CAACI_VYEHI !!= __@CODE@__ WG, Ill EA, Com CLEN SVLOW, Ess CLEN
+N+Sg+Nom: K ;               ! čääci
+N+Cmp/SgNom:%^RVSH%> R ; !čäcipoccee
+N+Cmp/SgGen:%^WG%> R ; !
+N+Cmp/SgNom:  Rhyph ; !
+N+Sg+Gen:%^WG K ;     ! čääsi
+N+Sg+Acc:%^WG K ;     ! čääsi
+N+Sg+Ill:%^EA%^CSH%>n K ;      ! čáácán
+N+Sg+Loc:%^WG%>st K ; ! čääsist
+N+Sg+Com:%^WG%^CLEN%^RVSH%>in K ; ! čassijn
+N+Sg+Abe:%^WG%^RVSH%>ttáá K ;     ! čäsittáá 
+N+Pl+Nom:%^WG%>h K ;  ! čääsih
+N+Pl+Gen:%^WG%>i K ;  ! čaasij
+N+Pl+Gen+Use/NG:%^WG%^RVSH%>i K ;  ! časij
+N+Pl+Acc:%^WG%>id K ; ! čaasijd
+N+Pl+Ill:%^WG%^CLEN%^RVSH%>id K ; ! čassijd  
+N+Pl+Loc:%^WG%>in K ; ! čaasijn
+N+Pl+Com:%^WG%^RVSH%>iguin K ;    ! časijguin 
+N+Pl+Com+Err/Orth:%^WG%^RVSH%>igijn K ;    ! časijguin 
+N+Pl+Abe:%^WG%^RVSH%>ittáá K ;    ! časijttáá  
+N:%^WG%> Gen_DER ;
+N:%> Nom_DER ;


LEXICON 2AIGI !!= __@CODE@__ similar to 2VADDU except from EA for Sg+Ill
+N: X_AIGI ;
+N+Cmp/SgNom: R ;                         ! äigi eeč%^RCi
+N+Cmp/SgGen:%^WG%^RLEN%> R ;                         ! äigi eeč%^RCi
+N+Sg+Gen:%^WG%^RLEN VPXsuff ;  !sajes
+N+Sg+Acc:%^WG%^RLEN VPXsuff ;  !sajes 
+N+Sg+Ill:%^EA%>s CPXsuff ; !sajasis ill páikkásis
+N+Sg+Loc:%>st CPXsuff ;  !sajestis 
+N+Sg+Com:%^WG%^CLEN%^SVLOW%>in CPXsuff ;  !sajestis sajasis ill
+N+Pl+Gen:%^WG%>id CPXsuff ;  !párnáidis
+N+Pl+Acc:%^WG%>id CPXsuff ;  !párnáidis
+N+Pl+Com:%^WG%>id CPXsuffCOM ;  !párnáidiskuin
+N+Sg+Gen:%^WG%^RLEN VPXsuff ;  !
+N+Sg+Acc:%^WG%^RLEN VPXsuff ;  !
+N+Sg+Ill:%^EA%>s CPXsuff ; ! iännásis páikkásis
+N+Sg+Loc:%>st CPXsuff ;  ! 
+N+Sg+Com:%^WG%^CLEN%^SVLOW%>in CPXsuff ;  !
+N+Ess:%^CLEN%>n CPXsuff ;
+N+Pl+Gen:%^WG%>id CPXsuff ;  !
+N+Pl+Acc:%^WG%>id CPXsuff ;  ! 
+N+Pl+Com:%^WG%>id CPXsuffCOM ;  ! 
+N+Der/Dimin+N+Sg+Nom:%^EA%^WG%>š K ; !uábáš uábbi
+N:%^WG%^RLEN%> Gen_DER ;
+N:%> Nom_DER ; !

LEXICON X_AIGI !!= __@CODE@__
+Sg+Nom: K ;                         ! äigi eeč%^RCi
+Sg+Gen:%^WG%^RLEN K ;               ! ääigi
+Sg+Acc:%^WG%^RLEN K ;               ! ääigi
+Sg+Ill:%^EA%^CSH%^RLEN%>n K ;       ! ááigán! DIFTONGISATION
+Sg+Loc:%^WG%^RLEN%>st K ;           ! ääigist
+Sg+Com:%^WG%^CLEN%^SVLOW%>in K ; ! aigijn      e ij i jn  č:i %^RC:j
+Sg+Abe:%^WG%^RVSH%>ttáá K ;         ! äigittáá
+Ess:%^CLEN%>n K ;                         ! äigin  eeč%^RCi
+Par:%^CLEN%>d K ;                         ! äigid
X_SAAGIH ;

LEXICON 2SAAGIH !!= __@CODE@__  plurals for 2AIGI
+N: X_SAAGIH ;

LEXICON X_SAAGIH !!= __@CODE@__  plurals for 2AIGI
+Pl+Nom:%^WG%^RLEN%>h K ;            ! ääigih
+Pl+Gen:%^WG%^RLEN%>i K ;            ! aaigij
+Pl+Gen+Use/NG:%^WG%^RVSH%>i K ;            ! aigij
+Pl+Acc:%^WG%^RLEN%>id K ;           ! aaigijd
+Pl+Ill:%^WG%^CLEN%^SVLOW%>id K ;    ! aigijd
+Pl+Loc:%^WG%^RLEN%>in K ;           ! aaigijn
+Pl+Com:%^WG%^RVSH%>iguin K ;        ! aigijguin
+Pl+Com+Err/Orth:%^WG%^RVSH%>igijn K ;        ! aigijguin
+Pl+Abe:%^WG%^RVSH%>ittáá K ;        ! aigijttáá


LEXICON 2KYELI  !!= __@CODE@__ i-e-á, similar to 2ALGE, except from -i some places. 
+N: X_KYELI ;
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%^RVSH%> R ;
+N+Sg+Gen:%^WG%^RVSH VPXsuff ;  !sajes
+N+Sg+Acc:%^WG%^RVSH VPXsuff ;  !sajes 
+N+Sg+Ill:%^EA%>s CPXsuff ; !sajasis ill páikkásis
+N+Sg+Loc:%^WG%^RVSH%>st CPXsuff ;  !sajestis 
+N+Sg+Com:%^WG%^RVSH%^SVLOW%>in CPXsuff ;  !sajestis sajasis ill
+N+Pl+Gen:%^WG%^RVSH%>id CPXsuff ;  !párnáidis
+N+Pl+Acc:%^WG%^RVSH%>id CPXsuff ;  !párnáidis
+N+Pl+Com:%^WG%^RVSH%>id CPXsuffCOM ;  !párnáidiskuin
+N:%^WG%^RVSH%> Gen_DER ;
+N:%> Nom_DER ;


LEXICON X_KYELI  !!= __@CODE@__ i-e-á, similar to 2ALGE, except from -i some places. 
+Sg+Com:%^WG%>in K ;         ! kuolijn
+Pl+Ill:%^WG%>id K ; 				! kuolijd
KYELI_SYEJI ;


LEXICON 2SYEJI  !!= __@CODE@__ i-e-á,
+N+Sg+Com:%^CLEN%^RVSH%>in K ;         ! suoijijn
+N+Pl+Ill:%^CLEN%^RVSH%>id K ; 				! suoijijd
+N: KYELI_SYEJI ;
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%^RVSH%> R ;
+N+Sg+Gen:%^WG%^RVSH VPXsuff ;  !sajes
+N+Sg+Acc:%^WG%^RVSH VPXsuff ;  !sajes 
+N+Sg+Ill:%^EA%>s CPXsuff ; !sajasis ill páikkásis
+N+Sg+Loc:%>st CPXsuff ;  !sajestis 
+N+Sg+Com:%^CLEN%^SVLOW%>in CPXsuff ;  !sajestis sajasis ill
+N+Pl+Gen:%^WG%^RVSH%>id CPXsuff ;  !párnáidis
+N+Pl+Acc:%^WG%^RVSH%>id CPXsuff ;  !párnáidis
+N+Pl+Com:%^WG%^RVSH%>id CPXsuffCOM ;  !párnáidiskuin
+N:%^WG%^RVSH%> Gen_DER ;
+N:%> Nom_DER ;


LEXICON KYELI_SYEJI  !!= __@CODE@__ i-e-á,
+Sg+Nom: K ;                 ! kyeli
+Sg+Gen:%^WG%^RVSH K ;         ! kyele
+Sg+Acc:%^WG%^RVSH K ;         ! kyele
+Sg+Ill:%^EA%^CSH%^RLEN%>n K ;     ! kuálán
+Sg+Loc:%^WG%^RVSH%>est K ;   ! kyeleest
+Sg+Abe:%^WG%^RVSH%>ttáá K ;  ! kyelettáá
+Ess:%^CLEN%>n K ;     ! kyellin
+Par:%^CLEN%>d K ; 					! kyellid
+Pl+Nom:%^WG%^RVSH%>h K ; 				! kyeleh
+Pl+Gen+Use/NG:%^WG%^RVSH%>i K ; 				! kuolij
+Pl+Gen:%^WG%>i K ; 					! kuolij
+Pl+Acc:%^WG%>id K ; 					! kuolijd
+Pl+Loc:%^WG%>in K ; 					! kuolijn
+Pl+Com:%^WG%^RVSH%>iguin K ;	 		! kuolijguin
+Pl+Com+Err/Orth:%^WG%^RVSH%>igijn K ;	 		! kuolijguin
+Pl+Abe:%^WG%^RVSH%>ittáá K ;			! kuolijttáá


LEXICON 2KIRKKO  !!= __@CODE@__  > KIRKKO, but WG for Sg+Loc: 
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%> R ;
+N+Sg+Loc:%^SV%^WG%^SLEN%>st K ;   !kirhoost
KIRKKO ;

LEXICON 2NJAMMA   !!= __@CODE@__ > KIRKKO, but WG CLEN for Sg+Loc: loho:lovvoost
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%> R ;
+N+Sg+Loc:%^SV%^WG%^CLEN%^SLEN%>st K ; 	
KIRKKO ;


LEXICON KIRKKO  !!= __@CODE@__   
+N+Sg+Nom: K ;   !kirkko
+N+Sg+Gen:%^WG K ;   !kirho
+N+Sg+Acc:%^WG K ;   !ki´rho
+N+Sg+Ill:%^CSH%^RLEN%>n K ;   !kiirkon
+N+Sg+Com:%^WG%^CLEN%>in K ;   !kirhoin
+N+Sg+Abe:%^WG%>ttáá K ;   !kirhottáá
+N+Ess:%>n    K ;   !kirkkon
+N+Par:%>d    K ;   !kirkkod
+N+Pl+Nom:%^WG%>h K ;   !kirhoh
+N+Pl+Gen:%^WG%>i K ;   !kirhoi
+N+Pl+Acc:%^WG%^CLEN%>id K ;   !kirhoid
+N+Pl+Ill:%^WG%^CLEN%>id K ;   !kirhoid
+N+Pl+Loc:%^WG%>in K ;   !kirhoin
+N+Pl+Com:%^WG%>iguin K ;   !kirhoiguin
+N+Pl+Com+Err/Orth:%^WG%>igijn K ;   !kirhoiguin
+N+Pl+Abe:%^WG%>ittáá K ;   !kirhoittáá
+N+Sg+Gen:%^WG VPXsuff ;  !
+N+Sg+Acc:%^WG VPXsuff ;  !
+N+Sg+Ill:%^WG%>s CPXsuff ; !
+N+Sg+Loc:%^WG%>st CPXsuff ;  !pargostis pargostân
+N+Sg+Com:%^WG%>in CPXsuff ;  !
+N+Pl+Gen:%^WG%^CLEN%>id CPXsuff ;  !
+N+Pl+Acc:%^WG%^CLEN%>id CPXsuff ;  !
+N+Pl+Com:%^WG%^CLEN%>id CPXsuffCOM ;  !
+N:%^WG%> Gen_DER ;
+N:%> Nom_DER ;




LEXICON 2SIJVO  !!= __@CODE@__
+N+Sg+Nom: K ;  ! sijvo
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%^RLEN%> R ;
+N+Sg+Gen:%^WG%^RLEN K ;  ! siijvo
+N+Sg+Gen:%^WG%^RVSH K ;  ! sijvo
+N+Sg+Acc:%^WG%^RLEN K ;  ! siijvo
+N+Sg+Acc:%^WG%^RVSH K ;  ! sijvo
+N+Sg+Ill:%^CSH%^RLEN%>n    K ;   ! siijvon
+N+Sg+Loc:%^WG%^RLEN%>st K ;  !siijvost
+N+Sg+Com:%^WG%^CLEN%>in K ;  !sijvoin
+N+Sg+Abe:%^WG%^RVSH%>ttáá K ;  !sijvottáá
+N+Ess:%>n    K ;  !sijvon
+N+Par:%>d    K ; ! sijvod
+N+Pl+Nom:%^WG%^RLEN%>h K ;  !siijvoh
+N+Pl+Nom:%^WG%^RVSH%>h K ;  !sijvoh
+N+Pl+Gen:%^WG%^RLEN%>i K ;  ! siijvoi
+N+Pl+Gen+Use/NG:%^WG%^RVSH%>i K ;  ! sijvoi
+N+Pl+Acc:%^WG%^RLEN%>id K ;  ! siijvoid
+N+Pl+Ill:%^WG%^CLEN%>id K ;  ! sijvoid
+N+Pl+Loc:%^WG%^RLEN%>in K ; ! siijvoin
+N+Pl+Com:%^WG%^RVSH%>iguin K ;  ! sijvoiguin
+N+Pl+Com+Err/Orth:%^WG%^RVSH%>igijn K ;  ! sijvoiguin
+N+Pl+Abe:%^WG%^RVSH%>ittáá K ;! sijvoittáá
+N:%^WG%^RLEN%> Gen_DER ;
+N:%> Nom_DER ;



LEXICON 2VASO !!= __@CODE@__ only for vašo, similar to 2LAJO except from Sg+Loc, almost similar to 2SPAHA and 2TAHO. Blank Nom, Ill, Ess SV CLEN SLEN, otherwise WG
+N+Sg+Nom:%^RVSH K ;  !vaṣ̌o
+N+Cmp/SgNom:%^RVSH%> R ;
+N+Cmp/SgGen:%^WG%^RVSH%> R ;
+N+Cmp/SgNom:  Rhyph ;
+N+Sg+Gen:%^WG K ;  !tavo
+N+Sg+Acc:%^WG K ;  !tavo
+N+Sg+Ill:%^CSH%>n    K ;  !tahon
+N+Sg+Loc:%^SV%^WG%^CLEN%^SLEN%>st K ;  !tavvoost
+N+Sg+Com:%^WG%>in K ;  !tavvoin
+N+Sg+Abe:%^WG%>ttáá K ;  !tavottáá
+N+Ess:%^SV%^CLEN%^SLEN%>n    K ;  !tahhoon
+N+Par:%^SV%^CLEN%^SLEN%>d    K ;  !tahhood
+N+Pl+Nom:%^WG%>h K ;  !tavoh
+N+Pl+Gen:%^WG%>i K ;  !tavoi
+N+Pl+Acc:%^WG%>id K ;  !tavoid
+N+Pl+Ill:%^WG%>id K ;  !tavvoid
+N+Pl+Loc:%^WG%>in K ;  !tavoin
+N+Pl+Com:%^WG%>iguin K ;  !tavoiguin
+N+Pl+Com+Err/Orth:%^WG%>igijn K ;  !tavoiguin
+N+Pl+Abe:%^WG%>ittáá K ;  !tavoittáá
+N:%^WG%> Gen_DER ;
+N:%> Nom_DER ;


LEXICON 2OKKO !!= __@CODE@__ Pl+Acc WG, Pl+Ill WG CLEN
+N: X_OKKO ;
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%^RVSH%> R ;
+N:%^WG%> Gen_DER ;
+N:%> Nom_DER ;


LEXICON X_OKKO !!= __@CODE@__ Pl+Acc WG, Pl+Ill WG CLEN
+Sg+Nom: K ;  !okko
+Sg+Gen:%^WG K ;  !oho
+Sg+Acc:%^WG K ;  !oho
+Sg+Ill:%^CSH%^RLEN%>n    K ;  !ookon
+Sg+Loc:%^SV%^WG%^CLEN%^SLEN%>st    K ;  !ohhoost
+Sg+Com:%^WG%^CLEN%>in    K ;  !ohhoin
+Sg+Abe:%^WG%>ttáá K ;   !ohottáá
+Ess:%>n    K ;  !okkon
+Par:%>d    K ;  !okkod
+Pl+Nom:%^WG%>h K ;  !ohoh
+Pl+Gen:%^WG%>i K ;  !ohoi
+Pl+Acc:%^WG%>id K ;  !ohoid
+Pl+Ill:%^WG%^CLEN%>id K ;  !ohhoid
+Pl+Loc:%^WG%>in K ;  !ohoin
+Pl+Com:%^WG%>iguin K ;  !ohoiguin
+Pl+Com+Err/Orth:%^WG%>igijn K ;  !ohoiguin
+Pl+Abe:%^WG%>ittáá K ;  !ohoittáá


LEXICON 2MAANU !!= __@CODE@__ WG, blank Ill, RVSH, CLEN for Ess/Par
+N+Sg+Nom: K ;               ! mááṇu
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%> R ;
+N+Sg+Gen:%^WG K ;     ! máánu
+N+Sg+Acc:%^WG K ;     ! máánu
+N+Sg+Ill:%^CSH%>n K ;      ! mááṇun
+N+Sg+Loc:%^WG%>st K ; ! máánust
+N+Sg+Com:%^WG%>in K ; ! máánuin
+N+Sg+Abe:%^WG%^RVSH%>ttáá K ;     ! mánuttáá 
+N+Ess:%^CLEN%>n K ;               ! máánnun
+N+Par:%^CLEN%>d K ;               ! máánnud 
+N+Pl+Nom:%^WG%>h K ;  ! máánuh
+N+Pl+Gen:%^WG%>i K ;  ! máánui
+N+Pl+Gen+Use/NG:%^WG%^RVSH%>i K ;  ! mánui
+N+Pl+Acc:%^WG%>id K ; ! máánuid
+N+Pl+Ill:%^WG%>id K ; ! máánuid  
+N+Pl+Loc:%^WG%>in K ; ! máánuin
+N+Pl+Com:%^WG%^RVSH%>iguin K ;    ! mánuiguin 
+N+Pl+Com+Err/Orth:%^WG%^RVSH%>igijn K ;    ! mánuiguin 
+N+Pl+Abe:%^WG%^RVSH%>ittáá K ;    ! mánuittáá  
+N:%^WG%> Gen_DER ;
+N:%> Nom_DER ;

LEXICON 2LAAVU !!= __@CODE@__ WG, Ill EA, Com SVLOW, Ess CLEN
+N+Sg+Nom: K ;               ! láávu
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%> R ;
+N+Sg+Gen:%^WG K ;     ! láávu
+N+Sg+Acc:%^WG K ;     ! láávu
+N+Sg+Ill:%^CSH%>n K ;      ! láávun
+N+Sg+Loc:%^WG%>st K ; ! láávust
+N+Sg+Com:%^WG%^SVLOW%>in K ; ! láávoin
+N+Sg+Abe:%^WG%^RVSH%>ttáá K ;     ! lávuttáá 
+N+Par:%^CLEN%>d K ;               ! láávvud 
+N+Ess:%^CLEN%>n K ;               ! láávvun
+N+Pl+Nom:%^WG%>h K ;  ! láávuh
+N+Pl+Gen:%^WG%>i K ;  ! láávui
+N+Pl+Gen+Use/NG:%^WG%^RVSH%>i K ;  ! lávui
+N+Pl+Acc:%^WG%>id K ; ! láávuid
+N+Pl+Ill:%^WG%^SVLOW%>id K ; ! láávoid  
+N+Pl+Loc:%^WG%>in K ; ! láávuin
+N+Pl+Com:%^WG%^RVSH%>iguin K ;    ! lávuiguin 
+N+Pl+Com+Err/Orth:%^WG%^RVSH%>igijn K ;    ! lávuiguin 
+N+Pl+Abe:%^WG%^RVSH%>ittáá K ;    ! lávuittáá
+N+Sg+Gen:%^WG VPXsuff ;  !
+N+Sg+Acc:%^WG VPXsuff ;  !
+N+Sg+Ill:%>s CPXsuff ; !
+N+Sg+Loc:%^WG%>st CPXsuff ;  !
+N+Sg+Com:%^WG%>in CPXsuff ;  !
+N+Pl+Gen:%^WG%>id CPXsuff ;  !
+N+Pl+Acc:%^WG%>id CPXsuff ;  ! 
+N+Pl+Com:%^WG%>id CPXsuffCOM ;  ! 
+N:%> Nom_DER ;
+N:%^WG%> Gen_DER ; !viärulâš



LEXICON 2VUAGGU !!= __@CODE@__ 
+N: X_VUAGGU ;
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%> R ;
+N+Sg+Ill:s CPXsuff ;
+N+Sg+Gen:%^WG VPXsuff ;  !
+N+Sg+Acc:%^WG VPXsuff ;  !
+N+Sg+Loc:%^WG%>st CPXsuff ;  !
+N+Sg+Com:%^WG%^CLEN%>in CPXsuff ;  !
+N+Pl+Gen:%^WG%>id CPXsuff ;  !
+N+Pl+Acc:%^WG%>id CPXsuff ;  ! 
+N+Pl+Com:%^WG%>id CPXsuffCOM ;  ! 
+N:%^WG%> Gen_DER ;
+N:%> Nom_DER ;



LEXICON X_VUAGGU !!= __@CODE@__ 
+Sg+Nom: K ;               ! vuággu
+Sg+Gen:%^WG K ;     ! vuágu
+Sg+Acc:%^WG K ;     ! vuágu
+Sg+Ill:%^CSH%>n K ;      ! vuágun
+Sg+Loc:%^WG%>st K ; ! vuágust
+Sg+Com:%^WG%^CLEN%^SVLOW%>in K ; ! vuággoin
+Sg+Abe:%^WG%^RVSH%>ttáá K ;     ! vuáguttáá
+Ess:%>n K ;               ! vuággun
+Par:%>d K ;               ! vuággud 
+Pl+Nom:%^WG%>h K ;  ! vuáguh
+Pl+Gen:%^WG%>i K ;  ! vuágui
+Pl+Gen+Use/NG:%^WG%^RVSH%>i K ;  ! vuágui
+Pl+Acc:%^WG%>id K ; ! vuáguid
+Pl+Ill:%^WG%^CLEN%^SVLOW%>id K ; ! vuággoid  
+Pl+Loc:%^WG%>in K ; ! vuáguin
+Pl+Com:%^WG%^RVSH%>iguin K ;    ! vuáguiguin 
+Pl+Com+Err/Orth:%^WG%^RVSH%>igijn K ;    ! vuáguiguin 
+Pl+Abe:%^WG%^RVSH%>ittáá K ;    ! vuáguittáá  


LEXICON 2VADDU !!= __@CODE@__ 2AIGI has EA for Sg+Ill, 2SOLLA has RLEN for Sg+Com and Pl+Ill
+N+Sg+Nom: K ;               ! váddu 
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%^RLEN%> R ; !kaaicâpoccee
+N+Sg+Gen:%^WG%^RLEN K ;     ! váádu
+N+Sg+Acc:%^WG%^RLEN K ;     ! váádu
+N+Sg+Ill:%^CSH%^RLEN%>n K ;      ! váádun
+N+Sg+Loc:%^WG%^RLEN%>st K ; ! váádust
+N+Sg+Com:%^WG%^CLEN%^SVLOW%>in K ; ! váddoin 
+N+Sg+Abe:%^WG%^RVSH%>ttáá K ;     ! váduttáá 
+N+Par:%>d K ;               ! váddud
2UAPUH ;

LEXICON 2UAPUH
+N+Ess:%>n K ;               ! váddun
+N+Pl+Nom:%^WG%^RLEN%>h K ;  ! vááduh
+N+Pl+Gen:%^WG%^RLEN%>i K ;  ! váádui
+N+Pl+Gen+Use/NG:%^WG%^RVSH%>i K ;  ! vádui
+N+Pl+Acc:%^WG%^RLEN%>id K ; ! vááduid
+N+Pl+Ill:%^WG%^CLEN%^SVLOW%>id K ; ! váddoid  
+N+Pl+Loc:%^WG%^RLEN%>in K ; ! vááduin
+N+Pl+Com:%^WG%^RVSH%>iguin K ;    ! váduiguin 
+N+Pl+Com+Err/Orth:%^WG%^RVSH%>igijn K ;    ! váduiguin 
+N+Pl+Abe:%^WG%^RVSH%>ittáá K ;    ! váduittáá  
+N:%^WG%> Nom_DER ;
+N:%^WG%^RLEN%> Gen_DER ;    




LEXICON 2TAALU2 !!= __@CODE@__ one variant for Pl+Gen, plus 2VUARU
+N+Pl+Gen+Use/NG:%^WG%^SVLOW%>i K ;        ! tááloi 
2VUARU ;

LEXICON 2VUARU !!= __@CODE@__ Blank Nom, Ill, CLEN Ess, otherwise WG RVSH, SVLOW
+N+Sg+Nom: K ;               ! vuáṛu
+N+Cmp/SgNom: R ; !stáálupoccee
+N+Cmp/SgGen:%^WG%^RVSH%^SVLOW%> R ;
+N+Sg+Gen:%^WG%^RVSH%^SVLOW K ;    ! vuáro
+N+Sg+Acc:%^WG%^RVSH%^SVLOW K ;           ! vuáro
+N+Sg+Ill:%^CSH%>n K ;      ! vuáṛun
+N+Sg+Loc:%^SV%^WG%^RVSH%^SLEN%^SVLOW%>st K ; ! vuároost
+N+Sg+Com:%^WG%^RVSH%^SVLOW%>in K ; ! vuároin
+N+Sg+Abe:%^WG%^RVSH%^SVLOW%>ttáá K ;     ! vuárottáá
+N+Ess:%^CLEN%>n K ;               ! vuárrun
+N+Par:%^CLEN%>d K ;               ! vuárrud
+N+Pl+Nom:%^WG%^RVSH%^SVLOW%>h K ;        ! vuároh
+N+Pl+Gen:%^WG%^RVSH%^SVLOW%>i K ;        ! vuároi
+N+Pl+Acc:%^WG%^RVSH%^SVLOW%>id K ; ! vuároid
+N+Pl+Ill:%^WG%^RVSH%^SVLOW%>id K ; ! vuároid
+N+Pl+Loc:%^WG%^RVSH%^SVLOW%>in K ; ! vuároin
+N+Pl+Com:%^WG%^RVSH%^SVLOW%>iguin K ;    ! vuároiguin
+N+Pl+Com+Err/Orth:%^WG%^RVSH%^SVLOW%>igijn K ;    ! vuároiguin
+N+Pl+Abe:%^WG%^RVSH%^SVLOW%>ittáá K ;    ! vuároittáá
+N+Sg+Gen:%^WG%^RVSH%^SVLOW VPXsuff ;  !
+N+Sg+Acc:%^WG%^RVSH%^SVLOW VPXsuff ;  !
+N+Sg+Ill:%^WG%^RVSH%^SVLOW%>s CPXsuff ; !
+N+Sg+Loc:%^WG%^RVSH%^SVLOW%>st CPXsuff ;  !pargostis
+N+Sg+Com:%^WG%^RVSH%^SVLOW%>in CPXsuff ;  !
+N+Pl+Gen:%^WG%^RVSH%^SVLOW%>id CPXsuff ;  !suájáidis
+N+Pl+Acc:%^WG%^RVSH%^SVLOW%>id CPXsuff ;  ! 
+N+Pl+Com:%^WG%^RVSH%^SVLOW%>id CPXsuffCOM ;  ! 
+N:%^WG%^RVSH%^SVLOW%> Gen_DER ;
+N:%> Nom_DER ;


LEXICON 2TAVLU !!= __@CODE@__ 
+N+Sg+Nom: K ;               ! lanttu
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen:%^WG%^RLEN%> R ;
+N+Sg+Gen:%^WG%^RLEN K ;     ! laantu
+N+Sg+Acc:%^WG%^RLEN K ;     ! laantu
+N+Sg+Ill:%^CSH%^RLEN%>n K ;  ! laantun
+N+Sg+Loc:%^WG%^RLEN%>st K ; ! laantust
+N+Sg+Com:%>in K ;           ! lanttuin
+N+Sg+Abe:%^WG%>ttáá K ;     ! lantuttáá 
+N+Ess:%>n K ;               ! lanttun
+N+Par:%>d K ;               ! kissád
+N+Pl+Nom:%^WG%^RLEN%>h K ;  ! laantuh
+N+Pl+Gen:%^WG%^RLEN%>i K ;  ! laantui
+N+Pl+Gen+Use/NG:%^WG%>i K ;        ! laantui
+N+Pl+Acc:%^WG%^RLEN%>id K ; ! laantuid
+N+Pl+Ill:%>id K ;     ! lanttuid
+N+Pl+Loc:%^WG%^RLEN%>in K ; ! laantuin
+N+Pl+Com:%^WG%>iguin K ;    ! lantuiguin 
+N+Pl+Com+Err/Orth:%^WG%>igijn K ;    ! lantuiguin 
+N+Pl+Abe:%^WG%>ittáá K ;    ! lantuittáá  
+N:%^WG%^RLEN%> Gen_DER ;
+N:%> Nom_DER ;


!LEXICON 2PUUVSAH !!= __@CODE@__  Plural tantum
!+N+Cmp/SgNom: R ; 						! puksâ ! especially in compounds
!+N+Pl+Nom:%>h K ;				! puuvsah
!+N+Pl+Nom:%^WG%>h K ;				! puuvsah
!+N+Pl+Gen:%^WG%>i K ; 				! puuvsâi
!+N+Pl+Acc:%^WG%>id K ; 				! puuvsâid
!+N+Pl+Ill:%^WG%^CLEN%>id K ;		! puvssáid
!+N+Pl+Loc:%^WG%>in K ; 				! puuvsâin
!+N+Pl+Com:%^WG%>iguin K ;		! puvsâiguin
!+N+Pl+Com+Err/Orth:%^WG%>igijn K ;		! puvsâiguin
!+N+Pl+Abe:%^WG%>ittáá K ; 		! puvsâittáá

LEXICON 2SKERREEH !!= __@CODE@__  Plural tantum, as 2PUUVSAH, but w/o WG
+N+Pl+Nom:%>h K ;			
+N+Pl+Gen:%^SVSH%>i3 K ; 			
+N+Pl+Acc:%^SVSH%>i3d K ; 			
+N+Pl+Ill:%^SVSH%>i3d K ;	
+N+Pl+Loc:%^SVSH%>i3n K ; 			
+N+Pl+Com:%^SVSH%>i3guin K ;		
+N+Pl+Com+Err/Orth:%^SVSH%>i3gijn K ;		
+N+Pl+Abe:%^SVSH%>i3ttáá K ; 	


LEXICON APINA !!= __@CODE@__  this is for loanwords without stem change
+N: X_APINA ;
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen: R ;
+N:%> Gen_DER ;
+N:%> Nom_DER ;

LEXICON 2APINAH
+N: X_APINAH ;

LEXICON X_APINA !!= __@CODE@__  this is for loanwords without stem change
+Sg+Nom: K ;			! apina
+Sg+Gen: K ; 			! apina
+Sg+Acc: K ; 			! apina
+Sg+Ill:%>n K ; 		! apinan
+Sg+Loc:%>st K ; 		! apinast
+Sg+Com:%>i5n K ; 		! apinain
+Sg+Abe:%>ttáá K ; 	! apinattáá
+Ess:%>n K ; 			! apinan
+Par:%>d K ;			! apinad
X_APINAH ;

LEXICON X_APINAH !!= __@CODE@__  this is for loanwords without stem change
+Pl+Nom:%>h K ; 		! apinah
+Pl+Gen:%>i K ; 		! apinai
+Pl+Acc:%>id K ; 		! apinaid
+Pl+Ill:%>id K ; 		! apinaid
+Pl+Loc:%>in K ;		! apinain
+Pl+Com:%>iguin K ; 	! apinaiguin
+Pl+Com+Err/Orth:%>igijn K ; 	! apinaiguin
+Pl+Abe:%>ittáá K ; 	! apinaittáá




!! !!Four-syllable stems
!    ===================



LEXICON 4KUNAGAS !!= __@CODE@__ >
+N: X_KUNAGAS ;
+N+Cmp/SgNom:%^FCD%> R ;
+N+Cmp/SgGen:%^FCD%> R ;
+N:%^FCD%> Nom_DER ;
+N:%^FCD%> Gen_DER ; !njuolgâduslâš



LEXICON X_KUNAGAS !!= __@CODE@__  this is for loanwords without stem change, 3-syll. cons end
+Sg+Nom:%^FCD K ;				! kunâgâs
+Sg+Gen:%^FCD K ; 				! kunâgâs
+Sg+Acc:%^FCD K ; 				! kunâgâs
+Sg+Ill:%^FCD%>ân K ; 			! kunâgâsân 
+Sg+Loc:%^FCD%>âst K ; 		! kunâgâsâst
+Sg+Com:%>áin K ;	 		! kunâgâssáin
+Sg+Abe:%^FCD%>ttáá K ; 		! kunâgâsttáá
+Ess:%>ân K ; 			! kunâgâssân
+Par:%>âd K ;				! kunâgâssâd
+Pl+Nom:%^FCD%>ah K ; 			! kunâgâsah
+Pl+Gen:%^FCD%>âi K ; 			! kunâgâsâi
+Pl+Acc:%^FCD%>âid K ; 		! kunâgâsâid
+Pl+Ill:%>áid K ; 		! kunâgâssáid
+Pl+Loc:%^FCD%>âin K ;			! kunâgâsâin
+Pl+Com:%^FCD%>âiguin K ; 		! kunâgâsâiguin
+Pl+Com+Err/Orth:%^FCD%>âigijn K ; 		! kunâgâsâiguin
+Pl+Abe:%^FCD%>âittáá K ; 		! kunâgâsâittáá
+Sg+Gen:%^FCD%> CPXsuff ;  ! 
+Sg+Acc:%^FCD%> CPXsuff ;  !  
+Sg+Com:%^FCD%>âin CPXsuff ;  !  
+Sg+Ill:âs CPXsuff ;  !  


LEXICON 4CAHALIG  !!= __@CODE@__
+N+Sg+Nom: K ;              ! čáhálig
+N+Cmp/SgNom: R ;
+N+Cmp/SgGen: R ;
+N+Sg+Gen: K ;            	! čáhálig   
+N+Sg+Acc: K ;              ! čáhálig
+N+Sg+Ill:%>án K ;     		! čáháligán
+N+Sg+Loc:%>est K ;   		! čáháligest 
+N+Sg+Com:%>áin K ; 			! čáháligáin
+N+Sg+Abe:%>ttáá K ;   		! čáháligttáá
+N+Ess:%>gen K ;        		! čáháliggen   
+N+Par:%>ged K ;       		! čáháligged    
+N+Pl+Nom:%>eh K ;    		! čáháligeh
+N+Pl+Gen:%>ij K ;  		! čáháligij
+N+Pl+Acc:%>ijd K ;   		! čáháligijd    
+N+Pl+Ill:%>áid K ; 		! čáháligáid
+N+Pl+Loc:%>ijn K ; 		! čáháligijn
+N+Pl+Com:%>ijguin K ;    	! čáháligijguin
+N+Pl+Com+Err/Orth:%>ijgijn K ;    	! čáháligijguin
+N+Pl+Abe:%>ijttáá K ;    	! čáháligijttáá
+N:%> Nom_DER ;
+N:%> Gen_DER ; 


LEXICON 4MANUTTEP   !!= __@CODE@__ 
+N+Sg+Nom:ep K ;				! mánuttep
+N+Cmp/SgNom:ep%> R ;
+N+Cmp/SgGen:eve%> R ;
+N+Sg+Gen:eve	K ;				! mánutteve
+N+Sg+Acc:eve	K ;				! mánutteve
+N+Sg+Ill:iäp%>án	K ;			! mánuttiäpán	
+N+Sg+Loc:eeve%>est	K ;			! mánutteeveest
+N+Sg+Com:eevi%>jn	K ;			! mánuttevijn
+N+Sg+Abe:eve%>ttáá	K ;			! mánuttevettáá
+N+Ess:eppe%>n		K ;			! mánutteppen
+N+Par:eppe%>d		K ;			! mánuttepped
+N+Pl+Nom:eve%>h	K ;				! mánutteveh
+N+Pl+Gen:evi%>j	K ;				! mánuttevij
+N+Pl+Acc:evi%>jd	K ;			! mánuttevijd
+N+Pl+Ill:eevi%>jd	K ;			! mánutteevijd
+N+Pl+Loc:evi%>jn	K ;			! mánuttevijn
+N+Pl+Com:evi%>jguin	K ;			! mánuttevijguin
+N+Pl+Com+Err/Orth:evi%>jgijn	K ;			! mánuttevijguin
+N+Pl+Abe:evi%>jttáá	K ;			! mánuttevijttáá
+N+Cmp/SgNom:e R ;


LEXICON 4PUHELIN		!!= __@CODE@__  Only a few words in this category
+N+Sg+Nom:%^FCDn K ;				! puhelin
+N+Cmp/SgNom:%^FCDn%> R ;
+N+Cmp/SgGen:%^FCDn%> R ;
+N+Sg+Gen:%^FCDn	K ;					! puhelin
+N+Sg+Acc:%^FCDn	K ;					! puhelin
+N+Sg+Ill:%>ân	K ;				! puhelimân			
+N+Sg+Loc:%>ist	K ;				! puhelimist
+N+Sg+Com:%>áin	K ;				! puhelimáin
+N+Sg+Abe:%^FCDn%>ttáá	K ;			! puhelinttáá
+N+Ess:%>in		K ;			! puhelimin
+N+Par:%>id		K ;				! puhelimid
+N+Pl+Nom:%>eh	K ;				! puhelimeh
+N+Pl+Gen:%>ij	K ;				! puhelimij
+N+Pl+Acc:%>ijd	K ;				! puhelimijd
+N+Pl+Ill:%>áid	K ;				! puhelimáid
+N+Pl+Loc:%>ijn	K ;				! puhelimijn
+N+Pl+Com:%>ijguin	K ;			! puhelimijguin
+N+Pl+Com+Err/Orth:%>ijgijn	K ;			! puhelimijguin
+N+Pl+Abe:%>ijttáá	K ;			! puhelimijttáá

LEXICON 2KOLLIISTALLAM  !!= __@CODE@__
+N+Sg+Nom:lâm K ;               ! kolliistâllâm
+N+Cmp/SgNom:lám R ;
+N+Cmp/SgGen:lám R ;
+N+Sg+Gen:lâm K ;            	! kolliistâllâm   
+N+Sg+Acc:lâm K ;               ! kolliistâllâm
+N+Sg+Ill:m%>ân K ;     ! kolliistâlmân
+N+Sg+Loc:m%>ist K ;   ! kolliistâlmist 
+N+Sg+Com:m%>áin K ; 	! kolliistâlmáin
+N+Sg+Abe:lâm%>ttáá K ;     !  kolliistâllâmttáá
+N+Ess:m%>in K ;        ! kolliistâlmin    
+N+Par:m%>id K ;        ! kolliistâlmid    
+N+Pl+Nom:m%>eh K ;     ! kolliistâlmeh
+N+Pl+Gen:m%>ij K ;  	! kolliistâlmij
+N+Pl+Acc:m%>ijd K ;     ! kolliistâlmijd    
+N+Pl+Ill:m%>áid K ; 	! kolliistâlmáid
+N+Pl+Loc:m%>ijn K ; 	! kolliistâlmijn
+N+Pl+Com:m%>ijguin K ;    !  kolliistâlmijguin
+N+Pl+Com+Err/Orth:m%>ijgijn K ;    !  kolliistâlmijguin
+N+Pl+Abe:m%>ijttáá K ;    ! kolliistâlmijttáá
+N:lâm%> Nom_DER ;
+N:lâm%> Gen_DER ; 



!! !!Odd-syllable stems
!    ===================


LEXICON 3KEPIDEM  !!= __@CODE@__ 
+N+Sg+Nom:em K ;          			! kepidem
+N+Cmp/SgNom:em R ;
+N+Sg+Gen:em K ;        				! kepidem
+N+Sg+Acc:em K ;        				! kepidem 
+N+Sg+Ill:m%^CLEN%^RLEN%^SLEN%>ân K ; 			! keeppidmân
+N+Sg+Loc:m%^CLEN%^RLEN%^SLEN%>ist K ; 			! keeppidmist 
+N+Sg+Com:m%^CLEN%^RLEN%^SLEN%>áin K ; 			! keeppidmáin
+N+Sg+Abe:em%>ttáá K ;     			! kepidemttáá
+N+Ess:m%^CLEN%^RLEN%^SLEN%>in K ;   				! keeppidmin   
+N+Par:m%^CLEN%^RLEN%^SLEN%>id K ;  				! keeppidmid    
+N+Pl+Nom:m%^CLEN%^RLEN%^SLEN%>eh K ; 			! keeppidmeh
+N+Pl+Gen:m%^CLEN%^RLEN%^SLEN%>ij K ; 			! keeppidmij
+N+Pl+Acc:m%^CLEN%^RLEN%^SLEN%>ijd K ;			! keeppidmijd
+N+Pl+Ill:m%^CLEN%^RLEN%^SLEN%>áid K ; 			! keeppidmáid
+N+Pl+Loc:m%^CLEN%^RLEN%^SLEN%>ijn K ;			! keeppidmijn
+N+Pl+Com:m%^CLEN%^RLEN%^SLEN%>ijguin K ; 		! keeppidmijguin
+N+Pl+Com+Err/Orth:m%^CLEN%^RLEN%^SLEN%>ijgijn K ; 		! keeppidmijguin
+N+Pl+Abe:m%^CLEN%^RLEN%^SLEN%>ijttáá K ; 		! keeppidmijttáá
+N:em%> Nom_DER ;
+N:em%> Gen_DER ; 


LEXICON 3POTTAAK  !!= __@CODE@__ 
+N+Sg+Nom: K ;        		   		! pottáák
+N+Cmp/SgNom: R ;
+N+Sg+Gen: K ;    		 			! pottáák
+N+Sg+Acc: K ;     		 			! pottáák 
+N+Sg+Ill:%^SC%^CSH%^SVSH%>ân K ;		! potákkân
+N+Sg+Loc:%^SC%^CSH%^SVSH%>ist K ; 		! potákkist 
+N+Sg+Com:%^SC%^CSH%^SVSH%>áin K ;		! potákkáin
+N+Sg+Abe:%>ttáá K ; 				! pottáákttáá
+N+Ess:%^SC%^CSH%^SVSH%>in K ; 			! potákkin 
+N+Par:%^SC%^CSH%^SVSH%>id K ; 			! potákkid
+N+Pl+Nom:%^SC%^CSH%^SVSH%>eh K ; 	 	! potákkeh
+N+Pl+Gen:%^SC%^CSH%^SVSH%>ij K ; 		! potákkij
+N+Pl+Acc:%^SC%^CSH%^SVSH%>ijd K ; 		! potákkijd
+N+Pl+Ill:%^SC%^CSH%^SVSH%>áid K ; 		! potákkáid
+N+Pl+Loc:%^SC%^CSH%^SVSH%>ijn K ; 		! potákkijn
+N+Pl+Com:%^SC%^CSH%^SVSH%>ijguin K ; 	! potákkijguin
+N+Pl+Com+Err/Orth:%^SC%^CSH%^SVSH%>ijgijn K ; 	! potákkijguin
+N+Pl+Abe:%^SC%^CSH%^SVSH%>ijttáá K ;   ! potákkijttáá
+N:%> Nom_DER ;
+N:%> Gen_DER ; 


LEXICON 3EEMEED  !!= __@CODE@__
+N: X_EEMEED ;
+N+Cmp/SgNom: R ;
+N+Pl+Gen:%^CSH%^SVSH%>ijd CPXsuff ;  ! 
+N+Pl+Acc:%^CSH%^SVSH%>ijd CPXsuff ;  ! skipárijdis
+N+Pl+Gen:%^CSH%^SVSH%> CPXsuff ;  ! elimis
+N:%> Gen_DER ; !
+N:%> Nom_DER ; !eennâmlâš

LEXICON 3STEVILEH  !!= __@CODE@__
+N: X_STEVILEH ;


LEXICON X_EEMEED  !!= __@CODE@__
+Sg+Nom: K ;					! eemeed
+Sg+Gen: K ;					! eemeed
+Sg+Acc: K ;					! eemeed
+Sg+Ill:%^CSH%^SVSH%>ân K ;	! emedân
+Sg+Loc:%^CSH%^SVSH%>ist K ;	! emedist
+Sg+Com:%^CSH%^SVSH%>áin K ;	! emedáin
+Sg+Abe:%>ttáá K ;		! eemeedttáá
+Ess:%^CSH%^SVSH%>in K ;		! emedin
+Par:%^CSH%^SVSH%>id K ;		! emedid
X_STEVILEH ;



LEXICON X_STEVILEH  !!= __@CODE@__  plurals for 3EEMEED
+Pl+Nom:%^CSH%^SVSH%>eh K ;		! emedeh
+Pl+Gen:%^CSH%^SVSH%>ij K ;		! emedij
+Pl+Acc:%^CSH%^SVSH%>ijd K ;	! emedijd
+Pl+Ill:%^CSH%^SVSH%>áid K ;	! emedáid
+Pl+Loc:%^CSH%^SVSH%>ijn K ;	! emedijn
+Pl+Com:%^CSH%^SVSH%>ijguin K ;	! emedijguin
+Pl+Com+Err/Orth:%^CSH%^SVSH%>ijgijn K ;	! emedijguin
+Pl+Abe:%^CSH%^SVSH%>ijttáá K ;	! emedijttáá

LEXICON 3LITTER !!= __@CODE@__ MO Ib 2nd syll vowel does not alternate
+N: X_LITTER ; 
+N+Cmp/SgNom: R ;
+N:%> Gen_DER ;
+N:%> Nom_DER ;

LEXICON X_LITTER !!= __@CODE@__ 
 ODD_SUFF_LONGSTEM ; 
 ODD_SUFF_SHORTSTEM ;

LEXICON X_LITTER_not_Par !!= __@CODE@__ 
ODD_SUFF_LONGSTEM ;
 +Sg+Loc:%>ist K ;
 +Sg+Com:%>áin K ;
 +Sg+Abe:%>ttáá K ;
 +Ess:%>in K ;
 +Pl+Nom:%>eh K ;
 +Pl+Gen:%>ij K ;
 +Pl+Acc:%>ijd K ;
 +Pl+Ill:%>áid K ;
! +Pl+Ill:%>ááid K ;
 +Pl+Loc:%>ijn K ;
 +Pl+Com+Err/Orth:%>ijgijn K ;
 +Pl+Com:%>ijguin K ;
 +Pl+Abe:%>ijttáá K ;



LEXICON 3PIEHAIN  !!= __@CODE@__ to v1 3PIEHAINonly and v2 3PIEHANJ
+v1: 3PIEHAINonly ;
+v2: 3PIEHANJ ;

LEXICON 3PIEHAINonly  !!= __@CODE@__ the v1 version
+N+Sg+Nom:%^CLEN%^RLEN K ;        		   		! piehâin
+N+Cmp/SgNom:%^CLEN%^RLEN%> R ;
+N+Sg+Gen:%^CLEN%^RLEN K ;    		 			! piehâin
+N+Sg+Acc:%^CLEN%^RLEN K ;     		 			! piehâin 
PIEHAIN/NJ ;

LEXICON 3PIEHANJ  !!= __@CODE@__  the v2 version
+N+Sg+Nom:j%^CLEN%^RLEN%^SVSH K ;        		   		! piehânj
+N+Cmp/SgNom:j%^CLEN%^RLEN%^SVSH%> R ;
+N+Sg+Gen:j%^CLEN%^RLEN%^SVSH K ;    		 			! piehânj
+N+Sg+Acc:j%^CLEN%^RLEN%^SVSH K ;     		 			! piehânj 
PIEHAIN/NJ ;

LEXICON PIEHAIN/NJ !!= __@CODE@__ this is the common part of v1, v2
+N+Sg+Ill:j%^SVSH%>ân K ;			! piehânjân
+N+Sg+Loc:j%^SVSH%>ist K ; 			! piehânjist 
+N+Sg+Com:j%^SVSH%>áin K ;			! piehânjáin
+N+Sg+Abe:%^CLEN%^RLEN%>ttáá K ;	! piehâinttáá
+N+Ess:j%^SVSH%>in K ; 				! piehânjin 
+N+Par:j%^SVSH%>id K ; 				! piehânjid
+N+Pl+Nom:j%^SVSH%>eh K ;  			! piehânjeh
+N+Pl+Gen:j%^SVSH%>ij K ;  			! piehânjij
+N+Pl+Acc:j%^SVSH%>ijd K ; 			! piehânjijd
+N+Pl+Ill:j%^SVSH%>áid K ; 			! piehânjáid
+N+Pl+Loc:j%^SVSH%>ijn K ; 			! piehânjijn
+N+Pl+Com:j%^SVSH%>ijguin K ; 		! piehânjijguin
+N+Pl+Com+Err/Orth:j%^SVSH%>ijgijn K ; 		! piehânjijguin
+N+Pl+Abe:j%^SVSH%>ijttáá K ;  		! piehânjijttáá 


LEXICON 3VANHIMEH !!= __@CODE@__ plurals for 3LITTER
+N+Cmp/SgNom: R ;
+N+Pl+Nom:%>eh K ;
+N+Pl+Gen:%>ij K ;
+N+Pl+Acc:%>ijd K ;
+N+Pl+Ill:%>áid K ;
+N+Pl+Loc:%>ijn K ;
+N+Pl+Com:%>ijguin K ;
+N+Pl+Com+Err/Orth:%>ijgijn K ;
+N+Pl+Abe:%>ijttáá K ;

LEXICON 3KIELES  !!= __@CODE@__  (to be unified?) 
+N:  X_KIELES ;
+N:%^WG%^RLEN%> Nom_DER ;
+N:%> Gen_DER ; 

LEXICON X_KIELES  !!= __@CODE@__  (to be unified?) 
+Sg+Nom:%^WG%^RLEN K ; !kielâs
+Cmp/SgNom:%^WG%> R ;
+Cmp/SgNom:%^WG%^RLEN%> Rhyph ;
+Ess:%^WG%^RLEN%>in K ;   !kielâsin 
+Par:%^WG%^RLEN%>id K ;    !kielâsid   
 KIELES_PELES ;

LEXICON 3PELES  !!= __@CODE@__ only peles (to be unified?) 
+N:  X_PELES ;
+N:%^WG%^RLEN%> Nom_DER ;
+N:%> Gen_DER ; 

LEXICON X_PELES  !!= __@CODE@__ only peles (to be unified?) 
+Sg+Nom:%^WG%^RVSH K ; !peles
+Cmp/SgNom:%^WG%^RVSH%> R ;
+Ess:%^WG%^RVSH%>in K ;   !pelesin 
+Par:%^WG%^RVSH%>id K ;    !pelesid   
 KIELES_PELES ;


LEXICON KIELES_PELES  !!= __@CODE@__  (to be unified?) 
+Sg+Gen: K ;   !kiellâs    
+Sg+Acc: K ;      !kiellâs
+Sg+Ill:%^CSH%>ân K ;  !kielâsân
+Sg+Loc:%^CSH%>ist K ; !kielâsist
+Sg+Com:%^CSH%>áin K ; !kielâsáin
+Sg+Abe:%>ttáá K ;  !kiellâsttáá
+Pl+Nom:%^CSH%>eh K ;  !kielâseh
+Pl+Gen:%^CSH%>ij K ;  !kielâsij
+Pl+Acc:%^CSH%>ijd K ; !kielâsijd
+Pl+Loc:%^CSH%>ijn K ; !kielâsáid
+Pl+Com:%^CSH%>ijguin K ; !kielâsijn
+Pl+Com+Err/Orth:%^CSH%>ijgijn K ; !kielâsijn
+Pl+Abe:%^CSH%>ijttáá K ; !kielâsijguin
+Pl+Ill:%^CSH%>áid K ; !kielâsijttáá

LEXICON 3JAAVVIL !!= __@CODE@__ 
+N: X_JAAVVIL ;
+N+Cmp/SgNom:%^WG%^CLEN%^RLEN%> R ;

LEXICON 3JAHALEH !!= __@CODE@__ 
+N: X_JAAVVIL_PL ;
+N+Cmp/SgNom:%^WG%^CLEN%^RLEN%> R ;


LEXICON X_JAAVVIL !!= __@CODE@__ 
+Sg+Nom:%^WG%^CLEN%^RLEN K ;	! jäävvil
+Sg+Gen:%^SLEN K ;     			! jáhháál
+Sg+Acc:%^SLEN K ;    			 ! jáhháál
+Sg+Ill:%^CSH%>ân K ; 			! jáhálân
+Sg+Loc:%^CSH%>ist K ; ! jáhálist
+Sg+Com:%^CSH%>áin K ; ! jáháláin
+Sg+Abe:%^SLEN%>ttáá K ; ! jáhháálttáá
+Ess:%^WG%^RVSH%>in K ;   ! jävilin  jáhálin
+Par:%^WG%^RVSH%>id K ;	! jävilid jáhálid
+Ess:%^CSH%>in K ;   ! pápárin 
+Par:%^CSH%>id K ;	! pápárid
X_JAAVVIL_PL ;

LEXICON X_JAAVVIL_PL !!= __@CODE@__ 
+Pl+Nom:%^CSH%>eh K ;  ! 
+Pl+Gen:%^CSH%>ij K ;  ! 
+Pl+Acc:%^CSH%>ijd K ; ! 
+Pl+Ill:%^CSH%>áid K ; ! 
+Pl+Loc:%^CSH%>ijn K ; ! 
+Pl+Com:%^CSH%>ijguin K ;  ! 
+Pl+Com+Err/Orth:%^CSH%>ijgijn K ;  ! 
+Pl+Abe:%^CSH%>ijttáá K ;  ! 


LEXICON 3PAAVIR !!= __@CODE@__ NGA+Abe blank, WG Ess, CSH for the rest
+N: X_PAAVIR ;
+N+Ess:%^CSH%>in K ;   ! pápárin 
+N+Par:%^CSH%>id K ;	! pápárid
+N+Cmp/SgNom:%^WG%^RLEN%> R ;
+N:%^WG%^RLEN%> Nom_DER ;
+N:%^SLEN%> Gen_DER ; 


LEXICON 3PAPAREH !!= __@CODE@__ plural forms
+N: X_PAPAREH ;

LEXICON X_PAAVIR !!= __@CODE@__ NGA+Abe blank, WG Ess, CSH for the rest
+Par:%^WG%^RVSH%>id K ;	! pävirid
X_PAAVIR_not_Par ;

LEXICON X_PAAVIR_not_Par !!= __@CODE@__ NGA+Abe blank, WG Ess, CSH for the rest
+Sg+Nom:%^WG%^RLEN K ;	! päävir
+Sg+Gen:%^SLEN K ;     			! pááppár
+Sg+Acc:%^SLEN K ;    			 ! pááppár
+Sg+Ill:%^CSH%>ân K ; 			! pápárân
+Sg+Loc:%^CSH%>ist K ; ! pápárist
+Sg+Com:%^CSH%>áin K ; ! pápáráin
+Sg+Abe:%^SLEN%>ttáá K ; ! pááppárttáá
+Ess:%^WG%^RVSH%>in K ;   ! pävirin 
X_PAPAREH ;



LEXICON X_PAPAREH !!= __@CODE@__ plural forms
+Pl+Nom:%^CSH%>eh K ;  ! pápáreh
+Pl+Gen:%^CSH%>ij K ;  ! pápárij
+Pl+Acc:%^CSH%>ijd K ; ! pápárijd
+Pl+Ill:%^CSH%>áid K ; ! pápáráid
+Pl+Loc:%^CSH%>ijn K ; ! pápárijn
+Pl+Com:%^CSH%>ijguin K ;  ! pápárijguin
+Pl+Com+Err/Orth:%^CSH%>ijgijn K ;  ! pápárijguin
+Pl+Abe:%^CSH%>ijttáá K ;  ! pápárijttáá


LEXICON 3SAJOS !!= __@CODE@__ 
+N: X_SAJOS ;
+N+Cmp/SgNom:%^WG%^SVSH%>  R ;
+N:%^WG%^SVSH%> Nom_DER ;
+N:%> Gen_DER ; 

LEXICON X_SAJOS !!= __@CODE@__ 
+Sg+Nom:%^WG%^SVSH K ;	! almos
+Sg+Gen: K ;     			! almoos  a%^RVlmoos
+Sg+Acc: K ;    			 ! almoos
+Sg+Ill:%^CSH%^SVSH%>ân K ; 			! almosân
+Sg+Loc:%^CSH%^SVSH%>ist K ; ! almosist
+Sg+Com:%^CSH%^SVSH%>áin K ; ! almosáin
+Sg+Abe:%>ttáá K ; ! almoosttáá
+Ess:%^WG%^SVSH%>in K ;   ! almosin 
+Ess:%^CSH%^SVSH%>in K ;   !   
+Par:%^WG%^SVSH%>id K ;	! almosid
+Par:%^CSH%^SVSH%>id K ;	! 
+Pl+Nom:%^CSH%^SVSH%>eh K ;  ! almoseh
+Pl+Gen:%^CSH%^SVSH%>ij K ;  ! almosij
+Pl+Acc:%^CSH%^SVSH%>ijd K ; ! almosijd
+Pl+Ill:%^CSH%^SVSH%>áid K ; ! almosáid
+Pl+Loc:%^CSH%^SVSH%>ijn K ; ! almosijn
+Pl+Com:%^CSH%^SVSH%>ijguin K ;  ! almosijguin
+Pl+Com+Err/Orth:%^CSH%^SVSH%>ijgijn K ;  ! almosijguin
+Pl+Abe:%^CSH%^SVSH%>ijttáá K ;  ! almosijttáá




LEXICON 3CIERMIH  !!= __@CODE@__ 
+N+Sg+Nom:%^WG K ;
+N+Cmp/SgNom:%^WG%>  R ;
+N+Sg+Gen: K ;     
+N+Sg+Acc: K ;     
+N+Sg+Ill:%^RVSH%>ân K ; 
+N+Sg+Loc:%^RVSH%>ist K ; 
+N+Sg+Com:%^RVSH%>áin K ; 
+N+Sg+Abe:%>ttáá K ; 
+N+Ess:%^RVSH%>in K ;   
+N+Par:%^RVSH%>id K ;       
+N+Pl+Nom:%^RVSH%>eh K ;  
+N+Pl+Gen:%^RVSH%>ij K ;  
+N+Pl+Acc:%^RVSH%>ijd K ; 
+N+Pl+Ill:%^RVSH%>áid K ; 
+N+Pl+Loc:%^RVSH%>ijn K ; 
+N+Pl+Com:%^RVSH%>ijguin K ; 
+N+Pl+Com+Err/Orth:%^RVSH%>ijgijn K ; 
+N+Pl+Abe:%^RVSH%>ijttáá K ; 
+N:%^WG%> Nom_DER ;
+N:%> Gen_DER ; 


LEXICON 3UAIVAT !!= @CODE@ > 3EEMEED
+N+Sg+Nom:%^SVSH K ;					! uáivát
+N+Cmp/SgNom:%^SVSH%>  R ;
+N+Sg+Gen: K ;					! uáivát
+N+Sg+Acc: K ;					! uáivát
+N+Sg+Ill:%>ân K ;	! uáivátân - uáiváttân
+N+Sg+Loc:%>ist K ;	! uáivátist uáiváttist
+N+Sg+Com:%>áin K ;	! uáivátáin uáiváttáin
+N+Sg+Abe:%>ttáá K ;		! uáiváttáá
+N+Ess:%>tin K ;		! uáiváttin (both uái4vat and uái4vatt)
+N+Par:%>tid K ;		! uáiváttid
+N+Pl+Nom:%>eh K ;		! uáiváteh uáivátteh
+N+Pl+Gen:%>ij K ;		! uáivátij uáiváttij
+N+Pl+Acc:%>ijd K ;	! uáivátijd uáiváttijd
+N+Pl+Ill:%>áid K ;	! uáivátáid uáiváttáid
+N+Pl+Loc:%>ijn K ;	! uáivátijn uáiváttijn
+N+Pl+Com:%>ijguin K ;	! uáivátijguin uáiváttijguin
+N+Pl+Com+Err/Orth:%>ijgijn K ;	! uáivátijgijn uáiváttijgijn
+N+Pl+Abe:%>ijttáá K ;	! uáivátijttáá uáiváttijttáá
+N:%^SVSH%> Nom_DER ;
+N:%> Gen_DER ; 

LEXICON 3RUOTAS !!= @CODE@ 
+N+Cmp/SgNom:%^CSH%^RLEN%> R ;
+N: X_RUOTAS ;
+N:%^CSH%^RLEN%> Nom_DER ;
+N:%> Gen_DER ; 


LEXICON X_RUOTAS !!= @CODE@ 
+Sg+Nom:%^WG%^RLEN K ;	!  
+Sg+Gen: K ;     			! viäskár máállás%^ÁI
+Sg+Acc: K ;    			 ! viäskár
+Sg+Ill:%>ân K ; 			! viäskárân
+Sg+Loc:%>ist K ; ! viäskárist
+Sg+Com:%>áin K ; ! viäskáráin
+Sg+Abe:%>ttáá K ; ! viäskárttáá
+Ess:%^WG%>in K ;   ! veskirin 
+Par:%^WG%>id K ;	! veskirid
+Sg+Gen: CPXsuff ;  ! 
+Sg+Acc: CPXsuff ;  ! pirrâsis
+Sg+Ill:âs CPXsuff ;  ! pirrâsâsân
X_VEESKIR_PL ;


LEXICON 3VEESKIR !!= __@CODE@__ 
+N: X_VEESKIR ;
+N+Ess:%>in K ;   ! viäskárin 
+N+Par:%>id K ;	! viäskárid
+N+Cmp/SgNom:%^WG%^RLEN%> R ;
+N:%^WG%^RLEN%> Nom_DER ;
+N:%> Gen_DER ; 


LEXICON 3PIIVTAS !!= __@CODE@__ WG for NEP,  no triggers for the other cases
+N: X_VEESKIR ;
+N+Cmp/SgNom:%^WG%> R ;


LEXICON X_VEESKIR !!= @CODE@ 
+Sg+Nom:%^WG%^RLEN K ;	! veeskir
+Sg+Gen: K ;     			! viäskár máállás%^ÁI
+Sg+Acc: K ;    			 ! viäskár
+Sg+Ill:%>ân K ; 			! viäskárân
+Sg+Loc:%>ist K ; ! viäskárist
+Sg+Com:%>áin K ; ! viäskáráin
+Sg+Abe:%>ttáá K ; ! viäskárttáá
+Ess:%^WG%^RVSH%>in K ;   ! veskirin 
+Par:%^WG%^RVSH%>id K ;	! veskirid
+Sg+Gen: CPXsuff ;  ! 
+Sg+Acc: CPXsuff ;  ! pirrâsis
+Sg+Ill:âs CPXsuff ;  ! pirrâsâsân
X_VEESKIR_PL ;

LEXICON X_VEESKIR_PL !!= @CODE@ 
+Pl+Nom:%>eh K ;  ! viäskáreh
+Pl+Gen:%>ij K ;  ! viäskárij
+Pl+Acc:%>ijd K ; ! viäskárijd
+Pl+Ill:%>áid K ; ! viäskáráid
+Pl+Loc:%>ijn K ; ! viäskárijn
+Pl+Com:%>ijguin K ;  ! viäskárijguin
+Pl+Com+Err/Orth:%>ijgijn K ;  ! viäskárijguin
+Pl+Abe:%>ijttáá K ;  ! viäskárijttáá
+Pl+Gen:%>ijd CPXsuff ;  !
+Pl+Acc:%>ijd CPXsuff ;  !
+Pl+Com:%>ijd CPXsuffCOM ;  !

LEXICON 3KAAVPUG  !!= __@CODE@__ 
+v1+N: X_LITTER ; !kaavpug kaavpugân
+v2: 3CIERMIH ; !kaavpug kavpugân


LEXICON 3AARRAN !!= __@CODE@__ 
+N: X_AARRAN ;
+N+Cmp/SgNom: R ;
+N:%> Nom_DER ;
+N:%> Gen_DER ; 

LEXICON X_AARRAN !!= __@CODE@__ 
+Sg+Nom: K ;					! aarrân
+Sg+Gen: K ;					! aarrân
+Sg+Acc: K ;					! aarrân
+Sg+Ill:%^CSH%>ân K ;	! aṛânân
+Sg+Loc:%^CSH%>ist K ;	! aṛânist
+Sg+Com:%^CSH%>áin K ;			! aṛânáin
+Sg+Abe:%>ttáá K ;			! aarrânttáá
+Ess:%^CSH%>in K ;		! aṛânin
+Par:%^CSH%>id K ;		! aṛânid
X_AARRAN_PL ;

LEXICON 3JESANEH !!= __@CODE@__ plurals for 3AARRAN
+N: X_AARRAN_PL ;

LEXICON X_AARRAN_PL !!= __@CODE@__ plurals for 3AARRAN
+Pl+Nom:%^CSH%>eh K ;	! aṛâneh
+Pl+Gen:%^CSH%>ij K ;	! aṛânij
+Pl+Acc:%^CSH%>ijd K ;	! aṛânijd
+Pl+Ill:%^CSH%>áid K ;	! aṛânáid
+Pl+Loc:%^CSH%>ijn K ;	! aṛânijn
+Pl+Com:%^CSH%>ijguin K ;	! aṛânijguin
+Pl+Com+Err/Orth:%^CSH%>ijgijn K ;	! aṛânijguin
+Pl+Abe:%^CSH%>ijttáá K ;	! aṛânijttáá



LEXICON 3RUONAS !!= __@CODE@__ WG NEP,  blank for the rest
+N: X_RUONAS ;
+N+Cmp/SgNom:%^WG%^RLEN%> R ;
+N:%^WG%^RLEN%> Nom_DER ;
+N:%> Gen_DER ; 

LEXICON X_RUONAS !!= __@CODE@__ WG NEP,  blank for the rest
+Sg+Nom:%^WG%^RLEN K ;	! mielgâs 
+Sg+Gen: K ;     			! mielgâs ! This is the stem
+Sg+Acc: K ;    			 ! mielgâs
+Sg+Ill:%^CSH%>ân K ; 			! mielgâsân
+Sg+Loc:%^CSH%>ist K ; ! mielgâsist
+Sg+Com:%^CSH%>áin K ; ! mielgâsáin
+Sg+Abe:%>ttáá K ; ! pihtâsttáá
+Ess:%^CSH%>in K ;   ! pivtâsin 
+Par:%^CSH%>id K ;	! pivtâsid
+Pl+Nom:%^CSH%>eh K ;  ! mielgâseh
+Pl+Gen:%^CSH%>ij K ;  ! mielgâsij
+Pl+Acc:%^CSH%>ijd K ; ! mielgâsijd
+Pl+Ill:%^CSH%>áid K ; ! mielgâsáid
+Pl+Loc:%^CSH%>ijn K ; ! mielgâsijn
+Pl+Com:%^CSH%>ijguin K ;  ! mielgâsijguin
+Pl+Com+Err/Orth:%^CSH%>ijgijn K ;  ! mielgâsijguin
+Pl+Abe:%^CSH%>ijttáá K ;  ! mielgâsijttáá



LEXICON 3KUALUS !!= __@CODE@__ ,  3RUONAS as CSH
+N+Sg+Nom:%^WG%^RLEN K ;	! kuálus 
+N+Cmp/SgNom:%^WG%^RLEN%> R ;
+N+Sg+Gen: K ;     			! kuállus
+N+Sg+Acc: K ;    			 ! kuállus
+N+Sg+Ill:%>ân K ; 			! kuállusân
+N+Sg+Loc:%>ist K ; ! kuállusist
+N+Sg+Com:%>áin K ; ! kuállusáin
+N+Sg+Abe:%>ttáá K ; ! kuállusttáá
+N+Ess:%^CSH%>in K ;   ! kuálusin
+N+Par:%^CSH%>id K ;	! kuálusid
+N+Pl+Nom:%>eh K ;  ! kuálluseh
+N+Pl+Gen:%>ij K ;  ! kuállusij
+N+Pl+Acc:%>ijd K ; ! kuállusijd
+N+Pl+Ill:%>áid K ; ! kuállusáid
+N+Pl+Loc:%>ijn K ; ! kuállusijn
+N+Pl+Com:%>ijguin K ;  ! kuállusijguin
+N+Pl+Com+Err/Orth:%>ijgijn K ;  ! kuállusijguin
+N+Pl+Abe:%>ijttáá K ;  ! kuállusijttáá
+N:%^WG%^RLEN%> Nom_DER ;
+N:%> Gen_DER ; 



LEXICON 3VUONJAS  !!= __@CODE@__ 
+N+Sg+Nom:š%^WG K ; 	! vuonjâš
+N+Cmp/SgNom:š%^WG%> R ;
+N+Sg+Gen:š K ; 		! vuonnjâš
+N+Sg+Acc:š K ; 		! vuonnjâš
+N+Sg+Ill:čč%^CSH%>ân K ;	! vuonjâččân
+N+Sg+Ill:č%^CSH%>ân K ;		! vuonjâčân
+N+Sg+Loc:čč%^CSH%>ist K ; 	! vuonjâččist
+N+Sg+Loc:č%^CSH%>ist K ; 	! vuonjâčist
+N+Sg+Com:čč%^CSH%>áin K ; 	! vuonjâččáin
+N+Sg+Com:č%^CSH%>áin K ; 	! vuonjâčáin
+N+Sg+Abe:%>šttáá K ; 	! vuonnjâšttáá
+N+Ess:čč%^CSH%>in K ; 		! vuonjâččin
+N+Par:čč%^CSH%>id K ; 		! vuonjâččid
+N+Pl+Nom:čč%^CSH%>eh K ; 	! vuonjâččeh
+N+Pl+Nom:%^CSH%>čeh K ; 	! vuonjâčeh
+N+Pl+Gen:čč%^CSH%>ij K ; 	! vuonjâččij
+N+Pl+Gen:%^CSH%>čij K ; 	! vuonjâčij
+N+Pl+Acc:%^CSH%>ččijd K ; 	! vuonjâččijd
+N+Pl+Acc:%^CSH%>čijd K ; 	! vuonjâčijd
+N+Pl+Ill:%^CSH%>ččáid K ; 	! vuonjâččáid
+N+Pl+Ill:%^CSH%>čáid K ; 	! vuonjâčáid
+N+Pl+Loc:%^CSH%>ččijn K ; 	! vuonjâččijn
+N+Pl+Loc:%^CSH%>čijn K ; 	! vuonjâčijn
+N+Pl+Com:%^CSH%>ččijguin K ; ! vuonjâččijguin
+N+Pl+Com+Err/Orth:%^CSH%>ččijgijn K ; ! vuonjâččijguin
+N+Pl+Com:%^CSH%>čijguin K ; ! vuonjâčijguin
+N+Pl+Com+Err/Orth:%^CSH%>čijgijn K ; ! vuonjâčijguin
+N+Pl+Abe:%^CSH%>ččijttáá K ; ! vuonjâččijttáá
+N+Pl+Abe:%^CSH%>čijttáá K ; ! vuonjâčijttáá



LEXICON 3CEVE !!= @CODE@ only for this lemma. It could go to 3LYEME but needs modifications of rules to get iä:e and -tt-, and the worst part is v:vv
+N+Sg+Nom:eve K ;
+N+Cmp/SgNom:eve R ;
+N+Sg+Gen:iäppát K ;
+N+Sg+Acc:iäppát K ;
+N+Sg+Ill:iäpáttân K ;
+N+Sg+Loc:iäpáttist K ;
+N+Sg+Com:iäpáttáin K ;
+N+Sg+Abe:iäppáttáá K ;
+N+Ess:iäpáttin K ;   
+N+Ess:evveen		    K ;
+N+Par:iäpáttid	    K ;
+N+Par:evveed		    K ;
+N+Pl+Nom:iäpátteh	    K ;
+N+Pl+Gen:iäpáttij	    K ;
+N+Pl+Acc:iäpáttijd    K ;
+N+Pl+Ill:iäpáttáid    K ;
+N+Pl+Loc:iäpáttijn    K ;
+N+Pl+Com:iäpáttijguin K ;
+N+Pl+Com+Err/Orth:iäpáttijgijn K ;
+N+Pl+Abe:iäpáttijttáá K ;


LEXICON 3LYEME !!= __@CODE@__
+N+Sg+Nom:%^FCD%^WG%^RLEN%^SVSH K ; 		! lyeme savo
+N+Cmp/SgNom:%^FCD%^WG%^RLEN%^SVSH%>  R ;
+N+Sg+Gen:K ;					! luámmán savvoon
+N+Sg+Acc:K ;		 ! luámmán
+N+Sg+Ill:%^CSH%^SVSH%>ân K ; 	! luáṃánân
+N+Sg+Loc:%^CSH%^SVSH%>ist K ;  ! luáṃánist
+N+Sg+Com:%^CSH%^SVSH%>áin K ;  ! luáṃánáin
+N+Sg+Abe:%>ttáá K ; 			! luámmánttáá
+N+Ess:%^FCD%^WG%^RLEN%^SLEN%>n K ;  	! lyemeen
+N+Ess:%^CSH%>in K ; 		! lyeme
+N+Par:%^FCD%^WG%^RLEN%^SLEN%>d K ;     ! lyemeed
+N+Par:%^CSH%>id K ; 		! lyeme
+N+Pl+Nom:%^CSH%^SVSH%>eh K ; ! luáṃáneh
+N+Pl+Gen:%^CSH%^SVSH%>ij K ;  ! luáṃánij
+N+Pl+Acc:%^CSH%^SVSH%>ijd K ; ! luáṃánijd
+N+Pl+Ill:%^CSH%^SVSH%>áid K ; ! luáṃánáid
+N+Pl+Loc:%^CSH%^SVSH%>ijn K ; ! luáṃánijn
+N+Pl+Com:%^CSH%^SVSH%>ijguin K ;  ! luáṃánijguin
+N+Pl+Com+Err/Orth:%^CSH%^SVSH%>ijgijn K ;  ! luáṃánijguin
+N+Pl+Abe:%^CSH%^SVSH%>ijttáá K ;  ! luáṃánijttáá
+N:%^FCD%^WG%^SVSH%> Nom_DER ;
+N:%> Gen_DER ; 



LEXICON 3HUKSIM  !!= __@CODE@__ NGA + Abe blank, SLEN for the rest
+N+Sg+Nom: K ;               ! huksim
+N+Cmp/SgNom: R ;
+N+Sg+Gen: K ;            	! huksim   
+N+Sg+Acc: K ;               ! huksim
+N+Sg+Ill:%^SLEN%>ân K ;     ! huksiimân
+N+Sg+Loc:%^SLEN%>ist K ;   ! huksiimist 
+N+Sg+Com:%^SLEN%>áin K ; 	! huksiimáin
+N+Sg+Abe:%>ttáá K ;     !  huksimttáá
+N+Ess:%^SLEN%>in K ;        ! huksiimin    
+N+Par:%^SLEN%>id K ;        ! huksiimid    
3VALJIIMEH ;

LEXICON 3VALJIIMEH  !!= __@CODE@__ plurals for 3HUKSIM
+N+Pl+Nom:%^SLEN%>eh K ;     ! huksiimeh
+N+Pl+Gen:%^SLEN%>ij K ;  	! huksiimij
+N+Pl+Acc:%^SLEN%>ijd K ;     ! huksiimijd    
+N+Pl+Ill:%^SLEN%>áid K ; 	! huksiimáid
+N+Pl+Loc:%^SLEN%>ijn K ; 	! huksiimijn
+N+Pl+Com:%^SLEN%>ijguin K ;    !  huksiimijguin
+N+Pl+Com+Err/Orth:%^SLEN%>ijgijn K ;    !  huksiimijguin
+N+Pl+Abe:%^SLEN%>ijttáá K ;    ! huksiimijttáá


LEXICON 3KIEDAVUSSAM !!= __@CODE@__ 
+N+Sg+Nom:%>âm K ;               ! kieđâvuššâm
+N+Cmp/SgNom:%>âm R ;
+N+Sg+Gen:%>âm K ;            	! kieđâvuššâm   
+N+Sg+Acc:%>âm K ;               ! kieđâvuššâm
+N+Sg+Ill:%>mân K ;     ! kieđâvušmân
+N+Sg+Loc:%>mist K ;   ! kieđâvušmist 
+N+Sg+Com:%>máin K ; 	! kieđâvušmáin
+N+Sg+Abe:%>âmttáá K ;     !  kieđâvuššâmttáá
+N+Ess:%>min K ;        ! kieđâvušmin    
+N+Par:%>mid K ;        ! kieđâvušmid    
+N+Pl+Nom:%>meh K ;     ! kieđâvušmeh
+N+Pl+Gen:%>mij K ;  	! kieđâvušmij
+N+Pl+Acc:%>mijd K ;     ! kieđâvušmijd    
+N+Pl+Ill:%>máid K ; 	! kieđâvušmáid
+N+Pl+Loc:%>mijn K ; 	! kieđâvušmijn
+N+Pl+Com:%>mijguin K ;    !  kieđâvušmijguin
+N+Pl+Com+Err/Orth:%>mijgijn K ;    !  tiggekieđâvušmijgijn
+N+Pl+Abe:%>mijttáá K ;    ! tiggekieđâvušmijttáá


LEXICON 3POOVSA !!= __@CODE@__  soovsa
+N+Sg+Nom:%^FCD%^WG%^RLEN K ;			! poovsâ
+N+Cmp/SgNom:%^FCD%^WG%^RLEN%>  R ;
+N+Sg+Gen: K ;				! poksâm
+N+Sg+Acc: K ;				! poksâm
+N+Sg+Ill:%>ân K ;			! poksâmân
+N+Sg+Loc:%>ist K ;			! poksâmist
+N+Sg+Com:%>áin K ; 		! poksâmáin
+N+Sg+Abe:%>ttáá K ; 		! poksâmttáá
+N+Ess:%^FCD%^WG%^RLEN%>n K ;			! poovsân
+N+Par:%^FCD%^WG%^RLEN%>d K ;			! poovsâd
3SOKSAMEH ;


LEXICON 3SOKSAMEH !!= __@CODE@__  plurals for 3POOVSA
+N+Pl+Nom:%>eh K ;			! poksâmeh
+N+Pl+Gen:%>ij K ;			! poksâmij
+N+Pl+Acc:%>ijd K ;			! poksâmijd
+N+Pl+Ill:%>áid K ;			! poksâmáid
+N+Pl+Loc:%>ijn K ;			! poksâmijn
+N+Pl+Com:%>ijguin K ;		! poksâmijguin
+N+Pl+Com+Err/Orth:%>ijgijn K ;		! poksâmijguin
+N+Pl+Abe:%>ijttáá K ;		! poksâmijttáá


!! !!Contracted stems
!    ===================

LEXICON C_KAAMUV  !!= __@CODE@__
+N: X_KAAMUV ;
+N+Sg+Ill:%>i   K ;       		! kammui
+N+Cmp/SgNom:%^WG%^RLEN%>  R ;

LEXICON X_KAAMUV  !!= __@CODE@__
+Sg+Nom:v%^WG%^RLEN K ;   	! kaamuv
+Ess:v%^CSH%>in K ;           ! kamuvin
+Par:v%^CSH%>id K ;            ! kamuvid
+Sg+Loc:%>st K ;        		! kammust
X_KAAMUV_COMMON ;

LEXICON X_KAAMUV_COMMON  !!= __@CODE@__
+Sg+Gen:%^SV%^SLEN K ;  			 	! kammuu 
+Sg+Acc:%^SV%^SLEN K ;   			! kammuu
+Sg+Com:%>in K;        		 ! kammuin
+Sg+Abe:%^SV%^SLEN%>ttáá K ;  		    ! kammuuttáá
+Pl+Nom:%^SV%^SLEN%>h K ;         ! kammuuh
+Pl+Gen:%>i K ;         ! kammui
+Pl+Acc:%>id K ;        ! kammuid
+Pl+Ill:%>id K ;        ! kammuid 
+Pl+Loc:%>in K ;        ! kammuin
+Pl+Com:%>iguin K ;     ! kammuiguin
+Pl+Com+Err/Orth:%>igijn K ;     ! kammuiguin
+Pl+Abe:%>ittáá K ;     ! kammuittáá



LEXICON C_POOVIJC  !!= __@CODE@__ 
+v1+N+Sg+Nom:j%^ÁI%^WG K ;   	! poovij puáhhá
+v1+N+Sg+Nom:j%^ÁI%^WG K ;   	! poovij
+v1+N+Sg+Ill:%>i   K ;       		! kammui
+v1+N+Sg+Loc:%>st K ;        		! puáhhást
+v1+N+Ess:n%^CSH%>in K ;           ! puáhánin
+v1+N+Par:n%^CSH%>id K ;            ! puáhánid
+v1+N: X_KAAMUV_COMMON ;
+v2: 3POOVIJ ;


LEXICON 3POOVIJ  !!= __@CODE@__
+N+Sg+Nom:j%^ÁI%^WG K ;   	! poovij
+N+Cmp/SgNom:j%^ÁI%^WG%>  R ;
+N+Sg+Gen:%^SV%^SLEN K ;  			 	! puáhháá 
+N+Sg+Acc:%^SV%^SLEN K ;   			! puáhháá
+N+Sg+Ill:nj%^CSH%>ân   K ;       		! puáhánjân 
+N+Sg+Loc:nj%^CSH%>ist K ;        		! puáhánjist 
+N+Sg+Com:nj%^CSH%>áin K;        		 ! puáhánjáin 
+N+Sg+Abe:%^SVnj%^SLEN%>ttáá K ;  		    ! puáhháánjttáá 
+N+Ess:nj%^CSH%>in K ;           ! puáhánjin
+N+Par:nj%^CSH%>id K ;            ! puáhánjid
+N+Pl+Nom:nj%^CSH%>eh K ;         ! puáhánjeh
+N+Pl+Gen:nj%^CSH%>ij K ;         ! puáhánjij
+N+Pl+Acc:nj%^CSH%>ijd K ;        ! puáhánjijd
+N+Pl+Ill:nj%^CSH%>áid K ;        ! puáhánjáid 
+N+Pl+Loc:nj%^CSH%>ijn K ;        ! puáhánjijn
+N+Pl+Com:nj%^CSH%>ijguin K ;     ! puáhánjijguin
+N+Pl+Com+Err/Orth:nj%^CSH%>ijgijn K ;     ! puáhánjijgijn
+N+Pl+Abe:nj%^CSH%>ijttáá K ;     ! puáhánjijttáá


LEXICON C_OLMOOS  !!= __@CODE@__ lexicon for olmooš
+N+Sg+Nom:%^SVš%^WG%^SLEN%^SVLOW K ;   			! olmooš
+N+Cmp/SgNom:%^SVš%^WG%^SLEN%^SVLOW%>  R ;
+N+Sg+Gen:%^SV%^SLEN K ;  			 	! ulmuu
+N+Sg+Acc:%^SV%^SLEN K ;   				! ulmuu
+N+Sg+Ill:%>i   K ;       	! ulmui
+N+Sg+Loc:%>st K ;        	! ulmust
+N+Sg+Com:%>in K; 			! ulmuin
+N+Sg+Abe:%^SV%^SLEN%>ttáá K ;  		    ! ulmuuttáá
+N+Ess:ž%^WG%^SVLOW%>in K ;          	! olmožin
+N+Par:ž%^WG%^SVLOW%>id K ;     		! olmožid
+N+Pl+Nom:%^SV%^SLEN%>h K ;       	  	! ulmuuh
+N+Pl+Gen:%>i K ;        	! ulmui
+N+Pl+Acc:%>id K ;        	! ulmuid
+N+Pl+Ill:%>id K ;        	! ulmuid 
+N+Pl+Loc:%>in K ;        	! ulmuin
+N+Pl+Com:%>iguin K ;     	! ulmuiguin
+N+Pl+Com+Err/Orth:%>igijn K ;     	! ulmuiguin
+N+Pl+Abe:%>ittáá K ;    	! ulmuittáá
+N:%^SVš%^WG%^SLEN%^SVLOW%> Nom_DER ;
+N:%^SV%^SLEN%> Gen_DER ; 


LEXICON C_KAARBIS  !!= __@CODE@__
+N: X_KAARBIS ;
+N+Cmp/SgNom:%^WG%^RVSH%>  R ; !njälgiskävppi kepispoccee
+N:%^WG%^RLEN%> Nom_DER ;
+N:%^FCD%> Gen_DER ; 

LEXICON X_KAARBIS  !!= __@CODE@__
+Sg+Nom:%^WG%^RLEN K ;  ! käärbis
+Sg+Gen:%^FCD K ;  			 	! kárbá
+Sg+Acc:%^FCD K ;   				! kárbá
+Sg+Ill:%^FCD%>s   K ;       		! kárbás
+Sg+Loc:%^FCD%>st K ;       	 	! kárbást
+Sg+Com:%^FCD%>in K; 				! kárbáin
+Sg+Abe:%^FCD%^RVSH%>ttáá K ;   ! kárbáttáá
+Sg+Abe:%^FCD%^WG%>ttáá K ;   ! riäggáttáá
+Ess:%^WG%^RVSH%>in K ;  ! kärbisin
+Par:%^WG%^RVSH%>id K ;  ! kärbisid
X_RATTAH ;

LEXICON C_RATTAH
+N:  X_RATTAH ;

LEXICON X_RATTAH
+Pl+Nom:%^FCD%>h K ;       	  	! kárbáh
+Pl+Gen:%^FCD%>i K ;        		! kárbái
+Pl+Acc:%^FCD%>id K ;   	     	! kárbáid
+Pl+Ill:%^FCD%>id K ;      	  	! kárbáid 
+Pl+Loc:%^FCD%>in K ;    	    	! kárbáin
+Pl+Com:%^FCD%>iguin K ;     		! kárbáiguin
+Pl+Com+Err/Orth:%^FCD%>igijn K ;     		! kárbáiguin
+Pl+Abe:%^FCD%>ittáá K ;  		  	! kárbáittáá
+N+Cmp/SgNom:%^ÁI%^WG%^RLEN%>  R ;


LEXICON C_MORCES  !!= __@CODE@__
+N+Sg+Nom:%^WG%^RVSH K ;  	! morčes
+N+Cmp/SgNom:%^WG%^RVSH%>  R ;
+N+Sg+Gen:%^FCD K ;  			 	! morččá
+N+Sg+Acc:%^FCD K ;   				! morččá
+N+Sg+Ill:%^FCD%>s   K ;       		! morččás
+N+Sg+Loc:%^FCD%>st K ;       	 	! morččást
+N+Sg+Com:%^FCD%>in K; 				! morččáin
+N+Sg+Abe:%^FCD%^WG%>ttáá K ; ! morčáttáá
+N+Ess:%^WG%^RVSH%>in K ;  ! morčesin
+N+Par:%^WG%^RVSH%>id K ;  ! morčesid
+N+Pl+Nom:%^FCD%>h K ;       	  	! morččáh
+N+Pl+Gen:%^FCD%>i K ;        		! morččái
+N+Pl+Acc:%^FCD%>id K ;   	     	! morččáid
+N+Pl+Ill:%^FCD%>id K ;      	  	! morččáid 
+N+Pl+Loc:%^FCD%>in K ;    	    	! morččáin
+N+Pl+Com:%^FCD%^WG%>iguin K ;    ! morčáiguin     
+N+Pl+Com+Err/Orth:%^FCD%^WG%>igijn K ;    ! morčáiguin
+N+Pl+Abe:%^FCD%^WG%>ittáá K ;  	! morčáittáá


LEXICON C_PUASUI  !!= __@CODE@__
+N+Ess:%>n K ;     ! poccun
+N+Par:%>d K ;     ! poccud
C_KUAVSUI ;


LEXICON C_ALMAI  !!= __@CODE@__
+N+Sg+Ill:j%>ân   K ;       		! almajân
C_KUAVSUI ;


LEXICON C_KUAVSUI  !!= __@CODE@__
+N+Sg+Nom:i4%^WG K ;   	! kuávsui  to get o:uá puásui OBS
+N+Sg+Gen:%^SV%^SLEN R ;   	! kuávsui
+N+Cmp/SgNom:i4%^WG%> R ;   	! kuávsui
+N+Sg+Gen:%^SV%^SLEN K ;  			 		! kuoksuu
+N+Sg+Acc:%^SV%^SLEN K ;   					! kuoksuu
+N+Sg+Ill:%>i   K ;       		! kuoksui
+N+Sg+Loc:%>st K ;        		! kuoksust
+N+Sg+Com:%>in K; 				! kuoksuin
+N+Sg+Abe:%^SV%^SLEN%>ttáá K ;  		   		! kuoksuuttáá
+N+Ess:j%^WG%^RVSH%>in K ;     ! kuávsujin
+N+Par:j%^WG%^RVSH%>id K ;     ! kuávsujid
+N+Pl+Nom:%^SV%^SLEN%>h K ;       	  		! kuoksuuh
+N+Pl+Gen:%>i K ;        		! kuoksui
+N+Pl+Acc:%>id K ;        		! kuoksuid
+N+Pl+Ill:%>id K ;        		! kuoksuid 
+N+Pl+Loc:%>in K ;        		! kuoksuin
+N+Pl+Com:%>iguin K ;     		! kuoksuiguin
+N+Pl+Com+Err/Orth:%>igijn K ;     		! kuoksuiguin
+N+Pl+Abe:%>ittáá K ;    		! kuoksuittáá


LEXICON S_AAIGA !!= __@CODE@__
+N+Sg+Nom:â%^WG%^RLEN K ;			! aaigâ
+N+Cmp/SgNom:â%^WG%^RLEN%> R ;
+N+Sg+Gen:%^SV%^SLEN K ; 		! aiguu 
+N+Sg+Acc:%^SV%^SLEN K ;			! aiguu
+N+Sg+Ill:%>i K ; 		! aigui 
+N+Sg+Loc:%>st K ; 		! aigust
+N+Sg+Com:%>in K ;		! aiguin
+N+Sg+Abe:%^SV%^SLEN%>ttáá K ;	! aiguuttáá
+N+Ess:â%^WG%^RLEN%>n K ;			! aaigân%^WG?
+N+Par:â%^WG%^RLEN%>d K ;			! aaigâd%^WG?
+N+Pl+Nom:%^SV%^SLEN%>h K ;		! aiguuh
+N+Pl+Gen:%>i K ;		! aigui
+N+Pl+Acc:%>id K ;		! aiguid
+N+Pl+Ill:%>id K ; 		! aiguid
+N+Pl+Loc:%>in K ;		! aiguin
+N+Pl+Com:%>iguin K ;	! aiguiguin
+N+Pl+Com+Err/Orth:%>igijn K ;	! aiguiguin
+N+Pl+Abe:%>ittáá K ; 	! aiguittáá
+N+Sg+Gen:%^SV%^SLEN VPXsuff ;  ! 
+N+Sg+Acc:%^SV%^SLEN VPXsuff ;  ! 
+N+Sg+Com:%>in CPXsuff ;  ! perruinis
+N+Pl+Gen:%>id CPXsuff ;  !
+N+Pl+Acc:%>id CPXsuff ;  !
+N+Pl+Com:%>id CPXsuffCOM ;  !
+N:%^SV%^SLEN%> Gen_DER ;
+N:â%^WG%^RLEN%> Nom_DER ; !tuoivâlâš



LEXICON S_KARRII !!= __@CODE@__
+N: X_KARRII ;
+N+Cmp/SgNom:%^SV%^SLEN%>  R ;

LEXICON X_KARRII !!= __@CODE@__
+Sg+Nom:%^SV%^SLEN K ;					! karrii
+Sg+Gen:%^SV%^SLEN K ; 					! karrii
+Sg+Acc:%^SV%^SLEN K ;					! karrii
+Sg+Ill:%>jân K ; 				! karrijân
+Sg+Ill:%>i K ; 					! karrij
+Sg+Loc:%>st K ; 					! karrist
+Sg+Com:%>in K ;					! karrijn
+Sg+Abe:%^SV%^SLEN%>ttáá K ;				! karriittáá
+Ess:%>n K ;						! karrin
+Par:%>d K ;						! karrid
+Pl+Nom:%^SV%^SLEN%>h K ;					! karriih
+Pl+Gen:%>i K ;					! karrij
+Pl+Acc:%>id K ;					! karrijd
+Pl+Ill:%>id K ; 					! karrijd
+Pl+Loc:%>in K ;					! karrijd
+Pl+Com:%>iguin K ;				! karrijguin
+Pl+Com+Err/Orth:%>igijn K ;				! karrijguin
+Pl+Abe:%>ittáá K ; 				! karrijttáá


LEXICON S_SPAALVAS !!= __@CODE@__ 
+N+Sg+Nom:š%^WG%^RLEN K ; ! spáálváš
+N+Sg+Nom+Err/Orth:âš%^WG%^RLEN K ; ! spáálvâš
+N+Cmp/SgNom:âš%^WG%^RLEN%> R ;
+N+Sg+Gen:%^SV%^SLEN K ; 			! spálváá
+N+Sg+Acc:%^SV%^SLEN K ;			! spálváá
+N+Sg+Ill:ž%^RVSH%>ân K ;	! spálvážân
+N+Sg+Ill:ž%^RLEN%>ân K ;	! spáálvážân
+N+Sg+Loc:%^SV%^SLEN%>st K ;			! spálváást
+N+Sg+Com:%>in K ; 			! spálváin
+N+Sg+Abe:%^SV%^SLEN%>ttáá K ; 		! spálvááttáá
+N+Ess:ž%^WG%>in K ;	! spálvážin
+N+Ess:ž%^WG%^RLEN%>in K ;	! spálvážin
+N+Par:ž%^WG%>id K ; ! spálvážid
+N+Par:%^WG%^RLEN%>žid K ; ! spálvážid
+N+Pl+Nom:%^SV%^SLEN%>h K ; 			! spálvááh
+N+Pl+Gen:%>i K ; 			! spálvái
+N+Pl+Acc:%>id K ; 			! spálváid
+N+Pl+Ill:%>id K ; 			! spálváid
+N+Pl+Loc:%>in K ; 			! spálváin
+N+Pl+Com:%>iguin K ; 		! spálváiguin
+N+Pl+Com+Err/Orth:%>igijn K ; 		! spálváiguin
+N+Pl+Abe:%>ittáá K ; 		! spálváittáá


LEXICON S_TUULAS !!= __@CODE@__ !Stem: tu%^RVli
+N+Sg+Nom:âš%^WG%^RLEN K ; ! tuulâš
+N+Cmp/SgNom:âš%^WG%^RLEN%> R ;
+N+Sg+Gen:%^SV%^WG%^RLEN%^SLEN K ; 	! tuulii
+N+Sg+Acc:%^SV%^WG%^RLEN%^SLEN K ;	! tuulii
+N+Sg+Ill:âž%^WG%^RVSH%>ân K ;	! tulâžân
+N+Sg+Ill:âž%^WG%^RLEN%>ân K ;	! tuulâžân
+N+Sg+Loc:%^SV%^WG%^RLEN%^SLEN%>st K ;			! tuuliist
+N+Sg+Com:%^WG%^RLEN%>in K ; 			! tuulijn
+N+Sg+Abe:%^SV%^WG%^RLEN%^SLEN%>ttáá K ; 		! tuuliittáá
+N+Ess:âž%^WG%^RVSH%>in K ;	! tulâžin
+N+Par:âž%^WG%^RVSH%>id K ; ! tulâžid
+N+Pl+Nom:%^SV%^WG%^RLEN%^SLEN%>h K ; 	! tuuliih
+N+Pl+Gen:%^WG%^RLEN%>i K ; 			! tuulij
+N+Pl+Acc:%^WG%^RLEN%>id K ; 			! tuulijd
+N+Pl+Ill:%^WG%^RLEN%>id K ; 			! tuulijd
+N+Pl+Loc:%^WG%^RLEN%>in K ; 			! tuulijn
+N+Pl+Com:%^WG%^RVSH%>iguin K ; 		! tulijguin
+N+Pl+Com+Err/Orth:%^WG%^RVSH%>igijn K ; 		! tulijgijn
+N+Pl+Abe:%^WG%^RVSH%>ittáá K ; 		! tulijttáá


LEXICON S_KAANDAS !!= __@CODE@__
+N+Sg+Nom:š%^WG%^RLEN K ; ! kaandâš
+N+Cmp/SgNom:š%^WG%^RLEN%>  R ;
+N+Sg+Gen:ii K ;
+N+Sg+Acc:ii K ;
+N+Sg+Ill:ž%^CSH%>ân K ;
+N+Sg+Loc:ii%>st K ;
+N+Sg+Com:i%>in K ;
+N+Sg+Abe:ii%>ttáá K ;
+N+Ess:ž%^WG%^RVSH%>in K ;
+N+Par:ž%^WG%^RVSH%>id K ;
+N+Pl+Nom:ii%>h K ;
+N+Pl+Gen:i%>i K ;
+N+Pl+Acc:i%>id K ;
+N+Pl+Ill:i%>id K ;
+N+Pl+Loc:i%>in K ;
+N+Pl+Com:i%^RVSH%>iguin K ;
+N+Pl+Com+Err/Orth:%^RVSH%>igijn K ; ! kandijguin
+N+Pl+Abe:i%^RVSH%>ittáá K ;


LEXICON S_KIEDAS !!= __@CODE@__ Not in use
+N+Sg+Nom:âš%^WG%^RLEN K ; ! kaandâš
+N+Cmp/SgNom:âš%^WG%^RLEN%>  R ;
+N+Sg+Gen:%^SV%^SLEN K ; ! kandii
+N+Sg+Acc:%^SV%^SLEN K ; ! kandii
+N+Sg+Ill:âž%^RVSH%>ân K ;  ! ka´ndâžân
+N+Sg+Loc:%^SV%^SLEN%>st K ; ! kandiist
+N+Sg+Com:%>in K ; ! kandijn
+N+Sg+Abe:%^SV%^SLEN%>ttáá K ; ! kandiittáá
+N+Ess:âž%^RVSH%>in K ; ! ka´ndâžin
+N+Par:âž%^RVSH%>id K ; ! ka´ndâžid
+N+Pl+Nom:%^SV%^SLEN%>h K ; ! kandiih
+N+Pl+Gen:%>i K ; ! kandij
+N+Pl+Acc:%>id K ; ! kandijd
+N+Pl+Ill:%>id K ; ! kandijd
+N+Pl+Loc:%>in K ; ! kandijn
+N+Pl+Com:%^RVSH%>iguin K ; ! kandijguin
+N+Pl+Com+Err/Orth:%^RVSH%>igijn K ; ! kandijguin
+N+Pl+Abe:%^RVSH%>ittáá K ; ! kandijttáá


LEXICON S_CISSAAS !!= __@CODE@__ 
+N:  X_CISSAAS ;
+N+Cmp/SgNom:%^SVš%^SLEN%>  R ;

LEXICON X_CISSAAS !!= __@CODE@__ 
+Sg+Nom:%^SVš%^SLEN K ; 							! cissááš
+Sg+Gen:%^SV%^SLEN K ; 							! cissáá
+Sg+Acc:%^SV%^SLEN K ;					! cissáá
+Sg+Ill:%^SVž%^SLEN%^SVLOW%>ân K ;		! cissáážân
+Sg+Loc:%^SV%^SLEN%>st K ;				! cissáást
+Sg+Com:%>in K ; 							! cissáin
+Sg+Abe:%^SV%^SLEN%>ttáá K ; 				! cissááttáá
+Ess:%^SVž%^SLEN%^SVLOW%>in K ;			! cissáážin
+Par:%^SVž%^SLEN%^SVLOW%>id K ; 		! cissáážid
+Pl+Nom:%^SV%^SLEN%>h K ; 				! cissááh
+Pl+Gen:%>i K ; 							! cissái
+Pl+Acc:%>id K ; 							! cissáid
+Pl+Ill:%>id K ; 							! cissáid
+Pl+Loc:%>in K ; 							! cissáin
+Pl+Com:%>iguin K ; 						! cissáiguin
+Pl+Com+Err/Orth:%>igijn K ; 						! cissáiguin
+Pl+Abe:%>ittáá K ; 						! cissáittáá

LEXICON S_VUALAAS !!= __@CODE@__ 
+N: X_VUALAAS;
+N+Cmp/SgNom:%^SVš%^RLEN%^SLEN%>  R ;

LEXICON X_VUALAAS !!= __@CODE@__ 
+Sg+Nom:%^SVš%^RLEN%^SLEN K ; 							! vuálááš
+Sg+Gen:%^SV%^RLEN%^SLEN K ; 								! vuáláá
+Sg+Acc:%^SV%^RLEN%^SLEN K ;								! vuáláá
+Sg+Ill:ž%>ân K ;							! vuáláážân
+Sg+Loc:%^SV%^RLEN%^SLEN%>st K ;							! vuáláást
+Sg+Com:%>in K ; 							! vuáláin
+Sg+Abe:%^SV%^RLEN%^SLEN%>ttáá K ; 						! vuálááttáá
+Ess:ž%>in K ;							! vuáláážin
+Par:ž%>id K ; 							! vuáláážid
+Pl+Nom:%^SV%^RLEN%^SLEN%>h K ; 							! vuálááh
+Pl+Gen:%>i K ; 							! vuálái
+Pl+Acc:%>id K ; 							! vuáláid
+Pl+Ill:%>id K ; 							! vuáláid
+Pl+Loc:%>in K ; 							! vuáláin
+Pl+Com:%>iguin K ; 						! vuáláiguin
+Pl+Com+Err/Orth:%>igijn K ; 						! vuáláiguin
+Pl+Abe:%>ittáá K ; 						! vuáláittáá


LEXICON S_KUAVDAS !!= __@CODE@__  WG or not? vuáŋŋáš not WG, kiärpáš WG
+N+Sg+Nom:š%^WG%^RLEN K ; 		! kuávdáš piäiváš
+N+Sg+Nom+Err/Orth:âš%^WG%^RLEN K ; 		! kuávdâš piäivâš
+N+Cmp/SgNom:š%^WG%^RLEN%>  R ;
+N+Sg+Gen:%^SV%^SLEN K ; 			! kuávdáá piäiváá
+N+Sg+Acc:%^SV%^SLEN K ;			! kuávdáá piäiváá
+N+Sg+Ill:ž%^WG%^RVSH%>ân K ;	! kuávdážân piäiváážân
+N+Sg+Ill:ž%^WG%^RLEN%>ân K ;		!  kuávdážân piäivážân
+N+Sg+Loc:%^SV%^SLEN%>st K ;			! kuávdáást
+N+Sg+Com:%>in K ; 			! kuávdáin
+N+Sg+Abe:%^SV%^SLEN%>ttáá K ; 		! kuávdááttáá
+N+Ess:ž%^WG%^RVSH%>in K ;		! kuávdážin
+N+Ess:ž%^WG%^RLEN%>in K ;			! kuávdáážin
+N+Par:ž%^WG%^RLEN%>id K ; 			! kuávdáážid
+N+Par:ž%^WG%^RVSH%>id K ;		! kuávdážid
+N+Pl+Nom:%^SV%^SLEN%>h K ; 			! kuávdááh
+N+Pl+Gen:%>i K ; 			! kuávdái
+N+Pl+Acc:%>id K ; 			! kuávdáid
+N+Pl+Ill:%>id K ; 			! kuávdáid
+N+Pl+Loc:%>in K ; 			! kuávdáin
+N+Pl+Com:%>iguin K ; 		! kuávdáiguin
+N+Pl+Com+Err/Orth:%>igijn K ; 		! kuávdáiguin
+N+Pl+Abe:%>ittáá K ; 		! kuávdáittáá
+Pl+Gen:%>id CPXsuff ;  !párnáidis
+Pl+Acc:%>id CPXsuff ;  !párnáidis
+Pl+Com:%>id CPXsuffCOM ;  !párnáidiskuin


LEXICON S_ALGAAS !!= __@CODE@__ 
+N+Sg+Ill:ž%>ân K ;	! algažân
+N+Ess:ž%>in K ;		! algažin
+N+Par:ž%>id K ;		! algažid
S_CISSAAS ;


LEXICON S_AAHUS !!= __@CODE@__ 
+N+Sg+Nom:š%^WG%^RLEN K ; 					! ááhuš
+N+Cmp/SgNom:š%^WG%^RLEN%> R ;
+N+Sg+Gen:%^SV%^SLEN K ; 						! ahhuu
+N+Sg+Acc:%^SV%^SLEN K ;						! ahhuu
+N+Sg+Ill:ž%^WG%>ân K ;				! áhužân
+N+Sg+Loc:%^SV%^SLEN%>st K ;						! ahhuust
+N+Sg+Com:%>in K ; 						! ahhuin
+N+Sg+Abe:%^SV%^SLEN%>ttáá K ; 					! ahhuuttáá
+N+Ess:ž%^WG%>in K ;					! áhužin
+N+Par:ž%^WG%>id K ; 						! áhužid
+N+Pl+Nom:%^SV%^SLEN%>h K ; 						! ahhuuh
+N+Pl+Gen:%>i K ; 						! ahhui
+N+Pl+Acc:%>id K ; 						! ahhuid
+N+Pl+Ill:%>id K ; 						! ahhuid
+N+Pl+Loc:%>in K ; 						! ahhuin
+N+Pl+Com:%>iguin K ; 					! ahhuiguin
+N+Pl+Com+Err/Orth:%>igijn K ; 					! ahhuiguin
+N+Pl+Abe:%>ittáá K ; 					! ahhuittáá

LEXICON S_KAAHUS !!= __@CODE@__ 
+N:  X_KAAHUS ;
+N+Sg+Ill:%^SVž%^SLEN%>ân K ;				! kahhuužân

LEXICON X_KAAHUS !!= __@CODE@__ 
+Sg+Nom:š%^CSH%^RLEN K ; 					! kááhuš ka%^RVhhu%^CSH%^RLEN%>š
+Cmp/SgNom:š%^RLEN%> R ;
+Sg+Gen:%^SV%^SLEN K ; 						! kahhuu
+Sg+Acc:%^SV%^SLEN K ;						! kahhuu
+Sg+Ill:ž%^CSH%>ân K ;						! káhužân
+Sg+Loc:%^SV%^SLEN%>st K ;						! kahhuust
+Sg+Com:%>in K ; 						! kahhuin
+Sg+Abe:%^SV%^SLEN%>ttáá K ; 					! kahhuuttáá
+Ess+Err/Orth:%^SVž%^SLEN%>in K ;					! kahhuužin
+Ess:ž%^CSH%>in K ;					! káhužin
+Par+Err/Orth:%^SVž%^SLEN%>id K ; 						! kahhuužid
+Par:ž%^CSH%>id K ; 						! káhužid
+Pl+Nom:%^SV%^SLEN%>h K ; 						! kahhuuh
+Pl+Gen:%>i K ; 						! kahhui
+Pl+Acc:%>id K ; 						! kahhuid
+Pl+Ill:%>id K ; 						! kahhuid
+Pl+Loc:%>in K ; 						! kahhuin
+Pl+Com:%>iguin K ; 					! kahhuiguin
+Pl+Com+Err/Orth:%>igijn K ; 					! kahhuiguin
+Pl+Abe:%>ittáá K ; 					! kahhuittáá



LEXICON 4C_RASTAGAS    !!= __@CODE@__ 
+N: X_RASTAGAS ;
+N+Cmp/SgNom:âš%>  R ;

LEXICON X_RASTAGAS  !!= __@CODE@__ 
+Sg+Nom:âš K ; 		! rástágâš
+Sg+Gen:%^SV%^SLEN K ; 		! rástágáá
+Sg+Acc:%^SV%^SLEN K ; 		! rástágáá
+Sg+Ill:âž%>ân K ;	! rástágâžân
+Sg+Loc:%^SV%^SLEN%>st K ; 	! rástágáást
+Sg+Com:%>in K ; 	! rástágáin
+Sg+Abe:%^SV%^SLEN%>ttáá K ; 	! rástágááttáá
+Ess:âžž%>ân K ; 		! rástágâžžân
+Par:âžž%>âd K ; 		! rástágâžžâd
+Pl+Nom:%^SV%^SLEN%>h K ; 	! rástágááh
+Pl+Gen:%>i K ; 		! rástágái
+Pl+Acc:%>id K ; 	! rástágáid
+Pl+Ill:%>id K ; 	! rástágáid
+Pl+Loc:%>in K ; 	! rástágáin
+Pl+Com:%>iguin K ; 	! rástágáiguin
+Pl+Com+Err/Orth:%>igijn K ; 	! rástágáiguin
+Pl+Abe:%>ittáá K ; 	! rástágáittáá


LEXICON 3OPPUS  !!= __@CODE@__ 
+N+Sg+Nom:š K ; 			! oppuš
+N+Cmp/SgNom:š%>  R ;
+N+Sg+Gen:%^SVž%^SLEN%>ii K ; 	! oppuužii
+N+Sg+Acc:%^SVž%^SLEN%>ii K ; 	! oppuužii
+N+Sg+Ill:%^SVž%^SLEN%>ân K ;	! oppuužân
+N+Sg+Loc:%^SVž%^SLEN%>iist K ; ! oppuužiist
+N+Sg+Com:%^SVž%^SLEN%>áin K ; 	! oppuužáin
+N+Sg+Abe:%^SVž%^SLEN%>iittáá K ; ! oppuužiittáá
+N+Ess:%^SVž%^SLEN%>in K ; 		! oppuužin
+N+Par:%^SVž%^SLEN%>id K ; 		! oppuužid
+N+Pl+Nom:%^SVž%^SLEN%>iih K ; 	! oppuužiih
+N+Pl+Gen:%^SVž%^SLEN%>ij K ; 	! oppuužij
+N+Pl+Acc:%^SVž%^SLEN%>ijd K ; 	! oppuužijd
+N+Pl+Ill:%^SVž%^SLEN%>áid K ; 	! oppuužáid
+N+Pl+Loc:%^SVž%^SLEN%>ijn K ; 	! oppuužijn
+N+Pl+Com:%^SVž%^SLEN%>ijguin K ; ! oppuužijguin
+N+Pl+Com+Err/Orth:%^SVž%^SLEN%>ijgijn K ; ! oppuužijguin
+N+Pl+Abe:%^SVž%^SLEN%>ijttáá K ; ! oppuužijttáá


!! !Gen sg odd-syllabic



LEXICON 4COHASAS  !!= __@CODE@__ 
+N: LAS_COMMON ;
+N:%>á LAS_SUF ;

LEXICON 4JOTOLAH  !!= __@CODE@__ 
+N+Sg+Nom:âh K ;
+N+Cmp/SgNom:âh R ;
JOHTOLAH_OBL ;

LEXICON JOHTOLAH_OBL    !!= __@CODE@__ common for N and A
+N+Sg+Gen:uv K ;
+N+Sg+Acc:uv K ;
+N+Sg+Ill:âh%>ân K ;
+N+Sg+Loc:uv%>âst K ;
+N+Sg+Com:uvv%>áin K ;
+N+Sg+Abe:uv%>ttáá K ;
+N+Ess:âhhâ%>n K ;
+N+Par:âhhâ%>d K ;
+N+Pl+Nom:uva%>h K ;
+N+Pl+Gen:uvâ%>i K ;
+N+Pl+Acc:uvâ%>id K ;
+N+Pl+Ill:uvvá%>id K ;
+N+Pl+Loc:uvâ%>in K ;
+N+Pl+Com:uvâ%>iguin K ;
+N+Pl+Com+Err/Orth:uvâ%>igijn K ;
+N+Pl+Abe:uvâ%>ittáá K ;


LEXICON 4KAPULUH  !!= __@CODE@__ 
+N+Sg+Nom:uh K ;
+N+Sg+Nom:oh K ;
+N+Sg+Nom:âh K ;
+N+Sg+Loc:uvvâst K ;
+N+Sg+Com:uváin K ;
+N+Ess:uhhân K ;
+N+Ess:ohhân K ;
+N+Par:uhhâd K ;
+N+Par:ohhâd K ;
JOHTOLAH_OBL ;

LEXICON 4KUBALOH  !!= __@CODE@__ 
+N+Sg+Nom:oh K ;
JOHTOLAH_OBL ;


LEXICON 4LAS_NOUN !!= __@CODE@__ 
+N: LAS_COMMON ;
+N+Cmp/SgNom:âš R ;

LEXICON LAS_COMMON !!= __@CODE@__ 
+Sg+Nom:âš K ;
+Sg+Nom+Err/Orth:aš K ;
+Sg+Gen:ii K ;
+Sg+Acc:ii K ;
+Sg+Ill:âžân K ;
+Sg+Loc:iist K ;
+Sg+Com:ijn K ;
+Sg+Abe:iittáá K ;
+Ess:âžžân K ;
+Par:âžžâd K ;
+Pl+Nom:iih K ;
+Pl+Gen:ij K ;
+Pl+Acc:ijd K ;
+Pl+Ill:áid K ;
+Pl+Loc:ijn K ;
+Pl+Com:ijguin K ;
+Pl+Com+Err/Orth:ijgijn K ;
+Pl+Abe:ijttáá K ;

LEXICON LAS_SUF  !!= __@CODE@__ 
+Sg+Gen:á K ;
+Sg+Acc:á K ;
+Sg+Ill:%>in K ;
+Sg+Loc:á%>st K ;
+Sg+Com:%>in K ;
+Ess:âžž%>ân K ;
+Par:âžž%>âd K ;
+Pl+Nom:%>áh K ;
+Pl+Gen:%>i K ;
+Pl+Acc:%>id K ;
+Pl+Ill:%>id K ;
+Pl+Loc:%>in K ;
+Pl+Com:%>iguin K ;
+Pl+Com+Err/Orth:%>igijn K ;
+Pl+Abe:%>ittáá K ;


!! !! Odd-syllable stems, sublexica
! - - - - - - - - - - - - - - - 

LEXICON ODD_SUFF_LONGSTEM  !!= * __@CODE@__ 
 +Sg+Nom: K ;
 +Sg+Gen: K ;
 +Sg+Acc: K ;
 +Sg+Ill:%>ân K ;

LEXICON ODD_SUFF_SHORTSTEM  !!= * __@CODE@__ 
 +Sg+Loc:%>ist K ;
 +Sg+Com:%>áin K ;
 +Sg+Abe:%>ttáá K ;
 +Ess:%>in K ;
 +Par:%>id K ;
 +Pl+Nom:%>eh K ;
 +Pl+Gen:%>ij K ;
 +Pl+Acc:%>ijd K ;
 +Pl+Ill:%>áid K ;
! +Pl+Ill:%>ááid K ;
 +Pl+Loc:%>ijn K ;
 +Pl+Com+Err/Orth:%>ijgijn K ;
 +Pl+Com:%>ijguin K ;
 +Pl+Abe:%>ijttáá K ;


!! !!Contracted stems
!    ================


LEXICON C_LOHHEE
+N+NomAg+Sg+Ill:%^SVSH%>i3 K ;		! tahhei ! stem e > 0 noun only
+N+NomAg: X_TAHHEE ;  !!= __@CODE@__ ee-final contract nomen agentis
+N+Cmp/SgNom: R ;

LEXICON C_TAHHEE !!= __@CODE@__ ee-final contract, -ei instead of -ij not-nomen-agentis
+N+Sg+Ill:%^SVSH%>i3 K ;		! tahhei ! stem e > 0 noun only
+N: X_TAHHEE ;
+N+Cmp/SgNom: R ;

LEXICON X_TAHHEE !!= __@CODE@__ ee-final contract, -ei instead of -ij
+Sg+Nom: K ;				! tahhee
+Sg+Gen: K ;				! tahhee
+Sg+Acc: K ;				! tahhee
+Sg+Ill:%^SVSH%>i3 K ;				! tahhei
+Sg+Loc:%^SVSH%>st K ; 		! tahhest ! stem e > 0
+Sg+Com:%^SVSH%>i3n K ;		! tahhein
+Sg+Abe:%>ttáá K ;		! tahheettáá
+Ess:%^SVSH%>n	K ;			! tahhen
+Par:%^SVSH%>d	K ;			! tahhed
X_KILDEEH ;

LEXICON C_KILDEEH !!= __@CODE@__ ee-final contract, plural forms
+N: X_KILDEEH ;

LEXICON X_KILDEEH !!= __@CODE@__ ee-final contract, plural forms
+Pl+Nom:%>h K ;			! tahheeh
+Pl+Gen:%^SVSH%>i3 K ; 		! tahhei
+Pl+Acc:%^SVSH%>i3d K ; 		! tahheid
+Pl+Ill:%^SVSH%>i3d K ; 		! tahheid
+Pl+Loc:%^SVSH%>i3n K ;		! tahhein
+Pl+Com:%^SVSH%>i3guin K ;	! tahheiguin
+Pl+Com+Err/Orth:%^SVSH%>igijn K ;	! tahheiguin
+Pl+Abe:%^SVSH%>i3ttáá K ; 	! tahheittáá



!! !!Derivations
! =================================

LEXICON Gen_DER   !!= __@CODE@__ 
+Der/vuálásâš:vuálás ANOLASJ ; 
+Der/lâš:l ANOLASJ ; 

LEXICON Nom_DER   !!= __@CODE@__ 
+Der/lâš:l ANOLASJ ; 


LEXICON SAS !!= * __@CODE@__ gives :»»» and goes to SASCont
+Cmp#:»»»# SASCont ;

LEXICON SASCont !!= * __@CODE@__ FROM NUMERALS, gives -kilosaš etc.
ihe:ihás DER-SAS ;

LEXICON DER-AG
+N+Der/âg:  PASTIL ;

LEXICON DER-SAS
+N+Der/sâš:  ANOLASJ ;


!! !!Sublexica for possessive suffixes
! =================================

!! To be written.



! Old notes, see these for paradigms


 	 	! silbâ
 	! siilbâ
 	! siilbâ
 	! siilbân
 	! siilbâst
 	! silbáin
	! silbâttáá
	! silbân
	! silbâd
 	! siilbah
 	! siilbâi
 	! siilbâid
	! silbáid 
	! siilbâin
	! silbâiguin
 	! silbâittáá

 	 	! nieidâ
 	! nieidâ
 	! nieidâ
 	! nieidân
 	! nieidâst
 	! nieidáin
	! nieidâttáá
	! nieidân
	! nieidâd
 	! nieidah
 	! nieidâi, niedâi
 	! nieidâid
	! nieidáid 
	! nieidâin
	! nieidâiguin
 	! nieidâittáá



 	 	! herttâ
 	! heertâ
 	! heertâ
 	! heertân
 	! heertâst
 	! herttáin
	! hertâttáá
	! herttân
	! herttâd
 	! heertah
 	! heertâi
 	! heertâid
	! herttáid 
	! heertâin
	! hertâiguin
 	! hertâittáá


 	 	! merkkâ
 	! meerhâ
 	! meerhâ
 	! meerkân
 	! meerhâst
 	! merháin
	! merhâttáá
	! merkkân
	! merkkâd
 	! meerhah
 	! meerhâi
 	! meerhâid
	! merháid 
	! meerhâin
	! merhâiguin
 	! merhâittáá


	 	! spellâ
 	! speelâ
 	! speelâ
	! speelân
 	! speelâst
 	! spelláin
	! spelâttáá
	! spellân
	! spellâd
 	! speelah
 	! speelâi
 	! speelâid
	! spelláid
 	! speelâin
	! spelâiguin 
	! spelâittáá




