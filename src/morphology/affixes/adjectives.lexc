! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no



! ===========================
!! !!!Adjective affixes
! ===========================


!! Order of triggers:
!! {{{
!! SV|ÁE|ÁI  SC  FCD WG|CSH CLEN  VHIGH|VBACK  RVSH|RLEN EA SLEN|SVSH  SVLOW
!! }}}

LEXICON adjstems  !!= __@CODE@__ temporary, for stems waiting for a lexicon
+A+Attr: K ;
+A+Sg+Nom: K ;
+A+Cmp/Attr:  R ;


LEXICON ATTR !!= __@CODE@__ for stems with only attribute form
+A+Attr:  K ;
+A+Cmp/Attr:  R ;
+A: A_LASJ ; ! rähislâš
+A: A_VUOTA ; !vuoigâvuotâ


!! !!Even-syllable stems
! -------------------

LEXICON ALKKEE  !!= __@CODE@__ directing to X_TAHHEE, attr. älkkees
+A+Attr:s K ; ! älkkees
ALKKEE_COMMON ;
+A:%> A_VUOTA ;
+A: VVCOMPe ; ! assab
+A:%> MUS ; ! váiváámus hoppuumus
+A+Cmp/Attr:s R ; 
+A+Cmp/SgNom: R ; !ruánáátiälkku

!LEXICON MAKKUU   directing to X_TAHHEE, attr. älkkees
!+A+Attr:s K ; ! älkkees
!+A+Sg+Ill:%^SVSH%>vân K ;		! Adjective illative uvân
!ALKKEE_COMMON ;
!+A:%> A_VUOTA ;

LEXICON PYECCEE  !!= __@CODE@__ directing to X_TAHHEE, attr. pyeccee
+A+Attr: K ; 
!+A+Sg+Ill:%>jân K ;		! Adjective illative jân
ALKKEE_COMMON ;
+A+Cmp/Attr: R ; !pyecceeviäsu ruánáátiälkku


LEXICON ALKKEE_COMMON !!= __@CODE@__ common for ALKKEE and PYECCEE
+A: X_TAHHEE ;
+A+Pl+Nom+Err/Orth:%^SVSH%>h K ;


LEXICON UCES !!= __@CODE@__ , not in use
+A+Attr:â K ;     !attr. uccâ
+A+Sg+Nom:e%^WG%>s K ;
+A:es%> COMP ;
+A:e%^WG%> MUS ;
+A:e%> A_VUOTA ;
+A+Cmp/Attr:â R ; !uccârávdu


LEXICON POCCA !!= __@CODE@__ directing to X_ALGA, attr počis
+A+Attr:i%^WG%>s K ; ! the final a > 0, počis
+A+Attr:â%^WG%>s K ; ! corp: počâs
+A:â X_ALGA ;
+A:â%> COMP ;
+A:â%> MUS ;
+A+Cmp/Attr:â R ; !poččâmielâst
+A+Cmp/Attr:â%^WG%>s R ; !
+A+Cmp/Attr:i%^WG%>s R ; !
+A:â%> A_VUOTA ; !poččâvuođâ


LEXICON SKELMA !!= __@CODE@__ directing to X_ALGA, attr skelmâ
+A+Attr: K ;
+A: X_ALGA ;
+A+Cmp/Attr: R ;
+A:%> A_VUOTA ;
+A:%^CSH%> COMP ;
+A:%^CSH%> MUS ;


LEXICON VANTTA !!= __@CODE@__ directing to X_ALGA, attr vantâs
+A+Attr:%^CSH%>s K ; !vantâs
+A: X_ALGA ;
+A+Cmp/Attr: R ;
+A:%> A_VUOTA ;
+A:%^CSH%> COMP ;
+A:%^CSH%> MUS ;


!LEXICON SKELMA_COMMON 
!+A+Sg+Nom: K ;
!+A+Sg+Gen:%^WG%^RLEN K ;
!+A+Sg+Acc:%^WG%^RLEN K ;
!+A+Sg+Ill:%^CSH%^RLEN%>n K ;
!+A+Sg+Loc:%^WG%^RLEN%>st K ;
!+A+Sg+Com:%^WG%^CLEN%^SVLOW%>in K ;
!+A+Sg+Abe:%^WG%>ttáá K ;
!+A+Ess:%>n K ;
!+A+Par:%>d K ;
!+A+Pl+Nom:%^WG%^RLEN%>h K ;
!+A+Pl+Gen:%^WG%^RLEN%>i K ;
!+A+Pl+Acc:%^WG%^RLEN%>id K ;
!+A+Pl+Ill:%^WG%^SVLOW%>id K ;
!+A+Pl+Loc:%^WG%^RLEN%>in K ;
!+A+Pl+Com:%^WG%>iguin K ;
!+A+Pl+Abe:%^WG%>ittáá K ;



LEXICON HIAJU !!= __@CODE@__ directing to X_VUAGGU, attr. hiäjus
+A+Attr:s K ; ! hiäjus
+A: X_VUAGGU ;
+A:%> COMP ;
+A:%> MUS ;
+A+Cmp/Attr:s R ;
+A:%> A_VUOTA ;

 
LEXICON CEPPI !!= __@CODE@__ directing to X_AIGI, attr. čepis
+A+Attr:%^WG%>s K ;
+A: X_AIGI ;
+A:b%^WG COMPe ;
+A:%^WG%> MUS ;
+A+Cmp/Attr:%^WG%>s R ; !
+A+Cmp/SgNom: R ; !luággijuolgijd herkkičuávjásiih
+A:%> A_VUOTA ;
+A:%^WG%>s A_LAGAN ; !ärgislágán
!+A:l A_ADV ; !how? tärkkilávt blo

LEXICON UCCE
+A+Attr+Use/NG:e%^WG%>s K ;
+A+Attr:â K ;
+A:e X_ALGE ;
+A:e%^SVb%^WG COMPclen ;
+A+Err/Orth:eb%^WG COMPe ;
+A:e%^WG%> MUS ;
+A+Cmp/Attr:â R ;
+A:e%> A_VUOTA ;


LEXICON KUKKE !!= __@CODE@__ directing to X_ALGE, attr. kuhes
+A+Attr:%^WG%>s K ;
+A: X_ALGE ;
+A:%^SVb%^WG COMPclen ;
+A+Err/Orth:b%^WG COMPe ;
+A:%^WG%> MUS ;
+A+Cmp/Attr:%^WG%>s R ;
+A:%> A_VUOTA ;


LEXICON VIDA !!= __@CODE@__ directing to X_NJUNE, no attr., no comparation
+A: X_NJUNE ;
+A+Cmp/SgNom: R ;
+A:%> A_VUOTA ;


LEXICON HUMO !!= __@CODE@__ directing to X_NJUNE, attr. humos
+A+Attr:%^WG%>s K ;
+A: X_NJUNE ;
+A:b COMPe ;
+A:%> MUS ;
+A+Cmp/Attr:%^WG%>s R ;
+A+Cmp/SgNom: R ;
+A:%> A_VUOTA ;

LEXICON JULME !!= __@CODE@__ directing to X_OKKO, attr. julmes
+A+Attr:%^WG%>s K ;
+A: X_ALGE ;
+A:b COMPe ;
+A:%> MUS ;
+A+Cmp/Attr:%^WG%>s R ;
+A:%> A_VUOTA ;


LEXICON LAAMI !!= __@CODE@__ directing to X_SAAMI, attr. lämis
+A+Attr:s  K ; !lämis
+A: X_SAAMI	  ;
!+A:%^WG%> eCOMP ; Adj-s seem to be absolutive adjectives without comparation
!+A:%^WG%^RVSH%> MUS ;
+A:%^CSH%> A_VUOTA ; ! lämivuotâ
+A+Cmp/Attr:s R ;

LEXICON PYERI !!= __@CODE@__ directing to X_KYELI, attr. pyeri
+A+Attr:  K ; 
+A: X_KYELI	  ;
+A:%^WG%> eCOMP ;
+A:%^WG%> MUS ;
+A:%> A_VUOTA ;
+A+Cmp/Attr: R ;

LEXICON HYENI !!= __@CODE@__ directing to X_KYELI, attr. pyeri
+A+Attr:%^WG%>s  K ; 
+A: X_KYELI	  ;
+A:%^WG%> eCOMP ;
+A:%^WG%> MUS ;
+A:%> A_VUOTA ;
+A+Cmp/Attr:%^WG%>s R ; 


LEXICON NUORA !!= __@CODE@__ directing to X_KIELA, attr. nuora
+A+Attr:  K ; 
+A: X_KIELA	  ;
+A:b COMPe ;
+A:%> MUS ;
+A:%> A_VUOTA ;
+A+Cmp/Attr: R ; 
+A+Cmp/SgNom:%^RVSH%> R ;
+A+Cmp/SgNom:  Rhyph ;
+A+Cmp/SgGen:%^WG%> R ;
+A+Cmp/PlGen:%^WG%>i R ;


LEXICON KIEVRA !!= __@CODE@__ directing to X_KIELA, attr. kievrâs
+A+Attr:s  K ; 
+A: X_KIELA	  ;
+A:b COMPe ;
+A:%> MUS ;
+A+Err/Orth:s%^WG%>u MOS ;
+A:%> A_VUOTA ;
+A+Cmp/Attr:s R ; 



!! !!Odd-syllable stems
! ------------------

LEXICON NJUORAS !!= __@CODE@__ directing to X_RUONAS, attr. njuorâs
+A+Attr:%^WG K ; !njuorâs
+A: X_RUONAS ;
+A:%^CSH%>u COMPo ; 
+A:%^CSH%>u MOS ;
+A:%> A_VUOTA ;
+A:%^WG A_ADV ; !ruávásávt kiärásávt
+A+Cmp/Attr:%^WG%> R ;
+A:%^WG%> A_LASJ ; !  


LEXICON UAMIS !!= __@CODE@__ directing to X_RUONAS, as an own group because of FCD, attr. uámi, čuámi, often used in compounds
+A+Attr:%^FCD%^WG K ; ! uámi, čuámi
+A: X_RUONAS ;
+A:%^CSH%>u COMPo ; 
+A:%^CSH%>u MOS ;
+A:%> A_VUOTA ;
+A+Cmp/Attr:%^FCD%^WG%> R ;
+A:%^WG%> A_LASJ ; !  



LEXICON HAANIS !!= __@CODE@__ directing to X_PAAVIR, attr. hänis
+A+Attr:%^WG%^RVSH K ; ! hänis
+A: X_PAAVIR	  ;
+A:%^CSH%>u COMPo ;  ! hánásuboh corpus: uáppásub
+A:%^CSH%>u MOS ; ! hánásumoseh; corpus: uáppásumos uáppásumoseh uáppásumosáid
+A:%^WG%^RVSH%> A_VUOTA ; 
+A:%^CSH%^RVSH A_ADV ; !hánásávt 
+A:%^WG%^RVSH%> A_LASJ ; !  
+A+Cmp/Attr:%^WG%^RVSH%> R ;
+A+Cmp/SgNom:%^WG%^RLEN%> R ; !
+A+Cmp/SgGen:%^SLEN%> R ; !
+A+Cmp/PlGen:%^CSH%>ij R ; !puárásijpargo

LEXICON TIMES !!= __@CODE@__ directing to X_PAAVIR, attr. timmâ
+A+Attr:%^FCDâ K ; 	! timmâ
+A: X_PAAVIR	  ;
+A:%^CSH%>u COMPo ;  ! timásuboh 
+A:%^CSH%>u MOS ; ! timásumoseh
+A+Attr:%^FCDâ%> R ;
+A:%^WG%^RVSH%> A_VUOTA ; 
+A:%^CSH%^RVSH A_ADV ; !timásávt 
+A:%^FCDâ%> A_LASJ ; !  


LEXICON KIARGUS_nocomp !!= __@CODE@__  like HAANIS, directing to X_PAAVIR, 
+A+Attr:%^WG%^RVSH K ; ! 
+A: X_PAAVIR	  ;
+A+Cmp/Attr:%^WG%^RVSH%> R ;
+A:%^WG%^RVSH%> A_VUOTA ;
+A:%^WG%^RVSH%> A_LASJ ; !  


LEXICON HAAITIS !!= __@CODE@__ directing to X_VEESKIR, attr. rähis, not in use
+A+Attr:%^WG%^RVSH  K ; !rähis
+A: X_VEESKIR	  ;
+A:%>u COMPo ; 
+A:%>u MOS ;
+A+Cmp/Attr:%^WG%^RVSH%> R ;
+A:%> A_VUOTA ;
+A:%^WG%^RVSH%> A_LASJ ; !  

LEXICON HEEITUG !!= __@CODE@__ directing to X_PAAVIR, attr. heitugis, heitugâs
+A+Attr:%^WG%^RVSH%>is  K ; !heitugis
+A+Attr:%^WG%^RVSH%>âs  K ; !heitugâs
+A: X_PAAVIR	  ;
+A:%>u COMPo ; 
+A:%>u MOS ;
+A+Cmp/Attr:%^WG%^RVSH%>is R ;
+A+Cmp/Attr:%^WG%^RVSH%>âs R ;
+A:%> A_VUOTA ;
+A:%^WG%^RVSH%> A_LASJ ; !  


LEXICON VAANIS !!= __@CODE@__ directing to  X_PAAVIR, attr. väni
+A+Attr:väni K ; ! 
+A+Attr:vanâ K ; ! 
+A:váánnás%^ÁI X_PAAVIR	  ;
+A:váánnás%^ÁI%^CSH%>u COMPo ; 
+A:váánnás%^ÁI%^CSH%>u MOS ;
+A+Cmp/Attr:väni%> R ;
+A:vänis A_LASJ ; !  
!+A:%^FCD%^CSH%^RVSH%> A_VUOTA ; ! vänivuotâ; is also lexicalized
!+A:%^FCD%^WG A_ADV_VAANIS ; ! vääniht; also lexicalized; why to create these here? A_ADV is wrong, so tried to change it, without success; I will comment this out for a while

LEXICON NJAALGIS !!= __@CODE@__ directing to NJAALGIS_COMMON, Attr. njaalgâ and njälgis
+A+Attr:njaalgâ K ;
+A+Attr+Use/NG:njälgis K ; ! njälgis
:njá%^RVlgás%^ÁI NJAALGIS_COMMON ;
+A:njälgis A_LASJ ; !  
+A+Cmp/Attr:njälgis R ;

LEXICON RAAPIS !!= __@CODE@__ directing to NJAALGIS_COMMON, Attr. räpis 
+A+Attr:%^WG%^RVSH K ; ! räpis
NJAALGIS_COMMON ;
+A:%^WG%^RVSH%> A_LASJ ; !  
+A+Cmp/Attr:%^WG%^RVSH%> R ;

LEXICON NJAALGIS_COMMON !!= __@CODE@__ directing to  X_KAARBIS
+A: X_KAARBIS ;
+A:%>u COMPo ; !njálgásub njá%^RVlgás%^ÁI
+A:%>u MOS ; ! njálgásumos; with Err/Orth njálgâsumos, even njálgsumos
+A:%^WG%^RVSH%> A_VUOTA ; !njälgisvuođâ

LEXICON KOSKES !!= __@CODE@__ directing to  X_VEESKIR, attr. koške
+A+Attr:%^FCD%^WG K ;     !attr. koške
+A: X_VEESKIR ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A:%^WG%> A_VUOTA ;
+A+Cmp/Attr:%^FCD%^WG%> R ;
+A:%^WG%> A_LASJ ; !  

LEXICON NJOLGES !!= __@CODE@__ directing to  X_VEESKIR, attr. njolges
+A+Attr:%^WG K ;     !attr. njolges
+A: X_VEESKIR ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A:%^WG%> A_VUOTA ;
+A+Cmp/Attr:%^WG%> R ;
+A:%^WG%> A_LASJ ; !  



LEXICON FIIJNES !!= __@CODE@__  no noun cont.lex with this pattern
+A+Attr:fiijnâ K ;     !attr. fiijnâ
+A+Attr+Use/NG:fijnâ K ;     !attr. fijnâ
+A+Cmp/Attr:fiijnâ R ;
+A+Sg+Nom:fiijnes K ;	! fiijnes
+A+Sg+Gen:fijnás K ;     			! fijnás
+A+Sg+Acc:fijnás K ;    			 ! fijnás
+A+Sg+Ill:fijnesân K ; 	! fijnesân
+A+Sg+Loc:fijnesist K ; ! fijnesist
+A+Sg+Com:fijnásáin K ; ! fijnásáin 
+A+Sg+Abe:fijnásttáá K ; !  
+A+Ess:fijnesin K ;   ! fijnesin 
+A+Par:fijnesid K ;	!  
+A+Pl+Nom:fijnáseh K ;  !  
+A+Pl+Gen:fijnásij K ;  !  
+A+Pl+Acc:fijnásijd K ; !  
+A+Pl+Ill:fijnásáid K ; !  
+A+Pl+Loc:fijnásijn K ; !  
+A+Pl+Com:fijnásijguin K ;  !  
+A+Pl+Com+Err/Orth:fijnásijgijn K ;  !  
+A+Pl+Abe:fijnásijttáá K ;  !  
+A:fijnásu COMPo ; !fijnásub fijnásubboid
+A:fijnásu MOS ; !fijnásumos
+A:fijnes%> A_VUOTA ; !fijnesvuođáid
+A:fijnás A_ADV ; !fijnásávt, fijnâsávt



LEXICON KOOIDAS !!= __@CODE@__ directing to  X_VEESKIR, attr. koidâ
+A+Attr:%^FCD%^RVSH K ;     !attr. koidâ
+A: X_VEESKIR ;
+A:%>u COMPo ; 
+A:%>u MOS ;
+A:%^WG%> A_VUOTA ;
+A: A_ADV ; !čuhâlávt
+A+Cmp/Attr:%^FCDâ%^RVSH%> R ;    
+A:%^FCD%^RVSH%> A_LASJ ; !  


LEXICON SUOTAS  !!= __@CODE@__ directing to  X_VEESKIR, attr. suotâs
+A+Attr:%^WG%^RVSH  K ; ! suotâs, vädis 
+A: X_VEESKIR ;
+A:u COMPo ; ! suottâsub
+A:%>u MOS ; !suottâsumos
+A:%^WG%^RVSH%> A_VUOTA ;
+A: A_ADV ; !suottâsávt
+A+Cmp/Attr:%^WG%^RVSH%>  R ; ! suotâs, vädis 
+A:%^WG%> A_LASJ ; ! rähislâš oleslâš

LEXICON LIAGUS  !!= __@CODE@__ like SUOTAS except for CSH
+A+Attr:%^WG%^RVSH  K ; ! suotâs, vädis
+A+Sg+Nom:%^WG%^RVSH K ; !peles
+A+Sg+Gen: K ;   !
+A+Sg+Acc: K ;      
+A+Sg+Ill:%^CSH%>ân K ; 
+A+Sg+Loc:%^CSH%>ist K ;
+A+Sg+Com:%^CSH%>áin K ;
+A+Sg+Abe:%>ttáá K ;  
+A+Ess:%^WG%^RVSH%>in K ; 
+A+Par:%^WG%^RVSH%>id K ; 
+A+Pl+Nom:%^CSH%>eh K ;  
+A+Pl+Gen:%^CSH%>ij K ;  
+A+Pl+Acc:%^CSH%>ijd K ; 
+A+Pl+Loc:%^CSH%>ijn K ; 
+A+Pl+Com:%^CSH%>ijguin K ; 
+A+Pl+Com+Err/Orth:%^CSH%>ijgijn K ; 
+A+Pl+Abe:%^CSH%>ijttáá K ; 
+A+Pl+Ill:%^CSH%>áid K ; 
+A:%>u COMPo ; 
+A:%>u COMPo ; ! liäggusub
+A+Err/Orth:%>u COMPo ; 
+A:%>u MOS ; !liäggusumos
+A+Err/Orth:%>u MOS ; 
+A:%^WG%^RVSH%> A_VUOTA ;
+A: A_ADV ; !liäggusávt
+A+Cmp/Attr:%^WG%^RVSH%>  R ; ! liägus 
+A:%^RVSH%> A_LASJ ; !  



LEXICON SENES !!= __@CODE@__ not fitting in X_PELES because of CSH attr. seŋes
+A+Attr:%^WG%^RVSH  K ; ! seŋes 
+A+Cmp/Attr:%^WG%^RVSH%> R ;
+A+Sg+Nom:%^WG%^RVSH K ; !peles
+A+Sg+Gen: K ;   !
+A+Sg+Acc: K ;      
+A+Sg+Ill:%>ân K ; 
+A+Sg+Loc:%>ist K ;
+A+Sg+Com:%>áin K ;
+A+Sg+Abe:%>ttáá K ;  
+A+Ess:%^WG%^RVSH%>in K ; 
+A+Par:%^WG%^RVSH%>id K ; 
+A+Pl+Nom:%>eh K ;  
+A+Pl+Gen:%>ij K ;  
+A+Pl+Acc:%>ijd K ; 
+A+Pl+Loc:%>ijn K ; 
+A+Pl+Com:%>ijguin K ; 
+A+Pl+Com+Err/Orth:%>ijgijn K ; 
+A+Pl+Abe:%>ijttáá K ; 
+A+Pl+Ill:%>áid K ; 
+A:%>u COMPo ; 
+A:%^WG%>u COMPo ; 
+A+Err/Orth:%>u COMPo ; 
+A:%^WG%>u MOS ;
+A+Err/Orth:%>u MOS ;
+A:%^WG%^RVSH%> A_VUOTA ;
+A: A_ADV ; !ollásávt
+A:%^WG%> A_LASJ ; !  


LEXICON A_VEESKIR  !!= __@CODE@__ directing to X_VEESKIR, no attr.
+A: X_VEESKIR ;
+A+Cmp/Attr:%^WG%> R ;


LEXICON KEPPIS !!= __@CODE@__ directing to A_LITTER, attr. keppâ
+A+Attr:â%> K ;    ! keppâ
+A+Attr:is%>is K ;    ! keppisis
:is A_LITTER ;
+A:is%>u COMPo ; 
+A:is%>u MOS ;
+A+Cmp/Attr:â%>  R ;
+A:is%> A_VUOTA ;
+A:âs A_ADV ;
+A: A_LASJ ; !  


LEXICON PASTIL !!= __@CODE@__ directing to A_LITTER, attr. pastelis; adj is PASTEL
+A+Attr:%>is  K ; !
A_LITTER ;
+A:%>u COMPo ; 
+A:%>u MOS ;
+A:%> A_VUOTA ;
+A: A_ADV ; !njyebžilávt
+A+Cmp/Attr:%>is R ;
+A+Cmp/SgNom: R ;
+A:%^FCD%> A_LASJ ; !  

LEXICON FISKAD !!= __@CODE@__ directing to A_LITTER, attr. fiskis (fikâdis)
+A+Attr:is%^CSH%> K ; ! fiskis sahis
+A+Attr+Err/Orth:âd%>is K ;
:âd A_LITTER ;
+A:âd%>u COMPo ; 
+A:âd%>u MOS ;
+A+Cmp/Attr:i%^CSH%>s R ;
+A+Cmp/Attr:âd%>is R ;
+A:âd%> A_VUOTA ;
+A:âd A_ADV ; !muččâdávt joskâdávt
+A+Attr:i%^WG%>s A_LAGAN ; !čapisláván
+A:is%^CSH%> A_LASJ ; !  


LEXICON JUOLLAD !!= __@CODE@__ directing to A_LITTER, attr, juolis, juolâdis, juolâdâs
+A+Attr:is%^WG K ; ! juolis
+A+Attr:âd%^WG%>is  K ; !
+A+Attr:âd%^WG%>âs  K ; !
:âd A_LITTER ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A+Cmp/Attr:is%^WG%> R ;
+A+Attr:âd%^WG%>is  R ; !
+A+Attr:âd%^WG%>âs  R ; !
+A:%> A_VUOTA ; 
+A:is%^WG%> A_LASJ ; !  

LEXICON A_LITTER !!= __@CODE@__ directing to X_LITTER, no attr.
+A: X_LITTER ;
!fiskâdub fiskâduboh fijnásuboi vaigâdub tergâdub pehtilub
+A: A_LASJ ; !  


LEXICON CAVOS_nocomp !!= __@CODE@__ directing to X_SAJOS, attr. almos
+A+Attr:%^FCD%^CSH%^SVSH  K ; ! čapo
+A: X_SAJOS	  ;
+A+Cmp/Attr:  R ;
+A:%> A_VUOTA ;
+A:%^CSH%^SVSH%> A_LASJ ; !  


LEXICON ALMOS !!= __@CODE@__ directing to X_SAJOS, attr. almos
+A+Attr:%^WG%^SVSH  K ; !
+A: X_SAJOS	  ;
+A:%^WG%^SVSH%>u COMPo ; 
+A:%^SVSH%>u MOS ;
+A+Cmp/Attr:%^WG%^SVSH%>  R ;
+A:%> A_VUOTA ;
+A:%^WG%^SVSH%> A_ADV ; !almoslávt
+A:%^WG%^SVSH%> A_LASJ ; !  


LEXICON A_APINA !!= __@CODE@__ directing to X_APINA, attr. apina
+A+Attr:  K ; !placeholder
+A: X_APINA	  ;
+A+Cmp/Attr: R ;
+A: A_LASJ ; !  

LEXICON SOOHAD_nocomp !!= __@CODE@__ directing to X_EEMEED, attr. sohâdis
+A+Attr:%^WG%^SVSH%>is  K ; !sapedis, sapedâs
+A+Attr+Use/NG:%^WG%^SVSH%>âs  K ; !
+A: X_EEMEED ;
+A:%> A_VUOTA ;
+A:%^WG%^SVSH A_ADV ; !jotelávt
+A+Cmp/Attr:%^WG%^SVSH%>is R ;
+A+Cmp/Attr:%^WG%^SVSH%>âs R ;
+A:%^WG%^SVSH%> A_LASJ ; !  


LEXICON SOOHAD !!= __@CODE@__ directing to X_EEMEED, attr. sohâdis
+A+Attr:%^WG%^SVSH%>is  K ; !sapedis, sapedâs
+A+Attr:%^WG%^SVSH%>âs  K ; !
+A: X_EEMEED ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A:%> A_VUOTA ;
+A:%^WG%^SVSH A_ADV ; !jotelávt
+A+Cmp/Attr:%^WG%^SVSH%>is R ;
+A+Cmp/Attr:%^WG%^SVSH%>âs R ;
+A:%^WG%^SVSH%> A_LASJ ; !  

LEXICON MAAJEEB-COMP !!= __@CODE@__ comparative lexicon
+A+Comp+Attr:b  K ; !
+A+Comp:b X_EEMEED ;
+A:%^RVSH%^SVSH%> MUSj ; !maajee


LEXICON PEEGGALM !!= __@CODE@__ directing to X_EEMEED, attr. sohâdis
+A+Attr:%^CSH%^SVSH%>âs  K ; !pegâlmâs
+A: X_EEMEED ;
+A:%^CSH%^SVSH%>u COMPo ;
+A:%>u MOS ;
+A:%> A_VUOTA ;
+A+Cmp/Attr:%^CSH%^SVSH%>âs R ; 
+A:%^WG%^SVSH%> A_LASJ ; !  


LEXICON JUARBUDEVES

+A+Sg+Nom:eves K ;	! juárbudeves
+A+Sg+Gen:iäkká K ;	! juárbudiäkká
+A+Sg+Acc:iäkká K ;	! juárbudiäkká
+A+Sg+Ill:iäkká%>s K ;	! juárbudiäkkás
+A+Sg+Loc:iäkká%>st K ;	! juárbudiäkkást
+A+Sg+Com:iäkk%>áin K ;	! juárbudiäkkáin
+A+Ess:eves%>in K; 		! juárbudevesin
+A+Pl+Nom:iäkká%>h K ;	! juárbudiäkkáh
+A+Pl+Gen:iäkká%>i K ;	! juárbudiäkkái
+A+Pl+Acc:iäkká%>id K ;	! juárbudiäkkáid
+A+Pl+Ill:iäkká%>id K ;	! juárbudiäkkáid
+A+Pl+Loc:iäkká%>in K ;	! juárbudiäkkáin
+A+Pl+Com:iäkká%>iguin K ;! juárbudiäkkáiguin
+A+Pl+Abe:iäkká%>ittáá K ;	! juárbudiäkkáittáá



LEXICON A_KOLME !!= @CODE@ ad hoc three syllable stem > LITTER
+A+Attr:  K ; !placeholder
 A_LITTER ;


LEXICON KUBDUV !!= __@CODE@__ . It doesn't fit with KAAMUV because of kubduvân, kubduvist
+A+Sg+Nom:uv K ;   	! kaamuv
+A+Ess:âg%>in K ;
+A+Par:âg%>id K ;
+A+Sg+Ill:uv%>ân K ;
+A+Sg+Loc:uv%>ist K ;        		! kammust
+A:u X_KAAMUV_COMMON ;	
+A+Attr:â K ;     !attr. kubdâ, ollâ 
+A+Cmp/Attr:â R ;
+A:u%> A_VUOTA ;
+A:âg A_ADV  ;     !kubdâgávt 

LEXICON KUBDUV_COMP
+A:%^WG%> CMUS ;
+A+Comp+Sg+Nom:%^WG%^RLEN%>eb K ;
+A+Comp+Sg+Gen:%^WG%^RLEN%>eb K ;
+A+Comp+Sg+Acc:%^WG%^RLEN%>eb K ;
+A+Comp+Sg+Ill:%^WG%>bân K ;
+A+Comp+Sg+Loc:%^WG%>bist K ;
+A+Comp+Sg+Com:%^WG%>báin K ;
+A+Comp+Sg+Abe:%^WG%^RLEN%>ebttáá K ;
+A+Comp+Ess:%^WG%>bin K ;
+A+Comp+Par:%^WG%>bid K ;
+A+Comp+Pl+Nom:%^WG%>beh K ;
+A+Comp+Pl+Gen:%^WG%>bij K ;
+A+Comp+Pl+Acc:%^WG%>bijd K ;
+A+Comp+Pl+Ill:%^WG%>báid K ;
+A+Comp+Pl+Loc:%^WG%>bijn K ;
+A+Comp+Pl+Com:%^WG%>bijguin K ;
+A+Comp+Pl+Abe:%^WG%>bijttáá K ;


!! !!Contracted stems
! ----------------
LEXICON STYERES !!= __@CODE@__ directing to X_KAARBIS, attr. stuorrâ
+A+Attr:%^FCDâ  K ; ! stuorrâ
+A:%^FCD CCOMPe ;
+A:%^FCD%^SLEN%> CMUS ;
+A+Err/Orth:%^FCD%> CMUS ;
+A+Err/Orth:%^FCD%^SLEN%> MUS ; 
+A: X_KAARBIS ; !
+A+Cmp/Attr:%^FCDâ%> R ;
+A:%^WG%^RLEN%> A_VUOTA ;

LEXICON RIGES !!= __@CODE@__ directing to X_KAARBIS, attr. riges, 3st comparision
+A+Attr:%^WG%^RLEN  K ; ! riges
+A:%>u COMPo ; 
+A:%>u MOS ; 
+A: X_KAARBIS ; !
+A+Cmp/Attr:%^WG%^RLEN%> R ;
+A:%^WG%^RLEN%> A_VUOTA ;


LEXICON A_CISSAAS !!= __@CODE@__ directing to  X_CISSAAS, not in use yet
!+A+Attr:  K ; !placeholder
+A: X_CISSAAS ;
+A+Cmp/Attr:  R ;


LEXICON MUOTTII !!= __@CODE@__ almost like KARRII, direct to it?
+A+Attr:is K ; ! muottiis
+A: X_KARRII ;
+A:%^SV CCOMPe ;
+A:i%> CMUS ;
+A+Cmp/Attr:is R ; 

!+A+Sg+Nom: K ;  ! muottii
!+A+Sg+Gen: K ;  ! muottii
!+A+Sg+Acc: K ;  ! muottii
!+A+Sg+Ill:%>jân K ;  ! muottiijân
!+A+Sg+Loc:%>st K ;  ! muottiist
!+A+Sg+Com:%^SVSH%>jn K ;  ! muottijn
!+A+Sg+Abe:%>ttáá K ;  ! muottiittáá
!+A+Ess:%>jin K ;  ! muottiijin
!+Al+Pl+Nom:%>h K ;  ! muottiih
!+A+Pl+Gen:%^SVSH%>j K ;  ! muottij
!+A+Pl+Acc:%^SVSH%>jd K ;  ! muottijd
!+A+Pl+Ill:%^SVSH%>jd K ;  ! muottijd
!+A+Pl+Loc:%^SVSH%>jn K ;  ! muottijn
!+A+Pl+Com:%>guin K ;  ! muottiiguin
!+A+Pl+Abe:%>ttáá K ;  ! muottiittáá

LEXICON A_VUALAAS !!= __@CODE@__ 
+A+Attr:%^SV%^SLEN K ;
+A+Sg+Nom:%^SV%^SLEN%>š K ; 							! kamevuálááš
+A+Sg+Gen:%^SV%^SLEN%> K ; 							! kamevuáláá
+A+Sg+Acc:%^SV%^SLEN%> K ;					! kamevuáláá
+A+Sg+Ill:%>šân K ;		! kamevuálášân
+A+Sg+Loc:%>šist K ;				! kamevuálášist
+A+Sg+Com:%>šáin K ; 							! kamevuálášáin
+A+Sg+Abe:%^SV%^SLEN%>ttáá K ; 				! cissááttáá
+A+Ess:%^SV%^SLEN%^SVLOW%>žin K ;			! cissáážin
+A+Par:%^SV%^SLEN%^SVLOW%>žid K ; 		! cissáážid
+A+Pl+Nom:%^SV%^SLEN%>h K ; 				! cissááh
+A+Pl+Gen:%>i K ; 							! cissái
+A+Pl+Acc:%>id K ; 							! cissáid
+A+Pl+Ill:%>id K ; 							! cissáid
+A+Pl+Loc:%>in K ; 							! cissáin
+A+Pl+Com:%>iguin K ; 						! cissáiguin
+A+Pl+Com+Err/Orth:%>igijn K ; 						! cissáiguin
+A+Pl+Abe:%>ittáá K ; 						! cissáittáá
+A+Cmp/Attr:%^SV%^SLEN%>š R ;


LEXICON SIAMMAS !!= __@CODE@__ 
!+A+Attr:áš%^WG  K ; !jiävtâš; attr in a partial conguence; attr should not be given?
+A+Sg+Nom:áš K ; 					! siämmáš
+A+Sg+Gen:áá K ; 					! siämmáá
+A+Sg+Acc:áá K ;					! siämmáá
+A+Sg+Ill:á%>žân K ;				! siämmážân
+A+Sg+Loc:á%>st K ;					! siämmást
+A+Sg+Com:á%>in K ; 				! siämmáin
+A+Sg+Abe:áá%>ttáá K ; 				! siämmááttáá
+A+Ess:á%>žin K ;					! siämmážin
+A+Par:á%>žid K ; 					! siämmážid
+A+Pl+Nom:áá%>h K ; 				! siämmááh
+A+Pl+Gen:á%>i K ; 					! siämmái
+A+Pl+Acc:á%>id K ; 				! siämmáid
+A+Pl+Ill:á%>id K ; 				! siämmáid
+A+Pl+Loc:á%>in K ; 				! siämmáin
+A+Pl+Com:á%>iguin K ; 				! siämmáiguin
+A+Pl+Com+Err/Orth:á%>igijn K ; 	! siämmáigijn
+A+Pl+Abe:á%>ittáá K ; 				! siämmáittáá


LEXICON JIAVTAS !!= __@CODE@__ 
!+A+Attr:áš%^WG  K ; !jiävtâš; attr in a partial conguence; attr should not be given?
+A+Sg+Nom:áš%^WG K ; 							! jiävtáš
+A+Sg+Gen:áá K ; 							! jiävttáá
+A+Sg+Acc:áá K ;					! jiävttáá
+A+Sg+Ill:á%^WG%>žân K ;		! jiävtážân
+A+Sg+Loc:áá%>st K ;				! jiävttáást
+A+Sg+Com:á%>in K ; 							! jiävttáin
+A+Sg+Abe:áá%>ttáá K ; 				! jiävttááttáá
+A+Ess:á%^WG%>žin K ;			! jiävtážin
+A+Par:á%^WG%>žid K ; 		! jiävtážid
+A+Pl+Nom:áá%>h K ; 				! jiävttááh
+A+Pl+Gen:á%>i K ; 							! jiävttái
+A+Pl+Acc:á%>id K ; 						! jiävttáid
+A+Pl+Ill:á%>id K ; 						! jiävttáid
+A+Pl+Loc:á%>in K ; 						! jiävttáin
+A+Pl+Com:á%>iguin K ; 						! jiävttáiguin
+A+Pl+Com+Err/Orth:á%>igijn K ; 			! jiävttáigijn
+A+Pl+Abe:á%>ittáá K ; 						! jiävttáittáá

!+A+Cmp/Attr:áš%^WG%> R ;


LEXICON TAASSAS
!+A+Attr:âš  K ; 				!táássáš; attr in a partial congruens
+A+Sg+Nom+Err/Orth:âš  K ; 				!táássâš ! from corpus Err/Orth?
+A+Sg+Nom:áš  K ; 				!táássáš
+A+Sg+Gen:áá%^RVSH K ; 			! tássáá
+A+Sg+Acc:áá%^RVSH K ;			! tássáá
+A+Sg+Ill:á%^CSH%>žân K ;		! táásážân
+A+Sg+Loc:áá%^RVSH%>st K ;		! tássáást
+A+Sg+Com:á%^RVSH%>in K ; 		! tássáin
+A+Sg+Abe:áá%^RVSH%>ttáá K ; 	! tássááttáá
+A+Ess:á%^CSH%>žin K ;			! táásážin
+A+Par:á%^CSH%>žid K ; 			! táásážid
+A+Pl+Nom:áá%^RVSH%>h K ; 		! tássááh
+A+Pl+Gen:á%^RVSH%>i K ; 		! tássái
+A+Pl+Acc:á%^RVSH%>id K ; 		! tássáid
+A+Pl+Ill:á%^RVSH%>id K ; 		! tássáid
+A+Pl+Loc:á%^RVSH%>in K ; 		! tássáin
+A+Pl+Com:á%^RVSH%>iguin K ; 	! tássáiguin
+A+Pl+Com+Err/Orth:á%^RVSH%>igijn K ; 	! tássáigijn
+A+Pl+Abe:á%^RVSH%>ittáá K ; 			! tássáittáá
!+A:%>á AS_SUF ;
+A+Cmp/Attr:áš R ; !kuávdášmuseo


!! !!4 syll stems
! ----------------

LEXICON A_KUNAGAS !!= __@CODE@__ directing to  X_KUNAGAS
+A+Attr:  K ; !placeholder
+A:s X_KUNAGAS	  ;
+A+Cmp/Attr: R ;
+A:%> COMP ; !to be added
+A:%>u MOS ; ! 
+A: A_ADV ; !


LEXICON UANIHAS !!= __@CODE@__ directing to LAS_SUF, attr, -âš
+A+Sg+Nom:âš K ;
+A+Attr:is K ;
+A:%>á LAS_SUF ;
+A:%>u COMPo ;
+A:%>u MOS ;
+A: A_ADV ;
+A:is%> A_VUOTA ;
+A+Cmp/Attr:is R ;


LEXICON UNOHAS !!= __@CODE@__ directing to LAS_SUF, attr, -is
+A+Sg+Nom:âs K ;
+A+Attr:is K ;
+A:%>á LAS_SUF ;
+A:u COMPo ;
+A:u MOS ;
+A+Cmp/Attr:is R ;


LEXICON ANOLASJ !!= __@CODE@__ directing to LAS_COMMON, attr, -âš
!! Adj - pooliitlâš: # attr. pooliitlâš; puolikongruenssi mahdollinen (#-paikat), puolikongr. asemesta SgNom.
+A+Attr:âš K ;
+A: LAS_COMMON ; 
+A:u COMPo ;
+A:u MOS ; ! tehálumos
+A+Cmp/Attr:âš R ;
+A+Cmp/SgNom:âš R ;
+A: A_ADV ; !máhđulávt
+A:âš%> A_VUOTA ;

LEXICON LAGAN !!= __@CODE@__ directing to A_LITTER, attr. -lágán
+A+Attr:lágán K ;
+A+Attr+Use/NG:láván K ;
:lágán A_LITTER ;
+Use/NG:láván A_LITTER ;
!+A:%>u MOS ; ! jiešlágánumosáid
+A+Cmp/Attr:lágán R ;

LEXICON RAPPADAS
+A+Attr:is K ;
+A:á X_RASTAGAS ;
+A+Cmp/Attr:is R ;

!+A+Sg+Nom:â%>š K ; ! rappâdâš
!+A+Sg+Gen:%>áá K ; 			! rappâdáá
!+A+Sg+Acc:%>áá K ; 			! rappâdáá
!+A+Sg+Ill:â%>žân K ;	! rappâdâžân
!+A+Sg+Loc:áá%>st K ;			! rappâdáást
!+A+Sg+Com:á%>in K ; 			! rappâdáin
!+A+Sg+Abe:áá%>ttáá K ; 		! rappâdááttáá
!+A+Ess:â%>žžân K ;	! rappâdâžžân
!+A+Par:â%>žžâd K ; ! rappâdâžžâd
!+A+Pl+Nom:áá%>h K ; 			! rappâdááh
!+A+Pl+Gen:á%>i K ; 			! rappâdái
!+A+Pl+Acc:á%>id K ; 			! rappâdáid
!+A+Pl+Ill:á%>id K ; 			! rappâdáid
!+A+Pl+Loc:á%>in K ; 			! rappâdáin
!+A+Pl+Com:á%>iguin K ; 		! rappâdáiguin
!+A+Pl+Com+Err/Orth:á%>igijn K ; 		! rappâdáigujn
!+A+Pl+Abe:á%>ittáá K ; 		! rappâdáittáá




LEXICON MUSTOTTEM !!= __@CODE@__ not all forms. attr. -es
+A+Attr:s K;
+A+Attr:mes K ;
+A+Sg+Nom:m K ;
+A+Sg+Ill:mân K ;
+A+Sg+Loc:mist K ;
+A+Sg+Com:máin K ;
+A+Cmp/Attr:m R ;

LEXICON A_INDECL !!= __@CODE@__ not all forms., attr = Sg. Nom.
+A+Attr: K ;    ! kulhijdum
+A+Sg+Nom: K ;  ! kulhijdum
+A+Cmp/Attr:  R ;
+A: A_LASJ ; !


LEXICON OLMOOSMETTUM !!= __@CODE@__, not all forms. attr. = Sg. Nom.
:máttum METTUMforms ;
:mettum METTUMforms ;

LEXICON METTUMforms
+A+Attr: K ;
+A+Sg+Nom: K ;
+A+Cmp/Attr: R ;
+A:%> A_VUOTA ;
+A: A_ADV ;

LEXICON AS_SUF  !!= __@CODE@__ 
+Sg+Gen:á%^RVSH K ;
+Sg+Acc:á%^RVSH K ;
+Sg+Ill:ž%>ân K ;
+Sg+Loc:á%>st K ;
+Sg+Com:%>jn K ;
+Ess:%>âžžân K ;
+Par:%>âžžâd K ;
+Pl+Nom:%>áh K ;
+Pl+Gen:%>j K ;
+Pl+Acc:%>id K ;
+Pl+Ill:%>id K ;
+Pl+Loc:%>in K ;
+Pl+Com:%>jguin K ;
+Pl+Com+Err/Orth:%>jgijn K ;
+Pl+Abe:%>jttáá K ;



!! !!Comparision
! -----------
 
 
LEXICON COMPe !!= * __@CODE@__ NUORA uses this lexicon
+Comp: COMP-SUF ;
+Comp+Attr:%^RLEN K ;
+Comp+Der/Dimin:âš K ; !needs lexicon


LEXICON VVCOMPe !!= * __@CODE@__ NUORA uses this lexicon
+Comp: VVCOMP-SUF ;
+Comp+Attr:b%^SVSH%^RLEN K ;
+Comp+Attr:p%^SVSH%^RLEN K ; !Err/Orth?
+Comp+Der/Dimin:b%^SVSH%>âš K ; !needs lexicon





LEXICON COMPclen !!= * __@CODE@__ NUORA uses this lexicon
+Comp: COMP-SUFclen ;
+Comp+Attr:%^RLEN K ;
+Comp+Der/Dimin:âš K ; !needs lexicon


LEXICON CCOMPe !!= * __@CODE@__ STYERES uses this lexicon
+Comp:b CCOMP-SUF ;
+Comp+Attr:b K ;
+Comp+Der/Dimin:bâš K ; !needs lexicon


LEXICON eCOMP !!= * __@CODE@__
+Comp: eCOMP-SUF ;
+Comp+Attr:eb K ;
 
LEXICON COMP !!= * __@CODE@__
+Comp:b COMP-SUF ;
+Comp+Attr: K ;
 
 
LEXICON COMPo !!= * __@CODE@__ NJUORAS, NJAALGIS uses this lexicon
+Comp: oCOMP-SUF ;
+Comp+Attr:b K ;
+Comp:bb  COMPA_ADV ; !vijđásubbooht

LEXICON eCOMP-SUF  !!= * __@CODE@__ 
+Sg+Nom+Use/NG:eb K ;
 :b COMP-SUF ;

LEXICON oCOMP-SUF  !!= * __@CODE@__ 
+Sg+Nom:b K ;
+Sg+Gen:b K ;
+Sg+Acc:b K ;
+Sg+Ill:ub%>on K ; !uu?
+Sg+Loc:bb%>oost K ;
+Sg+Com:bb%>oin K ; ! XXX
+Sg+Abe:b%>ttáá K ;
+Ess:bb%>on K ; 
+Ess+Err/Orth:b%>on K ; 
+Par:bb%>od K ;
+Pl+Nom:b%>oh K ;
+Pl+Gen:b%>oi K ;
+Pl+Acc:b%>oid K ;
+Pl+Ill:bb%>oid K ;
+Pl+Loc:b%>oin K ;
+Pl+Com:b%>oiguin K ;
+Pl+Abe:b%>oittáá K ;


LEXICON CCOMP-SUF  !!= * __@CODE@__  STYERES uses this lexicon
+Sg+Nom: K ;
+Sg+Gen: K ;
+Sg+Acc: K ;
+Sg+Ill:%^SLEN%>ân K ;
+Sg+Loc:%^SLEN%>ist K ;
+Sg+Com:%^SLEN%>áin K ;
+Sg+Abe:%>ttáá K ;
+Ess:%^SLEN%>in K ;
+Par:%^SLEN%>id K ;
+Pl+Nom:%^SLEN%>eh K ;
+Pl+Nom+Err/Orth:%>eh K ;
+Pl+Gen:%^SLEN%>ij K ;
+Pl+Gen+Err/Orth:%>ij K ;
+Pl+Acc:%^SLEN%>ijd K ;
+Pl+Acc+Err/Orth:%>ijd K ;
+Pl+Ill:%^SLEN%>áid K ;
+Pl+Ill+Err/Orth:%>áid K ;
+Pl+Loc:%^SLEN%>ijn K ;
+Pl+Loc+Err/Orth:%>ijn K ;
+Pl+Com+Err/Orth:%^SLEN%>ijgijn K ;
+Pl+Com:%^SLEN%>ijguin K ;
+Pl+Com+Err/Orth:%>ijguin K ;
+Pl+Abe:%^SLEN%>ijttáá K ;
+Pl+Abe+Err/Orth:%>ijttáá K ;


LEXICON COMP-SUF  !!= * __@CODE@__ 
+Sg+Nom:%^RLEN%^SLEN K ;
+Sg+Gen:%^RLEN%^SLEN K ;
+Sg+Acc:%^RLEN%^SLEN K ;
+Sg+Ill:%>ân K ;
+Sg+Loc:%>ist K ;
+Sg+Com:%>áin K ;
+Sg+Abe:%^RLEN%^SLEN%>ttáá K ;
+Ess:%>in K ;
+Par:%>id K ;
+Pl+Nom:%>eh K ;
+Pl+Gen:%>ij K ;
+Pl+Acc:%>ijd K ;
+Pl+Ill:%>áid K ;
+Pl+Loc:%>ijn K ;
+Pl+Com:%>ijguin K ;
+Pl+Abe:%>ijttáá K ;


LEXICON VVCOMP-SUF  !!= * __@CODE@__ 
+Sg+Nom:b%^SVSH%^RLEN%^SLEN K ;
+Sg+Gen:b%^SVSH%^RLEN%^SLEN K ;
+Sg+Acc:b%^SVSH%^RLEN%^SLEN K ;

+Sg+Ill:b%>ân K ;
+Sg+Loc:b%>ist K ;
+Sg+Com:b%>áin K ;
+Sg+Abe:b%^SVSH%^RLEN%^SLEN%>ttáá K ;
+Ess:b%>in K ;
+Par:b%>id K ;
+Pl+Nom:b%>eh K ;
+Pl+Gen:b%>ij K ;
+Pl+Acc:b%>ijd K ;
+Pl+Ill:b%>áid K ;
+Pl+Loc:b%>ijn K ;
+Pl+Com:b%>ijguin K ;
+Pl+Abe:b%>ijttáá K ;


LEXICON COMP-SUFclen  !!= * __@CODE@__ 
+Sg+Nom:%^CLEN%^SLEN K ;
+Sg+Gen:%^CLEN%^SLEN K ;
+Sg+Acc:%^CLEN%^SLEN K ;
+Sg+Ill:%>ân K ;
+Sg+Loc:%>ist K ;
+Sg+Com:%>áin K ;
+Sg+Abe:%^CLEN%^SLEN%>ttáá K ;
+Ess:%>in K ;
+Par:%>id K ;
+Pl+Nom:%>eh K ;
+Pl+Gen:%>ij K ;
+Pl+Acc:%>ijd K ;
+Pl+Ill:%>áid K ;
+Pl+Loc:%>ijn K ;
+Pl+Com:%>ijguin K ;
+Pl+Abe:%>ijttáá K ;

 
LEXICON SUP_MUS  !!= * __@CODE@__
+A: MUS ;

LEXICON SUP_MUSJ  !!= * __@CODE@__ majemuš
+A: MUSj ;


LEXICON MUS !!= * __@CODE@__
+Superl: MUS-SUF ;
+Superl+Attr:mus K ;
+Superl+Der/AAdv+Adv:mus%>ávt K ;
+Superl+Der/taa+Adv:mus%>táá K ; !pyeremustáá

LEXICON MUSj !!= * __@CODE@__ SUP_MUSJ uses this lexicon, eg. majemuš
+Superl: MUSj-SUF ;
+Superl+Attr:muš K ;
+Superl+Der/AAdv+Adv:muš%>ávt K ;


LEXICON CMUS !!= * __@CODE@__  STYERES uses this lexicon
+Superl: CMUS-SUF ;
+Superl+Attr:mus K ;
+Superl+Der/AAdv+Adv:mus%>ávt K ;


LEXICON MOS !!= * __@CODE@__
+Superl:mos X_LITTER ;
+Superl+Attr:mos K ;
+Superl+Der/AAdv+Adv:mos%>ávt K ;

LEXICON MUS-SUF !!= * __@CODE@__
+Sg+Nom:mus K ;
+Sg+Gen:muu K ;
+Sg+Acc:muu K ;
+Sg+Ill:musân K ;
+Sg+Loc:muust K ;
+Sg+Com:muin K ;
+Sg+Abe:muuttáá K ;
+Ess:mussân K ;
+Par:mussâd K ;
!+Pl+Nom:musah K ;
+Pl+Nom:muuh K ;
+Pl+Gen:mui K ;
+Pl+Acc:muid K ; !7 stuárráámussáid 6 stuárráámusâid
+Pl+Ill:muid K ;
!+Pl+Ill:%>ááid K ;
+Pl+Loc:muin K ;
+Pl+Com+Err/Orth:muigijn K ;
+Pl+Com:muiguin K ;
+Pl+Abe:muittáá K ;

LEXICON MUSj-SUF !!= * __@CODE@__
+Sg+Nom:muš K ;
+Sg+Gen:muu K ;
+Sg+Acc:muu K ;
+Sg+Ill:mužân K ;
+Sg+Loc:muust K ;
+Sg+Com:muin K ;
+Sg+Abe:muuttáá K ;
+Ess:mužžân K ;
+Par:mužžâd K ;
+Pl+Nom:muuh K ;
+Pl+Gen:mui K ;
+Pl+Acc:muid K ;
+Pl+Ill:muid K ;
!+Pl+Ill:%>ááid K ;
+Pl+Loc:muin K ;
+Pl+Com+Err/Orth:mušâijgijn K ;
+Pl+Com:muiguin K ;
+Pl+Abe:muittáá K ;
+Cmp/SgNom:muš R ;

LEXICON CMUS-SUF !!= * __@CODE@__
+Sg+Nom:mus K ;
+Sg+Gen:muu K ;
+Sg+Acc:muu K ;
+Sg+Ill:mui K ;
+Sg+Ill+Err/Orth:musân K ;
+Sg+Loc:muust K ;
+Sg+Com:muin K ;
+Sg+Abe:muuttáá K ;
+Ess+Err/Orth:mužžân K ;
+Ess:mussân K ;
+Par+Err/Orth:mužžâd K ;
+Par:mussâd K ;
+Pl+Nom:muuh K ;
+Pl+Nom+Err/Orth:musah K ;
+Pl+Gen:mui K ;
+Pl+Gen+Err/Orth:musâi K ;
+Pl+Acc:muid K ; !7 stuárráámussáid 6 stuárráámusâid
+Pl+Acc+Err/Orth:musâid K ; !7 stuárráámussáid 6 stuárráámusâid
+Pl+Ill:muid K ;
+Pl+Ill+Err/Orth:mussáid K ;
!+Pl+Ill:%>ááid K ;
+Pl+Loc:muin K ;
+Pl+Loc+Err/Orth:musâin K ;
+Pl+Com+Err/Orth:muigijn K ;
+Pl+Com:muiguin K ;
+Pl+Com+Err/Orth:musâiguin K ;
+Pl+Abe:muittáá K ;
+Pl+Abe+Err/Orth:musâittáá K ;


LEXICON A_VUOTA  !!= * __@CODE@__ for derivation
+Der/vuota:#vuot%^RCâ 2KIELA ;

LEXICON A_ADV  !!= * __@CODE@__ for derivation
+Der/AAdv+Adv:ávt K ; !pyeremusávt
+Der/AAdv+Adv+Use/NG:âht K ; !pyeremusâht

LEXICON COMPA_ADV  !!= * __@CODE@__ for derivation
+Der/AAdv+Adv:oht K ; !vijđásubboht


!LEXICON A_ADV_VAANIS  _ for derivation
!+Der/AAdv+Adv:ht K ; !vääniht


LEXICON A_LAGAN  !!= * __@CODE@__ for derivation
+Der/lagan:# LAGAN ;

LEXICON A_LASJ   !!= __@CODE@__ 
+Der/las:l ANOLASJ ; 

