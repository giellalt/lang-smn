! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no



! ===========================
!! !!!Adjective affixes
! ===========================


!! Order of triggers:
!! {{{
!! SV|ÁE|ÁI|EE  AO|EA|SC  FCD  EWG|WG|CSH  CLEN  VHIGH|VBACK  RVSH|RLEN  SLEN|SVSH  SVLOW
!! }}}

LEXICON adjstems  !!= __@CODE@__ temporary, for stems waiting for a lexicon
+A+Attr: K ;
+A+Sg+Nom: K ;

LEXICON ATTR !!= __@CODE@__ for stems with only attribute form
+A+Attr:  K ;


!! !!Even-syllable stems
! -------------------

LEXICON ALKKEE  !!= __@CODE@__ directing to X_TAHHEE, attr. älkkees
+A+Attr:s K ; ! älkkees
+A+Sg+Ill:%>jân K ;		! Adjective illative eejân
ALKKEE_COMMON ;
+A:%> A_VUOTA ;

LEXICON MAKKUU  !!= __@CODE@__ directing to X_TAHHEE, attr. älkkees
+A+Attr:s K ; ! älkkees
+A+Sg+Ill:%^EE%>vân K ;		! Adjective illative uvân
ALKKEE_COMMON ;
+A:%> A_VUOTA ;

LEXICON PYECCEE  !!= __@CODE@__ directing to X_TAHHEE, attr. pyeccee
+A+Attr: K ; 
+A+Sg+Ill:%>jân K ;		! Adjective illative eejân
ALKKEE_COMMON ;


LEXICON ALKKEE_COMMON !!= __@CODE@__ common for ALKKEE and PYECCEE
+A: X_TAHHEE ;
+A: VVCOMPe ; ! assab
+A:%> MUS ; ! váiváámus hoppuumus
+A+Cmp/SgNom:  R ;



LEXICON UCES !!= __@CODE@__ not yet done, attr uccâ
+A+Attr:â K ;     !attr. uccâ
+A+Sg+Nom:e%^WG%>s K ;
+A:es%> COMP ;
+A:â%^WG%> MUS ;
+A:e%^WG%> MUS ;
+A:e%> A_VUOTA ;
+A+Cmp/SgNom:â R ;
+A+Cmp/SgNom: R ;


LEXICON PUIGA !!= __@CODE@__ not yet done, attr puigâ
+A+Attr: K ; ! pu´igâ
+A+Sg+Nom: K ;
+A:%> COMP ;
+A:%> MUS ;
+A+Cmp/SgNom: R ;
+A:%> A_VUOTA ;


LEXICON HIAJU !!= __@CODE@__ directing to X_VUAGGU, attr. hiäjus
+A+Attr:s K ; ! hiäjus
+A: X_VUAGGU ;
+A:%> COMP ;
+A:%> MUS ;
+A+Cmp/SgNom: R ;

 
LEXICON CEPPI !!= __@CODE@__ directing to X_AIGI, attr. čepis
+A+Attr:%^WG%>s K ;
+A: X_AIGI ;
+A:b%^WG COMPe ;
+A:%^WG%> MUS ;
+A+Cmp/SgNom: R ;
+A:%> A_VUOTA ;


LEXICON KUKKE !!= __@CODE@__ directing to X_ALGE, attr. kuhes
+A+Attr:%^WG%>s K ;
+A: X_ALGE ;
+A:%^SVb%^WG COMPclen ;
+A+Err/Orth:b%^WG COMPe ;
+A:%^WG%> MUS ;
+A+Cmp/SgNom: R ;
+A:%> A_VUOTA ;



LEXICON HUMO !!= __@CODE@__ directing to X_NJUNE, attr. humos
+A+Attr:%^WG%>s K ;
+A: X_NJUNE ;
+A:b COMPe ;
+A:%> MUS ;
+A+Cmp/SgNom: R ;

LEXICON JULME !!= __@CODE@__ directing to X_OKKO, attr. julmes
+A+Attr:%^WG%>s K ;
+A: X_OKKO ;
+A:b COMPe ;
+A:%> MUS ;
+A+Cmp/SgNom: R ;


LEXICON LAAMI !!= __@CODE@__ directing to X_SAAMI, attr. lämis
+A+Attr:s  K ; !lämis
+A: X_SAAMI	  ;

LEXICON PYERI !!= __@CODE@__ directing to X_KYELI, attr. pyeri
+A+Attr:  K ; 
+A: X_KYELI	  ;
+A:%^WG%> eCOMP ;
+A:%^WG%> MUS ;

LEXICON HYENI !!= __@CODE@__ directing to X_KYELI, attr. pyeri
+A+Attr:%^WG%>s  K ; 
+A: X_KYELI	  ;
+A:%^WG%> eCOMP ;
+A:%^WG%> MUS ;


LEXICON NUORA !!= __@CODE@__ directing to X_KIELA, attr. nuora
+A+Attr:  K ; 
+A: X_KIELA	  ;
+A:b COMPe ;
+A:%> MUS ;




!! !!Odd-syllable stems
! ------------------

LEXICON NJUORAS !!= __@CODE@__ directing to X_RUONAS, attr. njuorâs
+A+Attr:%^EWG K ; !njuorâs
+A: X_RUONAS ;
+A:%^WG%>u COMPo ; 
+A:%^WG%>u MOS ;
+A:%> A_VUOTA ;
+A:%^WG A_ADV ; !ruávásávt kiärásávt
+A+Cmp/SgNom:%^EWG%^RLEN R ;


LEXICON HAANIS !!= __@CODE@__ directing to X_PAAVIR, attr. hänis
+A+Attr:%^EWG%^RVSH K ; ! hänis
+A: X_PAAVIR	  ;
+A:%>u COMPo ; 
+A:%>u MOS ;
+A+Cmp/SgNom:%^EWG%^RLEN R ;
+A:%^EWG%^RVSH%> A_VUOTA ;

LEXICON KIARGUS_nocomp !!= __@CODE@__  like HAANIS, directing to X_PAAVIR, 
+A+Attr:%^EWG%^RVSH K ; ! 
+A: X_PAAVIR	  ;
+A+Cmp/SgNom:%^EWG%^RLEN R ;
+A:%^EWG%^RVSH%> A_VUOTA ;


LEXICON HAAITIS !!= __@CODE@__ directing to X_PAAVIR, attr. rähis
+A+Attr:%^EWG%^RVSH  K ; !rähis
+A: X_PAAVIR	  ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A+Cmp/SgNom:%^EWG%^RLEN R ;
+A:%> A_VUOTA ;

LEXICON HEEITUG !!= __@CODE@__ directing to X_PAAVIR, attr. heitugis, heitugâs
+A+Attr:%^WG%^RVSH%>is  K ; !heitugis
+A+Attr:%^WG%^RVSH%>âs  K ; !heitugâs
+A: X_PAAVIR	  ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A+Cmp/SgNom:%^EWG%^RLEN R ;
+A:%> A_VUOTA ;


LEXICON VAANIS !!= __@CODE@__ directing to  X_PAAVIR, attr. väni
+A+Attr:%^FCD%^EWG%^RVSH K ; ! väni
+A: X_PAAVIR	  ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A+Cmp/SgNom:%^EWG%^RLEN R ;
+A:%^EWG%^RVSH%> A_VUOTA ;
+A:%^EWG%^RVSH A_ADV ;


LEXICON NJAALGIS !!= __@CODE@__ directing to  X_KAARBIS, Attr. not in the lexicon
+A: X_KAARBIS ;
+A:%^WG%>u COMPo ; !njálgásub njá%^RVlgás%^ÁI
+A:%>u MOS ;
+A+Cmp/SgNom:%^EWG%^RVSH R ;
+A:%^EWG%^RVSH%> A_VUOTA ; !njälgisvuođâ

LEXICON KOSKES !!= __@CODE@__ directing to  A_VEESKIR, attr. koške
+A+Attr:%^FCD%^EWG K ;     !attr. koške
A_VEESKIR ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;

+A:%^EWG%> A_VUOTA ;

LEXICON FIIJNES !!= __@CODE@__ directing to  A_VEESKIR, attr. fiijnâ and fijnâ, fijnásáin, fijnesist, fijnesân, fijnás
+A+Attr:%^FCDâ%^RLEN K ;     !attr. fiijnâ fi%^RVjnás%^ÁE
+A+Attr:%^FCDâ%^RVSH K ;     !attr. fijnâ
+A+Sg+Nom:%^EWG%^RLEN K ;	! veeskir
+A+Sg+Gen: K ;     			! viäskár máállás%^ÁI
+A+Sg+Acc: K ;    			 ! viäskár
+A+Sg+Ill:%^EWG%>ân K ; 	! fijnesân
+A+Sg+Loc:%^EWG%>ist K ; ! fijnesist
+A+Sg+Com:%>áin K ; ! viäskáráin
+A+Sg+Abe:%>ttáá K ; ! viäskárttáá
+A+Ess:%^EWG%^RVSH%>in K ;   ! veskirin 
+A+Par:%^EWG%^RVSH%>id K ;	! veskirid
+A+Ess:%>in K ;   ! viäskárin 
+A+Par:%>id K ;	! viäskárid
+A+Sg+Gen: CPXsuff ;  ! 
+A+Sg+Acc: CPXsuff ;  ! pirrâsis
+A+Sg+Ill:âs CPXsuff ;  ! pirrâsâsân
+A+Pl+Nom:%>eh K ;  ! viäskáreh
+A+Pl+Gen:%>ij K ;  ! viäskárij
+A+Pl+Acc:%>ijd K ; ! viäskárijd
+A+Pl+Ill:%>áid K ; ! viäskáráid
+A+Pl+Loc:%>ijn K ; ! viäskárijn
+A+Pl+Com:%>ijguin K ;  ! viäskárijguin
+A+Pl+Com+Err/Orth:%>ijgijn K ;  ! viäskárijguin
+A+Pl+Abe:%>ijttáá K ;  ! viäskárijttáá
+A+Pl+Gen:%>ijd CPXsuff ;  !
+A+Pl+Acc:%>ijd CPXsuff ;  !
+A+Pl+Com:%>ijd CPXsuffCOM ;  !
+A:%>u COMPo ; 
+A:%>u MOS ;
+A:%^EWG%> A_VUOTA ;
+A: A_ADV ;



LEXICON KOOIDAS !!= __@CODE@__ directing to  A_VEESKIR, attr. koidâ
+A+Attr:%^FCDâ%^RVSH K ;     !attr. koidâ
A_VEESKIR ;
+A:%>u COMPo ; 
+A:%>u MOS ;
+A:%^EWG%> A_VUOTA ;
+A: A_ADV ;


LEXICON SUOTAS  !!= __@CODE@__ directing to  A_VEESKIR, attr. suotâs
+A+Attr:%^EWG%^RVSH  K ; ! suotâs, vädis 
A_VEESKIR ;
+A:%>u COMPo ; 
+A:%^WG%>u COMPo ; 
+A+Err/Orth:%>u COMPo ; 
+A:%^WG%>u MOS ;
+A+Err/Orth:%>u MOS ;
+A:%^EWG%> A_VUOTA ;
+A: A_ADV ; !ollásávt

LEXICON SENES !!= __@CODE@__ not fitting in X_PELES because of CSH attr. seŋes
+A+Attr:%^EWG%^RVSH  K ; ! seŋes 
+A+Cmp/SgNom:%^EWG%^RVSH%> R ;
+A+Sg+Nom:%^EWG%^RVSH K ; !peles
+A+Sg+Gen: K ;   !
+A+Sg+Acc: K ;      
+A+Sg+Ill:%>ân K ; 
+A+Sg+Loc:%>ist K ;
+A+Sg+Com:%>áin K ;
+A+Sg+Abe:%>ttáá K ;  
+A+Ess:%^EWG%^RVSH%>in K ; 
+A+Par:%^EWG%^RVSH%>id K ; 
+A+Pl+Nom:%>eh K ;  
+A+Pl+Gen:%>ij K ;  
+A+Pl+Acc:%>ijd K ; 
+A+Pl+Loc:%>ijn K ; 
+A+Pl+Com:%>ijguin K ; 
+A+Pl+Com+Err/Orth:%>ijgijn K ; 
+A+Pl+Abe:%>ijttáá K ; 
+A+Pl+Ill:%>áid K ; 
+A:%>u COMPo ; 
+A:%^WG%>u COMPo ; 
+A+Err/Orth:%>u COMPo ; 
+A:%^WG%>u MOS ;
+A+Err/Orth:%>u MOS ;
+A:%^EWG%^RVSH%> A_VUOTA ;
+A: A_ADV ; !ollásávt


LEXICON A_VEESKIR  !!= __@CODE@__ directing to X_VEESKIR, no attr.
+A: X_VEESKIR ;
+A+Cmp/SgNom:%^EWG%^RLEN R ;


LEXICON KEPPIS !!= __@CODE@__ directing to A_LITTER, attr. keppâ
+A+Attr:â%> K ;    ! keppâ
+A+Attr:is%>is K ;    ! keppisis
:is A_LITTER ;
+A:is%>u COMPo ; 
+A:is%>u MOS ;
+A+Cmp/SgNom:is  R ;
+A:is%> A_VUOTA ;
+A:âs A_ADV ;


LEXICON PASTIL !!= __@CODE@__ directing to A_LITTER, attr. pastelis; adj is PASTEL
+A+Attr:%>is  K ; !
A_LITTER ;
+A:%>u COMPo ; 
+A:%>u MOS ;
+A:%> A_VUOTA ;
+A: A_ADV ; !njyebžilávt
+A+Cmp/SgNom: R ;

LEXICON FISKAD !!= __@CODE@__ directing to A_LITTER, attr. fiskis (fikâdis)
+A+Attr:i%^CSH%>s K ; ! fiskis sahis
+A+Attr+Err/Orth:âd%>is K ;
:âd A_LITTER ;
+A:âd%>u COMPo ; 
+A:âd%>u MOS ;
+A+Cmp/SgNom:i%^WG%>s R ;
+A:âd%> A_VUOTA ;
+A:âd A_ADV ; !muččâdávt joskâdávt
+A+Attr:i%^WG%>s A_LAGAN ; !čapisláván


LEXICON JUOLLAD !!= __@CODE@__ directing to A_LITTER, attr, juolis, juolâdis, juolâdâs
+A+Attr:i%^WG%>s K ; ! juolis
+A+Attr:âd%^WG%>is  K ; !
+A+Attr:âd%^WG%>âs  K ; !
:âd A_LITTER ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A+Cmp/SgNom:i%^WG%>s R ;
+A:%> A_VUOTA ; 

LEXICON A_LITTER !!= __@CODE@__ directing to X_LITTER, no attr.
+A: ODD_SUFF_LONGSTEM ;
+A: ODD_SUFF_SHORTSTEM ;
!fiskâdub fiskâduboh fijnásuboi vaigâdub tergâdub pehtilub


LEXICON CAVOS_nocomp !!= __@CODE@__ directing to X_SAJOS, attr. almos
+A+Attr:  K ; !
+A: X_SAJOS	  ;
+A+Cmp/SgNom:%^EWG%^SVSH  R ;
+A:%> A_VUOTA ;


LEXICON ALMOS !!= __@CODE@__ directing to X_SAJOS, attr. almos
+A+Attr:  K ; !
+A: X_SAJOS	  ;
+A:%^WG%^SVSH%>u COMPo ; 
+A:%^SVSH%>u MOS ;
+A+Cmp/SgNom:%^EWG%^SVSH  R ;
+A:%> A_VUOTA ;


LEXICON A_APINA !!= __@CODE@__ directing to X_APINA, attr. apina
+A+Attr:  K ; !placeholder
+A: X_APINA	  ;
+A+Cmp/SgNom: R ;

LEXICON SOOHAD_nocomp !!= __@CODE@__ directing to X_EEMEED, attr. sohâdis
+A+Attr:%^WG%^SVSH%>is  K ; !sapedis, sapedâs
+A+Attr:%^WG%^SVSH%>âs  K ; !
+A: X_EEMEED ;
+A:%> A_VUOTA ;
+A:%^WG A_ADV ; !jotelávt
+A+Cmp/SgNom: R ;


LEXICON SOOHAD !!= __@CODE@__ directing to X_EEMEED, attr. sohâdis
+A+Attr:%^WG%^SVSH%>is  K ; !sapedis, sapedâs
+A+Attr:%^WG%^SVSH%>âs  K ; !
+A: X_EEMEED ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A:%> A_VUOTA ;
+A:%^WG A_ADV ; !jotelávt
+A+Cmp/SgNom: R ;

LEXICON PEEGGALM !!= __@CODE@__ directing to X_EEMEED, attr. sohâdis
+A+Attr:%^CSH%^SVSH%>âs  K ; !pegâlmâs
+A: X_EEMEED ;
+A:%^CSH%^SVSH%>u COMPo ;
+A:%>u MOS ;
+A:%> A_VUOTA ;


LEXICON JUARBUDEVES

+A+Sg+Nom:%>eves K ;	! juárbudeves
+A+Sg+Gen:%>iäkká K ;	! juárbudiäkká
+A+Sg+Acc:%>iäkká K ;	! juárbudiäkká
+A+Sg+Ill:%>iäkkás K ;	! juárbudiäkkás
+A+Sg+Loc:%>iäkkást K ;	! juárbudiäkkást
+A+Sg+Com:%>iäkkáin K ;	! juárbudiäkkáin
+A+Ess:%>evesin K; 		! juárbudevesin
+A+Pl+Nom:%>iäkkáh K ;	! juárbudiäkkáh
+A+Pl+Gen:%>iäkkái K ;	! juárbudiäkkái
+A+Pl+Acc:%>iäkkáid K ;	! juárbudiäkkáid
+A+Pl+Ill:%>iäkkáid K ;	! juárbudiäkkáid
+A+Pl+Loc:%>iäkkáin K ;	! juárbudiäkkáin
+A+Pl+Com:%>iäkkáiguin K ;! juárbudiäkkáiguin
+A+Pl+Abe:%>iäkkáittáá K ;	! juárbudiäkkáittáá



LEXICON A_KOLME !!= @CODE@ ad hoc three syllable stem > LITTER
+A+Attr:  K ; !placeholder
 A_LITTER ;


LEXICON KUBDUV !!= __@CODE@__ . It doesn't fit with KAAMUV because of kubduvân, kubduvist
+A+Sg+Nom:%>v K ;   	! kaamuv
+A+Ess:â%>gin K ;
+A+Par:â%>gid K ;
+A+Sg+Ill:%>vân K ;
+A: X_KAAMUV_COMMON ;	
+A+Attr:â K ;     !attr. kubdâ, ollâ 
+A+Cmp/SgNom:â R ;
+A:%> A_VUOTA ;
+A+Attr:âg A_ADV  ;     !kubdâgávt 

LEXICON NELJA !!= @CODE@ ad hoc three syllable stem >
+A+Sg+Nom: K ;
+N+Sg+Nom: K ;

!! !!Contracted stems
! ----------------
LEXICON STYERES !!= __@CODE@__ directing to X_KAARBIS, attr. stuorrâ
+A+Attr:%^FCDâ  K ; ! stuorrâ
+A:%^FCD CCOMPe ;
+A:%^FCD%^SLEN%> CMUS ;
+A+Err/Orth:%^FCD%> CMUS ;
+A+Err/Orth:%^FCD%^SLEN%> MUS ; 
+A: X_KAARBIS ; !

+A+Cmp/SgNom:%^FCDâ R ;
+A:%^EWG%^RLEN%> A_VUOTA ;

LEXICON RIGES !!= __@CODE@__ directing to X_KAARBIS, attr. riges, 3st comparision
+A+Attr:%^EWG%^RLEN%>  K ; ! riges
+A:%>u COMPo ; 
+A:%>u MOS ; 
+A: X_KAARBIS ; !
+A+Cmp/SgNom:%^FCDâ R ;
+A:%^EWG%^RLEN%> A_VUOTA ;


LEXICON A_CISSAAS !!= __@CODE@__ directing to  X_CISSAAS, not in use yet
+A+Attr:  K ; !placeholder
+A: X_CISSAAS ;
+A+Cmp/SgNom:%^SV%^SLEN%>š  R ;


LEXICON MUOTTII !!= __@CODE@__ almost like KARRII, direct to it?
+A+Attr:is K ; ! muottiis
+A: X_KARRII ;
+A:%^SV CCOMPe ;
+A:i%> CMUS ;

!+A+Sg+Nom: K ;  ! muottii
!+A+Sg+Gen: K ;  ! muottii
!+A+Sg+Acc: K ;  ! muottii
!+A+Sg+Ill:%>jân K ;  ! muottiijân
!+A+Sg+Loc:%>st K ;  ! muottiist
!+A+Sg+Com:%^SVSH%>jn K ;  ! muottijn
!+A+Sg+Abe:%>ttáá K ;  ! muottiittáá
!+A+Ess:%>jin K ;  ! muottiijin
!+Al+Pl+Nom:%>h K ;  ! muottiih
!+A+Pl+Gen:%^SVSH%>j K ;  ! muottij
!+A+Pl+Acc:%^SVSH%>jd K ;  ! muottijd
!+A+Pl+Ill:%^SVSH%>jd K ;  ! muottijd
!+A+Pl+Loc:%^SVSH%>jn K ;  ! muottijn
!+A+Pl+Com:%>guin K ;  ! muottiiguin
!+A+Pl+Abe:%>ttáá K ;  ! muottiittáá

LEXICON A_VUALAAS !!= __@CODE@__ 
+A: X_VUALAAS ;

!+A+Sg+Nom:%^SV%^SLEN%>š K ; 							! kamevuálááš
!+A+Sg+Gen:%^SV%^SLEN%> K ; 							! kamevuáláá
!+A+Sg+Acc:%^SV%^SLEN%> K ;					! kamevuáláá
!+A+Sg+Ill:%>šân K ;		! kamevuálášân
!+A+Sg+Loc:%>šist K ;				! kamevuálášist
!+A+Sg+Com:%>šáin K ; 							! kamevuálášáin
!+A+Sg+Abe:%^SV%^SLEN%>ttáá K ; 				! cissááttáá
!+A+Ess:%^SV%^SLEN%^SVLOW%>žin K ;			! cissáážin
!+A+Par:%^SV%^SLEN%^SVLOW%>žid K ; 		! cissáážid
!+A+Pl+Nom:%^SV%^SLEN%>h K ; 				! cissááh
!+A+Pl+Gen:%>i K ; 							! cissái
!+A+Pl+Acc:%>id K ; 							! cissáid
!+A+Pl+Ill:%>id K ; 							! cissáid
!+A+Pl+Loc:%>in K ; 							! cissáin
!+A+Pl+Com:%>iguin K ; 						! cissáiguin
!+A+Pl+Com+Err/Orth:%>igijn K ; 						! cissáiguin
!+A+Pl+Abe:%>ittáá K ; 						! cissáittáá
!+A+Cmp/SgNom:%^SV%^SLEN%>š R ;


LEXICON JIAVTAS !!= __@CODE@__ directing to LAS_SUF, attr, -âš
+A+Attr:âš%^WG  K ; !jiävtâš
+A+Sg+Nom:áš%^WG  K ; !jiävtáš
+A:%>á LAS_SUF ;

LEXICON TAASSAS
+A+Attr:âš  K ; !táássáš
+A+Sg+Nom:áš  K ; !táássáš
+A:%>á LAS_SUF ;


!! !!4 syll stems
! ----------------

LEXICON A_KUNAGAS !!= __@CODE@__ directing to  X_KUNAGAS
+A+Attr:  K ; !placeholder
+A:s X_KUNAGAS	  ;
+A+Cmp/SgNom: R ;
+A:%> COMP ; !to be added
+A:%>u MOS ; ! 
+A: A_ADV ; !


LEXICON UANIHAS !!= __@CODE@__ directing to LAS_SUF, attr, -âš
+A+Sg+Nom:âš K ;
+A+Attr:is K ;
+A:%>á LAS_SUF ;
+A:%>u COMPo ;
+A:%>u MOS ;
+A: A_ADV ;
+A:is%> A_VUOTA ;


LEXICON UNOHAS !!= __@CODE@__ directing to LAS_SUF, attr, -is
+A+Sg+Nom:âs K ;
+A+Attr:is K ;
+A:%>á LAS_SUF ;
+A:u COMPo ;
+A:u MOS ;


LEXICON ANOLASJ !!= __@CODE@__ directing to LAS_COMMON, attr, -âš
!! Adj - pooliitlâš: # attr. pooliitlâš; puolikongruenssi mahdollinen (#-paikat), puolikongr. asemesta SgNom.
+A+Attr:âš K ;
+A: LAS_COMMON ; 
+A:u COMPo ;
+A:u MOS ; ! tehálumos
+A+Cmp/SgNom:âš R ;
+A: A_ADV ; !máhđulávt
+A:âš%> A_VUOTA ;

LEXICON LAGAN !!= __@CODE@__ directing to A_LITTER, attr. -lágán
+A+Attr:lágán K ;
+A+Attr+Use/NG:láván K ;
:lágán A_LITTER ;
+Use/NG:láván A_LITTER ;
!+A:%>u MOS ; ! jiešlágánumosáid

LEXICON RAPPADAS
+A+Attr:is K ;
+A:á X_RASTAGAS ;

!+A+Sg+Nom:â%>š K ; ! rappâdâš
!+A+Sg+Gen:%>áá K ; 			! rappâdáá
!+A+Sg+Acc:%>áá K ; 			! rappâdáá
!+A+Sg+Ill:â%>žân K ;	! rappâdâžân
!+A+Sg+Loc:áá%>st K ;			! rappâdáást
!+A+Sg+Com:á%>in K ; 			! rappâdáin
!+A+Sg+Abe:áá%>ttáá K ; 		! rappâdááttáá
!+A+Ess:â%>žžân K ;	! rappâdâžžân
!+A+Par:â%>žžâd K ; ! rappâdâžžâd
!+A+Pl+Nom:áá%>h K ; 			! rappâdááh
!+A+Pl+Gen:á%>i K ; 			! rappâdái
!+A+Pl+Acc:á%>id K ; 			! rappâdáid
!+A+Pl+Ill:á%>id K ; 			! rappâdáid
!+A+Pl+Loc:á%>in K ; 			! rappâdáin
!+A+Pl+Com:á%>iguin K ; 		! rappâdáiguin
!+A+Pl+Com+Err/Orth:á%>igijn K ; 		! rappâdáigujn
!+A+Pl+Abe:á%>ittáá K ; 		! rappâdáittáá




LEXICON MUSTOTTEM !!= __@CODE@__ not all forms. attr. -es
+A+Attr:s K;
+A+Attr:mes K ;
+A+Sg+Nom:m K ;
+A+Sg+Ill:mân K ;
+A+Sg+Loc:mist K ;
+A+Sg+Com:máin K ;
+A+Cmp/SgNom:m R ;

LEXICON A_INDECL !!= __@CODE@__ not all forms., attr = Sg. Nom.
+A+Attr: K ;    ! kulhijdum
+A+Sg+Nom: K ;  ! kulhijdum
+A+Cmp/SgNom:  R ;



LEXICON OLMOOSMETTUM !!= __@CODE@__, not all forms. attr. = Sg. Nom.
+A+Attr: K ;
+A+Sg+Nom: K ;
+A+Cmp/SgNom: R ;
+A:%> A_VUOTA ;
+A: A_ADV ;

!! !!Comparision
! -----------
 
 
LEXICON COMPe !!= * __@CODE@__ NUORA uses this lexicon
+Comp: COMP-SUF ;
+Comp+Attr:%^RLEN K ;
+Comp+Der/Dimin:âš K ; !needs lexicon


LEXICON VVCOMPe !!= * __@CODE@__ NUORA uses this lexicon
+Comp: VVCOMP-SUF ;
+Comp+Attr:b%^SVSH%^RLEN K ;
+Comp+Der/Dimin:b%^SVSHâš K ; !needs lexicon





LEXICON COMPclen !!= * __@CODE@__ NUORA uses this lexicon
+Comp: COMP-SUFclen ;
+Comp+Attr:%^RLEN K ;
+Comp+Der/Dimin:âš K ; !needs lexicon


LEXICON CCOMPe !!= * __@CODE@__ STYERES uses this lexicon
+Comp:b CCOMP-SUF ;
+Comp+Attr:b K ;
+Comp+Der/Dimin:bâš K ; !needs lexicon


LEXICON eCOMP !!= * __@CODE@__
+Comp: eCOMP-SUF ;
+Comp+Attr:eb K ;
 
LEXICON COMP !!= * __@CODE@__
+Comp:b COMP-SUF ;
+Comp+Attr: K ;
 
 
LEXICON COMPo !!= * __@CODE@__ NJUORAS, NJAALGIS uses this lexicon
+Comp: oCOMP-SUF ;
+Comp+Attr:b K ;

LEXICON eCOMP-SUF  !!= * __@CODE@__ 
+Sg+Nom:eb K ;
 :b COMP-SUF ;

LEXICON oCOMP-SUF  !!= * __@CODE@__ 
+Sg+Nom:b K ;
+Sg+Gen:b K ;
+Sg+Acc:b K ;
+Sg+Ill:ub%>on K ; !uu?
+Sg+Loc:bb%>oost K ;
+Sg+Com:bb%>oin K ; ! XXX
+Sg+Abe:b%>ttáá K ;
+Ess:bb%>on K ; 
+Ess+Err/Orth:b%>on K ; 
+Par:bb%>od K ;
+Pl+Nom:b%>oh K ;
+Pl+Gen:b%>oi K ;
+Pl+Acc:bb%>oid K ; ! XXX
+Pl+Ill:bb%>oid K ;
+Pl+Loc:bb%>oin K ;
+Pl+Com:b%>oiguin K ;
+Pl+Abe:b%>oittáá K ;


LEXICON CCOMP-SUF  !!= * __@CODE@__  STYERES uses this lexicon
+Sg+Nom: K ;
+Sg+Gen: K ;
+Sg+Acc: K ;
+Sg+Ill:%^SLEN%>ân K ;
+Sg+Loc:%^SLEN%>ist K ;
+Sg+Com:%^SLEN%>áin K ;
+Sg+Abe:%>ttáá K ;
+Ess:%^SLEN%>in K ;
+Par:%^SLEN%>id K ;
+Pl+Nom:%^SLEN%>eh K ;
+Pl+Nom+Err/Orth:%>eh K ;
+Pl+Gen:%^SLEN%>ij K ;
+Pl+Gen+Err/Orth:%>ij K ;
+Pl+Acc:%^SLEN%>ijd K ;
+Pl+Acc+Err/Orth:%>ijd K ;
+Pl+Ill:%^SLEN%>áid K ;
+Pl+Ill+Err/Orth:%>áid K ;
+Pl+Loc:%^SLEN%>ijn K ;
+Pl+Loc+Err/Orth:%>ijn K ;
+Pl+Com+Err/Orth:%^SLEN%>ijgijn K ;
+Pl+Com:%^SLEN%>ijguin K ;
+Pl+Com+Err/Orth:%>ijguin K ;
+Pl+Abe:%^SLEN%>ijttáá K ;
+Pl+Abe+Err/Orth:%>ijttáá K ;


LEXICON COMP-SUF  !!= * __@CODE@__ 
+Sg+Nom:%^RLEN%^SLEN K ;
+Sg+Gen:%^RLEN%^SLEN K ;
+Sg+Acc:%^RLEN%^SLEN K ;
+Sg+Ill:%>ân K ;
+Sg+Loc:%>ist K ;
+Sg+Com:%>áin K ;
+Sg+Abe:%^RLEN%^SLEN%>ttáá K ;
+Ess:%>in K ;
+Par:%>id K ;
+Pl+Nom:%>eh K ;
+Pl+Gen:%>ij K ;
+Pl+Acc:%>ijd K ;
+Pl+Ill:%>áid K ;
+Pl+Loc:%>ijn K ;
+Pl+Com:%>ijguin K ;
+Pl+Abe:%>ijttáá K ;


LEXICON VVCOMP-SUF  !!= * __@CODE@__ 
+Sg+Nom:b%^SVSH%^RLEN%^SLEN K ;
+Sg+Gen:b%^SVSH%^RLEN%^SLEN K ;
+Sg+Acc:b%^SVSH%^RLEN%^SLEN K ;

+Sg+Ill:b%>ân K ;
+Sg+Loc:b%>ist K ;
+Sg+Com:b%>áin K ;
+Sg+Abe:b%^SVSH%^RLEN%^SLEN%>ttáá K ;
+Ess:b%>in K ;
+Par:b%>id K ;
+Pl+Nom:b%>eh K ;
+Pl+Gen:b%>ij K ;
+Pl+Acc:b%>ijd K ;
+Pl+Ill:b%>áid K ;
+Pl+Loc:b%>ijn K ;
+Pl+Com:b%>ijguin K ;
+Pl+Abe:b%>ijttáá K ;


LEXICON COMP-SUFclen  !!= * __@CODE@__ 
+Sg+Nom:%^CLEN%^SLEN K ;
+Sg+Gen:%^CLEN%^SLEN K ;
+Sg+Acc:%^CLEN%^SLEN K ;
+Sg+Ill:%>ân K ;
+Sg+Loc:%>ist K ;
+Sg+Com:%>áin K ;
+Sg+Abe:%^CLEN%^SLEN%>ttáá K ;
+Ess:%>in K ;
+Par:%>id K ;
+Pl+Nom:%>eh K ;
+Pl+Gen:%>ij K ;
+Pl+Acc:%>ijd K ;
+Pl+Ill:%>áid K ;
+Pl+Loc:%>ijn K ;
+Pl+Com:%>ijguin K ;
+Pl+Abe:%>ijttáá K ;

 
LEXICON SUP_MUS  !!= * __@CODE@__
+A: MUS ;

LEXICON SUP_MUSJ  !!= * __@CODE@__ majemuš
+A: MUSj ;


LEXICON MUS !!= * __@CODE@__
+Superl: MUS-SUF ;
+Superl+Attr:mus K ;
+Superl+Der/avt+Adv:mus%>ávt K ;
+Superl+Der/taa+Adv:mus%>táá K ; !pyeremustáá

LEXICON MUSj !!= * __@CODE@__ SUP_MUSJ uses this lexicon, eg. majemuš
+Superl: MUSj-SUF ;
+Superl+Attr:muš K ;
+Superl+Der/avt+Adv:muš%>ávt K ;


LEXICON CMUS !!= * __@CODE@__  STYERES uses this lexicon
+Superl: CMUS-SUF ;
+Superl+Attr:mus K ;
+Superl+Der/avt+Adv:mus%>ávt K ;


LEXICON MOS !!= * __@CODE@__
+Superl:mos X_LITTER ;
+Superl+Attr:mos K ;
+Superl+Der/avt+Adv:mos%>ávt K ;

LEXICON MUS-SUF !!= * __@CODE@__
+Sg+Nom:mus K ;
+Sg+Gen:muu K ;
+Sg+Acc:muu K ;
+Sg+Ill:mui K ;
+Sg+Loc:muust K ;
+Sg+Com:muin K ;
+Sg+Abe:muuttáá K ;
+Ess:mussân K ;
+Par:mussâd K ;
+Pl+Nom:musah K ;
+Pl+Nom:muuh K ;
+Pl+Gen:mui K ;
+Pl+Acc:muid K ; !7 stuárráámussáid 6 stuárráámusâid
+Pl+Ill:muid K ;
!+Pl+Ill:%>ááid K ;
+Pl+Loc:muin K ;
+Pl+Com+Err/Orth:muigijn K ;
+Pl+Com:muiguin K ;
+Pl+Abe:muittáá K ;

LEXICON MUSj-SUF !!= * __@CODE@__
+Sg+Nom:muš K ;
+Sg+Gen:muu K ;
+Sg+Acc:muu K ;
+Sg+Ill:mužân K ;
+Sg+Loc:muust K ;
+Sg+Com:muin K ;
+Sg+Abe:muuttáá K ;
+Ess:mužžân K ;
+Par:mužžâd K ;
+Pl+Nom:muuh K ;
+Pl+Gen:mui K ;
+Pl+Acc:muid K ;
+Pl+Ill:muid K ;
!+Pl+Ill:%>ááid K ;
+Pl+Loc:muin K ;
+Pl+Com+Err/Orth:mušâijgijn K ;
+Pl+Com:muiguin K ;
+Pl+Abe:muittáá K ;

LEXICON CMUS-SUF !!= * __@CODE@__
+Sg+Nom:mus K ;
+Sg+Gen:muu K ;
+Sg+Acc:muu K ;
+Sg+Ill:mui K ;
+Sg+Ill:musân K ;
+Sg+Loc:muust K ;
+Sg+Com:muin K ;
+Sg+Abe:muuttáá K ;
+Ess:mužžân K ;
+Ess:mussân K ;
+Par:mužžâd K ;
+Par:mussâd K ;
+Pl+Nom:muuh K ;
+Pl+Nom:musah K ;
+Pl+Gen:mui K ;
+Pl+Gen:musâi K ;
+Pl+Acc:muid K ; !7 stuárráámussáid 6 stuárráámusâid
+Pl+Acc:musâid K ; !7 stuárráámussáid 6 stuárráámusâid
+Pl+Ill:muid K ;
+Pl+Ill:mussáid K ;
!+Pl+Ill:%>ááid K ;
+Pl+Loc:muin K ;
+Pl+Loc:musâin K ;
+Pl+Com+Err/Orth:muigijn K ;
+Pl+Com:muiguin K ;
+Pl+Com:musâiguin K ;
+Pl+Abe:muittáá K ;
+Pl+Abe:musâittáá K ;


LEXICON A_VUOTA  !!= * __@CODE@__ for derivation
+Der/vuotâ:vuot%^RCâ 2KIELA ;

LEXICON A_ADV  !!= * __@CODE@__ for derivation
+Der/avt+Adv:ávt K ; !pyeremusávt
+Der/avt+Adv+Use/NG:âht K ; !pyeremusâht

LEXICON A_LAGAN  !!= * __@CODE@__ for derivation
+Der/lagan: LAGAN ;

