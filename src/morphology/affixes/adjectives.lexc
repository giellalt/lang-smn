! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no



! ===========================
!! !!!Adjective affixes
! ===========================

! SV|ÁE|ÁI  EA|SC FCD  EWG|WG|CSH  CLEN  RVSH|RLEN   SLEN|SVSH  SLOW

LEXICON adjstems
+A+Attr: K ;
+A+Sg+Nom: K ;


! Even-syllable stems
! -------------------

LEXICON ATTR
+A+Attr:  K ;


LEXICON ANOLAS

!! Adj - pooliitlâš: # attr. pooliitlâš; 
!! puolikongruenssi mahdollinen (#-paikat), puolikongr. asemesta SgNom.
!! Case endings in the affixes/nouns.lexc file.
+A+Attr:âš K ;
+A: LAS_COMMON ; 
+A:%> COMP ;
+A:%>u MOS ; ! tehálumos


LEXICON SUOTAS  !!= __@CODE@__ ! see also KOOIDAS, diff in attr; inflection is the same than in SUOTAS
+A+Attr:%^CSH  K ; ! suotâs < suotâs
+A: X_PIIVTAS ;
+A:%^RVSH%>u COMP ;
+A:%^RVSH%>u MOS ; ! 

LEXICON A_CISSAAS  !!= __@CODE@__ 
+A+Attr:  K ; !placeholder
+A: X_CISSAAS ;


LEXICON ALKKEE
+A+Attr:s K ; ! muálkkáás
ALKKEE_COMMON ;

LEXICON PYECCEE
+A+Attr: K ; ! attr. pyeccee ! Inflection like in ALKKEE
 ALKKEE_COMMON ;


LEXICON ALKKEE_COMMON
+A: X_TAHHEE ;
+A:%^SVSH%> COMPe ; ! assab
+A:%> MUS ; ! váiváámus hoppuumus

!+A+Sg+Gen: K ;
!+A+Sg+Acc: K ;
!+A+Sg+Ill:%>i K ;
!+A+Sg+Loc:%>st K ;
!+A+Sg+Com:%>in K ;
!+A+Sg+Abe:%>ttáá K ;
!+A+Ess:%>n K ;
!+A+Par:%>d K ; 
!+A+Pl+Nom:%>h K ;
!+N+Pl+Gen:%>i K ;
!+N+Pl+Acc:%>id K ;
!+N+Pl+Ill:%>id K ;
!+N+Pl+Loc:%>in K ;
!+N+Pl+Com:%>iguin K ;
!+N+Pl+Abe:%>ittáá K ;


LEXICON KEPPIS	
+A+Attr:%^FCDâ%> K ;    ! keppâ
 A_LITTER ;
!+A+Attr:%>isâs K ;    ! keppisâs
!+A+Sg+Nom:%>is K ;	! keppis
!+A+Sg+Gen:%>is K ; ! keppis
!+A+Sg+Acc:%>is K ; ! keppis
!+A+Sg+Ill:%>ân K ; ! keppisân
!+A+Sg+Loc:%>ist K ; ! keppisist
!+A+Sg+Com:%>áin K ; ! keppisáin
!+A+Sg+Abe:%>ttáá K ; ! keppisttáá
!+A+Ess:%>in K ; ! keppisin
!+A+Par:%>id K ; ! keppisid
!+A+Pl+Nom:%>eh K ; ! keppiseh
!+N+Pl+Gen:%>ij K ; ! keppisij
!+N+Pl+Acc:%>ijd K ; ! keppisijd
!+N+Pl+Ill:%>áid K ; ! keppisáid
!+N+Pl+Loc:%>ijn K ; ! keppisijn
!+N+Pl+Com:%>ijguin K ; ! keppisijguin
!+N+Pl+Abe:%>ijttáá K ; ! keppisijttáá


LEXICON A_INDECL
+A+Attr: K ;    ! kulhijdum
+A+Sg+Nom: K ;  ! kulhijdum

LEXICON KOSKES
+A+Attr:%^FCD K ;     !attr. koške
+A: X_PIIVTAS ;
+A:%>u COMP ; !
+A:%>u MOS ;

LEXICON KOOIDAS
+A+Attr:%^FCDâ%^RVSH K ;     !attr. koidâ
+A: X_PIIVTAS ;
+A:%>u COMP ; !uđđâsub lieggâsub korrâsub
+A:%>u MOS ;

LEXICON UCES
+A+Attr:â K ;     !attr. uccâ
+A+Sg+Nom:%^WG%>s K ;
+A:s%> COMP ;
+A:%> MUS ;



!LEXICON KOOIDAS_COMMON
!+A+Sg+Nom:%^RLEN K ;  ! kooidâs These should find a lexicon in nouns.lexc; see SUOTAS (difference in attr)
!+A+Sg+Gen: K ; ! koidâs
!+A+Sg+Acc: K ; ! koidâs
!+A+Sg+Ill:%>ân K ; ! koidâsân
!+A+Sg+Loc:%>ist K ; ! koidâsist
!+A+Sg+Com:%>áin K ; ! koidâsáin
!+A+Sg+Abe:%>ttáá K ; ! koidâsttáá
!+A+Ess:%>in K ; ! koidâsin
!+A+Par:%>id K ; ! koidâsid
!+A+Pl+Nom:%>eh K ; ! koidâseh
!+N+Pl+Gen:%>ij K ; ! koidâsij
!+N+Pl+Acc:%>ijd K ; ! koidâsijd
!+N+Pl+Ill:%>id K ; ! koidâsáid
!+N+Pl+Loc:%>ijn K ; ! koidâsijn
!+N+Pl+Com:%>ijguin K ; ! koidâsijguin
!+N+Pl+Abe:%>ijttáá K ; ! koidâsijttáá



LEXICON KUBDUV ! doesn't fit with KAAMUV because of kubduvân, kubduvist
+A+Attr:%^FCDâ K ;     !attr. kubdâ, ollâ 
+A+Sg+Nom: K ;   	! kubduv
+A+Sg+Gen:%^FCDu K ;  			 	! kubduu 
+A+Sg+Acc:%^FCDu K ;   			! kubduu
+A+Sg+Ill:%>ân   K ;       		! kubduvân
+A+Sg+Loc:%>ist K ;        		! kubduvist
+A+Sg+Com:%>áin K;        		 ! kubduváin
+A+Sg+Abe:%^FCDu%>ttáá K ;  		    ! kubduuttáá
+A+Ess:%>in K ;           ! kubduvin
+A+Par:%>id K ;            ! kubduvid
+A+Pl+Nom:%>eh K ;         ! kubduveh
+A+Pl+Gen:%>ij K ;         ! kubduvij
+A+Pl+Acc:%>ijd K ;        ! kubduvijd
+A+Pl+Ill:%>áid K ;        ! kubduváid 
+A+Pl+Loc:%>ijn K ;        ! kubduvijn
+A+Pl+Com:%>ijguin K ;     ! kubduvijguin
+A+Pl+Com+Err/Orth:%>ijgijn K ;     ! 
+A+Pl+Abe:%>ijttáá K ;     ! kubduvijttáá


!+A+Sg+Nom:v K ; ! kubduv
!+A+Sg+Gen:%^SV%^SLEN  K ; ! kubduu
!+A+Sg+Acc:%^SV%^SLEN  K ; ! kubduu
!+A+Sg+Ill:v%>ân  K ; ! kubduvân
!+A+Sg+Loc:v%>ist  K ; ! kubduvist
!+A+Sg+Com:v%áin  K ; ! kubduváin
!+A+Sg+Abe:%^SV%^SLEN%>ttáá  K ; ! kubduuttáá
!+A+Ess:v%>in  K ; ! kubduvin
!+A+Par:v%>id  K ; ! kubduvid
!+A+Pl+Nom:v%>eh  K ; ! kubduveh
!+A+Pl+Gen:v%>ij  K ; ! kubduvij
!+A+Pl+Acc:v%>ijd  K ; ! kubduvijd
!+A+Pl+Ill:v%>áid  K ; ! kubduváid
!+A+Pl+Loc:v%>ijn  K ; ! kubduvijn
!+A+Pl+Com:v%>ijguin  K ; ! kubduvijguin
!+A+Pl+Abe:v%>ijttáá  K ; ! kubduvijttáá

LEXICON LAGAN
 A_LITTER ;

LEXICON NJUORAS
+A+Attr:%^WG K ; !njuorâs
+A+Sg+Nom:%^WG%^RLEN K ; ! njuorâs
+A+Sg+Gen:  K ; ! njuorrâs
+A+Sg+Acc:  K ; ! njuorrâs
+A+Sg+Ill:%^WG%>ân  K ; ! njuorâsân
+A+Sg+Loc:%^WG%>ist  K ; ! njuorâsist
+A+Sg+Com:%^WG%>áin  K ; ! njuorâsáin
+A+Sg+Abe:%>ttáá  K ; ! njuorrâsttáá
+A+Ess:%^WG%>in  K ; ! njuorâsin
+A+Par:%^WG%>id  K ; ! njuorâsid
+A+Pl+Nom:%^WG%>eh  K ; ! njuorâseh
+A+Pl+Gen:%^WG%>ij  K ; ! njuorâsij
+A+Pl+Acc:%^WG%>ijd  K ; ! njuorâsijd
+A+Pl+Ill:%^WG%>áid  K ; ! njuorâsáid
+A+Pl+Loc:%^WG%>ijn  K ; ! njuorâsijn
+A+Pl+Com:%^WG%>ijguin  K ; ! njuorâsijguin
+A+Pl+Abe:%^WG%>ijttáá  K ; ! njuorâsijttáá


LEXICON HAANIS
+A+Attr:%^EWG%^RVSH%> K ; ! hänis
+A: X_PAAVIR	  ;

LEXICON VAANIS
+A+Attr:%^FCD%^EWG%^RVSH K ; ! väni
+A: X_PAAVIR	  ;


LEXICON NJAALGIS ! Attr. not in the lexicon
+A: X_KAARBIS ;
+A:%> COMPo ;


LEXICON MUSTOTTEM
+A+Attr:s # ;
+A+Attr:mes # ;
+A+Sg+Nom:m # ;

LEXICON ASITTEM
+A+Attr: # ;
+A+Sg+Nom: # ;

LEXICON OLMOOSMETTUM
+A+Attr: # ;
+A+Sg+Nom: # ;


! upper:lower contlex "koriste" ; ! kommentára

LEXICON PUIGA
+A+Attr: # ; ! pu´igâ
+A+Sg+Nom: # ;
+A:%> COMP ;
+A:%> MUS ;


LEXICON AARRAD
+A+Attr: # ; ! arâdis
+A+Sg+Nom: # ;
 
LEXICON HIAJU
+A+Attr:s # ; ! hiäjus
+A: X_VUAGGU ;
+A:%> COMP ;
+A:%> MUS ;

 
LEXICON MUCCAD 
!! Adj - muččâd: # attr. mu´čis
+A+Attr:i%^WG%>s # ; ! mučis
:âd A_LITTER ;


LEXICON CEPPI
!! Adj - čeppi: # attr. če´pis
+A+Attr:%^WG%>s K ;
+A: X_AIGI ;
+A:%^WG%> COMPe ;
+A:%^WG%> MUS ;

LEXICON HUMO
+A+Attr:%^WG%>s K ;
+A: X_NJUNE ;
+A:%> COMPe ;
+A:%> MUS ;


LEXICON A_VUALAAS !!= __@CODE@__ 
+A+Sg+Nom:%^SV%^SLEN%>š K ; 							! kamevuálááš
+A+Sg+Gen:%^SV%^SLEN%> K ; 							! kamevuáláá
+A+Sg+Acc:%^SV%^SLEN%> K ;					! kamevuáláá
+A+Sg+Ill:%>šân K ;		! kamevuálášân
+A+Sg+Loc:%>šist K ;				! kamevuálášist
+A+Sg+Com:%>šáin K ; 							! kamevuálášáin
+A+Sg+Abe:%^SV%^SLEN%>ttáá K ; 				! cissááttáá
+A+Ess:%^SV%^SLEN%^SVLOW%>žin K ;			! cissáážin
+A+Par:%^SV%^SLEN%^SVLOW%>žid K ; 		! cissáážid
+A+Pl+Nom:%^SV%^SLEN%>h K ; 				! cissááh
+A+Pl+Gen:%>i K ; 							! cissái
+A+Pl+Acc:%>id K ; 							! cissáid
+A+Pl+Ill:%>id K ; 							! cissáid
+A+Pl+Loc:%>in K ; 							! cissáin
+A+Pl+Com:%>iguin K ; 						! cissáiguin
+A+Pl+Com+Err/Orth:%>igijn K ; 						! cissáiguin
+A+Pl+Abe:%>ittáá K ; 						! cissáittáá


! Fix for adjectives moved from the noun lexicon

LEXICON PASTIL
+A+Attr:%>is  K ; !
A_LITTER ;

LEXICON A_LITTER
+A: ODD_SUFF_LONGSTEM ;
+A: ODD_SUFF_SHORTSTEM ;
+A:%>u  COMP ; !fiskâdub fiskâduboh fijnásuboi vaigâdub tergâdub pehtilub
+A:%>u  MOS ; ! 


LEXICON FISKAD
+A+Attr:i%^WG%>s # ; ! fiskis
+A+Attr:âd%>is  K ; !
:âd A_LITTER ;


LEXICON A_EEHID
+A+Attr:  K ; !placeholder
+A: X_EEHID	  ;

LEXICON A_EEMEED
+A+Attr:  K ; !placeholder
+A: X_EEMEED ;

LEXICON A_KOLME !!= @CODE@ ad hoc three syllable stem > LITTER
+A+Attr:  K ; !placeholder
 A_LITTER ;

LEXICON HAAITIS
+A+Attr:%^EWG%^RVSH  K ; !rähis
+A: X_PAAVIR	  ;
+A:%>u COMP ;
+A:%>u MOS ;


LEXICON STYERES
+A+Attr:uorrâ  K ; ! stuorrâ
+A+Sg+Nom:yeres K ;	! 
+A+SgNomCmp:uorrâ R ;
+A:uárráá%> COMPe ;
+A:uárráá%> MUS ;


LEXICON A_KUNAGAS
+A+Attr:  K ; !placeholder
+A:s X_KUNAGAS	  ;


LEXICON ALMOS
+A+Attr:  K ; !placeholder
+A: X_SAJOS	  ;

LEXICON A_APINA
+A+Attr:  K ; !placeholder
+A: X_APINA	  ;


LEXICON LAAMI
+A+Attr:  K ; !placeholder
+A: X_SAAMI	  ;

LEXICON A_TAHHEE
+A+Attr:  K ; !placeholder
+A: X_TAHHEE	  ;

LEXICON RUAVIS
+A+Attr:%^EWG%^RLEN  K ; !ruávis 
+A+Sg+Nom:%^EWG%^RLEN K ;					!  
+A+SgNomCmp:%^EWG%^RLEN R ;
+A+Sg+Gen: K ;					!  
+A+Sg+Acc: K ;					!  
+A+Sg+Ill:%^CSH%>ân K ;	!  
+A+Sg+Loc:%^CSH%>ist K ;	!  
+A+Sg+Com:%^CSH%>áin K ;			!  
+A+Sg+Abe:%>ttáá K ;			!  
+A+Ess:%^CSH%>in K ;		!  
+A+Par:%^CSH%>id K ;		!  
+A: X_JESANEH ;

LEXICON PYERI
+A+Attr:  K ; 
+A: X_KYELI	  ;
+A:%^WG%> eCOMP ;
+A:%^WG%> MUS ;

LEXICON NUORA
+A+Attr:  K ; 
+A: X_KIELA	  ;
+A:%> COMPe ;
+A:%> MUS ;




LEXICON MAJEMUS  !!= __@CODE@__ 
+A+Sg+Nom:%>š K ;
+A+SgNomCmp:%>š R ;
+A+Sg+Gen:%^SV%^SLEN K ;
+A+Sg+Acc:%^SV%^SLEN K ;
+A+Sg+Ill:%>žân K ;
+A+Sg+Loc:%^SV%^SLEN%>st K ;
+A+Sg+Com:%>in K ;
+A+Sg+Abe:%^SV%^SLEN%>ttáá K ;
+A+Ess:%>žžân K ;
+A+Par:%>žžâd K ;
+A+Pl+Nom:%^SV%^SLEN%>h K ;
+A+Pl+Gen:%>i K ;
+A+Pl+Acc:%>id K ;
+A+Pl+Ill:%>id K ;
+A+Pl+Loc:%>in K ;
+A+Pl+Com:%>iguin K ;
+A+Pl+Com+Err/Orth:%>igijn K ;
+A+Pl+Abe:%>ittáá K ;



! These shall be added to the respective noun
! lexica, as was done for fkv.

LEXICON X_EEHID	  
+Sg+Nom: # ;

LEXICON NELJA !!= @CODE@ ad hoc three syllable stem >
+A+Sg+Nom: K ;
+N+Sg+Nom: K ;

LEXICON UANIHAS
+A+Sg+Nom:âš K ;
+A+Attr:is K ;
+A:%>á LAS_SUF ;
+A:%>u COMP ;
+A:%>u MOS ;

LEXICON UNOHAS
+A+Sg+Nom:âs K ;
+A+Attr:is K ;
+A:%>á LAS_SUF ;
+A:%>u COMP ;
+A:%>u MOS ;


! Odd-syllable stems
! ------------------
! Contracted stems
! ----------------

! Comparation
! -----------
 
LEXICON COMPe !!= * __@CODE@__
+Comp:b COMP-SUF ;
+Comp+Attr:b K ;
+Comp+Der/Dimin:bâš K ; !need paradigm

LEXICON eCOMP !!= * __@CODE@__
+Comp: eCOMP-SUF ;
+Comp+Attr:eb K ;
 
LEXICON COMP !!= * __@CODE@__
+Comp:b COMP-SUF ;
+Comp+Attr: K ;
 
 
LEXICON COMPo !!= * __@CODE@__
+Comp:b oCOMP-SUF ;
+Comp+Attr: K ;

LEXICON eCOMP-SUF  !!= * __@CODE@__ 
+Sg+Nom:eb K ;
 :b COMP-SUF ;

LEXICON oCOMP-SUF  !!= * __@CODE@__ 
+Sg+Nom: K ;
+Sg+Gen: K ;
+Sg+Acc: K ;
+Sg+Loc:b%>oost K ;
+Pl+Nom:%>oh K ;


LEXICON COMP-SUF  !!= * __@CODE@__ 
+Sg+Nom: K ;
+Sg+Gen: K ;
+Sg+Acc: K ;
+Sg+Ill:%>ân K ;
+Sg+Loc:%>ist K ;
+Sg+Com:%>áin K ;
+Sg+Abe:%>ttáá K ;
+Ess:%>in K ;
+Par:%>id K ;
+Pl+Nom:%>eh K ;
+Pl+Gen:%>ij K ;
+Pl+Acc:%>ijd K ;
+Pl+Ill:%>áid K ;
!+Pl+Ill:%>ááid K ;
+Pl+Loc:%>ijn K ;
+Pl+Com+Err/Orth:%>ijgijn K ;
+Pl+Com:%>ijguin K ;
+Pl+Abe:%>ijttáá K ;


LEXICON MUS !!= * __@CODE@__
+Superl: MUS-SUF ;
+Superl+Attr:mus K ;
+Superl+Der/avt+Adv:mus%>ávt K ;


LEXICON MOS !!= * __@CODE@__
+Superl:mos X_LITTER ;
+Superl+Attr:mos K ;
+Superl+Der/avt+Adv:mos%>ávt K ;

LEXICON MUS-SUF !!= * __@CODE@__
+Sg+Nom:mus K ;
+Sg+Gen:muu K ;
+Sg+Acc:muu K ;
+Sg+Ill:mui K ;
+Sg+Loc:muust K ;
+Sg+Com:muin K ;
+Sg+Abe:muuttáá K ;
+Ess:mussân K ;
+Par:mussâd K ;
+Pl+Nom:musah K ;
+Pl+Nom:muuh K ;
+Pl+Gen:mui K ;
+Pl+Acc:muid K ;
+Pl+Ill:muid K ;
!+Pl+Ill:%>ááid K ;
+Pl+Loc:muin K ;
+Pl+Com+Err/Orth:muigijn K ;
+Pl+Com:muiguin K ;
+Pl+Abe:muittáá K ;

LEXICON MUSj-SUF !!= * __@CODE@__
+Sg+Nom:muš K ;
+Sg+Gen:muu K ;
+Sg+Acc:muu K ;
+Sg+Ill:mužân K ;
+Sg+Loc:muust K ;
+Sg+Com:muin K ;
+Sg+Abe:muuttáá K ;
+Ess:mužžân K ;
+Pl+Nom:muuh K ;
+Pl+Gen:mui K ;
+Pl+Acc:muid K ;
+Pl+Ill:muid K ;
!+Pl+Ill:%>ááid K ;
+Pl+Loc:muin K ;
+Pl+Com+Err/Orth:mušâijgijn K ;
+Pl+Com:muiguin K ;
+Pl+Abe:muittáá K ;



