! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no



! ===========================
!! !!!Adjective affixes
! ===========================

! SV|ÁE|ÁI  EA|SC FCD  EWG|WG|CSH  CLEN  RVSH|RLEN   SLEN|SVSH  SLOW

LEXICON adjstems  !!= __@CODE@__ temporary, for stems waiting for a lexicon
+A+Attr: K ;
+A+Sg+Nom: K ;

LEXICON ATTR !!= __@CODE@__ for stems with only attribute form
+A+Attr:  K ;


!! !!Even-syllable stems
! -------------------

LEXICON ALKKEE  !!= __@CODE@__ directing to X_TAHHEE, attr. älkkees
+A+Attr:s K ; ! älkkees
ALKKEE_COMMON ;
+A: ADJ_DER ;

LEXICON PYECCEE  !!= __@CODE@__ directing to X_TAHHEE, attr. pyeccee
+A+Attr: K ; 
 ALKKEE_COMMON ;


LEXICON ALKKEE_COMMON !!= __@CODE@__ common for ALKKEE and PYECCEE
+A: X_TAHHEE ;
+A:%^SVSH%> COMPe ; ! assab
+A:%> MUS ; ! váiváámus hoppuumus
+A+SgNomCmp:  R ;


LEXICON UCES !!= __@CODE@__ not yet done, attr uccâ
+A+Attr:â K ;     !attr. uccâ
+A+Sg+Nom:e%^WG%>s K ;
+A:es%> COMP ;
+A:â%^WG%> MUS ;
+A:e%^WG%> MUS ;
+A:e ADJ_DER ;
+A+SgNomCmp:â R ;
+A+SgNomCmp: R ;


LEXICON PUIGA !!= __@CODE@__ not yet done, attr puigâ
+A+Attr: K ; ! pu´igâ
+A+Sg+Nom: K ;
+A:%> COMP ;
+A:%> MUS ;
+A+SgNomCmp: R ;


LEXICON HIAJU !!= __@CODE@__ directing to X_VUAGGU, attr. hiäjus
+A+Attr:s K ; ! hiäjus
+A: X_VUAGGU ;
+A:%> COMP ;
+A:%> MUS ;
+A+SgNomCmp: R ;

 
LEXICON CEPPI !!= __@CODE@__ directing to X_AIGI, attr. čepis
+A+Attr:%^WG%>s K ;
+A: X_AIGI ;
+A:%^WG%> COMPe ;
+A:%^WG%> MUS ;
+A+SgNomCmp: R ;
+A: ADJ_DER ;

LEXICON HERKKI !!= __@CODE@__ directing to X_KIRKKO, attr. herhis
+A+Attr:%^WG%>s K ;
+A: X_KIRKKO ;
+A:%^WG%> COMPe ;
+A:%^WG%> MUS ;
+A+SgNomCmp: R ;
+A: ADJ_DER ;


LEXICON HUMO !!= __@CODE@__ directing to X_NJUNE, attr. humos
+A+Attr:%^WG%>s K ;
+A: X_NJUNE ;
+A:%> COMPe ;
+A:%> MUS ;
+A+SgNomCmp: R ;

LEXICON JULME !!= __@CODE@__ directing to X_OKKO, attr. julmes
+A+Attr:%^WG%>s K ;
+A: X_OKKO ;
+A:%> COMPe ;
+A:%> MUS ;
+A+SgNomCmp: R ;


LEXICON LAAMI !!= __@CODE@__ directing to X_SAAMI, attr. lämis
+A+Attr:s  K ; !lämis
+A: X_SAAMI	  ;

LEXICON PYERI !!= __@CODE@__ directing to X_KYELI, attr. pyeri
+A+Attr:  K ; 
+A: X_KYELI	  ;
+A:%^WG%> eCOMP ;
+A:%^WG%> MUS ;

LEXICON NUORA !!= __@CODE@__ directing to X_KIELA, attr. nuora
+A+Attr:  K ; 
+A: X_KIELA	  ;
+A:%> COMPe ;
+A:%> MUS ;




!! !!Odd-syllable stems
! ------------------

LEXICON NJUORAS !!= __@CODE@__ directing to X_RUONAS, attr. njuorâs
+A+Attr:%^EWG K ; !njuorâs
+A: X_RUONAS ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A: ADJ_DER ;
+A+SgNomCmp:%^EWG%^RLEN R ;


LEXICON HAANIS !!= __@CODE@__ directing to X_PAAVIR, attr. hänis
+A+Attr:%^EWG%^RVSH%> K ; ! hänis
+A: X_PAAVIR	  ;
+A:%^WG%>u COMPo ; 
+A+SgNomCmp:%^EWG%^RLEN R ;
+A:%^EWG%^RVSH ADJ_DER ;

LEXICON HAAITIS !!= __@CODE@__ directing to X_PAAVIR, attr. rähis, different from HAANIS ?
+A+Attr:%^EWG%^RVSH  K ; !rähis
+A: X_PAAVIR	  ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A+SgNomCmp:%^EWG%^RLEN R ;
+A: ADJ_DER ;


LEXICON VAANIS !!= __@CODE@__ directing to  X_PAAVIR, attr. väni
+A+Attr:%^FCD%^EWG%^RVSH K ; ! väni
+A: X_PAAVIR	  ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A+SgNomCmp:%^EWG%^RLEN R ;
+A:%^EWG%^RVSH ADJ_DER ;


LEXICON NJAALGIS !!= __@CODE@__ directing to  X_KAARBIS, Attr. not in the lexicon
+A: X_KAARBIS ;
+A:%^WG%>u COMPo ; !njálgásub njá%^RVlgás%^ÁI
+A:%>u MOS ;
+A+SgNomCmp:%^EWG%^RVSH R ;
+A:%^EWG%^RVSH ADJ_DER ; !njälgisvuođâ

LEXICON KOSKES !!= __@CODE@__ directing to  A_VEESKIR, attr. koške
+A+Attr:%^FCD%^EWG K ;     !attr. koške
A_VEESKIR ;
+A:%^EWG ADJ_DER ;

LEXICON FIIJNES !!= __@CODE@__ directing to  A_VEESKIR, attr. fiijnâ and fijnâ
+A+Attr:%^FCD%^RLENâ K ;     !attr. fiijnâ
KOOIDAS ;

LEXICON KOOIDAS !!= __@CODE@__ directing to  A_VEESKIR, attr. koidâ
+A+Attr:%^FCDâ%^RVSH K ;     !attr. koidâ
A_VEESKIR ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A:%^EWG ADJ_DER ;


LEXICON SUOTAS  !!= __@CODE@__ directing to  A_VEESKIR, attr. suotâs
+A+Attr:%^EWG%^RVSH  K ; ! suotâs < suotâs
A_VEESKIR ;
+A:%^EWG ADJ_DER ;


LEXICON A_VEESKIR  !!= __@CODE@__ directing to X_VEESKIR, no attr.
+A: X_VEESKIR ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A+SgNomCmp:%^EWG%^RLEN R ;


LEXICON KEPPIS !!= __@CODE@__ directing to A_LITTER, attr. keppâ
+A+Attr:%^FCDâ%> K ;    ! keppâ
 A_LITTER ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A+SgNomCmp:  R ;
+A: ADJ_DER ;


!forms here for reference
!+A+Attr:%>isâs K ;    ! keppisâs
!+A+Sg+Nom:%>is K ;	! keppis
!+A+Sg+Gen:%>is K ; ! keppis
!+A+Sg+Acc:%>is K ; ! keppis
!+A+Sg+Ill:%>ân K ; ! keppisân
!+A+Sg+Loc:%>ist K ; ! keppisist
!+A+Sg+Com:%>áin K ; ! keppisáin
!+A+Sg+Abe:%>ttáá K ; ! keppisttáá
!+A+Ess:%>in K ; ! keppisin
!+A+Par:%>id K ; ! keppisid
!+A+Pl+Nom:%>eh K ; ! keppiseh
!+N+Pl+Gen:%>ij K ; ! keppisij
!+N+Pl+Acc:%>ijd K ; ! keppisijd
!+N+Pl+Ill:%>áid K ; ! keppisáid
!+N+Pl+Loc:%>ijn K ; ! keppisijn
!+N+Pl+Com:%>ijguin K ; ! keppisijguin
!+N+Pl+Abe:%>ijttáá K ; ! keppisijttáá

LEXICON PASTIL !!= __@CODE@__ directing to A_LITTER, attr. pastilis
+A+Attr:%>is  K ; !
A_LITTER ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A: ADJ_DER ;
+A+SgNomCmp: R ;

LEXICON FISKAD !!= __@CODE@__ directing to A_LITTER, attr. fiskis (fikâdis)
+A+Attr:i%^WG%>s # ; ! fiskis
+A+Attr+Err/Orth:âd%>is K ;
:âd A_LITTER ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A+SgNomCmp:i%^WG%>s R ;
+A: ADJ_DER ;


LEXICON JUOLLAD !!= __@CODE@__ directing to A_LITTER, attr, juolis, juolâdis, juolâdâs
+A+Attr:i%^WG%>s # ; ! juolis
+A+Attr:âd%^WG%>is  K ; !
+A+Attr:âd%^WG%>âs  K ; !
:âd A_LITTER ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A+SgNomCmp:i%^WG%>s R ;
+A: ADJ_DER ;


LEXICON A_LITTER !!= __@CODE@__ directing to X_LITTER, no attr.
+A: ODD_SUFF_LONGSTEM ;
+A: ODD_SUFF_SHORTSTEM ;
+A:%>u  COMP ; !fiskâdub fiskâduboh fijnásuboi vaigâdub tergâdub pehtilub
+A:%>u  MOS ; ! 



LEXICON ALMOS !!= __@CODE@__ directing to X_SAJOS, attr. almos
+A+Attr:  K ; !placeholder
+A: X_SAJOS	  ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A+SgNomCmp:%^EWG%^SVSH  R ;
+A: ADJ_DER ;


LEXICON A_APINA !!= __@CODE@__ directing to X_APINA, attr. apina
+A+Attr:  K ; !placeholder
+A: X_APINA	  ;
+A+SgNomCmp: R ;


LEXICON SOOHAD !!= __@CODE@__ directing to X_EEMEED, attr. soohad
+A+Attr:  K ; !placeholder
+A: X_EEMEED ;
+A:%^WG%>u COMPo ; 
+A:%>u MOS ;
+A: ADJ_DER ;
+A+SgNomCmp: R ;

LEXICON A_KOLME !!= @CODE@ ad hoc three syllable stem > LITTER
+A+Attr:  K ; !placeholder
 A_LITTER ;


LEXICON KUBDUV !!= __@CODE@__ . It doesn't fit with KAAMUV because of kubduvân, kubduvist
+A+Attr:%^FCDâ K ;     !attr. kubdâ, ollâ 
+A+Sg+Nom: K ;   	! kubduv
+A+Sg+Gen:%^FCDu K ;  			 	! kubduu 
+A+Sg+Acc:%^FCDu K ;   			! kubduu
+A+Sg+Ill:%>ân   K ;       		! kubduvân
+A+Sg+Loc:%>ist K ;        		! kubduvist
+A+Sg+Com:%>áin K;        		 ! kubduváin
+A+Sg+Abe:%^FCDu%>ttáá K ;  		    ! kubduuttáá
+A+Ess:%>in K ;           ! kubduvin
+A+Par:%>id K ;            ! kubduvid
+A+Pl+Nom:%>eh K ;         ! kubduveh
+A+Pl+Gen:%>ij K ;         ! kubduvij
+A+Pl+Acc:%>ijd K ;        ! kubduvijd
+A+Pl+Ill:%>áid K ;        ! kubduváid 
+A+Pl+Loc:%>ijn K ;        ! kubduvijn
+A+Pl+Com:%>ijguin K ;     ! kubduvijguin
+A+Pl+Com+Err/Orth:%>ijgijn K ;     ! 
+A+Pl+Abe:%>ijttáá K ;     ! kubduvijttáá
+A+SgNomCmp: R ;
+A: ADJ_DER ;


LEXICON NELJA !!= @CODE@ ad hoc three syllable stem >
+A+Sg+Nom: K ;
+N+Sg+Nom: K ;

!! !!Contracted stems
! ----------------
LEXICON STYERES !!= __@CODE@__ directing to X_KAARBIS, attr. stuorrâ
+A+Attr:%^FCDâ  K ; ! stuorrâ
+A:%^FCD CCOMPe ;
+A:%^FCD%^SLEN%> CMUS ;
+A+Err/Orth:%^FCD%> CMUS ;
+A: X_KAARBIS ; !
+A+SgNomCmp:%^FCDâ R ;


LEXICON A_CISSAAS !!= __@CODE@__ directing to  X_CISSAAS, not in use yet
+A+Attr:  K ; !placeholder
+A: X_CISSAAS ;
+A+SgNomCmp:%^SV%^SLEN%>š  R ;


LEXICON MUOTTII !!= __@CODE@__ almost like KARRII, direct to it?
+A+Attr:s K ; ! muottiis
+A+Sg+Nom: K ;  ! muottii
+A+Sg+Gen: K ;  ! muottii
+A+Sg+Acc: K ;  ! muottii
+A+Sg+Ill:%>jân K ;  ! muottiijân
+A+Sg+Loc:%>st K ;  ! muottiist
+A+Sg+Com:%^SVSH%>jn K ;  ! muottijn
+A+Sg+Abe:%>ttáá K ;  ! muottiittáá
+A+Ess:%>jin K ;  ! muottiijin
+Al+Pl+Nom:%>h K ;  ! muottiih
+A+Pl+Gen:%^SVSH%>j K ;  ! muottij
+A+Pl+Acc:%^SVSH%>jd K ;  ! muottijd
+A+Pl+Ill:%^SVSH%>jd K ;  ! muottijd
+A+Pl+Loc:%^SVSH%>jn K ;  ! muottijn
+A+Pl+Com:%>guin K ;  ! muottiiguin
+A+Pl+Abe:%>ttáá K ;  ! muottiittáá

LEXICON A_VUALAAS !!= __@CODE@__ 
+A+Sg+Nom:%^SV%^SLEN%>š K ; 							! kamevuálááš
+A+Sg+Gen:%^SV%^SLEN%> K ; 							! kamevuáláá
+A+Sg+Acc:%^SV%^SLEN%> K ;					! kamevuáláá
+A+Sg+Ill:%>šân K ;		! kamevuálášân
+A+Sg+Loc:%>šist K ;				! kamevuálášist
+A+Sg+Com:%>šáin K ; 							! kamevuálášáin
+A+Sg+Abe:%^SV%^SLEN%>ttáá K ; 				! cissááttáá
+A+Ess:%^SV%^SLEN%^SVLOW%>žin K ;			! cissáážin
+A+Par:%^SV%^SLEN%^SVLOW%>žid K ; 		! cissáážid
+A+Pl+Nom:%^SV%^SLEN%>h K ; 				! cissááh
+A+Pl+Gen:%>i K ; 							! cissái
+A+Pl+Acc:%>id K ; 							! cissáid
+A+Pl+Ill:%>id K ; 							! cissáid
+A+Pl+Loc:%>in K ; 							! cissáin
+A+Pl+Com:%>iguin K ; 						! cissáiguin
+A+Pl+Com+Err/Orth:%>igijn K ; 						! cissáiguin
+A+Pl+Abe:%>ittáá K ; 						! cissáittáá
+A+SgNomCmp:%^SV%^SLEN%>š R ;


!! !!4 syll stems
! ----------------

LEXICON A_KUNAGAS !!= __@CODE@__ directing to  X_KUNAGAS
+A+Attr:  K ; !placeholder
+A:s X_KUNAGAS	  ;
+A+SgNomCmp: R ;


LEXICON UANIHAS !!= __@CODE@__ directing to LAS_SUF, attr, -âš
+A+Sg+Nom:âš K ;
+A+Attr:is K ;
+A:%>á LAS_SUF ;
+A:%>u COMP ;
+A:%>u MOS ;

LEXICON UNOHAS !!= __@CODE@__ directing to LAS_SUF, attr, -is
+A+Sg+Nom:âs K ;
+A+Attr:is K ;
+A:%>á LAS_SUF ;
+A:%>u COMP ;
+A:%>u MOS ;


LEXICON ANOLAS !!= __@CODE@__ directing to LAS_COMMON, attr, -âš
!! Adj - pooliitlâš: # attr. pooliitlâš; 
!! puolikongruenssi mahdollinen (#-paikat), puolikongr. asemesta SgNom.
!! Case endings in the affixes/nouns.lexc file.
+A+Attr:âš K ;
+A: LAS_COMMON ; 
+A:%> COMP ;
+A:%>u MOS ; ! tehálumos
+A:âš ADJ_DER ;
+A+SgNomCmp:âš R ;

LEXICON LAGAN !!= __@CODE@__ directing to A_LITTER, attr. -lágán
+A+Attr: K ;
 A_LITTER ;

LEXICON MUSTOTTEM !!= __@CODE@__ not all forms. attr. -es
+A+Attr:s K;
+A+Attr:mes K ;
+A+Sg+Nom:m K ;
+A+SgNomCmp:m R ;

LEXICON A_INDECL !!= __@CODE@__ not all forms., attr = Sg. Nom.
+A+Attr: K ;    ! kulhijdum
+A+Sg+Nom: K ;  ! kulhijdum
+A+SgNomCmp:  R ;



LEXICON OLMOOSMETTUM !!= __@CODE@__, not all forms. attr. = Sg. Nom.
+A+Attr: K ;
+A+Sg+Nom: K ;
+A+SgNomCmp: R ;

!! !!Comparision
! -----------
 
 
LEXICON COMPe !!= * __@CODE@__ NUORA uses this lexicon
+Comp:b COMP-SUF ;
+Comp+Attr:b K ;
+Comp+Der/Dimin:bâš K ; !needs lexicon

LEXICON CCOMPe !!= * __@CODE@__ STYERES uses this lexicon
+Comp:b CCOMP-SUF ;
+Comp+Attr:b K ;
+Comp+Der/Dimin:bâš K ; !needs lexicon


LEXICON eCOMP !!= * __@CODE@__
+Comp: eCOMP-SUF ;
+Comp+Attr:eb K ;
 
LEXICON COMP !!= * __@CODE@__
+Comp:b COMP-SUF ;
+Comp+Attr: K ;
 
 
LEXICON COMPo !!= * __@CODE@__ NJUORAS, NJAALGIS uses this lexicon
+Comp: oCOMP-SUF ;
+Comp+Attr:b K ;

LEXICON eCOMP-SUF  !!= * __@CODE@__ 
+Sg+Nom:eb K ;
 :b COMP-SUF ;

LEXICON oCOMP-SUF  !!= * __@CODE@__ 
+Sg+Nom:b K ;
+Sg+Gen:b K ;
+Sg+Acc:b K ;
+Sg+Ill:ub%>on K ;
+Sg+Loc:bb%>oost K ;
+Sg+Com:bb%>oin K ;
+Sg+Abe:b%>ttáá K ;
+Ess:bb%>on K ;
+Par:bb%>od K ;
+Pl+Nom:b%>oh K ;
+Pl+Gen:b%>oi K ;
+Pl+Acc:bb%>oid K ;
+Pl+Ill:bb%>oid K ;
+Pl+Loc:bb%>oin K ;
+Pl+Com:b%>oiguin K ;
+Pl+Abe:b%>oittáá K ;

LEXICON CCOMP-SUF  !!= * __@CODE@__  STYERES uses this lexicon
+Sg+Nom: K ;
+Sg+Gen: K ;
+Sg+Acc: K ;
COMP-SUF_COMMON ;


LEXICON COMP-SUF  !!= * __@CODE@__ 
+Sg+Nom:%^SLEN K ;
+Sg+Gen:%^SLEN K ;
+Sg+Acc:%^SLEN K ;
COMP-SUF_COMMON ;

LEXICON COMP-SUF_COMMON  !!= * __@CODE@__
+Sg+Ill:%^SLEN%>ân K ;
+Sg+Loc:%^SLEN%>ist K ;
+Sg+Com:%^SLEN%>áin K ;
+Sg+Abe:%>ttáá K ;
+Ess:%^SLEN%>in K ;
+Par:%^SLEN%>id K ;
+Pl+Nom:%^SLEN%>eh K ;
+Pl+Gen:%^SLEN%>ij K ;
+Pl+Acc:%^SLEN%>ijd K ;
+Pl+Ill:%^SLEN%>áid K ;
!+Pl+Ill:%>ááid K ;
+Pl+Loc:%^SLEN%>ijn K ;
+Pl+Com+Err/Orth:%^SLEN%>ijgijn K ;
+Pl+Com:%^SLEN%>ijguin K ;
+Pl+Abe:%^SLEN%>ijttáá K ;

 
LEXICON SUP_MUS  !!= * __@CODE@__
+A: MUS ;

LEXICON SUP_MUSJ  !!= * __@CODE@__ majemuš
+A: MUSj ;


LEXICON MUS !!= * __@CODE@__
+Superl: MUS-SUF ;
+Superl+Attr:mus K ;
+Superl+Der/avt+Adv:mus%>ávt K ;

LEXICON MUSj !!= * __@CODE@__ SUP_MUSJ uses this lexicon, eg. majemuš
+Superl: MUSj-SUF ;
+Superl+Attr:muš K ;
+Superl+Der/avt+Adv:muš%>ávt K ;


LEXICON CMUS !!= * __@CODE@__  STYERES uses this lexicon
+Superl: CMUS-SUF ;
+Superl+Attr:mus K ;
+Superl+Der/avt+Adv:mus%>ávt K ;


LEXICON MOS !!= * __@CODE@__
+Superl:mos X_LITTER ;
+Superl+Attr:mos K ;
+Superl+Der/avt+Adv:mos%>ávt K ;

LEXICON MUS-SUF !!= * __@CODE@__
+Sg+Nom:mus K ;
+Sg+Gen:muu K ;
+Sg+Acc:muu K ;
+Sg+Ill:mui K ;
+Sg+Loc:muust K ;
+Sg+Com:muin K ;
+Sg+Abe:muuttáá K ;
+Ess:mussân K ;
+Par:mussâd K ;
+Pl+Nom:musah K ;
+Pl+Nom:muuh K ;
+Pl+Gen:mui K ;
+Pl+Acc:muid K ; !7 stuárráámussáid 6 stuárráámusâid
+Pl+Ill:muid K ;
!+Pl+Ill:%>ááid K ;
+Pl+Loc:muin K ;
+Pl+Com+Err/Orth:muigijn K ;
+Pl+Com:muiguin K ;
+Pl+Abe:muittáá K ;

LEXICON MUSj-SUF !!= * __@CODE@__
+Sg+Nom:muš K ;
+Sg+Gen:muu K ;
+Sg+Acc:muu K ;
+Sg+Ill:mužân K ;
+Sg+Loc:muust K ;
+Sg+Com:muin K ;
+Sg+Abe:muuttáá K ;
+Ess:mužžân K ;
+Par:mužžâd K ;
+Pl+Nom:muuh K ;
+Pl+Gen:mui K ;
+Pl+Acc:muid K ;
+Pl+Ill:muid K ;
!+Pl+Ill:%>ááid K ;
+Pl+Loc:muin K ;
+Pl+Com+Err/Orth:mušâijgijn K ;
+Pl+Com:muiguin K ;
+Pl+Abe:muittáá K ;

LEXICON CMUS-SUF !!= * __@CODE@__
+Sg+Nom:muuh K ;
+Sg+Gen:mui K ;
+Sg+Acc:muid K ;
+Sg+Ill:muid K ;
+Sg+Loc:muust K ;
+Sg+Com:muin K ;
+Sg+Abe:muuttáá K ;
+Ess:mussân K ;
+Par:mussâd K ;
+Pl+Nom:muuh K ;
+Pl+Gen:mui K ;
+Pl+Acc:muid K ; !7 stuárráámussáid 6 stuárráámusâid
+Pl+Ill:muid K ;
!+Pl+Ill:%>ááid K ;
+Pl+Loc:muin K ;
+Pl+Com+Err/Orth:muigijn K ;
+Pl+Com:muiguin K ;
+Pl+Abe:muittáá K ;


LEXICON ADJ_DER  !!= * __@CODE@__ for derivation
+Der/vuota:vuot%^RCâ KIELA ;

