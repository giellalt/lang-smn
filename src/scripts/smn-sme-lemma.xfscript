
! File to make smn into quasi-sme

! to compile
! xfst -f smn-sme-lemma.xfscript

! to use:
! cat list | lookup smnsme.fst



define Vow = [ a | e | i | o | u | ä | á | â | y ] ;

define initp  p -> b  || [.#.|h|â] _  ;
define initt  t -> d  || [.#.|m|â] _  ;
define initk  k -> g  || [.#.|m|t|h|â] _  ;

define dnnn [ n n -> d n ,
              v n -> v d n ,
              i n -> i d n ,
              l m -> l b m ,
              r m -> r b m ,
              i j -> d j ,
              m m -> p m ,
              h č -> k č ,
              k t -> k t , 
              š t -> i t , 
              š k -> i k ,
              p t -> k t ,
              p s -> k s 
              ] ;

define hppttkk [ p p -> h p p ,
                 t t -> h t t ,
                 k k -> h k k ,
                 c c -> h c c ,
                 č č -> h č č 
                 ] ;


define aVow [ ä -> á , â -> a ] ;

define uaVow  u á -> o a ;
define iaVow  i ä -> e a ;
define yeVow  y e -> u o ;

define finalt [ b | đ | g | p | d | h ] -> t || _ .#. ;

define finalm m -> n || _ .#. ;
define finale e -> i || _ .#. ;

define aaVow       [ a a | á á ] -> á ;
define aeaeVow       ä ä -> á ;
define eeVow     e e -> e a  ;
define iiVow       i i -> i ;
define ooVow       o o -> o ;
define uuVow       u u -> u ;

define reducedA â -> a ;

               

read regex [ 
initp
.o. initt
.o. initk    
.o. dnnn
.o. hppttkk
.o. uaVow
.o. iaVow
.o. yeVow
.o. aVow
.o. finalt
.o. finalm
.o. finale
.o. aaVow
.o. aeaeVow
.o. eeVow
.o. iiVow
.o. ooVow
.o. uuVow
.o. reducedA
] ;

invert net ;


save stack smnsme.fst

