! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ==================== !
! The North Sámi dates !
! ==================== !


! 12.8.
! Borgemánu guoktenuppelot beaivi

Multichar_Symbols

@U.MONTH.1@ 
@U.MONTH.2@ 
@U.MONTH.3@ 
@U.MONTH.4@ 
@U.MONTH.5@ 
@U.MONTH.6@ 
@U.MONTH.7@ 
@U.MONTH.8@ 
@U.MONTH.9@ 
@U.MONTH.10@ 
@U.MONTH.11@ 
@U.MONTH.12@ 


LEXICON Root

uđđâivemáánu%_@U.MONTH.1@:@U.MONTH.1@ 31DAYS ;
kuovâmáánu%_@U.MONTH.2@:@U.MONTH.2@ 29DAYS ;
njuhčâmáánu%_@U.MONTH.3@:@U.MONTH.3@ 31DAYS ;
cuáŋuimáánu%_@U.MONTH.4@:@U.MONTH.4@ 30DAYS ;
vyesimáánu%_@U.MONTH.5@:@U.MONTH.5@ 31DAYS ;
kesimáánu%_@U.MONTH.6@:@U.MONTH.6@ 30DAYS ;
syeinimáánu%_@U.MONTH.7@:@U.MONTH.7@ 31DAYS ;
porgemáánu%_@U.MONTH.8@:@U.MONTH.8@ 31DAYS ;
čohčâmáánu%_@U.MONTH.9@:@U.MONTH.9@ 30DAYS ;
roovvâdmáánu%_@U.MONTH.10@:@U.MONTH.10@ 31DAYS ;
skammâmáánu%_@U.MONTH.11@:@U.MONTH.11@ 30DAYS ;
juovlâmáánu%_@U.MONTH.12@:@U.MONTH.12@ 31DAYS ;
  
  
LEXICON 29DAYS
vuosttaš%_:1 B ;
nubbi%_:2 B ;
goalmmát%_:3 B ;
njealját%_:4 B ;
viđát%_:5 B ;
guđát%_:6 B ;
čihččet%_:7 B ;
gávccát%_:8 B ;
ovccát%_:9 B ;
logát%_:1%0 B ;
oktanuppelogát%_:11 B ;
guoktenuppelogát%_:12 B ;
golbmanuppelogát%_:13 B ;
njealljenuppelogát%_:14 B ;
vihttanuppelogát%_:15 B ;
guhttanuppelogát%_:16 B ;
čiežanuppelogát%_:17 B ;
gávccinuppelogát%_:18 B ;
ovccinuppelogát%_:19 B ;
guoktelogát%_:2%0 B ;
guoktelogivuosttaš%_:21 B ;
guokteloginubbi%_:22 B ;
guoktelogigoalmmát%_:23 B ;
guokteloginjealját%_:24 B ;
guoktelogiviđát%_:25 B ;
guoktelogiguđát%_:26 B ;
guoktelogičihččet%_:27 B ;
guoktelogigávccát%_:28 B ;
guoktelogiovccát%_:29 B ;
  
LEXICON 30DAYS
golbmalogát%_:3%0 B ;
29DAYS ;
  
LEXICON 31DAYS
golbmalogivuosttaš%_:31 B ;
30DAYS ;
  
  
LEXICON B
peeivvi%_:%. MONTH ;
  
LEXICON MONTH
@U.MONTH.1@:1@U.MONTH.1@ X ;
@U.MONTH.2@:2@U.MONTH.2@ X ;
@U.MONTH.3@:3@U.MONTH.3@ X ;
@U.MONTH.4@:4@U.MONTH.4@ X ;
@U.MONTH.5@:5@U.MONTH.5@ X ;
@U.MONTH.6@:6@U.MONTH.6@ X ;
@U.MONTH.7@:7@U.MONTH.7@ X ;
@U.MONTH.8@:8@U.MONTH.8@ X ;
@U.MONTH.9@:9@U.MONTH.9@ X ;
@U.MONTH.10@:1%0@U.MONTH.10@ X ;
@U.MONTH.11@:11@U.MONTH.11@ X ;
@U.MONTH.12@:12@U.MONTH.12@ X ;
  
LEXICON X
:%. # ;
