! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no


!! !!!Inari Saami number <-> text


Multichar_Symbols

@U.DATE.1@
@U.DATE.2@
@U.DATE.3@
@U.DATE.4@
@U.DATE.5@
@U.DATE.6@
@U.DATE.7@
@U.DATE.8@
@U.DATE.9@
@U.DATE.10@
@U.DATE.11@
@U.DATE.12@
@U.DATE.13@
@U.DATE.14@
@U.DATE.15@
@U.DATE.16@
@U.DATE.17@
@U.DATE.18@
@U.DATE.19@
@U.DATE.20@
@U.DATE.21@
@U.DATE.22@
@U.DATE.23@
@U.DATE.24@
@U.DATE.25@
@U.DATE.26@
@U.DATE.27@
@U.DATE.28@
@U.DATE.29@
@U.DATE.30@
@U.DATE.31@


  +Use/NG    ! Do not generate, for isme-ped.fst and apertium

! Note! the diacritic marks in this file are only part of the lower
! representation. This means they can only be used from number to letter
! conversion of date and month representation, not the other way around.
! In order to get a two-way conversion, they must be added to the 
! upper level as well.


LEXICON Root
HUNDREDSM ;		! 200M
čyeti:1 HUNDREDM ;	! 100M
TENSM ;			! 20-99M
TEENSM ;		! 10-19M
ONESM ;			! 1-9M
HUNDREDST ;		! 200000-999999
HUNDREDST ;		! 200000-999999
čyeti:1 HUNDREDT ;	! 100000-100999
TENST ;			! 20000-99999,10000-10999
TEENST ;		! 11000-19999
ONEST ;			! 2000-9999
tuhháát:1 THOUSAND ;	! 1000-1999
UNDERTHOUSAND ;		! 100-999
TENS ;			! 20-99
TEENS ;			! 10-19
ONES ;			! 1-9

LEXICON HUNDREDSM
kyehti:2  CUODIM ;
kulmâ:3 CUODIM ;
nelji:4 CUODIM ;
vittâ:5 CUODIM ;
kuttâ:6 CUODIM ;
čiččâm:7 CUODIM ;
käävci:8 CUODIM ;
oovce:9 CUODIM ;

LEXICON CUODIM
čyeti: HUNDREDM ;

LEXICON HUNDREDM
TENSM ;
TEENSM ;
:%0 ONESM ;
:%0%0 MILJON ;

LEXICON TEENSM
:1 TEENM ;

LEXICON TEENM
ohtâ:1 LOHKAIM ;
kyehti:2 LOHKAIM ;
kulmâ:3 LOHKAIM ;
nelji:4 LOHKAIM ;
vittâ+Use/NG:5 LOHKAIM ;
kuttâ+Use/NG:6 LOHKAIM ;
vitt:5 LOHKAIM ;
kutt:6 LOHKAIM ;
čiččâm:7 LOHKAIM ;
käävci:8 LOHKAIM ;
oovce:9 LOHKAIM ;

LEXICON LOHKAIM
nubáloh: MILJON ;

LEXICON TENSM
love:1%0 MILJON ;
kyehti:2 LOGIM ;
kulmâ:3 LOGIM ;
nelji:4 LOGIM ;
vittâ:5 LOGIM ;
kuttâ:6 LOGIM ;
čiččâm:7 LOGIM ;
käävci:8 LOGIM ;
oovce:9 LOGIM ;

LEXICON LOGIM
love:%0 MILJON ;
love: ONESM ;

LEXICON ONESM
miljon% :1 OVERTHOUSANDS ;
kyehti:2 MILJON ;
kulmâ:3 MILJON ;
nelji:4 MILJON ;
vittâ:5 MILJON ;
kuttâ:6 MILJON ;
čiččâm:7 MILJON ;
käävci:8 MILJON ;
oovce:9 MILJON ; 

LEXICON MILJON
% miljona% : OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERTHOUSANDS
HUNDREDST ;		! x00.000.000
čyeđe:1 HUNDREDT ;	! 100.000.000
:%0 TENST ;		!  20.000.000
:%0 TEENST ;		!  10.000.000
:%0%0 TWOST ;		!   2.000.000
:%0%0 SEVENST ;		!   7.000.000
tuhháát:%0%01 THOUSAND ;	!   1.000.000
:%0%0%0 UNDERTHOUSAND ;	
:%0%0%0%0 TENS ;
:%0%0%0%0 TEENS ;
:%0%0%0%0%0 ONES ;
:%0%0%0%0%0%0 # ;	!  x000000


LEXICON HUNDREDST
kyehti:2  TWOCUODIT ;	! 200000-299999
kulmâ:3 TWOCUODIT ;	! 300000-399999
nelji:4 TWOCUODIT ;	! 400000-499999
vittâ:5 TWOCUODIT ;	! 500000-599999
kuttâ:6 TWOCUODIT ;	! 600000-699999
čiččâm:7 SEVENCUODIT ;	! 700000-799999
käävci:8 SEVENCUODIT ;	! 800000-899999
oovce:9 SEVENCUODIT ;	! 900000-999999

LEXICON TWOCUODIT
čyeđe: HUNDREDT ;	! 100000-199999

LEXICON SEVENCUODIT
čyeđe: HUNDREDT ;

LEXICON HUNDREDT	! X = 1-9, Y = 0-9
TENST ;			! X2XYYY, X10YYY
TEENST ;		! X1XYYY
ohtâ:%01 TWOTHOUSANDS ; ! Same tuhháát as 2000
:%0 ONEST ;		! XX0YYY
:%0%0 THOUSANDS ;	! X00YYY

LEXICON TEENST
:1 TEENT ;

LEXICON TEENT
love:%0 THOUSANDS ;
ohtâ:1 LOHKAIT ;
kyehti:2 LOHKAIT ;
kulmâ:3 LOHKAIT ;
nelji:4 LOHKAIT ;
vitt:5 LOHKAIT ;
vittâ+Use/NG:5 LOHKAIT ;
kutt:6 LOHKAIT ;
kuttâ+Use/NG:6 LOHKAIT ;
čiččâm:7 LOHKAIT ;
käävci:8 LOHKAIT ;
oovce:9 LOHKAIT ;

LEXICON LOHKAIT
nubáloh: THOUSANDS ;

LEXICON TENST
kyehtlov:2 LOGIT ;
kulmâlov:3 LOGIT ;
kulmlov+Use/NG:3 LOGIT ;
neljilov:4 LOGIT ;
vittlov:5 LOGIT ;
kuttlov:6 LOGIT ;
čiččâmlov:7 LOGIT ;
kähcilov:8 LOGIT ;
ohcelov:9 LOGIT ;

LEXICON LOGIT
:%0 THOUSANDS ;
ohtâ:1 THOUSANDS ;
ONEST ;

LEXICON ONEST
TWOST ;			! 2000-6999
SEVENST ;               ! 7000-9999

LEXICON THOUSANDS
tuhháát:1 THOUSAND ;    ! 1000-1999

LEXICON TWOST
kyeht:2 TWOTHOUSANDS ;
kulmâ:3 TWOTHOUSANDS ;
nelji:4 TWOTHOUSANDS ;
vittâ:5 TWOTHOUSANDS ;
kuttâ:6 TWOTHOUSANDS ;

LEXICON SEVENST
čiččâm:7 SEVENTHOUSANDS ;
käävci:8 SEVENTHOUSANDS ;
oovce:9 SEVENTHOUSANDS ;

LEXICON TWOTHOUSANDS      ! 2000 - 6999
tuhháát: THOUSAND ;

LEXICON SEVENTHOUSANDS ! 7000 - 9999
tuhháát: THOUSAND ;


LEXICON THOUSAND
UNDERTHOUSAND ;	   ! 1100-1999
:%0 TENS ;	   ! 1020-1099
:%0 TEENS ;	   ! 1010-1019
:%0%0 ONES ;	   ! 1001-1009
:%0%0%0 # ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
SEVENHUNDREDS ;
TWOHUNDREDS ;
čyeti:1 HUNDRED ;


LEXICON SEVENHUNDREDS ! 700-900 with +Par
čiččâm:7 SEVENCUODI ;
käävci:8 SEVENCUODI ;
ohce:9 SEVENCUODI ;

LEXICON TWOHUNDREDS ! 200-699 with +Acc
kyehti:2 CUODI ;
kyeht+Use/NG:2 CUODI ;
kulmâ:3 CUODI ;
kulm+Use/NG:3 CUODI ;
nelji:4 CUODI ;
vittâ:5 CUODI ;
vitt+Use/NG:5 CUODI ;
vit+Use/NG:5 CUODI ;
kuttâ:6 CUODI ;
kutt+Use/NG:6 CUODI ;
kut+Use/NG:6 CUODI ;

LEXICON SEVENCUODI ! 700-900 with +Par
čyeđe: HUNDRED ;
čyet: HUNDRED ;

LEXICON CUODI ! 200-699 with +Acc
čyeđe: HUNDRED ;
čyet: HUNDRED ;

LEXICON HUNDRED
TENS ;
TEENS ;
:%0 ONES ;
:%0%0 # ;

LEXICON TEENS
:1 TEEN ;

LEXICON TEEN
oht:1 LOHKAI ;
kyeht:2 LOHKAI ;
kulmâ:3 LOHKAI ;
kulm+Use/NG: 3 LOHKAI ;
nelji:4 LOHKAI ;
vitt:5 LOHKAI ;
vit+Use/NG: 5 LOHKAI ;
kutt:6 LOHKAI ;
kut+Use/NG: 6 LOHKAI ;
čiččâm:7 LOHKAI ;
käävci:8 LOHKAI ;
oovce:9 LOHKAI ;

LEXICON LOHKAI
nubáloh: # ;
:%. LOGAAT ;

LEXICON LOGAAT
nubálovváád: # ; 

LEXICON TENS
love:1%0 # ;
lovváád:1%0%. # ; 
kyeht:2 LOGI ;
kulm+Use/NG:3 LOGI ;
kulmâ:3 LOGI ;
nelji:4 LOGI ;
vit+Use/NG:5 LOGI ;
vitt:5 LOGI ;
kut+Use/NG:6 LOGI ;
kutt:6 LOGI ;
čiččâm:7 LOGI ;
kähci:8 LOGI ;
ohce:9 LOGI ;

LEXICON LOGI
lov:%0 # ;
lov: ONES ;

LEXICON ONES
CARDINAL ;
ORDINAL ;

LEXICON CARDINAL
ohtâ:1 # ;
kyehti:2 # ;
kulmâ:3 # ;
nelji:4 # ;
vittâ:5 # ;
kuttâ:6 # ;
čiččâm:7 # ;
käävci:8 # ;
oovce:9 # ;


LEXICON ORDINAL		   
vuossâmuš:1%.     # ;   
nubbe:2%.   # ;		   
kuálmád:3%.   # ;	   
niäljád:4%. # ;	   
viiđâd:5%.   # ;		   
kuuđâd:6%.   # ;		   
čiččâd:7%.    # ;	   
káávcád:8%.   # ;	   
oovcád:9%.    # ;	   



